////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТекСтрокаВТаблице; // текущая строка в таблице значений  МФ 

Перем Документы;		// список значений с идентификаторами видов документов системы
Перем ОтобранныеДокументы;	// список отобранных документов        
    
Перем Таб;		
Перем Обновить;
Перем Расшифровка;

Перем ОткрытИзЖурналаИМ;
// используются для стандартного механизма кнопок "Обновить" и "Настройка"

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ

//******************************************************************************
// ПерерисовкаНазванийЗакладок
//
// Параметры:
// 	Нет
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Функция ПерерисовкаНазванийЗакладок()      

	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
	
КонецФункции // ПерерисовкаНазванийЗакладок	        

//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда           
		Форма.ИспользоватьСлой("Шапка,Подвал,Основной");
	Иначе     
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;
	
КонецПроцедуры //УправлениеДиалогом()   

//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)
	
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
	
КонецФункции //РасшифровкаОбновить

//******************************************************************************
// ВставитьВсе(СписокДокументов)
//
// Параметры:
// 	СписокДокументов - формируемый список значений
//
// Возвращаемое значение:
// 	Нет
//
// Вызывается из формул элементов диалога:
// 
// Описание:
//   формирует список значений со всеми видами документов
//
Процедура ВставитьВсе(СписокДокументов)
	
    СписокДокументов.УдалитьВсе();
	Для Инд=1 По Метаданные.Документ() Цикл
		СписокДокументов.ДобавитьЗначение(Инд,Метаданные.Документ(Инд).Представление());
	КонецЦикла;
	СписокДокументов.Сортировать();
	
КонецПроцедуры		// ВставитьВсе
    
//******************************************************************************
// ИсключитьВсе()
//
// Параметры:
//	Нет
//
// Возвращаемое значение:
// 	Нет
//
// Вызывается из формул элементов диалога:
//		кнопка "=>>"
// 
// Описание:
//    очищает список видов обрабатываемых документов
//
Процедура ИсключитьВсе()   
	
	ВыбранныеДокументы.УдалитьВсе();
	ВставитьВсе(ВсеДокументы);
	
КонецПроцедуры		// ИсключитьВсе()

//******************************************************************************
// ВключитьВсе()
//
// Параметры:
//	Нет
//
// Возвращаемое значение:
// 	Нет
//
// Вызывается из формул элементов диалога:
//		кнопка "<<="
// 
// Описание:
//   добавляет все виды в список видов обрабатываемых документов
//
Процедура ВключитьВсе()
	
	ВсеДокументы.УдалитьВсе();
	ВставитьВсе(ВыбранныеДокументы);
	
КонецПроцедуры    	// ВключитьВсе

//******************************************************************************
// Исключить ()
//
// Параметры:
// 	Нет
//
// Возвращаемое значение:
// 	Нет
//
// Вызывается из формул элементов диалога:
//		кнопка "- >"
// 
// Описание:
//  исключает текущую позицию из списка видов обрабатываемых документов
//
Процедура Исключить()
	
	ТекПоз = ВыбранныеДокументы.ТекущаяСтрока();
	Если ТекПоз = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Представление = "";
	Позиция = ВыбранныеДокументы.ПолучитьЗначение(ТекПоз, Представление);
	
	ВсеДокументы.ДобавитьЗначение(Позиция, Представление);
	ВсеДокументы.Сортировать();                
	
	ВыбранныеДокументы.УдалитьЗначение(ТекПоз);
	
	Если ВыбранныеДокументы.РазмерСписка() > 0 Тогда
		Если ТекПоз > ВыбранныеДокументы.РазмерСписка() Тогда
			ТекПоз = ВыбранныеДокументы.РазмерСписка();
		КонецЕсли;
		ВыбранныеДокументы.ТекущаяСтрока(ТекПоз);
	КонецЕсли;                                 
	
КонецПроцедуры  	// Исключить

//******************************************************************************
// Включить()
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
// 	Нет
//
// Вызывается из формул элементов диалога:
//	кнопка "< -"
// 
// Описание:
//  включает текущую позицию в список видов обрабатываемых документов
//
Процедура Включить()
	
	ТекПоз = ВсеДокументы.ТекущаяСтрока();
	Если ТекПоз = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Представление = "";
	Позиция = ВсеДокументы.ПолучитьЗначение(ТекПоз, Представление);
	
	ВыбранныеДокументы.ДобавитьЗначение(Позиция, Представление);
	ВыбранныеДокументы.Сортировать();
	
	ВсеДокументы.УдалитьЗначение(ТекПоз);
	
	Если ВсеДокументы.РазмерСписка() > 0 Тогда
		Если ТекПоз > ВсеДокументы.РазмерСписка() Тогда
			ТекПоз = ВсеДокументы.РазмерСписка();
		КонецЕсли;
		ВсеДокументы.ТекущаяСтрока(ТекПоз);
	КонецЕсли;                           
	
КонецПроцедуры 	// Включить

Функция ПроверитьПоМФ(Док, Реквизит, РеквизитДокумента = "")
	Если глМножественныйФильтрЗадан(ТаблицаМФ) = 0 Тогда
		Возврат 1;
	КонецЕсли;
	ТаблицаМФ.ВыбратьСтроки();
	Пока ТаблицаМФ.ПолучитьСтроку()=1 Цикл
		Если (ТаблицаМФ.СписокЭлементов.РазмерСписка()>0) и 
		(ТаблицаМФ.ИмяПеременной=Реквизит) и
		(ТаблицаМФ.ФлВкл=2) Тогда
			Если (глЕстьРеквизитШапки(Реквизит, Док.Вид()) = 0) И (глЕстьРеквизитШапки(РеквизитДокумента, Док.Вид()) = 0) Тогда
				Возврат 0;
			ИначеЕсли глЕстьРеквизитШапки(Реквизит, Док.Вид()) = 1 Тогда
				РеквизитШапки = Реквизит;
			ИначеЕсли глЕстьРеквизитШапки(РеквизитДокумента, Док.Вид()) = 1 Тогда
				РеквизитШапки = РеквизитДокумента;
			КонецЕсли;             
			ЗначениеРеквизита = Док.ПолучитьАтрибут(РеквизитШапки);
			ХорошийДокумент = 0;
			Для х = 1 По ТаблицаМФ.СписокЭлементов.РазмерСписка() Цикл
				ЗначениеФильтра = ТаблицаМФ.СписокЭлементов.ПолучитьЗначение(х);
				Если ЗначениеФильтра = ЗначениеРеквизита Тогда
					ХорошийДокумент = 1;
				КонецЕсли;
			КонецЦикла;
			Возврат ХорошийДокумент;
		КонецЕсли;                         
	КонецЦикла;
	Возврат 1;
КонецФункции

//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:    
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог
//
// Возвращаемое значение:
// 	Нет
//
// Вызывается из формул элементов диалога:
//	кнопки "сформировать" и "ОК"
// 
// Описание:
//    формирует реестр документов
//
Процедура Сформировать(ЗакрытьДиалог=0)
	
	ВидыДок = "";
	Для Инд=1 По ВыбранныеДокументы.РазмерСписка() Цикл
		ВидыДок = ВидыДок + Документы.ПолучитьЗначение(ВыбранныеДокументы.ПолучитьЗначение(Инд))+",";
	КонецЦикла;
	
	Если ПустаяСтрока(ВидыДок)=1 Тогда
		Возврат;
	КонецЕсли;
	
	Док = СоздатьОбъект("Документ");       

	Если (ВыбКонтрагент.Выбран()=1) Тогда
		Док.ВыбратьПоЗначению(ДатаНачала,ДатаКонца,"Контрагент",ВыбКонтрагент);
	ИначеЕсли (ВыбПроект.Выбран()=1) Тогда
		Док.ВыбратьПоЗначению(ДатаНачала,ДатаКонца,"Проект",    ВыбПроект);
	ИначеЕсли (ВыбАвтор.Выбран()=1) Тогда
		Док.ВыбратьПоЗначению(ДатаНачала,ДатаКонца,"Автор",     ВыбАвтор);
	ИначеЕсли (ВыбФирма.Выбран()=1) Тогда
		Док.ВыбратьПоЗначению(ДатаНачала,ДатаКонца,"Фирма",     ВыбФирма);
	ИначеЕсли (ВыбЮрЛицо.Выбран()=1) Тогда
		Док.ВыбратьПоЗначению(ДатаНачала,ДатаКонца,"ЮрЛицо",    ВыбЮрЛицо);
	ИначеЕсли (ВыбСклад.Выбран()=1) Тогда
		Док.ВыбратьПоЗначению(ДатаНачала,ДатаКонца,"Склад",     ВыбСклад);
	Иначе
		Док.ВыбратьДокументы(ДатаНачала,ДатаКонца);
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
		Таб = СоздатьОбъект("Таблица");
	Иначе
		Таб.Очистить();
	КонецЕсли;
	Таб.ИсходнаяТаблица( "РеестрДокументов" );
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "РеестрДокументов");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
	Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	
	Расшифровка.Установить("ВыбФирма", 		ВыбФирма);
	Расшифровка.Установить("ВыбЮрЛицо",		ВыбЮрЛицо);
	Расшифровка.Установить("ВыбКонтрагент", ВыбКонтрагент);
	Расшифровка.Установить("ВыбАвтор", 		ВыбАвтор);          
	Расшифровка.Установить("ВыбПроект", 	ВыбПроект);
	Расшифровка.Установить("ВыбСклад", 	    ВыбСклад);
	
	Расшифровка.Установить("ВклПроведенные",ВклПроведенные); 
	Расшифровка.Установить("ВклТекущие", 	ВклТекущие);
	Расшифровка.Установить("ВклУдаленные", 	ВклУдаленные);
	
	Расшифровка.Установить("ВыбранныеДокументы", ВыбранныеДокументы);
	Расшифровка.Установить("ВсеДокументы", 	ВсеДокументы);
	Расшифровка.Установить("ОткрытИзЖурналаИМ", 	ОткрытИзЖурналаИМ);
	

	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были

	// запомним МФ только если он задан
    Если глМножественныйФильтрЗадан(ТаблицаМФ) = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ); 
	КонецЕсли;
	
	
	Заг = "";
	
	Если ВыбЮрЛицо.Выбран()>0 Тогда
		Заг=Заг+"По юр. лицу "+СокрП(ВыбЮрЛицо)+". ";
	КонецЕсли; 
	Если ВыбФирма.Выбран()>0 Тогда
		Заг=Заг+"По фирме "+СокрП(ВыбФирма)+". ";
	КонецЕсли; 
	Если ВыбКонтрагент.Выбран()>0 Тогда
		Заг=Заг+"По контрагенту "+СокрП(ВыбКонтрагент)+". ";
	КонецЕсли; 
	Если ВыбАвтор.Выбран()>0 Тогда
		Заг=Заг+"Автор документов: "+СокрП(ВыбАвтор)+". ";
	КонецЕсли;                            
	Если ВыбПроект.Выбран()>0 Тогда
		Заг=Заг+"Проект документов: "+СокрП(ВыбПроект)+". ";
	КонецЕсли;                            
	Если ВыбСклад.Выбран()>0 Тогда
		Заг=Заг+"Склад документов: "+СокрП(ВыбСклад)+". ";
	КонецЕсли;                            
	
	глЧислоСтрок = 0;
	
	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	Таб.ВывестиСекцию("ШапкаТаблицы");
	
	// выводим шапку на каждой странице
	НачПовт = Таб.ВысотаСекции("Кнопки") + Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	глОживить(1);
	
	НПП=0;
	ИтогоПечСумма = 0;
	
	СуммыПоВалютам = СоздатьОбъект("ТаблицаЗначений");
	СуммыПоВалютам.НоваяКолонка("Валюта", "Справочник.Валюты");
	СуммыПоВалютам.НоваяКолонка("Сумма", "Число");
	
	Пока Док.ПолучитьДокумент()=1 Цикл
		
		//Проверка статуса
		Если (Док.ПометкаУдаления()>0) Тогда
			Если ВклУдаленные=0 Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли (Док.Проведен()>0) Тогда
			Если ВклПроведенные=0 Тогда
				Продолжить;
			КонецЕсли;	       
		Иначе	           
			Если ВклТекущие=0 Тогда
				Продолжить;
			КонецЕсли;	        
		КонецЕсли;	       
		
		//Проверка юр. лица, к которому относится документ
		Если ВыбЮрЛицо.Выбран() = 0 Тогда
		ИначеЕсли (Док.ЮрЛицо <> ВыбЮрЛицо) Тогда
			Продолжить;
		КонецЕсли;
		
		//Проверка фирмы, к которой относится документ
		Если ВыбФирма.Выбран() = 0 Тогда
		ИначеЕсли (Док.Фирма <> ВыбФирма) Тогда
			Продолжить;
		КонецЕсли;
		
		//Проверка автора
		Если ВыбАвтор.Выбран() = 0 Тогда
		ИначеЕсли (Док.Автор <> ВыбАвтор) Тогда
			Продолжить;
		КонецЕсли;
		
		//Проверка проекта
		Если ВыбПроект.Выбран() = 0 Тогда
		ИначеЕсли (Док.Проект <> ВыбПроект) Тогда
			Продолжить;
		КонецЕсли;
		
		//Проверка склада
		Если ВыбСклад.Выбран() = 1 Тогда
			ХорошийДокумент = 0;
			Если глЕстьРеквизитШапки("Склад",Док.Вид()) = 1 Тогда
				Если (Док.Склад = ВыбСклад) Тогда
					ХорошийДокумент = 1;
				КонецЕсли;             
			КонецЕсли;             
			Если глЕстьРеквизитШапки("СкладПолучатель",Док.Вид()) = 1 Тогда
				Если (Док.СкладПолучатель = ВыбСклад) Тогда
					ХорошийДокумент = 1;
				КонецЕсли;               
			КонецЕсли;
			Если ХорошийДокумент = 0  Тогда
			    Продолжить;
			КонецЕсли;
		КонецЕсли;

		//Проверка вида документа
		Если Найти(ВидыДок,Док.Вид()+",")=0 Тогда
			Продолжить;
		КонецЕсли;
		
		НетОш = 1; // нет ошибок при наложении фильтров
		НетОш = НетОш * ПроверитьПоМФ(Док, "Склад");
		НетОш = НетОш * ПроверитьПоМФ(Док, "Склад", "СкладПолучатель");
		НетОш = НетОш * ПроверитьПоМФ(Док, "Проект");
		НетОш = НетОш * ПроверитьПоМФ(Док, "Автор");
		Если (Док.Вид() = "ЗаявкаПокупателя") или (Док.Вид() = "ЗаявкаКлиентаРозница") или (Док.Вид() = "СчетБнРозница") Тогда
			НетОш = НетОш * ПроверитьПоМФ(Док, "АвторЗаявки", "Автор");
		Иначе
			НетОш = НетОш * ПроверитьПоМФ(Док, "АвторЗаявки");
		КонецЕсли;
		НетОш = НетОш * ПроверитьПоМФ(Док, "Контрагент");
		Если НетОш = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Состояние("Обработка "+Док.ДатаДок);
		
		НПП			= НПП + 1;
		ПечСумма	= глСуммаДокументаВЖурнале(Док.ТекущийДокумент(), "число");
		ПечВалюта	= глВалютаДокументаВЖурнале(Док.ТекущийДокумент());   
		
		
		Если ПустоеЗначение(ПечВалюта) = 0 Тогда
			Поз			= 0;
			Если СуммыПоВалютам.НайтиЗначение(ПечВалюта, Поз, "Валюта") = 1 Тогда
				СуммыПоВалютам.ПолучитьСтрокуПоНомеру(Поз);
			    СуммыПоВалютам.Сумма = СуммыПоВалютам.Сумма + Число(ПечСумма);
			Иначе
				СуммыПоВалютам.НоваяСтрока();
				СуммыПоВалютам.Сумма	= Число(ПечСумма);
				СуммыПоВалютам.Валюта	= ПечВалюта;
			КонецЕсли;
		КонецЕсли;
		
		ПечИнформация = СокрЛП(глИнформацияПоДокументуВЖурнале(Док.ТекущийДокумент()));
		
		ПечСтатус = "";
		Если (Док.ПометкаУдаления()>0) Тогда
			ПечСтатус = "Помечен на уд.";
		ИначеЕсли (Док.Проведен()>0) Тогда
			ПечСтатус = "Проведен";
		КонецЕсли;	  
		
		Если глЕстьРеквизитШапки("ДатаДокВходящий", Док.Вид()) = 1 тогда
			ПечДата = Док.ДатаДокВходящий;
		Иначе
			ПечДата = Док.ДатаДок;
		КонецЕсли;
		
		Таб.ВывестиСекцию("Докум");
		глОживить(1);
	КонецЦикла;
	
	//Таб.ВывестиСекцию("КонецОтчета");
	//глОживить(1);
	
	КолвоСтрок = СуммыПоВалютам.КоличествоСтрок();
	Если КолвоСтрок >= 1 Тогда
		СуммыПоВалютам.ПолучитьСтрокуПоНомеру(1);
		Сумма = СуммыПоВалютам.Сумма;
		Валюта = СуммыПоВалютам.Валюта;
		
		Таб.ВывестиСекцию("ИтогоСнадписью");
		глОживить(1);
		
		Для Сч = 2 По КолвоСтрок Цикл
			СуммыПоВалютам.ПолучитьСтрокуПоНомеру(Сч);
			Сумма = СуммыПоВалютам.Сумма;
			Валюта = СуммыПоВалютам.Валюта;
    		
			Таб.ВывестиСекцию("Итого");
			глОживить(1);
		КонецЦикла;
	КонецЕсли;

	Таб.Опции(0, 0, 8, 0, "РеестрДокументов", "РеестрДокументов");
	Таб.ТолькоПросмотр(1);
	Таб.ОбластьПечати(2);                
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Реестр документов","");
	
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры 		// Сформировать

Процедура УстановитьОтборПоПроектам()
	Если ТаблицаМФ.НайтиЗначение("Проекты", ТекСтрокаВТаблице, "Вид") = 1 Тогда
		ТаблицаМФ.ПолучитьСтрокуПоНомеру(ТекСтрокаВТаблице);
	Иначе
		Возврат;
	КонецЕсли;
	ОсновнойПроект = глЗначениеПоУмолчанию("ОсновнойПроект");
	Если глПроектИнтернетМагазин(ОсновнойПроект) = 1 Тогда
		СписокЭлементовМФ.ДобавитьЗначение(ОсновнойПроект.ТекущийЭлемент(), ""+ОсновнойПроект.ТекущийЭлемент());
		ТаблицаМФ.ФлВкл=2;
	Иначе
		Спр = СоздатьОбъект("Справочник.Проекты");
		Спр.ВыбратьЭлементы(); 	
		Пока Спр.ПолучитьЭлемент() = 1 Цикл  		
			Если глПроектИнтернетМагазин(Спр.ТекущийЭлемент()) = 1 Тогда
				СписокЭлементовМФ.ДобавитьЗначение(Спр.ТекущийЭлемент(), ""+Спр.ТекущийЭлемент());
				ТаблицаМФ.ФлВкл=2;
			КонецЕсли;                 		
		КонецЦикла;
	КонецЕсли;
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии()	
	ОткрытИзЖурналаИМ = 0;

	//                  тип          вид           переменная  название
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Склады", "Склад",  "По складам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Пользователи", "Автор",  "По авторам документов");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Пользователи", "АвторЗаявки",  "По авторам заявок");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Проекты", "Проект",  "По проектам");               
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты", "Контрагент",  "По контрагентам");
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
	                        
		ВыбФирма 		= глРасшифровка.Получить("ВыбФирма");
		ВыбЮрЛицо 		= глРасшифровка.Получить("ВыбЮрЛицо");
		ВыбКонтрагент 	= глРасшифровка.Получить("ВыбКонтрагент"); 
		ВыбАвтор 		= глРасшифровка.Получить("ВыбАвтор");
		ВыбПроект 		= глРасшифровка.Получить("ВыбПроект");
		ВыбСклад 		= глРасшифровка.Получить("ВыбСклад");
		
		ВклПроведенные 	= глРасшифровка.Получить("ВклПроведенные"); 
		ВклТекущие 		= глРасшифровка.Получить("ВклТекущие"); 
		ВклУдаленные 	= глРасшифровка.Получить("ВклУдаленные"); 
		                                                       
		ВыбранныеДокументы.УдалитьВсе();
	  	глРасшифровка.Получить("ВыбранныеДокументы").Выгрузить(ВыбранныеДокументы); 
	
		ВсеДокументы.УдалитьВсе();
		глРасшифровка.Получить("ВсеДокументы").Выгрузить(ВсеДокументы); 
		
		ОткрытИзЖурналаИМ = глРасшифровка.Получить("ОткрытИзЖурналаИМ");
		
		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;
	    
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать("Печать");
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;
	
	ПерерисовкаНазванийЗакладок();

	ТаблицаМФ.ВидимостьКолонки("Тип",0);
	ТаблицаМФ.ВидимостьКолонки("Вид",0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");


	УправлениеДиалогом();
	
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда
	   
		ДатаНачала = Форма.Параметр.ПолучитьЗначение(1);
		ДатаКонца = Форма.Параметр.ПолучитьЗначение(2);
		Попытка
			ОткрытИзЖурналаИМ = Форма.Параметр.ПолучитьЗначение(3);
			Если ОткрытИзЖурналаИМ = 1 Тогда
				УстановитьОтборПоПроектам();
			КонецЕсли;
		Исключение КонецПопытки;
		Сформировать();
		Форма.Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры	 	// ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	ПерерисовкаНазванийЗакладок();
	УправлениеДиалогом();       
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	
КонецПроцедуры // ВводНового()   

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореЗакладки(Номер,Значение)	
	// закладки
    Если Номер=1 Тогда
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	КонецЕсли;      
	УправлениеДиалогом();
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки

Процедура ОбработкаКликаПоТаблицеМФ(Конт)
	Если ОткрытИзЖурналаИМ = 0 Тогда
		глОбработкаКликаПоТаблицеМФ(Конт);
	Иначе
		ТекСтр	= ТаблицаМФ.ТекущаяСтрока();
		Если ТекСтр = 0 Тогда Возврат КонецЕсли;
	    ТаблицаМФ.ПолучитьСтрокуПоНомеру(ТекСтр);
		Если ТаблицаМФ.Вид = "Проекты" Тогда
			Предупреждение("При открытии отчета из журнала 'Интернет-магазин' запрещено изменение фильтра по проектам");
			Возврат;
		Иначе
			глОбработкаКликаПоТаблицеМФ(Конт);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура РаботаСТаблицейМФ(Действие, Конт)
	Если ОткрытИзЖурналаИМ = 0 Тогда
		глРаботаСТаблицейМФ(Действие, Конт);
	Иначе
		Если ТаблицаМФ.Вид = "Проекты" Тогда
			Если (Действие = "УдалитьВсе") или (Действие = "Удалить") Тогда
				глРаботаСТаблицейМФ(Действие, Конт);
				Если СписокЭлементовМФ.РазмерСписка() = 0 Тогда
					УстановитьОтборПоПроектам();
				КонецЕсли;
			Иначе
				глРаботаСТаблицейМФ(Действие, Конт);
			КонецЕсли;
		Иначе
			глРаботаСТаблицейМФ(Действие, Конт);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(Значение)  
	
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;
		Если (ТаблицаМФ.Вид = "Проекты") и (ОткрытИзЖурналаИМ = 1) Тогда
        	Если глПроектИнтернетМагазин(Значение) = 1 Тогда 
				СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
				ТаблицаМФ.ФлВкл=2;
			Иначе
				Предупреждение("При открытии отчета из журнала 'Интернет-магазин' запрещено использовать в подборе проекты не относящимся к интернет-магазинам");
				Возврат;
			КонецЕсли;
		Иначе
			СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
			ТаблицаМФ.ФлВкл=2;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора

//******************************************************************************
// ТЕЛО МОДУЛЯ
//
ДатаКонца	= ПолучитьДатуТА();
ДатаНачала  = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
Если ПустоеЗначение(ДатаНачала) = 1 Тогда
	ДатаНачала      = НачМесяца(ДатаКонца);    
КонецЕсли;


ВыбКонтрагент.ВыборГруппы(0);
	
Документы = СоздатьОбъект("СписокЗначений");
Документы.УдалитьВсе();
Для Инд=1 По Метаданные.Документ() Цикл
	Документы.ДобавитьЗначение(Метаданные.Документ(Инд).Идентификатор);
КонецЦикла;
	           
ВключитьВсе();

ВклПроведенные= 1; 
ВклТекущие    = 1; 
ВклУдаленные  = 1; 

// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
                                                                                
ТекСтрокаВТаблице="";

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

