//*******************************************
Процедура Сформировать()
	
	СпрНоменклатура = СоздатьОбъект("Справочник.Номенклатура");
	ЭтаГруппа = СоздатьОбъект("Справочник.Номенклатура");
	СпрСклады = СоздатьОбъект("Справочник.Склады");
	СпрОстаткиНоменклатуры = СоздатьОбъект("Справочник.ОстаткиНоменклатуры"); 
	ЭтотЭлемент = СоздатьОбъект("Справочник.ОстаткиНоменклатуры");
	                	
	Если СпрСклады.НайтиЭлемент(ВыбСклад) = 0 Тогда
		Сообщить("Выберите склад!","!!");
		Возврат;
	КонецЕсли;
	
	ИмяФайла = "";   
	ИмяКаталога = "";
	
	Если ФС.ВыбратьФайл(0,ИмяФайла,ИмяКаталога,"Выберите файл",,"xls","Таблицы Excel (*.xls) |*.xls") = 0 Тогда		
		Возврат;		
	КонецЕсли;
		
	Эксел = СоздатьОбъект("Excel.Application");
	
	Попытка
		ФайлЭксел = Эксел.Workbooks.Open(СокрЛП(ИмяКаталога+ИмяФайла));
	Исключение
		Сообщить("Файл " + ИмяКаталога+ИмяФайла + " не найден!","!!");
		Эксел.Quit();
		Возврат;
	КонецПопытки;
	
	ЛистЭксел = ФайлЭксел.Sheets(1);	
		   
	СтрокаНоменклатура = "*";
	МаксОстаток = 0;
	НомерСтроки = 2;
	ВсегоСтрок = 1;
	ЭлементНайден = 0;
	ПроцентЗагрузки = 0;
	
	ВсегоСтрок = Число(ЛистЭксел.Cells(1,3).Value);
	Если ВсегоСтрок < 1 Тогда
		Сообщить("Количество обрабатываемых строк должно быть указано в ячейке C1!","!!");
		Эксел.WorkBooks.close();
		Эксел.Quit();
		Возврат;
	КонецЕсли;
	
	Состояние("Прогресс: 0%");
	Сообщить("Загрузка остатков для склада <<"+Строка(ВыбСклад)+">>...");
	
	Пока НомерСтроки <= ВсегоСтрок Цикл
		
		СтрокаНоменклатура = ЛистЭксел.Cells(НомерСтроки,1).Value;
		Пока (ПустаяСтрока(СтрокаНоменклатура) = 1) И (НомерСтроки <= ВсегоСтрок) Цикл
			НомерСтроки = НомерСтроки + 1;
			СтрокаНоменклатура = ЛистЭксел.Cells(НомерСтроки,1).Value;
		КонецЦикла;
					
		МаксОстаток = Число(ЛистЭксел.Cells(НомерСтроки,3).Value);
		
		Если СпрНоменклатура.НайтиПоНаименованию(СтрокаНоменклатура,1,1) = 0 Тогда	// Если не найдена в ЭтаГруппа			
			Если СпрНоменклатура.НайтиПоНаименованию(СтрокаНоменклатура,0,1) = 1  Тогда	// Если найдена во всей базе
				ЭлементНайден = 1;				
			Иначе	// Если не найдена во всей базе				
				ЭлементНайден = 0;				
			КонецЕсли;			
		Иначе		// Если найдена в ЭтаГруппа			
			ЭлементНайден = 1;			
		КонецЕсли;	// Поиск закончен
		
		Если ЭлементНайден = 1 Тогда
		
			ЭтаНоменклатура = СпрНоменклатура.ТекущийЭлемент();
			Если ЭтаНоменклатура.ЭтоГруппа() = 0 Тогда
					
				Если ЭтаГруппа <> ЭтаНоменклатура.Родитель Тогда	// Если новая группа
					ЭтаГруппа = ЭтаНоменклатура.Родитель;
					СпрНоменклатура.ИспользоватьРодителя(ЭтаГруппа);
					//Сообщить(Строка(ЭтаГруппа) + ":");
				КонецЕсли;
								
				СпрОстаткиНоменклатуры.ИспользоватьВладельца(ЭтаНоменклатура);
				Если СпрОстаткиНоменклатуры.НайтиПоРеквизиту("Склад",ВыбСклад,0) = 1 Тогда
					ЭтотЭлемент.ИспользоватьВладельца(ЭтаНоменклатура);
					ЭтотЭлемент.НайтиЭлемент(СпрОстаткиНоменклатуры.ТекущийЭлемент());
					ЭтотЭлемент.МаксимальныйОстаток = МаксОстаток;
					ЭтотЭлемент.МинимальныйОстаток = МаксОстаток / 4;
					ЭтотЭлемент.Записать();	
				Иначе
					ЭтотЭлемент.ИспользоватьВладельца(ЭтаНоменклатура);
					ЭтотЭлемент.Новый();
					ЭтотЭлемент.Наименование = ЭтаНоменклатура.Наименование;
					ЭтотЭлемент.Склад =  ВыбСклад;
					ЭтотЭлемент.МаксимальныйОстаток = МаксОстаток;
					ЭтотЭлемент.МинимальныйОстаток = МаксОстаток / 4;
					ЭтотЭлемент.Записать();
				КонецЕсли;
				//Сообщить(Строка(НомерСтроки) + ": номенклатура <<" + СтрокаНоменклатура + ">> обработана!");
				
			Иначе	// Если ЭтоГруппа
			
				ЭтаГруппа = СпрНоменклатура.ТекущийЭлемент();
				СпрНоменклатура.ИспользоватьРодителя(ЭтаГруппа); 
				//Сообщить(Строка(ЭтаГруппа) + ":");
				
			КонецЕсли;
			
		Иначе	// Если не найдена
			
			Сообщить(Строка(НомерСтроки) + ": номенклатура <<" + СтрокаНоменклатура + ">> не найдена!","!");			
		
		КонецЕсли;
		
		Если НомерСтроки*100/ВсегоСтрок > ПроцентЗагрузки Тогда
			ПроцентЗагрузки = Окр(НомерСтроки*100/ВсегоСтрок);
			Состояние("Прогресс: " + Строка(ПроцентЗагрузки) + "%");
		КонецЕсли;
				
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	Состояние("Загрузка завершена.");
	Сообщить("Загрузка остатков для склада <<"+Строка(ВыбСклад)+">> завершена!");

	Эксел.WorkBooks.close();
	Эксел.Quit(); 

КонецПроцедуры
