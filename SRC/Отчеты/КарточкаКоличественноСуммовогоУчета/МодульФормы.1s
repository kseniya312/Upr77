////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Управляет видимостью слоев формы в зависимости от выбранного переключателя
//
Процедура УправлениеДиалогом()
	
	Форма.ИспользоватьСлой("Шапка,Подвал,Основной1,Основной2,Разделитель" +
	                       СокрЛП(ВидРазделителя));
	
КонецПроцедуры // УправлениеДиалогом()

//******************************************************************************
// ПриИзмененииСклада()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Вызывается при изменении значения в реквизите формы склад.
//  Проверяет, чтобы выбранный склад был розничным
//
Процедура ПриИзмененииСклада()
	
	Если ВыбСклад.РозничныйСклад = 0 Тогда
		Форма.ТипЦены.Доступность(1);
		Форма.ТекстТипЦены.Доступность(1);
	Иначе
		ТипЦены	= "";
		Форма.ТипЦены.Доступность(0);
		Форма.ТекстТипЦены.Доступность(0);
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииСклада()

//******************************************************************************
// УстановитьФильтрНаФирму(ТекстЗапроса)
//
// Параметры: 
//  ТекстЗапроса
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура УстановитьФильтрНаФирму(ТекстЗапроса)
	
	Если ВидРазделителя = 1 Тогда
		Если ПустоеЗначение(ВыбРазделитель1) = 0 Тогда
		    ТекстЗапроса	= ТекстЗапроса + "Условие(Фирма=ВыбРазделитель1);";
		КонецЕсли;
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		Если ПустоеЗначение(ВыбРазделитель2) = 0 Тогда
		    ТекстЗапроса	= ТекстЗапроса + "Условие(ЮрЛицо=ВыбРазделитель2);";
		КонецЕсли;
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		Если ПустоеЗначение(ВыбРазделитель3) = 0 Тогда
		    ТекстЗапроса	= ТекстЗапроса + "Условие(УпрАналитика=ВыбРазделитель3);";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // УстановитьФильтрНаФирму()

//******************************************************************************
// ПолучитьДатуДокумента(Док)
//
// Параметры: 
//  Док
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Функция ПолучитьДатуДокумента(Док)
	
	Если глЕстьРеквизитШапки("ДатаДокВходящий", Док.Вид()) = 1 Тогда
	    ДатаДок		= Док.ДатаДокВходящий;
	Иначе
	    ДатаДок		= Док.ДатаДок;
	КонецЕсли;
	
	Возврат ДатаДок;
	
КонецФункции // ПолучитьДатуДокумента()

//******************************************************************************
// ПоказатьОтчет(Табл)
//
// Параметры: 
//  Табл
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура ПоказатьОтчет(Таб)
		
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0, 0, 0, 0, "КарточкаКолицественноСуммовогоУчета", "КарточкаКолицественноСуммовогоУчета");
	Таб.Показать("Карточка количественно-стоимостного учета (ТОРГ-28)", "");
		
	Номер	= Номер + 1;
КонецПроцедуры // ПоказатьОтчет()

//******************************************************************************
// СформироватьПоРознице()
//
// Параметры: 
//  
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура СформироватьПоРознице()
	
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = "//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Фирма			= Регистр.ОстаткиТМЦ.Фирма;
	|УпрАналитика	= Регистр.ОстаткиТМЦ.Фирма.УпрАналитика;
	|ЮрЛицо			= Регистр.ОстаткиТМЦ.Фирма.ЮрЛицо;
	|Номенклатура	= Регистр.ОстаткиТМЦ.Номенклатура;
	|Склад			= Регистр.ОстаткиТМЦ.Склад;
	|Количество		= Регистр.ОстаткиТМЦ.Количество;
	|ЦенаПрод		= Регистр.ОстаткиТМЦ.ЦенаПрод;
	|ТекДок			= Регистр.ОстаткиТМЦ.ТекущийДокумент;
	|
	|Функция КоличествоПриход = Приход(Количество);
	|Функция КоличествоРасход = Расход(Количество);
	|Функция КоличествоНачОст = НачОст(Количество);
	|
	|Группировка ЦенаПрод;
	|Группировка Номенклатура без групп;
	|Группировка ТекДок упорядочить по ТекДок.ДатаДок, ТекДок.ВремяДок;
	|
	|Условие(Номенклатура	= ВыбНоменклатура);
	|Условие(Склад			= ВыбСклад);
	|"//}}ЗАПРОС
	;

	УстановитьФильтрНаФирму(ТекстЗапроса);
	
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;

	Если ПустоеЗначение(ВыбЮрЛицо) = 0 Тогда
	    ПечЮрЛицо	= СокрЛП(ВыбЮрЛицо.ПолнНаименование);
	Иначе
		ПечЮрЛицо	= "";
	КонецЕсли;
	
	Таб	= СоздатьОбъект("Таблица");
	
	КолвоОтчетов	= 0;
	Пока Запрос.Группировка("ЦенаПрод") = 1 Цикл
		КолвоОтчетов	= КолвоОтчетов + 1;
		  
		Таб	= СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица("ТОРГ-28");
		
		Цена	= Запрос.ЦенаПрод;
		Океи	= ВыбНоменклатура.БазоваяЕдиница.ОКЕИ;
		
		Таб.ВывестиСекцию("Титул");
		Таб.ВывестиСекцию("ШапкаТаб2");
		
		НачПовтор	= Таб.ВысотаСекции("Титул");
		КонПовтор	= НачПовтор + Таб.ВысотаСекции("ШапкаТаб2");
		
		Таб.ПовторятьПриПечатиСтроки(НачПовтор + 1, КонПовтор);
		
		НомСтроки = 0;
		Пока Запрос.Группировка("Номенклатура") = 1 Цикл
			ОстКолво	= Запрос.КоличествоНачОст;
			Пока Запрос.Группировка("ТекДок") = 1 Цикл
				
				НомСтроки	= НомСтроки + 1;
				
				СуммаПриход	= Цена * Запрос.КоличествоПриход;
				СуммаРасход = Цена * Запрос.КоличествоРасход;
				
				ОстКолво	= ОстКолво + Запрос.КоличествоПриход - Запрос.КоличествоРасход;
				ОстСумма	= Цена * ОстКолво; 
				
				ТекДок		= Запрос.ТекДок;
				
				ДатаДок		= ПолучитьДатуДокумента(ТекДок);
				ДатаЗаписи	= ТекДок.ДатаДок;
				Контрагент	= глНазваниеДокументаВЖурнале(ТекДок);
				Инфо		= глИнформацияПоДокументуВЖурнале(ТекДок);
				
				Если ПустоеЗначение(Инфо) = 0 Тогда
				    Контрагент	= Контрагент + " (" + Инфо + ")";
				КонецЕсли;
				
				Таб.ВывестиСекцию("Строка2");
			КонецЦикла;
		КонецЦикла;
		
		Если НомСтроки > 0 Тогда
			// если в отчет выведены строки
			
			Таб.ВывестиСекцию("Подвал");
			ПоказатьОтчет(Таб);
		КонецЕсли;
	КонецЦикла;
	
	Если КолвоОтчетов = 0 Тогда
		Цена	= 0;
		Океи	= ВыбНоменклатура.БазоваяЕдиница.ОКЕИ;
		
		Таб	= СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица("ТОРГ-28");
		Таб.ВывестиСекцию("Титул");
		Таб.ВывестиСекцию("ШапкаТаб2");
		Таб.ВывестиСекцию("Подвал");
		ПоказатьОтчет(Таб);
	КонецЕсли;
	
КонецПроцедуры // СформироватьПоРознице()

//******************************************************************************
// СформироватьПоОпту()
//
// Параметры: 
//  
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура СформироватьПоОпту()
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = "//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Фирма			= Регистр.ОстаткиТМЦ.Фирма;
	|УпрАналитика	= Регистр.ОстаткиТМЦ.Фирма.УпрАналитика;
	|ЮрЛицо			= Регистр.ОстаткиТМЦ.Фирма.ЮрЛицо;
	|Номенклатура	= Регистр.ОстаткиТМЦ.Номенклатура;
	|Склад			= Регистр.ОстаткиТМЦ.Склад;
	|Количество		= Регистр.ОстаткиТМЦ.Количество;
	|ЦенаПрод		= Регистр.ОстаткиТМЦ.ЦенаПрод;
	|ТекДок			= Регистр.ОстаткиТМЦ.ТекущийДокумент;
	|
	|Функция КоличествоПриход = Приход(Количество);
	|Функция КоличествоРасход = Расход(Количество);
	|Функция КоличествоНачОст = НачОст(Количество);
	|
	|Группировка Номенклатура без групп;
	|Группировка ТекДок упорядочить по ТекДок.ДатаДок, ТекДок.ВремяДок;
	|
	|Условие(Номенклатура	= ВыбНоменклатура);
	|Условие(Склад			= ВыбСклад);
	|"//}}ЗАПРОС
	;

	УстановитьФильтрНаФирму(ТекстЗапроса);
	
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;

	// Подготовка к заполнению выходных форм данными запроса
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("ТОРГ-28");
		
	Цена	= глПолучитьЦену(ВыбНоменклатура, ТипЦены, РабочаяДата(), ВыбНоменклатура.БазоваяЕдиница, глРубли, 1);
	Океи	= ВыбНоменклатура.БазоваяЕдиница.ОКЕИ;
		
	Если ПустоеЗначение(ВыбЮрЛицо) = 0 Тогда
	    ПечЮрЛицо	= СокрЛП(ВыбЮрЛицо.ПолнНаименование);
	Иначе
		ПечЮрЛицо	= "";
	КонецЕсли;
	
	Таб.ВывестиСекцию("Титул");
	Таб.ВывестиСекцию("ШапкаТаб2");
		
	НачПовтор	= Таб.ВысотаСекции("Титул");
	КонПовтор	= НачПовтор + Таб.ВысотаСекции("ШапкаТаб2");
		
	Таб.ПовторятьПриПечатиСтроки(НачПовтор + 1, КонПовтор);
		
	НомСтроки = 0;
	Пока Запрос.Группировка("Номенклатура") = 1 Цикл
		ОстКолво	= Запрос.КоличествоНачОст;
		Пока Запрос.Группировка("ТекДок") = 1 Цикл
				
			НомСтроки	= НомСтроки + 1;
				
			СуммаПриход	= Цена * Запрос.КоличествоПриход;
			СуммаРасход = Цена * Запрос.КоличествоРасход;
			
			ОстКолво	= ОстКолво + Запрос.КоличествоПриход - Запрос.КоличествоРасход;
			ОстСумма	= Цена * ОстКолво; 
			
			ТекДок		= Запрос.ТекДок;
			ВидДок		= ТекДок.Вид();
			
			ДатаДок		= ПолучитьДатуДокумента(ТекДок);
			ДатаЗаписи	= ТекДок.ДатаДок;
			Контрагент	= глНазваниеДокументаВЖурнале(ТекДок);
			Инфо		= глИнформацияПоДокументуВЖурнале(ТекДок);
			
			Если ПустоеЗначение(Инфо) = 0 Тогда
			    Контрагент	= Контрагент + " (" + Инфо + ")";
			КонецЕсли;
			
			Таб.ВывестиСекцию("Строка2");
		КонецЦикла;
	КонецЦикла;
		
	Таб.ВывестиСекцию("Подвал");
			
	// Вывод заполненной формы
	ПоказатьОтчет(Таб);
			
КонецПроцедуры // СформироватьПоОпту()

//******************************************************************************
// Сформировать()
//
// Параметры: 
//  ФлагЗакрытия	- если равен 1, то закрывать форму после после формирования
//                    отчета, 0 - не закрывать
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Формирует печантую форму ТОРГ-29
//
Процедура Сформировать(ФлагЗакрытия=0)
	Перем Запрос, ТекстЗапроса, Таб;

	Если глПроверкаДаты(ДатаНачала,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустоеЗначение(ВыбНоменклатура) = 1 Тогда
	    Предупреждение("Не выбрана номеклатура");
		Возврат;
	КонецЕсли;
	
	Если ПустоеЗначение(ВыбСклад) = 1 Тогда
	    Предупреждение("Не выбран склад");
		Возврат;
	КонецЕсли;
	
	Если ВыбСклад.РозничныйСклад = 0 Тогда
	    Если ПустоеЗначение(ТипЦены) = 1 Тогда
		    Предупреждение("Не указан тип учетной цены");
			Возврат;
		КонецЕсли;
		
		СформироватьПоОпту();
	Иначе
		СформироватьПоРознице();
	КонецЕсли;
	
	Если ФлагЗакрытия  = 1 Тогда
	    Форма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии(ФлагЧтенияНастройки)
	
	Если ВидРазделителя = 0 Тогда
	    ВидРазделителя	= 1;
	КонецЕсли;
	
	Форма.ВыбНоменклатура.ВыборГруппы(0);
	Форма.ВыбРазделитель1.ВыборГруппы(0);
	Форма.ВыбРазделитель2.ВыборГруппы(0);
	Форма.ВыбРазделитель3.ВыборГруппы(0);
	
	Номер	= ВосстановитьЗначение("НомКарточкиКолСтоимУчета");
	Если Номер = 0 Тогда
	    Номер	= 1;
	КонецЕсли;
	
	Если ФлагЧтенияНастройки = 0 Тогда
	    ДатаНачала	= глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
		ДатаКонца	= РабочаяДата();
	КонецЕсли;
	
	УправлениеДиалогом();
	ПриИзмененииСклада();
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗакрытии()
	
	СохранитьЗначение("НомКарточкиКолСтоимУчета", Номер);
	
КонецПроцедуры // ПриЗакрытии()