////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТекСтрокаВТаблице; // текущая строка в таблице значений  МФ

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка;      

// группировки в отчете
Перем СписокГруппировок;
Перем КоличествоГруппировок;
Перем НомерГруппировкиПоТМЦ;
Перем НомерГруппировкиПоДоговору;
Перем НомерГруппировкиПоПокупателю;
Перем НомерГруппировкиПоЗаявке;

Перем ДокументЗаказаПоставщику; // для формирования/заполнения заказа поставщику      
Перем ТаблЗначВДок; // табл.значений которую предадим в документ "Заказ поставщику"
					// по кнопке "Сформировать заказ"    

Процедура ВывестиГруппировку(Запрос,Ном) Далее

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//                          
//******************************************************************************
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

//******************************************************************************
// ПерерисовкаНазванийЗакладок
//
// Параметры:
// 	Нет
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Функция ПерерисовкаНазванийЗакладок()      
	
	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
	
КонецФункции // ПерерисовкаНазванийЗакладок()

//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)
	
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
	
КонецФункции //РасшифровкаОбновить()                                            
                                             
//******************************************************************************   
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление видимостью элементов формы.
//
Процедура УправлениеДиалогом()

	Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда           
		Форма.ИспользоватьСлой("Шапка,Подвал,Основной1,Основной2,Разделитель"+СокрЛП(ВидРазделителя));
	Иначе     
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;
	
КонецПроцедуры //УправлениеДиалогом()

//******************************************************************************
// ИзменениеПорядкаГрупп(НаправлениеСдвига)
//
// Параметры:
//  НаправлениеСдвига = 	-1 - вниз
//							 1 - вверх
// Возвращаемое значение: 
//	Нет
//
// Вызывается из формул элементов диалога:
//  кнопки "вверх" и "вниз" рядом со списком группировок
//
// Описание:  
//	Процедура производит сдвиг текущей группировки в общем
// 	списке группировок на "НаправлениеСдвига" позиций
//
Процедура ИзменениеПорядкаГрупп(НаправлениеСдвига)
	ТекСтр = Группировки.ТекущаяСтрока();
	Группировки.СдвинутьЗначение(НаправлениеСдвига, ТекСтр);
КонецПроцедуры // ИзменениеПорядкаГрупп()

//******************************************************************************
// УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
//
// Параметры:  ТекстЗапроса - переданный по ссылке текст запроса
// 			   ТекстЗагол   - переданный по ссылке текст заголовка
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Дополняет строку запроса и заголовка в соответствии с выбранными группировками.
//    
Процедура УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
	СписокГруппировок = СоздатьОбъект("СписокЗначений");
	
	Для Сч=1 По Группировки.РазмерСписка() Цикл
		Если Группировки.Пометка(Сч)=1 Тогда
			ПредставлениеГрупп="";
			ТекстГрупп=Группировки.ПолучитьЗначение(Сч,ПредставлениеГрупп);
			Если  (ТекстГрупп = "Покупатель") или (ТекстГрупп = "Номенклатура") Тогда
				ТекстБезГрупп = " без групп";
			Иначе
				ТекстБезГрупп = "";
			КонецЕсли;
			ТекстЗапроса 	= ТекстЗапроса 	+ "Группировка "+ТекстГрупп+ТекстБезГрупп+";";
			ТекстЗагол 		= ТекстЗагол 	+ ?(ТекстЗагол="",""," / ")+ПредставлениеГрупп;
			СписокГруппировок.ДобавитьЗначение(ТекстГрупп,ПредставлениеГрупп);
			Если ТекстГрупп = "Номенклатура" Тогда
				НомерГруппировкиПоТМЦ 			= СписокГруппировок.РазмерСписка();
			ИначеЕсли ТекстГрупп = "Договор" Тогда
				НомерГруппировкиПоДоговору 		= СписокГруппировок.РазмерСписка();
			ИначеЕсли ТекстГрупп = "Покупатель" Тогда
				НомерГруппировкиПоПокупателю 	= СписокГруппировок.РазмерСписка();
			ИначеЕсли ТекстГрупп = "Заявка" Тогда
				НомерГруппировкиПоЗаявке 		= СписокГруппировок.РазмерСписка();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // УстановитьГруппировкиЗапроса()

//******************************************************************************
// ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка, ТекРасшифровкаНачало="")
//
// Параметры:
//  Запрос - объект "Запрос", на основании которого строится отчет
//	Ном - Номер группировки запроса (Число)
//	НазваниеСекции - название секции, которую следует использовать (Строка)
//	ПечТекстСтроки - текстовое представление текущей строки
//	ТекРасшифровка - расшифровка текущей строки
//	ТекРасшифровкаНачало - расшифровка начала тетекущей строки
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Производит вывод в печатную форму одной строки запроса.
//
Процедура ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка, ТекРасшифровкаНачало="")   
	
	Если (Ном >= НомерГруппировкиПоДоговору) Тогда
		ПечЗаявкаС = глФРМ(Запрос.КонОстЗаявкаС,"");
		Вал = Запрос.Договор.ВалютаВзаиморасчетов;        
		
		//Если (Ном>= НомерГруппировкиПоТМЦ) и (Запрос.КонОстЗаказК<>0) Тогда
		//	ПечЗаказС = " ----- ";
		//Иначе     
		//	ПечЗаказС = "";
		//КонецЕсли;
		//
		Секц =  Таб.ПолучитьСекцию(НазваниеСекции);
		Если ПустоеЗначение(ТекРасшифровка) = 1 Тогда
			Секц.Область(1,1,1,1).Расшифровка("",2);
		КонецЕсли;
		Таб.ВывестиСекцию(Секц);
	Иначе
		Вал = "";
		ПечЗаявкаС = "";
		
		Секц =  Таб.ПолучитьСекцию(НазваниеСекции);
		Если Ном < НомерГруппировкиПоТМЦ Тогда
			Секц.Область(1,1,1,6).Объединить();
		КонецЕсли;
		          
		Если ПустоеЗначение(ТекРасшифровка) = 1 Тогда
			Секц.Область(1,1,1,1).Расшифровка("",2);
		КонецЕсли;
		
		Таб.ВывестиСекцию(Секц);
	КонецЕсли;
	    
	Если Ном>= НомерГруппировкиПоТМЦ Тогда            
		Если ВидЕдиницы = 1 Тогда
			Ед 	= Запрос.Номенклатура.ОсновнаяЕдиница;
		Иначе     
			Ед 	= Запрос.Номенклатура.БазоваяЕдиница;
		КонецЕсли;
		ПечЗаявкаК = глФРМКоличество(Запрос.КонОстЗаявкаК,Ед);
		ПечРезервК = глФРМКоличество(Запрос.КонОстРезерв,Ед);
		ПечЗаказК  = глФРМКоличество(Запрос.КонОстЗаказК,Ед);
		Секц =  Таб.ПолучитьСекцию(НазваниеСекции+"К");
		Секц.Область(1,1,1,6).Рамка(,0,,);
		Если ПустоеЗначение(ТекРасшифровка) = 1 Тогда
			Секц.Область(1,1,1,1).Расшифровка("",2);
		КонецЕсли;
		Таб.ВывестиСекцию(Секц);
	КонецЕсли;
	глОживить(1);
	
КонецПроцедуры // ПечатьСтроки()
          
//******************************************************************************
// ПечатьОснований(Заявка)
//
// Параметры:
//  Заявка         - заявка покупателя, для которой надо вывести заявки введенные на основании
//	НазваниеСекции - название выводимой секции
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//	Выводит на печать всю цепочку заявок-оснований для переданной заявки
//                                                                      
Процедура ПечатьОснований(Заявка, НазваниеСекции)  
	
	ПечТекстСтрокиСтар = Заявка;

	Если ТипЗначенияСтр(Заявка) <> "Документ" Тогда
		Возврат;
	КонецЕсли;
	
	Если Заявка.Вид() <> "ЗаявкаПокупателя" Тогда
		Возврат;
	КонецЕсли;
	
	СписокДокументов = СоздатьОбъект("СписокЗначений");
	
	Пока 1 = 1 Цикл
		ДокОсн = Заявка.ДокОснование; 
		
		Если (ДокОсн.Выбран() = 0) или (ДокОсн.Вид() <> "ЗаявкаПокупателя") Тогда
			Прервать;
		КонецЕсли;
		
		СписокДокументов.ВставитьЗначение(1, ДокОсн);
		
		Заявка = ДокОсн;	
	КонецЦикла;         
	
	Для к = 1 по СписокДокументов.РазмерСписка() Цикл     
		ТекРасшифровка       = СписокДокументов.ПолучитьЗначение(к);
		ТекРасшифровкаНачало = ТекРасшифровка;
		ПечТекстСтроки       = глПредставлениеДокумента(ТекРасшифровка);
		Вал = "";
		ПечЗаявкаС = "";
		Таб.ВывестиСекцию(НазваниеСекции);
	КонецЦикла;                              
	
	Заявка = ПечТекстСтрокиСтар;
	
КонецПроцедуры
	
		

//******************************************************************************
// ВывестиГруппировку(Запрос,Ном)
//
// Параметры:
//  Запрос - объект "Запрос"
//	Ном - номер выводимой группировки
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Выводит в печатную форму одну группировку запроса. Если
//	Есть нижележащие группировки, они выводятся также с использованием рекурсивного
//	вызова этой же процедуры.
//
Процедура ВывестиГруппировку(Запрос,Ном)
	  
	Если Ном <= КоличествоГруппировок Тогда
		ИмяГруппировки = СписокГруппировок.ПолучитьЗначение(Ном);
		
		Пока Запрос.Группировка(Ном) = 1 Цикл
			НазваниеСекции="Строка"+Ном;
			ПечТекстСтроки = Запрос.ПолучитьАтрибут(ИмяГруппировки);
			Если Ном <> НомерГруппировкиПоЗаявке Тогда
				ТекРасшифровка = ПечТекстСтроки;
				ТекРасшифровкаНачало = ПечТекстСтроки;
			Иначе             
				ТекРасшифровкаНачало = ПечТекстСтроки;
				ТекРасшифровка = СоздатьОбъект("СписокЗначений");
				ТекРасшифровка .Установить("Отчет", "СостояниеЗаявки");
				
				// все настройки помещаем в список
				ТекРасшифровка .Установить("ВыбДокумент", Запрос.Заявка);
				ТекРасшифровка .Установить("ДатаКонца",   ДатаКонца);
				Если Ном>= НомерГруппировкиПоТМЦ Тогда
					ТекРасшифровка .Установить("ВыбНоменклатура", Запрос.Номенклатура);
				КонецЕсли;
			КонецЕсли;
			Если ПустоеЗначение(ПечТекстСтроки)=1 Тогда
				ПечТекстСтроки = глПредставлениеПустогоЗначения(ИмяГруппировки);
			ИначеЕсли (ИмяГруппировки = "Договор") и (Ном < НомерГруппировкиПоПокупателю) Тогда
				ПечТекстСтроки = ""+СокрП(ПечТекстСтроки.Владелец.Наименование)+"; "+ПечТекстСтроки;
			ИначеЕсли (ИмяГруппировки = "Заявка") Тогда
				ПечатьОснований(ПечТекстСтроки, НазваниеСекции);
				ПечТекстСтроки = глПредставлениеДокумента(ПечТекстСтроки);
			КонецЕсли;
			
			ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка, ТекРасшифровкаНачало);
		
			// если есть более детальная группировка - выведем ее
			Если КоличествоГруппировок > Ном Тогда
				ВывестиГруппировку(Запрос,Ном+1);
			КонецЕсли;          
		
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ВывестиГруппировку()                                          

//******************************************************************************
// Сформировать(ЗакрытьДиалог=0, ЗаполнитьДок=0)
//
// Параметры:
//  ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог
//  ЗаполнитьДок - флаг заполнения документа ЗаказПоставщику
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   
//	Запускает отчет или получает таблицу для заполнения заказа поставщику.
Процедура Сформировать(ЗакрытьДиалог=0, ЗаполнитьДок=0)
	
	Перем Запрос, ТекстЗапроса;
	
	Если глПроверкаДаты(ДатаКонца,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ЗаявкиПокупателей");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаКонца", 	 ДатаКонца);
	Расшифровка.Установить("ВыбТМЦ",		 ВыбТМЦ);
	Расшифровка.Установить("ВыбДоговор",	 ВыбДоговор);
	Расшифровка.Установить("ВыбПокупатель",	 ВыбПокупатель);
	Расшифровка.Установить("ВидЗаявок",	     ВидЗаявок.ТекущаяСтрока());

	Расшифровка.Установить("ВыбРазделитель1",ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2",ВыбРазделитель2);
	Расшифровка.Установить("ВыбРазделитель3",ВыбРазделитель3);
	Расшифровка.Установить("ВидРазделителя", ВидРазделителя);
	
	Расшифровка.Установить("ВидЕдиницы",	 ВидЕдиницы);
	
	Расшифровка.Установить("Группировки",	 Группировки);   
	
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	
	// запомним МФ только если он задан
    Если глМножественныйФильтрЗадан(ТаблицаМФ) = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ); 
	КонецЕсли;

	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	СтрПериод = ?(ДатаКонца>= ПолучитьДатуТА(),"", 
	"Период с ДатаКонца по ДатаКонца;"); 	
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|" + СтрПериод +
	"
	|Фирма             = Регистр.Заявки.      Фирма,                      
	|                    Регистр.ЗаказыЗаявки.ЗаявкаПокупателя.Фирма,
	|                    Регистр.РезервыТМЦ.  Фирма;
	|ЮрЛицо            = Регистр.Заявки.      Фирма.ЮрЛицо,                      
	|                    Регистр.ЗаказыЗаявки.ЗаявкаПокупателя.Фирма.ЮрЛицо,
	|                    Регистр.РезервыТМЦ.  Фирма.ЮрЛицо;
	|УпрАналитика      = Регистр.Заявки.      Фирма.УпрАналитика,                      
	|                    Регистр.ЗаказыЗаявки.ЗаявкаПокупателя.Фирма.УпрАналитика,
	|                    Регистр.РезервыТМЦ.  Фирма.УпрАналитика;
	|Заявка		       = Регистр.Заявки.      ЗаявкаПокупателя,           
	|                    Регистр.ЗаказыЗаявки.ЗаявкаПокупателя,
	|                    Регистр.РезервыТМЦ.  ЗаявкаПокупателя;
	|Проект		       = Регистр.Заявки.      ЗаявкаПокупателя.Проект,           
	|                    Регистр.ЗаказыЗаявки.ЗаявкаПокупателя.Проект,
	|                    Регистр.РезервыТМЦ.  ЗаявкаПокупателя.Проект;
	|ОперацияЗаявка    = Регистр.Заявки.      ЗаявкаПокупателя.ВидОперации,           
	|                    Регистр.ЗаказыЗаявки.ЗаявкаПокупателя.ВидОперации,
	|                    Регистр.РезервыТМЦ.  ЗаявкаПокупателя.ВидОперации;
	|ДатаОтгрузкиЗаявка= Регистр.Заявки.      ЗаявкаПокупателя.ДатаОтгрузки,           
	|                    Регистр.ЗаказыЗаявки.ЗаявкаПокупателя.ДатаОтгрузки,
	|                    Регистр.РезервыТМЦ.  ЗаявкаПокупателя.ДатаОтгрузки;
	|Номенклатура      = Регистр.Заявки.      Номенклатура,               
	|                    Регистр.ЗаказыЗаявки.Номенклатура,
	|                    Регистр.РезервыТМЦ.  Номенклатура;
	|ВидНоменклатура   = Регистр.Заявки.      Номенклатура.ВидНоменклатуры,               
	|                    Регистр.ЗаказыЗаявки.Номенклатура.ВидНоменклатуры,
	|                    Регистр.РезервыТМЦ.  Номенклатура.ВидНоменклатуры;
	|Покупатель        = Регистр.Заявки.      ДоговорПокупателя.Владелец, 
	|                    Регистр.ЗаказыЗаявки.ЗаявкаПокупателя.Контрагент,
	|                    Регистр.РезервыТМЦ.  ДоговорПокупателя.Владелец;
	|Договор     	   = Регистр.Заявки.      ДоговорПокупателя,          
	|                    Регистр.ЗаказыЗаявки.ЗаявкаПокупателя.Договор,
	|                    Регистр.РезервыТМЦ.  ДоговорПокупателя;
	|КолЗаявки   	   = Регистр.Заявки.      КоличествоРасход;
	|КолРезерва	       = Регистр.РезервыТМЦ.  Количество;
	|КолЗаказа         = Регистр.ЗаказыЗаявки.Количество;
	|СумЗаявки         = Регистр.Заявки.      СтоимостьРасход;";
	
	ТекстЗапроса = ТекстЗапроса +
	"Функция КонОстЗаявкаК = КонОст(КолЗаявки);
	|Функция КонОстЗаявкаС = КонОст(СумЗаявки);
	|Функция КонОстЗаказК  = КонОст(КолЗаказа);
	|Функция КонОстРезерв  = КонОст(КолРезерва);";
	
	Загол="";
	ПечЗаголовок = "Заявки покупателей";
	
	НетОш = 1; // нет ошибок при наложении фильтров
	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",ВыбРазделитель3,"ВыбРазделитель3",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
	КонецЕсли;
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Номенклатура",ВыбТМЦ,       "ВыбТМЦ",       ТекстЗапроса,Загол,"СвойстваНоменклатуры");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Покупатель",  ВыбПокупатель,"ВыбПокупатель",ТекстЗапроса,Загол,"СвойстваКонтрагентов");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Договор",     ВыбДоговор,   "ВыбДоговор",   ТекстЗапроса,Загол);
	
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗаполнитьДок = 1 Тогда
		
		Если ПустоеЗначение(ДокументЗаказаПоставщику) = 0  Тогда // вызвали из документа
		    ДатаОтгрузки = ДокументЗаказаПоставщику.ДатаОтгрузки;
		Иначе // из отчета, формируем новый документ 
			ДатаОтгрузки = ДатаКонца;	
		КонецЕсли;
		
		ТаблЗначВДок.УдалитьСтроки();
		
		ТекстЗапроса = ТекстЗапроса	+ "Группировка Номенклатура без групп;
		                              |Условие (ОперацияЗаявка     = Перечисление.ВидыОперацийЗаявок.НаПоставку);
		                              |Условие (ДатаОтгрузкиЗаявка >= ДатаОтгрузки);
		                              |Условие (ПустоеЗначение(Номенклатура) = 0);
		                              |Условие (НЕ(ВидНоменклатура = Перечисление.ВидыНоменклатуры.Услуга));";
		
		// Если ошибка в запросе, то выход из процедуры
		Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Пока Запрос.Группировка("Номенклатура") = 1 Цикл
			
			КоличествоВЗаказ = Запрос.КонОстЗаявкаК - Запрос.КонОстРезерв - Запрос.КонОстЗаказК;
			Если КоличествоВЗаказ > 0 Тогда
				ТаблЗначВДок.НоваяСтрока();
				ТаблЗначВДок.Номенклатура = Запрос.Номенклатура;
				ТаблЗначВДок.Количество = КоличествоВЗаказ;
			КонецЕсли;
	    КонецЦикла;

		Если ЗакрытьДиалог = 1 Тогда
			СтрокаДействийФормы = "#Закрыть";
		КонецЕсли;
		
		Возврат;
	КонецЕсли; // Если ЗаполнитьДок = 1
	
	// Фильтр по виду заявок 
	ВыбВидЗаявки = ВидЗаявок.ПолучитьЗначение(ВидЗаявок.ТекущаяСтрока());
	Если ПустоеЗначение(ВыбВидЗаявки) = 0 Тогда
        ТекстЗапроса = ТекстЗапроса + "Условие (ОперацияЗаявка = ВыбВидЗаявки);";
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;      
	 
	Таб.ИсходнаяТаблица("ЗаявкиПокупателей");
	
	НомерГруппировкиПоТМЦ 		= 9999; // невозможно большие значения
	НомерГруппировкиПоДоговору 	= 9999;
	НомерГруппировкиПоПокупателю= 9999;
	НомерГруппировкиПоЗаявке 	= 9999;
	
	ПечЗаголовокСтолбца = "";  
	УстановитьГруппировкиЗапроса(ТекстЗапроса, ПечЗаголовокСтолбца);
	КоличествоГруппировок = СписокГруппировок.РазмерСписка();
	
	Если (НомерГруппировкиПоТМЦ = 9999) и (НомерГруппировкиПоДоговору=9999) Тогда
		Предупреждение("Обязательно установите группировку по номенклатуре или договору!",60);
		Возврат;
	КонецЕсли;
	
	Если КоличествоГруппировок > 5 Тогда
		Предупреждение("Нельзя сделать больше 5 группировок!",60);
		Возврат;
	КонецЕсли;
	
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;       
	
	глЧислоСтрок = 0;

	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	Таб.ВывестиСекцию("ШапкаТаблицы");

	// выводим шапку на каждой странице
	НачПовт = Таб.ВысотаСекции("Кнопки") + Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	глОживить(1);
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 0, "ЗаявкиПокупателей", "ЗаявкиПокупателей");
	
	// ВЫВОД ГРУППИРОВОК ЗАПРОСА
	ВывестиГруппировку(Запрос,1);
	
	// Заполнение полей "Итого"
	ПечатьСтроки(Запрос,0,"Итого","","");
	Таб.ОбластьПечати(3);
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);                    
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Заявки покупателей", "");   
	       
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

//******************************************************************************
// ПоКнопкеСформироватьЗаказ()
//
// Параметры:
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "Сформировать заказ".
//
// Описание:   
//	Формирует документ "Заказ поставщику" и заполняет его строками из отчета
//
Процедура ПоКнопкеСформироватьЗаказ()
	
	Перем СписокПараметров;
	
	// Проверки
	Если (ВидРазделителя <> 1) или (ВыбРазделитель1.Выбран() = 0) Тогда
	    Предупреждение("Для формирования заказа выберите фирму!", 60);     
		Возврат;
	КонецЕсли;
	// для правильного функционирования алгоритма необходимо
	// чтобы группировка заявка была помечена.
	Если Группировки.Пометка(Группировки.НайтиЗначение("Заявка"))=0 Тогда
	    Предупреждение("Для формирования заказа пометьте группировку заявка!", 60);     
		Возврат;
	КонецЕсли;
	
	Сформировать(1,1); 
	
	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.Установить("ТаблЗнач", ТаблЗначВДок);
	
	Если ПустоеЗначение(ДокументЗаказаПоставщику) = 0 Тогда
		
		// отчет вызвали из документа
		ОткрытьФорму(ДокументЗаказаПоставщику, СписокПараметров);  
	Иначе
		
		// будем формировать новый документ => добавим параметры
		СписокПараметров.Установить("ДатаДок", ДатаКонца);
		СписокПараметров.Установить("Фирма", ВыбРазделитель1);
		
		ОткрытьФорму("Документ.ЗаказПоставщику", СписокПараметров);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеСформироватьЗаказ()         

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	//                  тип          вид           переменная  название
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Номенклатура", "Номенклатура",  "По номенклатуре");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты",  "Покупатель",  "По покупателям");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Номенклатура",  "По свойствам номенклатуры");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Покупатель",  "По свойствам покупателей");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Фирмы", "Фирма",  "По фирмам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","СвоиЮрЛица", "ЮрЛицо",  "По юр. лицам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","УпрАналитика", "УпрАналитика",  "По упр. аналитике");
	
	Если ФлагВосстановленияНастройки = 0 Тогда
		
		ВидЕдиницы = 1;
		ВидРазделителя 	= 1;                     
	Иначе
		Если Группировки.НайтиЗначение("Проект") = 0 Тогда
			Группировки.ДобавитьЗначение("Проект", "Проекты");
		КонецЕсли;
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений") и (глОбновить = 0)  Тогда
		глРасшифровка = Форма.Параметр;
		ВызовИзДокумента = 1;                              
		Форма.кнСформироватьЗаказ.Заголовок("Перенести в документ");   
		Форма.кнОК.				Доступность(0);
		Форма.кнСформировать.	Заголовок("Просмотр");
		Форма.ДатаКонца.		Доступность(0);
		Форма.ВидРазделителя.	Доступность(0); 
		Форма.ЮрЛицо.			Доступность(0);
		Форма.УпрАналитика.		Доступность(0);
		Форма.ВыбРазделитель1.	Доступность(0);
		Форма.кнХРазделитель1.	Доступность(0);
	КонецЕсли;
	
	Если (глФлагРасшифровки = 1) или (ВызовИзДокумента = 1) Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		
		ВыбТМЦ 			= глРасшифровка.Получить("ВыбТМЦ");
		ВыбДоговор		= глРасшифровка.Получить("ВыбДоговор");
		ВыбПокупатель	= глРасшифровка.Получить("ВыбПокупатель"); 
		ВидЗаявок.ТекущаяСтрока(глРасшифровка.Получить("ВидЗаявок")); 
		
		ВидРазделителя	= глРасшифровка.Получить("ВидРазделителя");
		ВыбРазделитель1	= глРасшифровка.Получить("ВыбРазделитель1");
		ВыбРазделитель2	= глРасшифровка.Получить("ВыбРазделитель2");
		ВыбРазделитель3	= глРасшифровка.Получить("ВыбРазделитель3");
		
		ВидЕдиницы		= глРасшифровка.Получить("ВидЕдиницы");
		
		глРасшифровка.Получить("Группировки").Выгрузить(Группировки);
		
		Если ВызовИзДокумента = 1  Тогда
		    ДокументЗаказаПоставщику = глРасшифровка.Получить("ДокументЗаказаПоставщику");
		КонецЕсли;

		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;
	
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если (Обновить <> 2) и (ВызовИзДокумента <> 1) Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;                      
    
	ПерерисовкаНазванийЗакладок();

	ТаблицаМФ.ВидимостьКолонки("Тип",0);
	ТаблицаМФ.ВидимостьКолонки("Вид",0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");

	УправлениеДиалогом();

КонецПроцедуры		// ПриОткрытии()       

//******************************************************************************
// Предопределенная процедура.
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	ПерерисовкаНазванийЗакладок();
	УправлениеДиалогом();       
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	
КонецПроцедуры // ВводНового()            

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореЗакладки(Номер,Значение)	
	// закладки
    Если Номер=1 Тогда
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	КонецЕсли;      
	УправлениеДиалогом();
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки
                                       
//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(Значение)  
	
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора
                                                                                
////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ДатаКонца = ПолучитьДатуТА();

ВидЗаявок.УдалитьВсе();                             
ВидЗаявок.ДобавитьЗначение(ПолучитьПустоеЗначение("Перечисление.ВидыОперацийЗаявок"), "Все");
ВидЗаявок.ДобавитьЗначение(Перечисление.ВидыОперацийЗаявок.Неподтвержденная);
ВидЗаявок.ДобавитьЗначение(Перечисление.ВидыОперацийЗаявок.НаСклад);
ВидЗаявок.ДобавитьЗначение(Перечисление.ВидыОперацийЗаявок.НаПоставку);
ВидЗаявок.ТекущаяСтрока(1);
// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
                                                                                
ТекСтрокаВТаблице="";

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

Группировки.ДобавитьЗначение("Покупатель", 	"Покупатель");
Группировки.ДобавитьЗначение("Договор", 	"Договор покупателя");
Группировки.ДобавитьЗначение("Заявка", 		"Заявка");
Группировки.ДобавитьЗначение("Номенклатура","Номенклатура");
Группировки.ДобавитьЗначение("Проект",      "Проект");
Группировки.Пометка(2, 1);
Группировки.Пометка(3, 1);

ТаблЗначВДок = СоздатьОбъект("ТаблицаЗначений");
ТаблЗначВДок.НоваяКолонка("Номенклатура");
ТаблЗначВДок.НоваяКолонка("Количество");
ТаблЗначВДок.НоваяКолонка("ПоЗаявке", "Документ.ЗаявкаПокупателя");

