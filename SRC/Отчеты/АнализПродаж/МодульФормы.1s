////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//   
Перем ТекСтрокаВТаблице; // текущая строка в таблице значений  МФ 

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 
                        
// переменные предназначены для механизма универсальных группировок отчета
Перем СписокГруппировок;
Перем КоличествоГруппировок;
Перем НомерГруппировкиПоТМЦ;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ    
//******************************************************************************
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

//******************************************************************************
// ПерерисовкаНазванийЗакладок
//
// Параметры:
// 	Нет
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Функция ПерерисовкаНазванийЗакладок()      

	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
	
КонецФункции // ПерерисовкаНазванийЗакладок	

//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда           
		Если (ВыбСвойствоТМЦ.		Выбран()=1) 
		 или (ВыбСвойствоПоставщика.Выбран()=1)
		 или (ВыбСвойствоПокупателя.Выбран()=1) 
		 Тогда     
		 	ДопСлой = "ВыбСвойства,";
		 Иначе                    
		 	ДопСлой = "";
		 КонецЕсли;
		Форма.ИспользоватьСлой(ДопСлой+"Шапка,Подвал,Основной1,Основной2,Разделитель"+СокрЛП(ВидРазделителя));
	Иначе     
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;
	
КонецПроцедуры //УправлениеДиалогом()   

//******************************************************************************                                                    
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)  
	
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;                       
	
КонецФункции //РасшифровкаОбновить()                                            

//******************************************************************************
// ИзменениеПорядкаГрупп(НаправлениеСдвига)
//
// Параметры:
//  НаправлениеСдвига = 	-1 - вниз
//							 1 - вверх
// Возвращаемое значение: 
//	Нет
//
// Вызывается из формул элементов диалога:
//  кнопки "вверх" и "вниз" рядом со списком группировок
//
// Описание:  
//	Процедура производит сдвиг текущей группировки в общем
// 	списке группировок на "НаправлениеСдвига" позиций
//
Процедура ИзменениеПорядкаГрупп(НаправлениеСдвига)
	
	ТекСтр = Группировки.ТекущаяСтрока();
	ПослСдвигСтр = Группировки.РазмерСписка();
	Если не((НаправлениеСдвига = 1) и (ТекСтр = ПослСдвигСтр)) Тогда
		Группировки.СдвинутьЗначение(НаправлениеСдвига, ТекСтр);
	КонецЕсли;
	
КонецПроцедуры // ИзменениеПорядкаГрупп()

//******************************************************************************
// УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
//
// Параметры:  ТекстЗапроса - переданный по ссылке текст запроса
// 			   ТекстЗагол   - переданный по ссылке текст заголовка
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Дополняет строку запроса и заголовка в соответствии с выбранными группировками.
//       
Процедура УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
	                       
	НомерГруппировкиПоТМЦ = 9999;
	
	СписокГруппировок = СоздатьОбъект("СписокЗначений");
	
	Для Сч=1 По Группировки.РазмерСписка() Цикл
		
		Если Группировки.Пометка(Сч)=1 Тогда
			ПредставлениеГрупп="";
			ТекстГрупп		= Группировки.ПолучитьЗначение(Сч,ПредставлениеГрупп);
			Если  (ТекстГрупп = "Поставщик") или (ТекстГрупп = "Покупатель") или 
				  ((ТекстГрупп = "Номенклатура") и (ПоГруппам = 0)) Тогда
				ТекстБезГрупп = " без групп";
			Иначе
				ТекстБезГрупп = "";
			КонецЕсли;
			ТекстЗапроса 	= ТекстЗапроса 	+ "Группировка "+ТекстГрупп+ТекстБезГрупп+";";
			ТекстЗагол 		= ТекстЗагол 	+ ?(ТекстЗагол="",""," / ")+ПредставлениеГрупп;
			СписокГруппировок.ДобавитьЗначение(ТекстГрупп,ПредставлениеГрупп);
			Если ТекстГрупп = "Номенклатура" Тогда
				НомерГруппировкиПоТМЦ = СписокГруппировок.РазмерСписка();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // УстановитьГруппировкиЗапроса()

//******************************************************************************
// ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка)
//
// Параметры:
//  Запрос - объект "Запрос", на основании которого строится отчет
//	Ном - Номер группировки запроса (Число)
//	НазваниеСекции - название секции, которую следует использовать (Строка)
//	ПечТекстСтроки - текстовое представление текущей строки
//	ТекРасшифровка - расшифровка текущей строки
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Производит вывод в печатную форму одной строки запроса.
//                                                
Процедура ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка = "")
	                         
	ПечЕд = "";
	Если (Ном >= НомерГруппировкиПоТМЦ) Тогда
		Если Запрос.ЭтоГруппа("Номенклатура") = 0 Тогда
			ПечЕд 	= ?(ВидЕдиницы=1,Запрос.Номенклатура.ОсновнаяЕдиница,Запрос.Номенклатура.БазоваяЕдиница);
			ПечКол 	= глФРМКоличество(Запрос.СуммаКоличество - Запрос.СуммаКоличествоВ,ПечЕд);  
			ПечАртикул = Запрос.Номенклатура.Артикул;
			ПечПроизв = Запрос.Номенклатура.Производитель;
		Иначе                
			// если не в базовых единицах, то не имеет смысла количество
			//ПечКол 	= ?(ВидЕдиницы=1,"",глФРМКоличество(Запрос.СуммаКоличество - Запрос.СуммаКоличествоВ,ПечЕд)); 
			ПечКол 	= глФРМКоличество(Запрос.СуммаКоличество - Запрос.СуммаКоличествоВ,ПечЕд);
		КонецЕсли;
	Иначе
		//	ПечКол = "";
		ПечКол 	= глФРМКоличество(Запрос.СуммаКоличество - Запрос.СуммаКоличествоВ,ПечЕд);
	КонецЕсли;
		
	Себест		  = Запрос.СуммаСебест - Запрос.СуммаСебестВ;
	ПродСт 		  = Запрос.СуммаПродСт - Запрос.СуммаПродСтВ;
	Профит 		  = ПродСт - Себест;
	
	ЗнакПрофит 	  = ?(ПродСт<0,-1,1);
	
	ПечСебест 	  = глФРМ(Себест,"");
	ПечПродСт 	  = глФРМ(ПродСт,"");
	ПечПрофит 	  = глФРМ(Профит,"");
	ПечПрофитПроц = ?(Себест = 0, "!!!!",СокрЛ(Формат(ЗнакПрофит*Профит/Себест*100,"Ч12.3")+"%"));
	
	Таб.ВывестиСекцию(НазваниеСекции);     
	глОживить(1);
	
КонецПроцедуры //ПечатьСтроки()

//******************************************************************************
// ВывестиГруппировку(Запрос,Ном)
//
// Параметры:
//  Запрос - объект "Запрос"
//	Ном - номер выводимой группировки
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Выводит в печатную форму одну группировку запроса. Если
//	Есть нижележащие группировки, они выводятся также с использованием рекурсивного
//	вызова этой же процедуры.
//
Процедура ВывестиГруппировку(Запрос,Ном)
	  
	Если Ном <= КоличествоГруппировок Тогда                           
		
		НазваниеГруппировки = СписокГруппировок.ПолучитьЗначение(Ном);
		Пока Запрос.Группировка(Ном) = 1 Цикл
		
			Если НазваниеГруппировки = "Документ" Тогда
				НазваниеСекции="Документ";   
			ИначеЕсли Запрос.ЭтоГруппа(НазваниеГруппировки) = 1 Тогда
				НазваниеСекции="Строка"+СокрЛП(Ном)+"Г";
			Иначе
				НазваниеСекции="Строка"+Ном;
			КонецЕсли;      
			ПечТекстСтроки = Запрос.ПолучитьАтрибут(НазваниеГруппировки);
			Расшифровка = ПечТекстСтроки;
			Если НазваниеГруппировки = "Документ" Тогда
				ПечТекстСтроки = ""+глНазваниеДокументаВЖурнале(ПечТекстСтроки)+" № "+ПечТекстСтроки.НомерДок;
			КонецЕсли;
			ПечТекстСтроки = ?(ПустоеЗначение(ПечТекстСтроки)=1,глПредставлениеПустогоЗначения(НазваниеГруппировки),ПечТекстСтроки);
		
			ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,Расшифровка);
		
			// если есть более детальная группировка - выведем ее
			Если КоличествоГруппировок > Ном Тогда
				ВывестиГруппировку(Запрос,Ном+1);
			КонецЕсли;          
		
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ВывестиГруппировку()     

//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог	
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   
//	Запускает отчет.
//
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;
	
	Если глПроверкаДаты(ДатаНачала,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;      
	                                        
	Таб.ИсходнаяТаблица( "АнализПродаж" );  
	
	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	Если глПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонца,Последовательность.ОсновнаяПоследовательность)=0 Тогда
		Возврат;
	КонецЕсли;	  
	
	    
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "АнализПродаж");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1",ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2",ВыбРазделитель2);
	Расшифровка.Установить("ВыбРазделитель3",ВыбРазделитель3);
	Расшифровка.Установить("ВидРазделителя",ВидРазделителя);
	
	Расшифровка.Установить("ВыбТМЦ",		ВыбТМЦ);
	Расшифровка.Установить("ВыбПокупатель",	ВыбПокупатель);
	Расшифровка.Установить("ВыбПоставщик",	ВыбПоставщик);
	Расшифровка.Установить("Группировки",	Группировки);
	Расшифровка.Установить("ВидЕдиницы",	ВидЕдиницы);  
	Расшифровка.Установить("ПоГруппам",		ПоГруппам);    
	
	Расшифровка.Установить("ВыбСвойствоТМЦ",		ВыбСвойствоТМЦ);
	Расшифровка.Установить("ВыбСвойствоПокупателя",	ВыбСвойствоПокупателя);
	Расшифровка.Установить("ВыбСвойствоПоставщика",	ВыбСвойствоПоставщика);

	
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были

	// запомним МФ только если он задан
    Если глМножественныйФильтрЗадан(ТаблицаМФ) = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ); 
	КонецЕсли;
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Фирма			= Регистр.Продажи.Фирма;
	|УпрАналитика	= Регистр.Продажи.Фирма.УпрАналитика;
	|ЮрЛицо 		= Регистр.Продажи.Фирма.ЮрЛицо;
	|Покупатель 	= Регистр.Продажи.Покупатель;
	|СвойствоПок 	= Регистр.Продажи.Покупатель.ОсновноеСвойство.ЗначениеСвойства;
	|Номенклатура	= Регистр.Продажи.Номенклатура;                
	|СвойствоТМЦ 	= Регистр.Продажи.Номенклатура.ОсновноеСвойство.ЗначениеСвойства;
	|Поставщик		= Регистр.Продажи.Поставщик;
	|СвойствоПост 	= Регистр.Продажи.Поставщик.ОсновноеСвойство.ЗначениеСвойства;"; 
	
	ЕстьАвтор = 0;
	ЕстьАвторЗаявки = 0;
	ЕстьПроект = 0;                          
	НомСтроки = 0;
	НомКолонки = 0;

	// Проверка на необходимость включения в запрос переменной "Автор"
	// Сначала проверим, есть ли множественный фильтр по автору.
	Если ТаблицаМФ.НайтиЗначение("Автор", НомСтроки, НомКолонки) = 1 Тогда
		Если ТаблицаМФ.ПолучитьЗначение(НомСтроки, "ФлВкл") = 2  Тогда
		    ЕстьАвтор = 1;
		КонецЕсли;
	КонецЕсли;                                                  
	
	// Если фильтра нет, то возможно есть группировка
	Если ЕстьАвтор = 0  Тогда
		НомСтроки = Группировки.НайтиЗначение("Автор");
		Если  НомСтроки > 0 Тогда
			Если Группировки.Пометка(НомСтроки)=1 Тогда
				ЕстьАвтор = 1;			    
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
    
	НомСтроки = 0;
	НомКолонки = 0;
	
	Если ТаблицаМФ.НайтиЗначение("АвторЗаявки", НомСтроки, НомКолонки) = 1 Тогда
		Если ТаблицаМФ.ПолучитьЗначение(НомСтроки, "ФлВкл") = 2  Тогда
		    ЕстьАвторЗаявки = 1;
		КонецЕсли;
	КонецЕсли;                                                  
	
	// Если фильтра нет, то возможно есть группировка
	Если ЕстьАвторЗаявки = 0  Тогда
		НомСтроки = Группировки.НайтиЗначение("АвторЗаявки");
		Если  НомСтроки > 0 Тогда
			Если Группировки.Пометка(НомСтроки)=1 Тогда
				ЕстьАвторЗаявки = 1;			    
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Проверка на необходимость включения в запрос переменной "Проект"    
	НомСтроки = 0;
	НомКолонки = 0;
	Если ТаблицаМФ.НайтиЗначение("Проект", НомСтроки, НомКолонки) = 1 Тогда
		Если ТаблицаМФ.ПолучитьЗначение(НомСтроки, "ФлВкл") = 2  Тогда
		    ЕстьПроект = 1;
		КонецЕсли;
	КонецЕсли;            
	Если ЕстьПроект = 0  Тогда    
		НомСтроки = Группировки.НайтиЗначение("Проект");
		Если  НомСтроки > 0 Тогда
			Если Группировки.Пометка(НомСтроки)=1 Тогда
				ЕстьПроект = 1;			    
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьАвтор=1 Тогда
		ТекстЗапроса = ТекстЗапроса +
		"
		|Автор      = Регистр.Продажи.ТекущийДокумент.Автор;";
	КонецЕсли;     
	
	Если ЕстьАвторЗаявки=1 Тогда
		ТекстЗапроса = ТекстЗапроса +
		"
		|АвторЗаявки = Регистр.Продажи.ТекущийДокумент.АвторЗаявки;";
	КонецЕсли;     

	Если ЕстьПроект=1 Тогда
		ТекстЗапроса = ТекстЗапроса +
		"
		|Проект  = Регистр.Продажи.ТекущийДокумент.Проект;";
	    
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"
	|Себест      = Регистр.Продажи.Себестоимость;
	|Количество  = Регистр.Продажи.Количество;
	|ПродСт      = Регистр.Продажи.ПродСтоимость;
	|СебестВ     = Регистр.Продажи.СебестоимостьВ;
	|КоличествоВ = Регистр.Продажи.КоличествоВ;
	|ПродСтВ     = Регистр.Продажи.ПродСтоимостьВ;
	|Функция СуммаСебест      = Сумма(Себест);
	|Функция СуммаКоличество  = Сумма(Количество);
	|Функция СуммаПродСт      = Сумма(ПродСт);
	|Функция СуммаСебестВ     = Сумма(СебестВ);
	|Функция СуммаКоличествоВ = Сумма(КоличествоВ);
	|Функция СуммаПродСтВ     = Сумма(ПродСтВ);";
	                                             
	Загол="";
	
	НетОш = 1; // нет ошибок при наложении фильтров
	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",ВыбРазделитель3,"ВыбРазделитель3",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
	КонецЕсли;
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Номенклатура",ВыбТМЦ,               "ВыбТМЦ",ТекстЗапроса,Загол,"СвойстваНоменклатуры");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Поставщик",   ВыбПоставщик,         "ВыбПоставщик",ТекстЗапроса,Загол,"СвойстваКонтрагентов");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Покупатель",  ВыбПокупатель,        "ВыбПокупатель",ТекстЗапроса,Загол,"СвойстваКонтрагентов");
	Если (ВыбСвойствоТМЦ.Выбран() = 1) Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "СвойствоТМЦ", ВыбСвойствоТМЦ,       "ВыбСвойствоТМЦ",       ТекстЗапроса,Загол);
	КонецЕсли;                              
	Если (ВыбСвойствоПоставщика.Выбран() = 1) Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "СвойствоПост",ВыбСвойствоПоставщика,"ВыбСвойствоПоставщика",ТекстЗапроса,Загол);
	КонецЕсли;                                     
	Если (ВыбСвойствоПокупателя.Выбран() = 1) Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "СвойствоПок", ВыбСвойствоПокупателя,"ВыбСвойствоПокупателя",ТекстЗапроса,Загол);
	КонецЕсли;                                     
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Автор",   ,        ,ТекстЗапроса,Загол,"Автор");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "АвторЗаявки",   ,  ,ТекстЗапроса,Загол,"АвторЗаявки");

	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Проект",  ,        ,ТекстЗапроса,Загол,"Проект");
	
	ПечЗаголовок = "Анализ продаж ("+глДоллары.Наименование+")";      
	                                    
	ПечЗаголовокСтолбца = "";  
	УстановитьГруппировкиЗапроса(ТекстЗапроса, ПечЗаголовокСтолбца);
	КоличествоГруппировок = СписокГруппировок.РазмерСписка();
	
	Если КоличествоГруппировок > 5 Тогда
		Предупреждение("Нельзя сделать больше 5 группировок!",60);
		Возврат;
	КонецЕсли;
	                                   
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;  
	
	глЧислоСтрок = 0;

	Таб.ВывестиСекцию("Кнопки");
	НачПовт = Таб.ВысотаСекции("Кнопки");
	
	Таб.ВывестиСекцию("Шапка");
	НачПовт = НачПовт + Таб.ВысотаСекции("Шапка");
	
	Таб.ВывестиСекцию("ЗаголовокТаблицы");
	КонПовт = НачПовт + Таб.ВысотаСекции("ЗаголовокТаблицы");
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	глОживить(1);
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 0, "АнализПродаж", "АнализПродаж");
	                         
	// ВЫВОД ГРУППИРОВОК ЗАПРОСА
	ВывестиГруппировку(Запрос,1);
	
	ПечатьСтроки(Запрос,0,"Всего","Всего:");              
	
	Таб.ОбластьПечати(3);
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);       
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;

	Таб.Показать("Анализ продаж", ""); 
	
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура      
//
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	//                  тип          вид           переменная  название
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Номенклатура", "Номенклатура",  "По номенклатуре");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты", "Поставщик",  "По поставщикам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты", "Покупатель",  "По покупателям");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Номенклатура",  "По свойствам номенклатуры");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Поставщик",  "По свойствам поставщиков");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Покупатель",  "По свойствам покупателей");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Пользователи", "Автор",  "По авторам документов");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Пользователи", "АвторЗаявки",  "По авторам заявок");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Проекты", "Проект",  "По проектам");               
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Фирмы", "Фирма",  "По фирмам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","СвоиЮрЛица", "ЮрЛицо",  "По юр. лицам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","УпрАналитика", "УпрАналитика",  "По упр. аналитике");

	Если ФлагВосстановленияНастройки = 0 Тогда     
		
		ВидРазделителя = 1;
		ВидЕдиницы 	   = 1;
		ПоГруппам 	   = 1;
		ДатаНачала      = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
		Если ПустоеЗначение(ДатаНачала) = 1 Тогда
			ДатаНачала      = НачМесяца(ДатаКонца);    
		КонецЕсли;    
	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВидРазделителя	= глРасшифровка.Получить("ВидРазделителя");
		ВыбРазделитель1	= глРасшифровка.Получить("ВыбРазделитель1");
		ВыбРазделитель2	= глРасшифровка.Получить("ВыбРазделитель2");
		ВыбРазделитель3	= глРасшифровка.Получить("ВыбРазделитель3");
		
		ВыбПокупатель 	= глРасшифровка.Получить("ВыбПокупатель");
		ВыбПоставщик 	= глРасшифровка.Получить("ВыбПоставщик");
		ВыбТМЦ 			= глРасшифровка.Получить("ВыбТМЦ");         
		
		ВыбСвойствоТМЦ 			= глРасшифровка.Получить("ВыбСвойствоТМЦ");
		ВыбСвойствоПокупателя 	= глРасшифровка.Получить("ВыбСвойствоПокупателя");
		ВыбСвойствоПоставщика 	= глРасшифровка.Получить("ВыбСвойствоПоставщика");
		
		глРасшифровка.Получить("Группировки").Выгрузить(Группировки);
		ВидЕдиницы		= глРасшифровка.Получить("ВидЕдиницы");
		ПоГруппам		= глРасшифровка.Получить("ПоГруппам");
		
		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;            
	
	ПерерисовкаНазванийЗакладок();

	ТаблицаМФ.ВидимостьКолонки("Тип",0);
	ТаблицаМФ.ВидимостьКолонки("Вид",0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");


	УправлениеДиалогом();  
	
	//++Горлач
	//Ограничиваем возможность открытия отчета
	Если НазваниеНабораПрав() = "Администратор_магазина1" тогда
		Если ( (ИмяПользователя() <> "Энгельса_админ") И (ИмяПользователя() <> "Стахановцев_админ") И (ИмяПользователя() <> "Шугин_А")) тогда
		Предупреждение("Вы не имеете права доступа к этому отчету!");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//--Горлач	
	
КонецПроцедуры		// ПриОткрытии()       

//******************************************************************************
// Предопределенная процедура.
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	ПерерисовкаНазванийЗакладок();
	УправлениеДиалогом();       
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	
КонецПроцедуры // ВводНового()   

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореЗакладки(Номер,Значение)	
	// закладки
    Если Номер=1 Тогда
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	КонецЕсли;      
	УправлениеДиалогом();
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки
                                       
//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(Значение)  
	
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ДатаКонца = ПолучитьДатуТА();
                                
// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
                                                                             
ТекСтрокаВТаблице="";

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

Группировки.ДобавитьЗначение("Фирма", 		"Фирма");
Группировки.ДобавитьЗначение("СвойствоПок", "Свойство покупателя");
Группировки.ДобавитьЗначение("Покупатель", 	"Покупатель");
Группировки.ДобавитьЗначение("СвойствоПост","Свойство поставщика");
Группировки.ДобавитьЗначение("Поставщик", 	"Поставщик");           
Группировки.ДобавитьЗначение("СвойствоТМЦ", "Свойство номенклатуры");
Группировки.ДобавитьЗначение("Номенклатура","Номенклатура");
Группировки.ДобавитьЗначение("Автор", 	"Авторы документов");
Группировки.ДобавитьЗначение("АвторЗаявки", 	"Авторы заявок");
Группировки.ДобавитьЗначение("Проект", 	"Проекты");
Группировки.ДобавитьЗначение("Документ", 	"Документы движения");

Группировки. Пометка(6,1);         

