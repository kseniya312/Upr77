////////////////////////////////////////////////////////////////////////////////
//  ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
Перем ТекСтрокаВТаблице; // текущая строка в таблице значений  МФ

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 
Перем РасшифровкаТочки;
Перем БылЗаданМФ; // применяем для ускорения расчетов в создании расшифровок

Перем ТаблицаПрибылей, ТаблицаУбытков;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//                          
//******************************************************************************
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

//******************************************************************************
// ПерерисовкаНазванийЗакладок
//
// Параметры:
// 	Нет
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Функция ПерерисовкаНазванийЗакладок()      
	
	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
	
КонецФункции // ПерерисовкаНазванийЗакладок	

//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда           
		Форма.ИспользоватьСлой("Шапка,Подвал,Основной1,Основной2,Разделитель"+СокрЛП(ВидРазделителя));
	Иначе     
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;
	
КонецПроцедуры //УправлениеДиалогом()   

//******************************************************************************
// АбсЗнач(Числ)
//
// Параметры: Числ - числовое значение
//
// Возвращаемое значение: Абсолютное значение переданного числа
//
// Описание: вычисление абсолютного значения числа

Функция АбсЗнач(Числ)
	Если Числ<0 Тогда
		Возврат (Числ * (-1));
	Иначе     
		Возврат (Числ);
	КонецЕсли;
КонецФункции                                                                    

//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список 
//
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции //РасшифровкаОбновить()                                            

//******************************************************************************
// ПоказательАнализа(Запрос)
//
// Параметры: Запрос - объект агрегатного типа "Запрос"
//
// Возвращаемое значение: число
//
// Описание: в зависимости от текущего положения переключателей в диалоге,
// вычисляет и возвращает анализируемый показатель (прибыли или выручку)

Функция ПоказательАнализа(Запрос)
	
	Если ВыбПоказатель=1 Тогда // выручка
		Возврат (Запрос.СуммаПродСт - Запрос.СуммаПродСтВ);
	Иначе
		Возврат (Запрос.СуммаПродСт - Запрос.СуммаСебест - (Запрос.СуммаПродСтВ - Запрос.СуммаСебестВ)); // прибыль
	КонецЕсли;
	
КонецФункции //ПоказательАнализа()

//******************************************************************************
// ПостроитьДиаграммуВсего(Диаграмма,Запрос) 
//
// Параметры: 	Диаграмма - ссылка на объект "Диаграмма"
// 				Запрос - ссылка на объект "Запрос"
//
// Возвращаемое значение: НЕТ
//
// Описание: производит заполнение и вывод диаграммы общей суммы прибыли / убытка

Процедура ПостроитьДиаграммуВсего(Диаграмма,Запрос) 
	
	ЧислоПериодов = 0; // по горизонтали
	Диаграмма.Обновление(0);
	Диаграмма.Заголовок = ?(ВыбПоказатель=1,"Выручка всего - "+глДоллары.Наименование,"Прибыль всего - "+глДоллары.Наименование);
	Диаграмма.КоличествоСерий(1);
	
	Пока Запрос.Группировка(1)=1 Цикл // по периодам
		                  
		Дата1 = Запрос.НачалоПериода();
		Дата2 = Запрос.КонецПериода();
		
		ПериодСтрокой 	= ?(Дата1=Дата2,Строка(Дата1),ПериодСтр(Дата1,Дата2));
		ЧислоПериодов	= ЧислоПериодов+1;        
		
		Диаграмма.УстановитьИмяТочки(ЧислоПериодов,ПериодСтрокой);    
		Показатель		= ПоказательАнализа(Запрос);
		Диаграмма.УстановитьЗначение(ЧислоПериодов,1,Показатель,ПериодСтрокой+": "+глФРМ(Показатель,глДоллары,1));
		
	КонецЦикла;
	                        
	Диаграмма.Обновление(1);
	
КонецПроцедуры 

//******************************************************************************
// ПодготовкаРасшифровки()
//
// Параметры:
//  
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Здесь можно перечислить элементы диалога.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПодготовкаРасшифровки()
	
	// создаем стандартную расшифровку отчета "Анализ продаж"
	Группировки = СоздатьОбъект ("СписокЗначений");
	Группировки.ДобавитьЗначение("Фирма", 		"Фирма");
	Группировки.ДобавитьЗначение("Покупатель", 	"Покупатель");
	Группировки.ДобавитьЗначение("СвойствоПок", "Свойство покупателя");
	Группировки.ДобавитьЗначение("Поставщик", 	"Поставщик");           
	Группировки.ДобавитьЗначение("СвойствоПост", "Свойство поставщика");
	Группировки.ДобавитьЗначение("Номенклатура", 		"Номенклатура");
	Группировки.ДобавитьЗначение("СвойствоНоменклатуры", "Свойство номенклатуры");
	Группировки.ДобавитьЗначение("Документ", 	"Документы движения");
	
	Группировки. Пометка(6,1);	
	Группировки. Пометка(8,1);
	
	РасшифровкаТочки = СоздатьОбъект("СписокЗначений");
    РасшифровкаТочки.Установить("Отчет", "АнализПродаж");

	РасшифровкаТочки.Установить("ВыбРазделитель1",ВыбРазделитель1);
	РасшифровкаТочки.Установить("ВыбРазделитель2",ВыбРазделитель2);
	РасшифровкаТочки.Установить("ВыбРазделитель3",ВыбРазделитель3);
	РасшифровкаТочки.Установить("ВидРазделителя", ВидРазделителя);

	РасшифровкаТочки.Установить("ВыбТМЦ",		    ВыбТМЦ);
	РасшифровкаТочки.Установить("ВыбПокупатель",	ВыбПокупатель);
	РасшифровкаТочки.Установить("ВыбПоставщик",	    ВыбПоставщик);
	РасшифровкаТочки.Установить("Группировки",	    Группировки);
	РасшифровкаТочки.Установить("ВидЕдиницы",	    1);
	
	Если БылЗаданМФ = 1 Тогда
		РасшифровкаТочки.Установить("ТаблицаМФ",	ТаблицаМФ);
	КонецЕсли;
	
КонецПроцедуры // ПодготовкаРасшифровки()

//******************************************************************************
// СоздатьРасшифровку(Объект, пДата1, пДата2)      
//
// Параметры:
//		Объект
//		пДата1
//		пДата2
//		Показатель
//
// Возвращаемое значение: значение расшифровки текущей точки диаграммы
//
// Описание: формирует расшифровку текущей точки диаграммы

Функция СоздатьРасшифровку(Объект, пДата1, пДата2,Показатель)
	
	Если (ПустоеЗначение(Объект)=1) 
	или  (ТипЗначенияСтр(Объект)<>"Справочник") 
	или	 (ПустоеЗначение(пДата1)=1) 
	или	 (ПустоеЗначение(пДата2)=1)
	Тогда
		 // передано что-то не то
		 Возврат ""+Объект+": "+глФРМ(Показатель,глДоллары,1);
	КонецЕсли;
    
	// все настройки помещаем в список
	РасшифровкаТочки.Установить("ДатаНачала", 	пДата1);
    РасшифровкаТочки.Установить("ДатаКонца", 	пДата2);
	
	Если ВидОбъекта = 1 Тогда //Номенклатура
		РасшифровкаТочки.Установить("ВыбТМЦ",		Объект);
		
	ИначеЕсли ВидОбъекта = 2 Тогда // свойство Номенклатуры                 
		РасшифровкаТочки.Установить("ВыбСвойствоТМЦ",		Объект);
	
	ИначеЕсли ВидОбъекта = 3 Тогда // Покупатель
		РасшифровкаТочки.Установить("ВыбПокупатель",	Объект);
		
	ИначеЕсли ВидОбъекта = 4 Тогда // свойство покупатель
		РасшифровкаТочки.Установить("ВыбСвойствоПокупателя",		Объект);
	                                                                     
	ИначеЕсли ВидОбъекта = 5 Тогда // поставщик
		РасшифровкаТочки.Установить("ВыбПоставщик",	Объект);
		
	ИначеЕсли ВидОбъекта = 6 Тогда // свойство поставщика
		РасшифровкаТочки.Установить("ВыбСвойствоПоставщика",		Объект);
		
	ИначеЕсли ВидОбъекта = 7 Тогда // упр аналитика
		РасшифровкаТочки.Установить("ВыбРазделитель3",Объект);
    	РасшифровкаТочки.Установить("ВидРазделителя", 3);
	
	КонецЕсли;	
	
	Возврат РасшифровкаТочки;
	
КонецФункции //СоздатьРасшифровку()

//******************************************************************************
// ПостроитьДиаграмму(Диаграмма,Запрос)
// 
// Параметры: 	Диаграмма - ссылка на объект "Диаграмма"
// 				Запрос - ссылка на объект "Запрос"
//				ВидДиаграммы - строка ("Прибыли" / "Убытки")
// 
// Возвращаемое значение: НЕТ
//
// Описание: Содержит алгоритм построения диаграммы на основании заполненных таблиц 
//	с данными

Процедура ПостроитьДиаграмму(Диаграмма,Запрос, ВидДиаграммы)
	
	Если ВидДиаграммы = "Прибыли" Тогда
		РабТаблица = ТаблицаПрибылей;  
		Диаграмма.Заголовок = ?(ВыбПоказатель=1,"Выручка (продажи) - "+глДоллары.Наименование,"Прибыль - "+глДоллары.Наименование);
	Иначе
		РабТаблица = ТаблицаУбытков;
		Диаграмма.Заголовок = ?(ВыбПоказатель=1,"Возвраты от покупателей - "+глДоллары.Наименование,"Убытки - "+глДоллары.Наименование);
	КонецЕсли;                      
	              
	РабТаблица.Сортировать("Объект");
	// у таблиц первая колонка - заголовки строк
	
	ЧислоПериодов = РабТаблица.КоличествоКолонок()-1; // по горизонтали
	ЧислоОбъектов = РабТаблица.КоличествоСтрок(); // по вертикали в столбце
	
	Диаграмма.Обновление(0);
	Диаграмма.КоличествоСерий(ЧислоОбъектов); // (в одном столбце)
	Диаграмма.КоличествоТочек(ЧислоПериодов); // (по горизонтали)
		    
	// цикл по периодам
	Для Сч1 =1 По ЧислоПериодов Цикл
		                                             
		ЗаголовокКолонки = "";
		РабТаблица.ПолучитьПараметрыКолонки(Сч1+1,,,,ЗаголовокКолонки);
		// в заголовке колонок таблицы содержатся даты начала и конца периода
		Дата1 = Дата(Сред(ЗаголовокКолонки,1 ,10));
		Дата2 = Дата(Сред(ЗаголовокКолонки,11,10));
		
		Диаграмма.УстановитьИмяТочки(Сч1,?(Дата1=Дата2,Строка(Дата1),ПериодСтр(Дата1,Дата2)));
		               
		// цикл по объектам (Номенклатура, поставщики, ...)
		Для Сч2=1 По ЧислоОбъектов Цикл      
			
			Объект 		= РабТаблица.ПолучитьЗначение(Сч2,1); //строка, колонка
			Показатель 	= Число(РабТаблица.ПолучитьЗначение(Сч2,Сч1+1));   
			Диаграмма.УстановитьИмяСерии(Сч2,Лев(Строка(Объект),30));
			Расшифровка = СоздатьРасшифровку(Объект,Дата1 ,Дата2, Показатель);
			Диаграмма.УстановитьЗначение(Сч1,Сч2,Показатель,Расшифровка);	// точка, серия, знач, расш
			
		КонецЦикла;
		
	КонецЦикла;
	
	Диаграмма.Обновление(1);    
	 
КонецПроцедуры // ПостроитьДиаграмму()  

//******************************************************************************                          
Процедура ДобавитьЗначениеВТаблицу(РабТаблица,Объект,Колонка,Значение)
	
	НомСтроки = 0;
	Если РабТаблица.НайтиЗначение(Объект,НомСтроки,"Объект") = 0 Тогда
		РабТаблица.НоваяСтрока();
		РабТаблица.Объект = Объект;
		НомСтроки = РабТаблица.КоличествоСтрок();
	Иначе
		СтароеЗначение = Число(РабТаблица.ПолучитьЗначение(НомСтроки,Колонка));
	КонецЕсли;
			
	РабТаблица.УстановитьЗначение(НомСтроки,Колонка,Значение+СтароеЗначение);
	
КонецПроцедуры //ДобавитьЗначениеВТаблицу()

//******************************************************************************
// ПодготовкаТаблиц(ИмяОбъекта,Запрос)
//
// Параметры:
//			ИмяОбъекта - название переменной запроса, являющейся объектом анализа
//			Запрос -  ссылка на агрегатный объект "Запрос"
//
// Возвращаемое значение: НЕТ
//
// Описание: производит заполнение таблиц данных, необходимых для построения диаграммы 

Процедура ПодготовкаТаблиц(ИмяОбъекта,Запрос)
	
	ТаблицаПрибылей = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаПрибылей.НоваяКолонка("Объект");
	
	ТаблицаУбытков = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаУбытков.НоваяКолонка("Объект");
	
	КолПериодов = 0;
	Пока Запрос.Группировка(1)=1 Цикл // периодам
		
		КолПериодов = КолПериодов+1;
		
		// заголовки колонок будут содержать описания периода
		ПериодСтрокой = ""+Формат(Запрос.НачалоПериода(),"ДДДММГГГГ")+Формат(Запрос.КонецПериода(),"ДДДММГГГГ");
		ТаблицаПрибылей	.НоваяКолонка(,,,,ПериодСтрокой);
		ТаблицаУбытков	.НоваяКолонка(,,,,ПериодСтрокой);
		
		Пока Запрос.Группировка(2)=1 Цикл // по объектам
			
			Показатель	= ПоказательАнализа(Запрос); // прибыль, выручка
			
			Если АбсЗнач(Показатель)<ПорогПрочие Тогда
				Объект = "<Прочие>";
			Иначе
				Объект = Запрос.ПолучитьАтрибут(ИмяОбъекта);
				Если ПустоеЗначение(Объект) = 1 Тогда
					Объект = "<не выбран>";
				КонецЕсли;
			КонецЕсли;
			
			Если Показатель<0 Тогда
				РабТаблица = ТаблицаУбытков;
			Иначе
				РабТаблица = ТаблицаПрибылей;
			КонецЕсли;           
			
			ДобавитьЗначениеВТаблицу(РабТаблица,Объект,КолПериодов+1,АбсЗнач(Показатель));
			Объект = "<   Всего   >";
			ДобавитьЗначениеВТаблицу(РабТаблица,Объект,КолПериодов+1,АбсЗнач(Показатель));
			
			
		КонецЦикла;
		
	КонецЦикла;
	            
КонецПроцедуры  //ПодготовкаТаблиц()

//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог
//
// Возвращаемое значение: НЕТ
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   запускает отчет
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса, ИмяОбъекта;
	
	Если глПроверкаДаты(ДатаНачала,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;      
	                                        
	Если СписВидОтчета.ТекущаяСтрока()=1 Тогда //гистограмма
		Таб.ИсходнаяТаблица( "ДинамикаПродажГи" );
	ИначеЕсли СписВидОтчета.ТекущаяСтрока()=2 Тогда // график
		Таб.ИсходнаяТаблица( "ДинамикаПродажГра" );
	Иначе // с областями
		Таб.ИсходнаяТаблица( "ДинамикаПродажОбл" );
	КонецЕсли;
	
	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	Если глПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонца,Последовательность.ОсновнаяПоследовательность)=0 Тогда
		Возврат;
	КонецЕсли;	  
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ДинамикаПродаж");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1",ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2",ВыбРазделитель2);
	Расшифровка.Установить("ВыбРазделитель3",ВыбРазделитель3);
	Расшифровка.Установить("ВидРазделителя",ВидРазделителя);

	Расшифровка.Установить("ВыбТМЦ",		ВыбТМЦ);
	Расшифровка.Установить("ВыбПокупатель",	ВыбПокупатель);
	Расшифровка.Установить("ВыбПоставщик",	ВыбПоставщик);
	Расшифровка.Установить("СписПериоды",	СписПериоды.ТекущаяСтрока());
	Расшифровка.Установить("ВидОбъекта",	ВидОбъекта);
	Расшифровка.Установить("ВыбПоказатель",	ВыбПоказатель);
	Расшифровка.Установить("ПорогПрочие",	ПорогПрочие);
	Расшифровка.Установить("СписВидОтчета",	СписВидОтчета.ТекущаяСтрока());

	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
                
	БылЗаданМФ = глМножественныйФильтрЗадан(ТаблицаМФ);
	// запомним МФ только если он задан
    Если  БылЗаданМФ = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ); 
	КонецЕсли;
	
	Если ДатаКонца<ПолучитьДатуТА() Тогда                             
		СтрокаПо = "по ДатаКонца";
	Иначе     
		СтрокаПо = "";
	КонецЕсли;
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала "+СтрокаПо+";
	|Фирма 			= Регистр.Продажи.Фирма;
	|УпрАналитика	= Регистр.Продажи.Фирма.УпрАналитика;
	|ЮрЛицо 		= Регистр.Продажи.Фирма.ЮрЛицо;
	|Покупатель 	= Регистр.Продажи.Покупатель;
	|СвойствоПок 	= Регистр.Продажи.Покупатель.ОсновноеСвойство.ЗначениеСвойства;
	|Номенклатура	= Регистр.Продажи.Номенклатура;                
	|СвойствоТМЦ 	= Регистр.Продажи.Номенклатура.ОсновноеСвойство.ЗначениеСвойства;
	|Поставщик		= Регистр.Продажи.Поставщик;
	|СвойствоПост 	= Регистр.Продажи.Поставщик.ОсновноеСвойство.ЗначениеСвойства;";
	
	// Проверка на необходимость включения в запрос переменной "Автор"
	ЕстьАвтор = 0;
	ЕстьПроект = 0;                          
	НомСтроки = 0;
	НомКолонки = 0;
	
	// Сначала проверим, есть ли множественный фильтр по автору.
	Если ТаблицаМФ.НайтиЗначение("Автор", НомСтроки, НомКолонки) = 1 Тогда
		Если ТаблицаМФ.ПолучитьЗначение(НомСтроки, "ФлВкл") = 2  Тогда
		    ЕстьАвтор = 1;
		КонецЕсли;
	КонецЕсли;                                                  
	
	// Проверка на необходимость включения в запрос переменной "Проект"    
	НомСтроки = 0;
	НомКололонки = 0;
	Если ТаблицаМФ.НайтиЗначение("Проект", НомСтроки, НомКолонки) = 1 Тогда
		Если ТаблицаМФ.ПолучитьЗначение(НомСтроки, "ФлВкл") = 2  Тогда
		    ЕстьПроект = 1;
		КонецЕсли;
	КонецЕсли;            
	
	Если ЕстьАвтор=1 Тогда
		ТекстЗапроса = ТекстЗапроса +
		"
		|Автор      = Регистр.Продажи.ТекущийДокумент.Автор;";
	КонецЕсли;     
	
	Если ЕстьПроект=1 Тогда
		ТекстЗапроса = ТекстЗапроса +
		"
		|Проект  = Регистр.Продажи.ТекущийДокумент.Проект;";
	КонецЕсли;
	
	Если ВыбПоказатель = 2 Тогда // прибыль
		ТекстЗапроса = ТекстЗапроса +
		"
		|Себест  = Регистр.Продажи.Себестоимость;
		|Функция СуммаСебест = Сумма(Себест);
		|СебестВ = Регистр.Продажи.СебестоимостьВ;
		|Функция СуммаСебестВ = Сумма(СебестВ);";
	КонецЕсли;
    
	ТекстЗапроса = ТекстЗапроса +
	"
	|ПродСт  = Регистр.Продажи.ПродСтоимость;
	|Функция СуммаПродСт  = Сумма(ПродСт);
	|ПродСтВ = Регистр.Продажи.ПродСтоимостьВ;
	|Функция СуммаПродСтВ = Сумма(ПродСтВ);
	|Группировка "+СписПериоды.ПолучитьЗначение(СписПериоды.ТекущаяСтрока())+" все;";
	                                             
	Если ВидОбъекта = 1 Тогда //Номенклатура
		ТекстЗапроса = ТекстЗапроса + "Группировка Номенклатура без групп;";
		ИмяОбъекта = "Номенклатура";
		
	ИначеЕсли ВидОбъекта = 2 Тогда // свойство Номенклатуры                 
		ТекстЗапроса = ТекстЗапроса + "Группировка СвойствоТМЦ;";
		ИмяОбъекта = "СвойствоТМЦ";
	
	ИначеЕсли ВидОбъекта = 3 Тогда // Покупатель
		ТекстЗапроса = ТекстЗапроса + "Группировка Покупатель без групп;";
		ИмяОбъекта = "Покупатель";
		
	ИначеЕсли ВидОбъекта = 4 Тогда // свойство покупатель
		ТекстЗапроса = ТекстЗапроса + "Группировка СвойствоПок;";
		ИмяОбъекта = "СвойствоПок";
	                                                                     
	ИначеЕсли ВидОбъекта = 5 Тогда // поставщик
		ТекстЗапроса = ТекстЗапроса + "Группировка Поставщик без групп;";
		ИмяОбъекта = "Поставщик";
		
	ИначеЕсли ВидОбъекта = 6 Тогда // свойство поставщика
		ТекстЗапроса = ТекстЗапроса + "Группировка СвойствоПост;";
		ИмяОбъекта = "СвойствоПост";
		
	ИначеЕсли ВидОбъекта = 7 Тогда // упр аналитика
		ТекстЗапроса = ТекстЗапроса + "Группировка УпрАналитика;";
		ИмяОбъекта = "УпрАналитика";
	
	КонецЕсли;	
	
	Загол="";
	
	НетОш = 1; // нет ошибок при наложении фильтров
	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",ВыбРазделитель3,"ВыбРазделитель3",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
	КонецЕсли;
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Номенклатура",ВыбТМЦ,"ВыбТМЦ",ТекстЗапроса,Загол,"СвойстваНоменклатуры");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Поставщик",ВыбПоставщик,"ВыбПоставщик",ТекстЗапроса,Загол,"СвойстваКонтрагентов");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Покупатель",ВыбПокупатель,"ВыбПокупатель",ТекстЗапроса,Загол,"СвойстваКонтрагентов");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Автор",   ,        ,ТекстЗапроса,Загол,"Автор");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Проект",  ,        ,ТекстЗапроса,Загол,"Проект");
	
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПечЗаголовок = "Динамика продаж";
	                                   
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;

	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");      
	                         
	Таб.ВывестиСекцию("Всего");  
	
	ПодготовкаТаблиц(ИмяОбъекта, Запрос);          
	ПодготовкаРасшифровки();
	
	Таб.ВывестиСекцию("Диаграмма");
	Таб.Опции(0, 0, 2, 0, "ДинамикаПродаж", "ДинамикаПродаж");
	                                                      
	Таб.ОбластьПечати(3);
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Динамика продаж", ""); 
	
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
//  ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ

//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	//                  тип          вид           переменная  название
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Номенклатура", "Номенклатура",  "По номенклатуре");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты", "Поставщик",  "По поставщикам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты", "Покупатель",  "По покупателям");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Номенклатура",  "По свойствам номенклатуры");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Поставщик",  "По свойствам поставщиков");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Покупатель",  "По свойствам покупателей");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Пользователи", "Автор",  "По авторам документов");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Проекты", "Проект",  "По проектам");               
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Фирмы", "Фирма",  "По фирмам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","СвоиЮрЛица", "ЮрЛицо",  "По юр. лицам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","УпрАналитика", "УпрАналитика",  "По упр. аналитике");
	
	Если ФлагВосстановленияНастройки = 0 Тогда
		
		ВидОбъекта     = 1;
		ВыбПоказатель  = 1;     
		ВидРазделителя = 1;
		ДатаНачала      = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
		Если ПустоеЗначение(ДатаНачала) = 1 Тогда
			ДатаНачала      = НачМесяца(ДатаКонца);    
		КонецЕсли;
		
	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВидРазделителя	= глРасшифровка.Получить("ВидРазделителя");
		ВыбРазделитель1	= глРасшифровка.Получить("ВыбРазделитель1");
		ВыбРазделитель2	= глРасшифровка.Получить("ВыбРазделитель2");
		ВыбРазделитель3	= глРасшифровка.Получить("ВыбРазделитель3");

		ВыбПокупатель 	= глРасшифровка.Получить("ВыбПокупатель");
		ВыбПоставщик 	= глРасшифровка.Получить("ВыбПоставщик");
		ВыбТМЦ 			= глРасшифровка.Получить("ВыбТМЦ");
		
		СписПериоды.ТекущаяСтрока(глРасшифровка.Получить("СписПериоды"));
		ВидОбъекта 		= глРасшифровка.Получить("ВидОбъекта");
		ВыбПоказатель	= глРасшифровка.Получить("ВыбПоказатель");
		ПорогПрочие		= глРасшифровка.Получить("ПорогПрочие");         
		СписВидОтчета.ТекущаяСтрока(глРасшифровка.Получить("СписВидОтчета"));        
		
		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;                      
	
	ПерерисовкаНазванийЗакладок();

	ТаблицаМФ.ВидимостьКолонки("Тип",0);
	ТаблицаМФ.ВидимостьКолонки("Вид",0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");

	УправлениеДиалогом();
	
КонецПроцедуры		// ПриОткрытии()       

//******************************************************************************
// Предопределенная процедура.
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	ПерерисовкаНазванийЗакладок();
	УправлениеДиалогом();       
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	
КонецПроцедуры // ВводНового()
                                                                                
//******************************************************************************
// Предопределенная процедура
Процедура ПриВыбореЗакладки(Номер,Значение)	
	// закладки
    Если Номер=1 Тогда
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	КонецЕсли;      
	УправлениеДиалогом();
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки
                                       
//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(Значение)  
	
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора

ДатаКонца = ПолучитьДатуТА();

// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
                                                                                
ТекСтрокаВТаблице="";

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

СписПериоды.ДобавитьЗначение("Год");
СписПериоды.ДобавитьЗначение("Квартал");
СписПериоды.ДобавитьЗначение("Месяц");
СписПериоды.ДобавитьЗначение("Неделя");
СписПериоды.ДобавитьЗначение("День");
СписПериоды.ТекущаяСтрока(3);

СписВидОтчета.ДобавитьЗначение("Гистограмма");
СписВидОтчета.ДобавитьЗначение("График");
СписВидОтчета.ДобавитьЗначение("С областями");
СписВидОтчета.ТекущаяСтрока(1);
