перем каталог; //Каталог данных
перем файл; //Файл данных
перем каталогнастроек; //Каталог настроек
перем файлнастроек; //файл настроек

//разложить строку с разделителями в список значений
Функция Разложить(Знач Стр, Разделитель = ",")
	
	Список = СоздатьОбъект("СписокЗначений");
	Длина  = СтрДлина(Разделитель);
	
	Стр = СокрЛП(Стр);
	Поз = Найти(Стр, Разделитель);
	
	Пока 0 < Поз Цикл
		Список.ДобавитьЗначение(СокрП(Лев(Стр, Поз-1)));
		
		Стр = СокрЛ(Сред(Стр, Поз+Длина));
		Поз = Найти(Стр, Разделитель);
	КонецЦикла;

	Список.ДобавитьЗначение(Стр);
	
	Возврат Список;
	
КонецФункции // Разложить()


//*******************************************
//Загрузка справочника из Excel
Процедура Сформировать()
	НачатьТранзакцию();
	спр = СоздатьОбъект("Справочник." + СпрВид.ПолучитьЗначение(СпрВид.ТекущаяСтрока()));
	Если очищать=1 Тогда
		спр.ВыбратьЭлементы();
		Пока спр.ПолучитьЭлемент()=1 Цикл
			спр.Удалить(1);
		КонецЦикла;
	КонецЕсли;
	спр.ИспользоватьДату(ПериодичРеквизиты);
	Ексель=СоздатьОбъект("Excel.Application");
	Книга = Ексель.Workbooks.Open(каталог + "\" + файл,, 1);
	ИЗСтр=НачальнаяСтрока; колпустых=0;
	Пока колпустых<20 Цикл
		ИЗСтр=ИЗСтр+1;
		Если (ПустоеЗначение(""+Ексель.Cells( ИЗСтр, Пров1).Value)=0) и
			 (ПустоеЗначение(""+Ексель.Cells( ИЗСтр, Пров2).Value)=0) Тогда
			код=ТЗреквизитов.ПолучитьЗначение(1,2);
			код=""+Ексель.Cells( ИЗСтр, код).Value;
			Если спр.НайтиПоКоду(код)=0 Тогда
			    спр.Новый();
			КонецЕсли;
			ТЗреквизитов.ВыбратьСтроки();
			Пока ТЗреквизитов.ПолучитьСтроку()=1 Цикл
				Если ТЗреквизитов.КолонкаExcel>0 Тогда
					значен=Ексель.Cells( ИЗСтр, ТЗреквизитов.КолонкаExcel).Value;
				    спр.УстановитьАтрибут(ТЗреквизитов.Наименование,значен);
				ИначеЕсли ПустоеЗначение(ТЗреквизитов.Стандартное)=0 Тогда
				    спр.УстановитьАтрибут(ТЗреквизитов.Наименование,ТЗреквизитов.Стандартное);
				КонецЕсли;
			КонецЦикла;
		
			колпустых=0;
			спр.Записать();
			Состояние(спр.Наименование);
		Иначе
			колпустых=колпустых+1;
		КонецЕсли;
	КонецЦикла;
	Книга.Close();
	ЗафиксироватьТранзакцию();
КонецПроцедуры

//_____________________________________________________________________________
//Заполнение видов реквизитов и их параметров при выборе вида справочника
Процедура ПриВыбореСправочника(парам)
	видспр=СпрВид.ПолучитьЗначение(парам);
	спр=СоздатьОбъект("Справочник."+видспр);
	ТЗреквизитов.УдалитьСтроки();
	Если Метаданные.Справочник(видспр).Владелец.Выбран()=1 Тогда
		Предупреждение("Нельзя загружать подчиненный справочник");
	Иначе
		ТЗреквизитов.НоваяСтрока();
		ТЗреквизитов.Наименование="Код";
		Если МетаДанные.Справочник(видспр).ТипКода="Числовой" Тогда
			ТЗреквизитов.Стандартное=0;
		Иначе
			ТЗреквизитов.Стандартное=спр.Наименование;
		КонецЕсли;
		ТЗреквизитов.НоваяСтрока();
		ТЗреквизитов.Наименование="Наименование";
		ТЗреквизитов.Стандартное=спр.Наименование;
		НачатьТранзакцию();
		спр.ИспользоватьДату(ПериодичРеквизиты, 1);
		спр.Новый();
		спр.Записать();
		for хх=1 to Метаданные.Справочник(видспр).Реквизит() do
			ТЗреквизитов.НоваяСтрока();
			ТЗреквизитов.Наименование=МетаДанные.Справочник(видспр).Реквизит(хх).Идентификатор;
			вд=спр.ПолучитьАтрибут(ТЗреквизитов.Наименование);
			Если МетаДанные.Справочник(видспр).Реквизит(хх).Периодический=1 Тогда
				вд=вд.Получить(РабочаяДата());
			КонецЕсли;
			ТЗреквизитов.Стандартное=вд;
		EndDo;
		ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры //ПриВыбореСправочника

//_____________________________________________________________________________
Процедура ПриОткрытии() //предопределенная
	for х=1 to Метаданные.Справочник() do   // перебор справочников
		спрвид.ДобавитьЗначение(МетаДанные.Справочник(х).Идентификатор, );
	EndDo;
	ТЗреквизитов.НоваяКолонка("Наименование", "Строка");
	ТЗреквизитов.НоваяКолонка("КолонкаExcel", "Число", 14);
	ТЗреквизитов.НоваяКолонка("Стандартное");
	ПриВыбореСправочника(1);
	каталог="C:\";
	каталогнастроек=каталог;
	файл="";
КонецПроцедуры //ПриОткрытии

//Ввод значения параметров реквизитов
Процедура ВыборСтрокиТЗ()
	Если ТЗреквизитов.ТекущаяКолонка()="Стандартное" Тогда
		ном=ТЗреквизитов.Стандартное;
		Если ВвестиЗначение(ном, "Стандартное значение для "+ТЗреквизитов.Наименование)=1 Тогда
		    ТЗреквизитов.Стандартное=ном;
		КонецЕсли;
	Иначе
		ном=ТЗреквизитов.КолонкаExcel;
		Если ВвестиЧисло(ном, "Колонка Excel для "+ТЗреквизитов.Наименование, 14, 0)=1 Тогда
		    ТЗреквизитов.КолонкаExcel=ном;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры  

//Сохранение/восстановление настроек в/из текстовый(ого) файл(а)
Процедура СохранитьНастройки(парам=1)
	Если парам=1 Тогда
		Если ФС.ВыбратьФайл(1,файлнастроек,каталогнастроек,"Выбирите файл","Файл настроек(*.txt)|*.txt","*.txt")=1 Тогда
			ткт=СоздатьОбъект("Текст");
			Если ФС.СуществуетФайл(каталогнастроек+файлнастроек)=1 тогда
				ткт.Открыть(каталогнастроек+файлнастроек);
				ткт.Очистить();
			КонецЕсли;
			ткт.ДобавитьСтроку("ВИДСП&"+СпрВид.ПолучитьЗначение(СпрВид.ТекущаяСтрока()));
			ткт.ДобавитьСтроку("ОЧСПР&"+Очищать);
			ткт.ДобавитьСтроку("НАСТР&"+НачальнаяСтрока);
			ткт.ДобавитьСтроку("ПРОВ1&"+Пров1);
			ткт.ДобавитьСтроку("ПРОВ2&"+Пров2);
			ткт.ДобавитьСтроку("ПЕРЕК&"+ПериодичРеквизиты);
			ткт.ДобавитьСтроку("КАТДН&"+каталог);
			ткт.ДобавитьСтроку("ФАЙДН&"+файл);
			ТЗреквизитов.ВыбратьСтроки();
			Пока ТЗреквизитов.ПолучитьСтроку()=1 Цикл
			    ткт.ДобавитьСтроку("РЕКВЗ&"+ТЗреквизитов.Наименование
					+"&"+ТЗреквизитов.КолонкаExcel
					+"&"+ЗначениеВСтроку(ТЗреквизитов.Стандартное));
			КонецЦикла;
			ткт.Записать(каталогнастроек+файлнастроек);
		КонецЕсли;
	ИначеЕсли парам=0 Тогда
		ткт=СоздатьОбъект("Текст");
		Если ФС.ВыбратьФайл(0,файлнастроек,каталогнастроек,"Выбирите файл","Файл настроек(*.txt)|*.txt","*.txt")=1 тогда
			ткт.Открыть(каталогнастроек+файлнастроек);
			для пр=1 по ткт.КоличествоСтрок() Цикл
				стр=ткт.ПолучитьСтроку(пр);
				сз=Разложить(стр, "&");
				Если сз.ПолучитьЗначение(1)="ВИДСП" Тогда
					СпрВид.ТекущаяСтрока(СпрВид.НайтиЗначение(сз.ПолучитьЗначение(2)));
					ПриВыбореСправочника(СпрВид.ТекущаяСтрока());
				ИначеЕсли сз.ПолучитьЗначение(1)="ОЧСПР" Тогда
					Очищать=0+сз.ПолучитьЗначение(2);
				ИначеЕсли сз.ПолучитьЗначение(1)="НАСТР" Тогда
					НачальнаяСтрока=0+сз.ПолучитьЗначение(2);
				ИначеЕсли сз.ПолучитьЗначение(1)="ПЕРЕК" Тогда
					ПериодичРеквизиты=Дата(сз.ПолучитьЗначение(2));
				ИначеЕсли сз.ПолучитьЗначение(1)="КАТДН" Тогда
					каталог=сз.ПолучитьЗначение(2);
				ИначеЕсли сз.ПолучитьЗначение(1)="ФАЙДН" Тогда
					файл=сз.ПолучитьЗначение(2);
				ИначеЕсли сз.ПолучитьЗначение(1)="ПРОВ1" Тогда
					Пров1=сз.ПолучитьЗначение(2);
				ИначеЕсли сз.ПолучитьЗначение(1)="ПРОВ2" Тогда
					Пров2=сз.ПолучитьЗначение(2);
				ИначеЕсли сз.ПолучитьЗначение(1)="РЕКВЗ" Тогда
					стрк=0;
					Если ТЗреквизитов.НайтиЗначение(сз.ПолучитьЗначение(2),стрк,"Наименование")=1 Тогда
					    ТЗреквизитов.УстановитьЗначение(стрк, "КолонкаExcel", 0+сз.ПолучитьЗначение(3));
						значен=ЗначениеИзСтроки(сз.ПолучитьЗначение(4));
						Если ПустоеЗначение(значен)=0 Тогда
						    ТЗреквизитов.УстановитьЗначение(стрк, "Стандартное", значен);
						КонецЕсли;
					Иначе
						Сообщить("Не найдено поле "+сз.ПолучитьЗначение(2));
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли парам=2 Тогда
		ТЗреквизитов.Стандартное="";
	КонецЕсли;
КонецПроцедуры