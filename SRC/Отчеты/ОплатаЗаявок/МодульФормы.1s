////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//   
Перем ТекСтрокаВТаблице; // текущая строка в таблице значений  МФ 

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка;      

// переменные предназначены для механизма универсальных группировок отчета
Перем СписокЗначенийГруппировок;
Перем КоличествоГруппировок;


Перем ЗапросВзаиморасчеты;
Перем ЗапросЗаявки;          

Перем Загол2;
Перем Загол1;                  

Перем ПозицияФирмы;
Перем ПозицияДоговора;         

Перем ФирмаВыведена;
Перем ДоговорВыведен;
Перем КонтрагентВыведен;
Перем СвойствоВыведено;   

Перем СписокЗаявок;     

Перем ВызовИзДокумента;
Перем ДокументОтменыЗаявок;      
Перем Заголовок;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ    
//
//******************************************************************************
// УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
//
// Параметры:  ТекстЗапроса - переданный по ссылке текст запроса
// 			   ТекстЗагол   - переданный по ссылке текст заголовка    
//			   Режим: 1 - запрос по заявкам, 2 - запрос по взаиморасчетам
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Дополняет строку запроса и заголовка в соответствии с выбранными группировками.
//       
Процедура УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол, Режим)
	                       
	НомерГруппировкиПоТМЦ = 9999;
	НетФирм = 0;
	НетДоговоров = 0;	
	Для Сч=1 По Группировки.РазмерСписка() Цикл  
		ПредставлениеГрупп="";
		ЕстьПометка = Группировки.Пометка(Сч);
		ТекстГрупп  = Группировки.ПолучитьЗначение(Сч,ПредставлениеГрупп); 
		Если (ТекстГрупп = "Фирма") и (ЕстьПометка = 0)  Тогда
		    НетФирм = 1;
		КонецЕсли; 
		Если (ТекстГрупп = "Договор") и (ЕстьПометка = 0)  Тогда
		    НетДоговоров = 1;
		КонецЕсли; 
		Если ЕстьПометка=1 Тогда
			Если  ТекстГрупп = "Контрагент" Тогда
				ТекстБезГрупп = " без групп";
			Иначе
				ТекстБезГрупп = "";
			КонецЕсли; 
			Если ТекстГрупп = "Заявка" Тогда
				Если НетФирм = 1 Тогда
					ТекстЗапроса 	= ТекстЗапроса 	+ "Группировка Фирма;";    
				КонецЕсли;
				Если НетДоговоров = 1 Тогда
					ТекстЗапроса 	= ТекстЗапроса 	+ "Группировка Договор;";    
				КонецЕсли;
			КонецЕсли;                            
			Если (ТекстГрупп <> "Заявка") или (Режим <> 2) Тогда                                         
				УпорядочитьПо = ?(ТекстГрупп = "Заявка", " Упорядочить по Заявка.ДатаДок", "");
				ТекстЗапроса 	= ТекстЗапроса 	+ "Группировка "+ТекстГрупп+ТекстБезГрупп+УпорядочитьПо +";";  
			КонецЕсли;
			ТекстЗагол 		= ТекстЗагол 	+ ?(ТекстЗагол="",""," / ")+ПредставлениеГрупп;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // УстановитьГруппировкиЗапроса()

//******************************************************************************
// ЗапросПоЗаявкам()
//
// Параметры: 
//  Нет 
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Формирует текст запроса по заявкам и выполняет запрос
//
Процедура ЗапросПоЗаявкам()
	Перем ТекстЗапроса;
	//Создание объекта типа Запрос
	ЗапросЗаявки = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Договор = Регистр.Заявки.ДоговорПокупателя;
	|Док = Регистр.Заявки.ТекущийДокумент;
	|Контрагент = Регистр.Заявки.ДоговорПокупателя.Владелец;
	|Заявка = Регистр.Заявки.ЗаявкаПокупателя;
	|Дата = Регистр.Заявки.ЗаявкаПокупателя.ДатаДок;
	|Фирма = Регистр.Заявки.Фирма;
	|ОперацияЗаявка = Регистр.Заявки.ЗаявкаПокупателя.ВидОперации;           
	|УпрАналитика	= Регистр.Заявки.Фирма.УпрАналитика;
	|ЮрЛицо 		= Регистр.Заявки.Фирма.ЮрЛицо;
	|СвойствоПок 	= Регистр.Заявки.ДоговорПокупателя.Владелец.ОсновноеСвойство.ЗначениеСвойства;
	|СуммаПоЗаявке = Регистр.Заявки.СтоимостьРасход;
	|Функция СуммаПоЗаявкеНачОст = НачОст(СуммаПоЗаявке);
	|Функция СуммаПоЗаявкеПриход = Приход(СуммаПоЗаявке);
	|Функция СуммаПоЗаявкеРасход = Расход(СуммаПоЗаявке);
	|Функция СуммаПоЗаявкеКонОст = КонОст(СуммаПоЗаявке);
	|"//}}ЗАПРОС
	;                                              
	
	Если ЗаПериод = 1 Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|Условие (Дата >= ДатаНачала);
		|Условие (Дата <= ДатаКонца);
		|";                        
	КонецЕсли;
		
	// Фильтр по виду заявок 
	ВыбВидЗаявки = ВидЗаявок.ПолучитьЗначение(ВидЗаявок.ТекущаяСтрока());
	Если ПустоеЗначение(ВыбВидЗаявки) = 0 Тогда
        ТекстЗапроса = ТекстЗапроса + "Условие (ОперацияЗаявка = ВыбВидЗаявки);";
	КонецЕсли;
	
	Загол2="";

	УстановитьГруппировкиЗапроса(ТекстЗапроса, Загол2, 1);
                                                              
	Загол1="";
	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,Загол1);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол1);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол1);
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,Загол1);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол1);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол1);
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",ВыбРазделитель3,"ВыбРазделитель3",ТекстЗапроса,Загол1);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол1);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол1);
	КонецЕсли;
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Контрагент" , ВыбПокупатель       , "ВыбПокупатель"        ,ТекстЗапроса, Загол1, "Контрагенты");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Договор"    , ВыбДоговор          , "ВыбДоговор"           ,ТекстЗапроса, Загол1, "Договоры");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "СвойствоПок",,, ТекстЗапроса, Загол1, "ЗначенияСвойств");

	// Если ошибка в запросе, то выход из процедуры
	Если ЗапросЗаявки.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры //ЗапросПоЗаявкам()  

//******************************************************************************
// ЗапросПоВзаиморасчетам()
//
// Параметры: 
//  Нет 
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Формирует текст запроса по заявкам и выполняет запрос
//
Процедура ЗапросПоВзаиморасчетам()
	Перем ТекстЗапроса;
	//Создание объекта типа Запрос
	ЗапросВзаиморасчеты = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Взаиморасчеты)
	|Период с ДатаНачала по ДатаКонца;
	|Контрагент = Регистр.Покупатели.Договор.Владелец;
	|Договор = Регистр.Покупатели.Договор;
	|Док = Регистр.Покупатели.КредДокумент;
	|Фирма = Регистр.Покупатели.Фирма;
	|УпрАналитика	= Регистр.Покупатели.Фирма.УпрАналитика;
	|ЮрЛицо 		= Регистр.Покупатели.Фирма.ЮрЛицо;
	|СвойствоПок 	= Регистр.Покупатели.Договор.Владелец.ОсновноеСвойство.ЗначениеСвойства;
	|Долг = Регистр.Покупатели.СуммаВал;
	|Функция ДолгКонОст = КонОст(Долг);
	|Функция ДолгНачОст = НачОст(Долг);
	|Функция ДолгПриход = Приход(Долг);
	|Функция ДолгРасход = Расход(Долг);
	|"//}}ЗАПРОС
	;

	Загол2="";

	УстановитьГруппировкиЗапроса(ТекстЗапроса, Загол2, 2); 
	
	Загол3 = "";

	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,Загол3);
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,Загол3);
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",ВыбРазделитель3,"ВыбРазделитель3",ТекстЗапроса,Загол3);
	КонецЕсли;
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Контрагент" , ВыбПокупатель        , "ВыбПокупатель"        ,ТекстЗапроса,Загол3,"Контрагенты");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Договор"    , ВыбДоговор           , "ВыбДоговор"           ,ТекстЗапроса,Загол3,"Договоры");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "СвойствоПок",,,ТекстЗапроса,Загол3, "ЗначенияСвойств");

	// Если ошибка в запросе, то выход из процедуры
	Если ЗапросВзаиморасчеты.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;                                
	                   
КонецПроцедуры //ЗапросПоВзаиморасчетам   

//******************************************************************************
// ВыводитьЗаявку(ОстатокПоЗаявке, СуммаОплаты, ДатаОплаты)
//
// Параметры:  ОстатокПоЗаявке - сумма неотгруженного остатка по заявке
// 			   СуммаОплаты - сумма оплаты по заявке                 
//			   СуммаПоЗаявке - сумма по заявке, равная, обычно, остаток на начало по данной заявке плюс приход
//			   ДатаОплаты - дата оплаты заявки
//
// Возвращаемое значение: 
//	1 - Если заявка соответствует условиям вывода, 0 - не соответствует.
//
// Описание: 
//	Определяет, попадает ли заявка в отчет на основании сумм остатка и оплаты по заявке и даты оплаты заявке, 
// а также выбранных видов заявок.
//       
Функция ВыводитьЗаявку(ОстатокПоЗаявке, СуммаОплаты, СуммаПоЗаявке, ДатаОплаты)
	Если ОстатокПоЗаявке = 0 Тогда
		ВидОтгрузки = 2; // отгруженные полностью
	ИначеЕсли СуммаПоЗаявке > ОстатокПоЗаявке Тогда
		ВидОтгрузки = 3; // частично отгруженные   
	ИначеЕсли СуммаПоЗаявке = ОстатокПоЗаявке Тогда
		ВидОтгрузки = 1; // неотгруженные
	КонецЕсли;
	
	Если СуммаОплаты =  ОстатокПоЗаявке Тогда
	    ВидОплаты = 2;   // оплаченные
	ИначеЕсли СуммаОплаты <= 0 тогда
		ВидОплаты = 1;   // неоплаченные
	ИначеЕсли СуммаОплаты < ОстатокПоЗаявке тогда
		ВидОплаты = 3;   // частично оплаченные 
	КонецЕсли;    
	
	Просрочена = 0;
	Если ДатаОплаты < ДатаКонца Тогда
	    Просрочена = 1;
	КонецЕсли;           
	
	Если СостоянияОтгрузки.Пометка(ВидОтгрузки) = 0  Тогда
		Возврат 0;
	КонецЕсли;     
	Если СостоянияОплаты.Пометка(ВидОплаты) = 0  Тогда
		Возврат 0;
	КонецЕсли;                            
	Если (Просроченные = 1) и (Просрочена = 0) Тогда
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции  //ВыводитЗаявку()        

//******************************************************************************
// ВывестиСтроки()
//
// Параметры: нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Выводит, в случае необходимости секции группировок верхних уровней.
//       
Процедура ВывестиСтроки()
	Если ФирмаВыведена = 0  Тогда
	    Таб.ВывестиСекцию("Фирма");
		ФирмаВыведена = 1;
	КонецЕсли;
	Если СвойствоВыведено = 0  Тогда
	    Таб.ВывестиСекцию("СвойствоПок");
		СвойствоВыведено = 1;
	КонецЕсли;
	Если КонтрагентВыведен = 0  Тогда
	    Таб.ВывестиСекцию("Контрагент");
		КонтрагентВыведен = 1;
	КонецЕсли;
	Если ДоговорВыведен = 0  Тогда
	    Таб.ВывестиСекцию("Договор");
		ДоговорВыведен = 1;
	КонецЕсли;           
КонецПроцедуры //ВывестиСтроки

	                                  
//******************************************************************************
// ВывестиГруппировку(ТекстЗапроса, ТекстЗагол)
//
// Параметры:  	Режим - название выводимой группировки
// 			    УровеньВложенности - уровень группировки
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Осуществляет действия необходимые для подготовки и вывода данных отчета.
//       
Процедура ВывестиГруппировку(Режим, УровеньВложенности)     
	Если (СписокЗначенийГруппировок.РазмерСписка() < УровеньВложенности) и (Режим <> "Заявка") Тогда
	    СписокЗначенийГруппировок.ДобавитьЗначение("");
	КонецЕсли;
	Если Режим = "Фирма" Тогда
	    Пока ЗапросЗаявки.Группировка("Фирма") = 1 цикл 
			ФирмаВыведена = 0;
			СписокЗначенийГруппировок.УстановитьЗначение(УровеньВложенности, ЗапросЗаявки.Фирма);
			Позиция = Группировки.НайтиЗначение("СвойствоПок");
			Если Группировки.Пометка(Позиция) = 1  Тогда
			    ВывестиГруппировку("СвойствоПок", УровеньВложенности+1);
			Иначе
				Позиция = Группировки.НайтиЗначение("Контрагент");
				Если Группировки.Пометка(Позиция) = 1  Тогда
			    	ВывестиГруппировку("Контрагент", УровеньВложенности+1);
				Иначе
					Позиция = Группировки.НайтиЗначение("Договор");
					Если Группировки.Пометка(Позиция) = 1 Тогда
					    ВывестиГруппировку("Договор", УровеньВложенности+1);
					Иначе
						Позиция = Группировки.НайтиЗначение("Заявка");
						Если Группировки.Пометка(Позиция) = 1 Тогда
						    ВывестиГруппировку("Заявка", УровеньВложенности+1);
						Иначе                         
							Предупреждение("Группировка по заявке должна быть обязательно установлена!", 60);
							Возврат;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли Режим = "СвойствоПок" тогда    
		Пока ЗапросЗаявки.Группировка("СвойствоПок") = 1  Цикл
			СвойствоВыведено = 0;
			СписокЗначенийГруппировок.УстановитьЗначение(УровеньВложенности, ЗапросЗаявки.СвойствоПок);
			Позиция = Группировки.НайтиЗначение("Контрагент");
			Если Группировки.Пометка(Позиция) = 1  Тогда
	    		ВывестиГруппировку("Контрагент", УровеньВложенности+1);
			Иначе
				Позиция = Группировки.НайтиЗначение("Договор");
				Если Группировки.Пометка(Позиция) = 1 Тогда
				    ВывестиГруппировку("Договор", УровеньВложенности+1);
				Иначе
					Позиция = Группировки.НайтиЗначение("Заявка");
					Если Группировки.Пометка(Позиция) = 1 Тогда
					    ВывестиГруппировку("Заявка", УровеньВложенности+1);
					Иначе                         
						Предупреждение("Группировка по заявке должна быть обязательно установлена!", 60);
						Возврат;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли Режим = "Контрагент" тогда
		Пока ЗапросЗаявки.Группировка("Контрагент") = 1  Цикл
			КонтрагентВыведен = 0;
			СписокЗначенийГруппировок.УстановитьЗначение(УровеньВложенности, ЗапросЗаявки.Контрагент);
			Позиция = Группировки.НайтиЗначение("Договор");
			Если Группировки.Пометка(Позиция) = 1  Тогда
		    	ВывестиГруппировку("Договор", УровеньВложенности+1);
			Иначе
				Позиция = Группировки.НайтиЗначение("Заявка");
				Если Группировки.Пометка(Позиция) = 1 Тогда
				    ВывестиГруппировку("Заявка", УровеньВложенности+1);
				Иначе                         
					Предупреждение("Группировка по заявке должна быть обязательно установлена!", 60);
					Возврат;
				КонецЕсли;
			КонецЕсли;   
		КонецЦикла;
	ИначеЕсли Режим = "Договор" тогда
		Пока ЗапросЗаявки.Группировка("Договор") = 1  Цикл
			СписокЗначенийГруппировок.УстановитьЗначение(УровеньВложенности, ЗапросЗаявки.Договор);
			ДоговорВыведен = 0;
			Позиция = Группировки.НайтиЗначение("Заявка");
			Если Группировки.Пометка(Позиция) = 1 Тогда
			    ВывестиГруппировку("Заявка", УровеньВложенности+1);
			Иначе                         
				Предупреждение("Группировка по заявке должна быть обязательно установлена!", 60);
				Возврат;
			КонецЕсли;   
		КонецЦикла;
	ИначеЕсли Режим = "Заявка" тогда
		Позиция = Группировки.НайтиЗначение("Фирма");
		ПоФирмам = Группировки.Пометка(Позиция);    
		Позиция = Группировки.НайтиЗначение("Договор");
		ПоДоговорам = Группировки.Пометка(Позиция);    
		Позиция = Группировки.НайтиЗначение("Док");
		ПоДокументам = Группировки.Пометка(Позиция);   
		ЕстьФирма = 0;
		ЕстьДоговор = 0;   
		Пока 1 = 1 Цикл       
			
			// Если не выбрана группировка по фирмам, пройдем по группировке по фирмам в 
			// запросе по заявкам
			Если (ПоФирмам = 0) и (ЕстьДоговор = 0) Тогда
			    ЕстьФирма = ЗапросЗаявки.Группировка("Фирма");
				Если ЕстьФирма = 1  Тогда
					Если ПозицияФирмы = 0  Тогда
					    СписокЗначенийГруппировок.ДобавитьЗначение(ЗапросЗаявки.Фирма);
						ПозицияФирмы = СписокЗначенийГруппировок.РазмерСписка();
					Иначе 
						СписокЗначенийГруппировок.УстановитьЗначение(ПозицияФирмы, ЗапросЗаявки.Фирма);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;           
			
			// Если не выбрана группировка по договорам, пройдем по группировке по договорам в 
			// запросе по заявкам
			Если ПоДоговорам = 0 Тогда
				Если (ПоФирмам + ЕстьФирма) = 0 тогда
					Прервать;
				КонецЕсли;
			    ЕстьДоговор = ЗапросЗаявки.Группировка("Договор");
				Если ЕстьДоговор = 1  Тогда
					Если ПозицияДоговора = 0  Тогда
					    СписокЗначенийГруппировок.ДобавитьЗначение(ЗапросЗаявки.Договор);
						ПозицияДоговора = СписокЗначенийГруппировок.РазмерСписка();
					Иначе 
						СписокЗначенийГруппировок.УстановитьЗначение(ПозицияДоговора, ЗапросЗаявки.Договор);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;     
			
			// Если все группировки пройдены выходим из цикла
			Если (ЕстьФирма = 0) и (ЕстьДоговор = 0) и (ПоФирмам * ПоДоговорам = 0) Тогда
			    Прервать;
			КонецЕсли;             
			
			// Если группировка по договорам пройдена - выходим из цикла
			Если (ПоДоговорам + ЕстьДоговор) = 0 тогда
				Продолжить;
			КонецЕсли;                                                  
			
			// Теперь надо спозиционировать запрос по взаиморасчетам на нужной строке
			ЗапросВзаиморасчеты.ВНачалоВыборки();
			Если СписокЗначенийГруппировок.РазмерСписка() = 1 тогда
				Если  ПоДокументам = 1 Тогда
					ЕстьДок = ЗапросВзаиморасчеты.Получить(СписокЗначенийГруппировок.ПолучитьЗначение(1),);
				Иначе
					ЕстьДок = ЗапросВзаиморасчеты.Получить(СписокЗначенийГруппировок.ПолучитьЗначение(1));
				КонецЕсли;
			ИначеЕсли СписокЗначенийГруппировок.РазмерСписка() = 2 тогда
				Если  ПоДокументам = 1 Тогда
					ЕстьДок = ЗапросВзаиморасчеты.Получить(СписокЗначенийГруппировок.ПолучитьЗначение(1), СписокЗначенийГруппировок.ПолучитьЗначение(2),);
				Иначе
					ЕстьДок = ЗапросВзаиморасчеты.Получить(СписокЗначенийГруппировок.ПолучитьЗначение(1), СписокЗначенийГруппировок.ПолучитьЗначение(2));
				КонецЕсли;
			ИначеЕсли СписокЗначенийГруппировок.РазмерСписка() = 3 тогда
				Если  ПоДокументам = 1 Тогда
					ЕстьДок = ЗапросВзаиморасчеты.Получить(СписокЗначенийГруппировок.ПолучитьЗначение(1), СписокЗначенийГруппировок.ПолучитьЗначение(2), СписокЗначенийГруппировок.ПолучитьЗначение(3),);
				Иначе
					ЕстьДок = ЗапросВзаиморасчеты.Получить(СписокЗначенийГруппировок.ПолучитьЗначение(1), СписокЗначенийГруппировок.ПолучитьЗначение(2),СписокЗначенийГруппировок.ПолучитьЗначение(3));
				КонецЕсли;
			ИначеЕсли СписокЗначенийГруппировок.РазмерСписка() =4 тогда
				Если  ПоДокументам = 1 Тогда
					ЕстьДок = ЗапросВзаиморасчеты.Получить(СписокЗначенийГруппировок.ПолучитьЗначение(1), СписокЗначенийГруппировок.ПолучитьЗначение(2), СписокЗначенийГруппировок.ПолучитьЗначение(3), СписокЗначенийГруппировок.ПолучитьЗначение(4),);
				Иначе
					ЕстьДок = ЗапросВзаиморасчеты.Получить(СписокЗначенийГруппировок.ПолучитьЗначение(1), СписокЗначенийГруппировок.ПолучитьЗначение(2),СписокЗначенийГруппировок.ПолучитьЗначение(3), СписокЗначенийГруппировок.ПолучитьЗначение(4));
				КонецЕсли;
			КонецЕсли; 
			
			Если ЕстьДок = 1  Тогда                                                                                      
				ДолгПокупателю = -ЗапросВзаиморасчеты.ДолгКонОст;
				ОплатаПоДоговору = ДолгПокупателю;    
				ОстатокПоДокументу = 0;
				Если ДолгПокупателю < 0  Тогда                 
					
					// Если у покупателя есть задолженность придется информацию по документам 
					// собирать в таблицу
					ТабДокументов = СоздатьОбъект("ТаблицаЗначений");
					ТабДокументов.НоваяКолонка("Документ", "Документ");
					ТабДокументов.НоваяКолонка("Заявка", "Документ");
					ТабДокументов.НоваяКолонка("Сумма", "Число");     
					
					// И о заявках тоже
					ТабЗаявок = СоздатьОбъект("ТаблицаЗначений");
					ТабЗаявок.НоваяКолонка("Заявка");
					ТабЗаявок.НоваяКолонка("НачОст");
					ТабЗаявок.НоваяКолонка("Приход");
					ТабЗаявок.НоваяКолонка("Расход");
					ТабЗаявок.НоваяКолонка("КонОст");
				    
				КонецЕсли;
					
				Пока ЗапросЗаявки.Группировка("Заявка") = 1  Цикл        
				
					Если ДолгПокупателю >= 0  Тогда                 
						ОплаченоПоЗаявке = Мин(ОплатаПоДоговору, ЗапросЗаявки.СуммаПоЗаявкеКонОст);
					Иначе   
						ОплаченоПоЗаявке = 0;
						ТабЗаявок.НоваяСтрока();
						ТабЗаявок.Заявка = ЗапросЗаявки.Заявка;
						ТабЗаявок.НачОст = ЗапросЗаявки.СуммаПоЗаявкеНачОст;
						ТабЗаявок.Приход = ЗапросЗаявки.СуммаПоЗаявкеПриход;
						ТабЗаявок.Расход = ЗапросЗаявки.СуммаПоЗаявкеРасход;
						ТабЗаявок.КонОст = ЗапросЗаявки.СуммаПоЗаявкеКонОст;
					КонецЕсли;
					ОплатаПоДоговору = ОплатаПоДоговору - ОплаченоПоЗаявке;
					Выводить = ВыводитьЗаявку(ЗапросЗаявки.СуммаПоЗаявкеКонОст, ОплаченоПоЗаявке, ЗапросЗаявки.СуммаПоЗаявкеНачОст+ЗапросЗаявки.СуммаПоЗаявкеПриход, ЗапросЗаявки.Заявка.ДатаОплаты);
					Если Выводить = 1  Тогда
						Если ДолгПокупателю >= 0 Тогда
							ВывестиСтроки();   
							ПечНачОст = ЗапросЗаявки.СуммаПоЗаявкеНачОст;
							ПечПриход = ЗапросЗаявки.СуммаПоЗаявкеПриход;
							ПечРасход = ЗапросЗаявки.СуммаПоЗаявкеРасход;
							ПечКонОст = ЗапросЗаявки.СуммаПоЗаявкеКонОст;        
							ПечЗаявка = ЗапросЗаявки.Заявка;     
							СписокЗаявок.ДобавитьЗначение(ПечЗаявка);
						    Таб.ВывестиСекцию("Заявка");                                                
						КонецЕсли;
					КонецЕсли;

					Если ОстатокПоДокументу <> 0  Тогда
						Если ДолгПокупателю > 0  Тогда
						    ОплатаПоДокументу = Мин(ОстатокПоДокументу, ОплаченоПоЗаявке);
							ОплаченоПоЗаявке = ОплаченоПоЗаявке - ОплатаПоДокументу;     
							ОстатокПоДокументу = ОстатокПоДокументу - ОплатаПоДокументу;
							Если Выводить = 1  Тогда
								Таб.ВывестиСекцию("Док");  
							КонецЕсли;
						Иначе 
							ОплаченоПоЗаявке = 0;
						КонецЕсли;
					КонецЕсли;

					Если ПоДокументам = 1  Тогда
						Пока (ОплаченоПоЗаявке > 0) или (ДолгПокупателю < 0)  Цикл   
							Если ЗапросВзаиморасчеты.Группировка("Док") = 1 тогда  
								Док  = ЗапросВзаиморасчеты.Док;
								ОстатокПоДокументу = -ЗапросВзаиморасчеты.ДолгКонОст;
								Если ЗапросВзаиморасчеты.ДолгКонОст <> 0  Тогда   
									Если ДолгПокупателю > 0 Тогда
									    ОплатаПоДокументу = Мин(-ЗапросВзаиморасчеты.ДолгКонОст, ОплаченоПоЗаявке);
										ОплаченоПоЗаявке = ОплаченоПоЗаявке - ОплатаПоДокументу;
										ОстатокПоДокументу = ОстатокПоДокументу - ОплатаПоДокументу;
										Если Выводить = 1 Тогда
											ПечДок = Док;
											Таб.ВывестиСекцию("Док");     
										КонецЕсли;
									Иначе    
										Пока ЗапросЗаявки.Группировка("Док") = 1 Цикл
											Если ЗапросЗаявки.Док <> ЗапросВзаиморасчеты.Док Тогда
												Продолжить;
											КонецЕсли;
											НомСтроки = 0;
											НомКолонки = 0;
											Если ТабДокументов.НайтиЗначение(ЗапросВзаиморасчеты.Док, НомСтроки, НомКолонки) = 0 Тогда
												ТабДокументов.НоваяСтрока();
												ТабДокументов.Документ = ЗапросВзаиморасчеты.Док;
												ТабДокументов.Сумма = -ЗапросВзаиморасчеты.ДолгКонОст;
												ТабДокументов.Заявка = ЗапросЗаявки.Заявка;
											КонецЕсли;
										КонецЦикла;
									КонецЕсли;      
								КонецЕсли;	
							Иначе
								Прервать;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
				
				Если ДолгПокупателю < 0  Тогда    
					
					// Для вывода заявок и документов в хронологическом порядке придется использовать еще таблицу
					ТабВывода = СоздатьОбъект("ТаблицаЗначений");
					ТабВывода.НоваяКолонка("ПечНачОст", "Число");
					ТабВывода.НоваяКолонка("ПечКонОст", "Число");
					ТабВывода.НоваяКолонка("ПечРасход", "Число");
					ТабВывода.НоваяКолонка("ПечПриход", "Число");
					ТабВывода.НоваяКолонка("ПечЗаявка", "Документ");
					ТабВывода.НоваяКолонка("ПечДок",    "Документ");
					ТабВывода.НоваяКолонка("ОплатаПоДокументу",    "Число");   
					
					ТабЗаявок.Сортировать("-Заявка");
					ТабДокументов.Сортировать("-Документ");
					ТабЗаявок.ВыбратьСтроки();
					ТекСтрока = 0;                 
					ОстатокПоДокументу = 0;
					Пока ТабЗаявок.ПолучитьСтроку() = 1 Цикл
						ОплаченоПоЗаявке = Макс(ОплатаПоДоговору, -ТабЗаявок.Расход);  
						ОплатаПоДоговору = ОплатаПоДоговору - ОплаченоПоЗаявке;  
						Выводить = ВыводитьЗаявку(ТабЗаявок.КонОст, ОплаченоПоЗаявке, ТабЗаявок.НачОст+ТабЗаявок.Приход, ТабЗаявок.Заявка.ДатаОплаты);
						Если Выводить = 1 Тогда  
							ВывестиСтроки();
							ТабВывода.НоваяСтрока();
							ТабВывода.ПечНачОст = ТабЗаявок.НачОст;
							ТабВывода.ПечКонОст = ТабЗаявок.КонОст;
							ТабВывода.ПечРасход = ТабЗаявок.Расход;
							ТабВывода.ПечПриход = ТабЗаявок.Приход; 
							ТабВывода.ПечЗаявка = ТабЗаявок.Заявка;   
							СписокЗаявок.ДобавитьЗначение(ПечЗаявка);  
						КонецЕсли;
					    ТабДокументов.ВыбратьСтроки();
						Если ОстатокПоДокументу <> 0  Тогда
						    ОплатаПоДокументу = Макс(ОстатокПоДокументу, ОплаченоПоЗаявке);
							ОплаченоПоЗаявке = ОплаченоПоЗаявке - ОплатаПоДокументу; 
							ОстатокПоДокументу = ОстатокПоДокументу - ОплатаПоДокументу;      
							Если ОплатаПоДокументу <> 0 Тогда
								Если Выводить = 1 Тогда    
									ТабВывода.НоваяСтрока();
									ТабВывода.ПечДок	        = ПечДок; 
									ТабВывода.ПечЗаявка			= ТабЗаявок.Заявка;
									ТабВывода.ОплатаПоДокументу = ОплатаПоДокументу;
							    КонецЕсли;
							КонецЕсли;
						КонецЕсли;
						Пока ТабДокументов.ПолучитьСтроку() = 1  Цикл     
							Если ТабДокументов.Заявка <> ТабЗаявок.Заявка Тогда
								Продолжить;
							КонецЕсли;
							Если ТекСтрока > 0 Тогда
							    Пока Таб.ТекущаяСтрока() <> ТекСтрока Цикл
							        ТабДокументов.ПолучиьСтроку();
								КонецЦикла;             
								Если ОстатокПоДокументу = 0 Тогда
								    Продолжить;
								КонецЕсли;
							КонецЕсли;
							ОстатокПоДокументу = ТабДокументов.Сумма;
							Если ОплаченоПоЗаявке >= 0  Тогда
								ТекСтрока = ТабДокументов.ТекущаяСтрока();
							    Прервать; 
							КонецЕсли;   
							Если ТабДокументов.Сумма = 0 Тогда
							    Продолжить;
							КонецЕсли;
						    ОплатаПоДокументу = Макс(ТабДокументов.Сумма, ОплаченоПоЗаявке);
							ОплаченоПоЗаявке = ОплаченоПоЗаявке - ОплатаПоДокументу;
							ОстатокПоДокументу = ОстатокПоДокументу - ОплатаПоДокументу; 
							Если Выводить = 1  Тогда
								ПечДок = ТабДокументов.Документ;
								ТабВывода.НоваяСтрока();
								ТабВывода.ПечДок = ПечДок;
								ТабВывода.ПечЗаявка			= ТабЗаявок.Заявка;
								ТабВывода.ОплатаПоДокументу = ОплатаПоДокументу;
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
					
					ТабВывода.Сортировать("ПечЗаявка, ПечДок", 1);
					ТабВывода.ВыбратьСтроки();
					Пока ТабВывода.ПолучитьСтроку() = 1 Цикл 
						Если ТабВывода.ПечДок.Выбран() = 0 Тогда
							ПечЗаявка = ТабВывода.ПечЗаявка;
							ПечНачОст = ТабВывода.ПечНачОст;
							ПечКонОст = ТабВывода.ПечКонОст;
							ПечРасход = ТабВывода.ПечРасход;
							ПечПриход = ТабВывода.ПечПриход; 
							Таб.ВывестиСекцию("Заявка");
						Иначе
							ПечДок    = ТабВывода.ПечДок;
							ОплатаПоДокументу = ТабВывода.ОплатаПоДокументу;
							Таб.ВывестиСекцию("Док");
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			Иначе
				ОплатаПоДоговору = 0;
				Пока ЗапросЗаявки.Группировка("Заявка") = 1  Цикл        
					ОплаченоПоЗаявке = 0;
					Если ВыводитьЗаявку(ЗапросЗаявки.СуммаПоЗаявкеКонОст, ОплаченоПоЗаявке, ЗапросЗаявки.СуммаПоЗаявкеНачОст+ЗапросЗаявки.СуммаПоЗаявкеПриход, ЗапросЗаявки.Заявка.ДатаОплаты) = 0 Тогда
					    Продолжить;
					КонецЕсли;    
					ВывестиСтроки();
					ПечНачОст = ЗапросЗаявки.СуммаПоЗаявкеНачОст;
					ПечПриход = ЗапросЗаявки.СуммаПоЗаявкеПриход;
					ПечРасход = ЗапросЗаявки.СуммаПоЗаявкеРасход;
					ПечКонОст = ЗапросЗаявки.СуммаПоЗаявкеКонОст;        
					ПечЗаявка = ЗапросЗаявки.Заявка;   
					СписокЗаявок.ДобавитьЗначение(ПечЗаявка);   
				    Таб.ВывестиСекцию("Заявка");
				КонецЦикла;
			КонецЕсли;
			Если ПоФирмам*ПоДоговорам = 1  Тогда
			    Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры //ВывестиГруппировку()

//******************************************************************************                                                    
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)  
	
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;                       
	
КонецФункции //РасшифровкаОбновить()                                            
                                      
//******************************************************************************
// Сформировать()
//
// Параметры: 
//  ЗакрытьДиалог - закрыть диалог после вывода отчета
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Вызывается по одноименной кнопке. Формирует отчет и выводит его на экран
//
Процедура Сформировать(ЗакрытьДиалог=0)        
	
	СписокЗаявок = СоздатьОбъект("СписокЗначений");
	
	Если глПроверкаДаты(ДатаКонца,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;                                                 
	
	ПозицияФирмы = 0;
	ПозицияДоговора = 0;  
	
	ФирмаВыведена = -1;
	ДоговорВыведен = -1;
	КонтрагентВыведен = -1;
	СвойствоВыведено = -1;

	Расшифровка = СоздатьОбъект("СписокЗначений");
	Путь     = "";
	ИмяФайла = "";
	ФайлОтчета = РасположениеФайла(Путь,ИмяФайла);
    Расшифровка.Установить("Отчет", ?(ПустоеЗначение(ФайлОтчета) = 1,"ОплатаЗаявок", ФайлОтчета));
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1",ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2",ВыбРазделитель2);
	Расшифровка.Установить("ВыбРазделитель3",ВыбРазделитель3);
	Расшифровка.Установить("ВидРазделителя",ВидРазделителя);
	
	Расшифровка.Установить("ВыбПокупатель",	ВыбПокупатель);
	Расшифровка.Установить("Группировки",	Группировки);
	Расшифровка.Установить("СостоянияОплаты",	СостоянияОплаты);
	Расшифровка.Установить("СостоянияОтгрузки",	СостоянияОтгрузки);
	
	Расшифровка.Установить("Просроченные",	Просроченные);
	Расшифровка.Установить("ЗаПериод",		ЗаПериод);
	Расшифровка.Установить("ВидЗаявок",     ВидЗаявок.ТекущаяСтрока());
  
	
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были

	// запомним МФ только если он задан
    Если глМножественныйФильтрЗадан(ТаблицаМФ) = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ); 
	КонецЕсли;

	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли; 
	 
	Таб.ИсходнаяТаблица("Таблица");
	                                        
	ЗапросПоЗаявкам();
	ЗапросПоВзаиморасчетам();                            
	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 0, "ОплатаЗаявок", "ОплатаЗаявок");

	СписокЗначенийГруппировок = СоздатьОбъект("СписокЗначений");
	Позиция = Группировки.НайтиЗначение("Фирма");
	Если Группировки.Пометка(Позиция) = 1 Тогда
	    ВывестиГруппировку("Фирма", 1); 
	Иначе
		Позиция = Группировки.НайтиЗначение("СвойствоПок");
		Если Группировки.Пометка(Позиция) = 1 Тогда
		    ВывестиГруппировку("СвойствоПок", 1); 
		Иначе
			Позиция = Группировки.НайтиЗначение("Контрагент");
			Если Группировки.Пометка(Позиция) = 1 Тогда
			    ВывестиГруппировку("Контрагент", 1); 
			Иначе
				Позиция = Группировки.НайтиЗначение("Договор");
				Если Группировки.Пометка(Позиция) = 1 Тогда
				    ВывестиГруппировку("Договор", 1); 
				Иначе
					Позиция = Группировки.НайтиЗначение("Заявка");
					Если Группировки.Пометка(Позиция) = 1 Тогда
					    ВывестиГруппировку("Заявка", 1); 
					Иначе
						Предупреждение("Группировка по заявке должна быть обязательно установлена!", 60);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли
		КонецЕсли;
	КонецЕсли;                  

	Таб.ОбластьПечати(2);

	Таб.ТолькоПросмотр(1);
    Таб.Показать("Оплата заявок",);

	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;

КонецПроцедуры  //Сформировать()
	
//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда           
		Форма.ИспользоватьСлой("Шапка,Подвал,Основной1,Основной2,Разделитель"+СокрЛП(ВидРазделителя));
	Иначе     
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;
	
КонецПроцедуры //УправлениеДиалогом()                      
                                                                                                               
Процедура ПриПометкеГруппировок()                                                                                   
	ИмяГруппировки = Группировки.ПолучитьЗначение(Группировки.ТекущаяСтрока());
	Если ИмяГруппировки = "Заявка" Тогда
		Группировки.Пометка(Группировки.ТекущаяСтрока(),1);	    Возврат;                                           
	КонецЕсли;
	Группировки.Пометка(Группировки.ТекущаяСтрока(), ?(Группировки.Пометка(Группировки.ТекущаяСтрока()) = 1, 0, 1));
КонецПроцедуры //УправлениеДиалогом()

//******************************************************************************
// ПерерисовкаНазванийЗакладок
//
// Параметры:
// 	Нет
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Функция ПерерисовкаНазванийЗакладок()      

	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
	
КонецФункции // ПерерисовкаНазванийЗакладок()

//******************************************************************************
// ПоКнопкеОтменаЗаявок()
//
// Параметры:
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "Отмена заявок".
//
// Описание:   
//	Формирует документ "Отмена заявок" и заполняет	его строками из отчета
//
Процедура ПоКнопкеОтменаЗаявок()   
	Если (ВидРазделителя <> 1) или (ВыбРазделитель1.Выбран() = 0) Тогда
	    Предупреждение("Для отмены заявок выберите фирму!", 60);     
		Возврат;
	КонецЕсли;
	Заголовок = "Перенесено в документ отмены заявок";
	Сформировать(1); 
	Таб.Показать(,, -1);
	Если ПустоеЗначение(ДокументОтменыЗаявок) = 0 Тогда
		ОткрытьФорму(ДокументОтменыЗаявок, СписокЗаявок);  
	Иначе
		ОткрытьФорму("Документ.ОтменаЗаявок", СписокЗаявок);
	КонецЕсли;
КонецПроцедуры         

//******************************************************************************
// ДействияПриОткрытии(ФлагВосстановленияНастройки)
//
// Параметры:
// 	ФлагВосстановленияНастройки - параметр, принимаемый процедурой ПриОткрытии
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Процедура ДействияПриОткрытии(ФлагВосстановленияНастройки)
	
	Заголовок = "Оплата заявок";
	
	//                  тип          вид           переменная  название
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты", "Контрагент",  "По покупателям");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Договоры", "Договор",  "По договорам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "СвойствоПок",  "По свойствам покупателей");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Фирмы", "Фирма",  "По фирмам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","СвоиЮрЛица", "ЮрЛицо",  "По юр. лицам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","УпрАналитика", "УпрАналитика",  "По упр. аналитике");
	 
	Если ФлагВосстановленияНастройки = 0 Тогда     
		
		ВидРазделителя = 1;
		ДатаНачала      = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
		Если ПустоеЗначение(ДатаНачала) = 1 Тогда
			ДатаНачала      = НачМесяца(ДатаКонца);    
		КонецЕсли;

	КонецЕсли;
		
	Если (ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений") и (глОбновить = 0)  Тогда
		глРасшифровка = Форма.Параметр;
		ВызовИзДокумента = 1;                              
		Форма.кнОтменаЗаявок.	Заголовок("Перенести в документ");   
		Форма.кнОК.				Доступность(0);
		Форма.кнСформировать.	Заголовок("Просмотр");
		Форма.ВидРазделителя.	Доступность(0); 
		Форма.ЮрЛицо.			Доступность(0);
		Форма.УпрАналитика.		Доступность(0);
		Форма.ВыбРазделитель1.	Доступность(0);
		Форма.кнХРазделитель1.	Доступность(0);
	КонецЕсли;
	
	Если (глФлагРасшифровки = 1) или (ВызовИзДокумента = 1) Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВидРазделителя	= глРасшифровка.Получить("ВидРазделителя");
		ВыбРазделитель1	= глРасшифровка.Получить("ВыбРазделитель1");
		ВыбРазделитель2	= глРасшифровка.Получить("ВыбРазделитель2");
		ВыбРазделитель3	= глРасшифровка.Получить("ВыбРазделитель3");     
		ВидЗаявок.ТекущаяСтрока(глРасшифровка.Получить("ВидЗаявок")); 
		
		ВыбПокупатель 	= глРасшифровка.Получить("ВыбПокупатель");
		
		ЗаПериод	 			= глРасшифровка.Получить("ЗаПериод");
		
		глРасшифровка.Получить("Группировки").Выгрузить(Группировки);
		глРасшифровка.Получить("СостоянияОплаты").Выгрузить(СостоянияОплаты);
		глРасшифровка.Получить("СостоянияОтгрузки").Выгрузить(СостоянияОтгрузки);
		                                                                           
		Если ВызовИзДокумента = 1  Тогда
		    ДокументОтменыЗаявок = глРасшифровка.Получить("ДокументОтменыЗаявок");
		КонецЕсли;
		
		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если (Обновить <> 2) и (ВызовИзДокумента <> 1) Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;            
	
	ПерерисовкаНазванийЗакладок();

	ТаблицаМФ.ВидимостьКолонки("Тип",0);
	ТаблицаМФ.ВидимостьКолонки("Вид",0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");


	УправлениеДиалогом();
КонецПроцедуры
	
////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии(ФлагВосстановленияНастройки)   
	ДействияПриОткрытии(ФлагВосстановленияНастройки);
КонецПроцедуры // ПриОткрытии()             

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриПовторномОткрытии()
	ДействияПриОткрытии(0);
КонецПроцедуры  //ПриПовторномОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореЗакладки(Номер,Значение)	
	// закладки
    Если Номер=1 Тогда
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	КонецЕсли;      
	УправлениеДиалогом();
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки()     

//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(Значение)  
	
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ДатаКонца = ПолучитьДатуТА();
                                
// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");

ТекСтрокаВТаблице="";
                                       
Группировки.ДобавитьЗначение("Фирма", 		"Фирма");
Группировки.ДобавитьЗначение("СвойствоПок", "Свойство покупателя");
Группировки.ДобавитьЗначение("Контрагент", 	"Покупатель");
Группировки.ДобавитьЗначение("Договор","Договор");
Группировки.ДобавитьЗначение("Заявка","Заявка");
Группировки.ДобавитьЗначение("Док", 	"Кредитный документ");

Группировки. Пометка(1, 1);         
Группировки. Пометка(3, 1);         
Группировки. Пометка(4, 1);         
Группировки. Пометка(5, 1);         
Группировки. Пометка(6, 1);   

СостоянияОплаты.ДобавитьЗначение("Неоплаченные",	"Неоплаченные");
СостоянияОплаты.ДобавитьЗначение("Оплаченные", "Оплаченные полностью");
СостоянияОплаты.ДобавитьЗначение("ЧастичноОплаченные", 	"Частично оплаченные");

СостоянияОтгрузки.ДобавитьЗначение("Неотгруженные", 	"Неотгруженные");  
СостоянияОтгрузки.ДобавитьЗначение("Отгруженные", 	"Отгруженные полностью");  
СостоянияОтгрузки.ДобавитьЗначение("ЧастичноОтгруженные", 	"Частично отгруженные");  

СостоянияОплаты. Пометка(1, 1);         
СостоянияОплаты. Пометка(2, 1);         
СостоянияОплаты. Пометка(3, 1);         

СостоянияОтгрузки. Пометка(1, 1);         
СостоянияОтгрузки. Пометка(2, 1);         
СостоянияОтгрузки. Пометка(3, 1);         


// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

ВидЗаявок.УдалитьВсе();                             
ВидЗаявок.ДобавитьЗначение(ПолучитьПустоеЗначение("Перечисление.ВидыОперацийЗаявок"), "Все");
ВидЗаявок.ДобавитьЗначение(Перечисление.ВидыОперацийЗаявок.Неподтвержденная);
ВидЗаявок.ДобавитьЗначение(Перечисление.ВидыОперацийЗаявок.НаСклад);
ВидЗаявок.ДобавитьЗначение(Перечисление.ВидыОперацийЗаявок.НаПоставку);
ВидЗаявок.ТекущаяСтрока(1);
