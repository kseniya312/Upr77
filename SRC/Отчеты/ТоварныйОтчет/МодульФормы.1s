//******************************************************************************
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ

Перем ТекНомерОтчета;		// номер отчета из константы
Перем Сформирован;			// флаг того, что отчет был сформирован

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Т;		
Перем Обновить;
Перем Расшифровка; 

//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Форма.ИспользоватьСлой("Шапка,Подвал,Основной1,Разделитель"+СокрЛП(ВидРазделителя));
	
КонецПроцедуры //УправлениеДиалогом()   

//******************************************************************************
// Название: РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Найтройка"	
//
// Описание:  функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//   помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции //РасшифровкаОбновить

//******************************************************************************
// Название: Сформировать(ЗакрытьЭкран=0)
// 
// Параметры:
//  ЗакрытьЭкран - флаг того, что после формирования отчета надо закрыть экран	
//
// Возвращаемое значение:
//  Нет
// 
// Описание:
//    запускает отчет
//
Процедура Сформировать(ЗакрытьЭкран=0)    
	Перем СуммаПоДок, СписокПрихода, СписокРасхода, НужноВключатьПеремещения;         
	
	Перем Запрос, ТекстЗапроса;
	
	// Перемещения нужно включать, только если выбран фильтр по фирме или по МОЛ-у
	НужноВключатьПеремещения = 0;
	
	Если глПроверкаДаты(ДатаНачала,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидРазделителя = 1 Тогда
		Если ПустоеЗначение(ВыбРазделитель1) = 1 Тогда
			Предупреждение("Не выбрана собственная фирма", 60);
			Возврат;
		КонецЕсли;
	ИначеЕсли ВидРазделителя = 2 Тогда
		Если ПустоеЗначение(ВыбРазделитель2) = 1 Тогда
			Предупреждение("Не выбрано собственное юридическое лицо", 60);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Т) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;      
	               
   	Т.ИсходнаяТаблица( "ТОРГ-29" );
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ТоварныйОтчет");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала"		, ДатаНачала);
    Расшифровка.Установить("ДатаКонца"		, ДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1", ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2", ВыбРазделитель2);
	Расшифровка.Установить("ВидРазделителя"	, ВидРазделителя);
	Расшифровка.Установить("ВыбМол"			, ВыбМол);
	
	Если ПустоеЗначение(ВыбМОЛ) = 0 Тогда
		// МОЛ выбран
		УсловиеДляТовара	= "";
		УсловиеДляТары		= " Когда ((СтатусПартии = глСП.Т_Тара) ИЛИ (СтатусПартии = глСП.М_Тара))";
	Иначе
		// МОЛ не выбран. Не учитываем в приходе и расходе внутреннее перемещения
		УсловиеДляТовара	= " Когда (КодОперации <> глКО.Перемещение)";
		УсловиеДляТары		= " Когда (((СтатусПартии = глСП.Т_Тара) ИЛИ (СтатусПартии = глСП.М_Тара)) И (КодОперации <> глКО.Перемещение))";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ПЕРИОД С ДатаНачала По ДатаКонца;
	|
	|Докум        = Регистр.ПартииНаличие.ТекущийДокумент;
	|Фирма        = Регистр.ПартииНаличие.Фирма;
	|МОЛ          = Регистр.ПартииНаличие.МОЛ;
	|УпрАналит    = Регистр.ПартииНаличие.Фирма.УпрАналитика;
	|ЮрЛицо       = Регистр.ПартииНаличие.Фирма.ЮрЛицо;
	|СтатусПартии = Регистр.ПартииНаличие.СтатусПартии;
	|Стоимость    = Регистр.ПартииНаличие.СуммаБезНДС; 
	|КодОперации  = Регистр.ПартииНаличие.КодОперации;
	|
	|Группировка Докум;
	|
	|Функция НачСтоимостьВсего = НачОст(Стоимость);
	|Функция НачСтоимостьТара  = НачОст(Стоимость) 
	|        Когда ((СтатусПартии = глСП.Т_Тара) ИЛИ (СтатусПартии = глСП.М_Тара));
	|Функция ПрихСтоимостьВсего = Приход(Стоимость)" + УсловиеДляТовара + ";
	|Функция ПрихСтоимостьТара  = Приход(Стоимость)" + УсловиеДляТары   + ";
	|Функция РасхСтоимостьВсего = Расход(Стоимость)" + УсловиеДляТовара + ";
	|Функция РасхСтоимостьТара  = Расход(Стоимость)" + УсловиеДляТары   + ";
	|Функция КонСтоимостьВсего = КонОст(Стоимость);
	|Функция КонСтоимостьТара  = КонОст(Стоимость) 
	|        Когда ((СтатусПартии = глСП.Т_Тара) ИЛИ (СтатусПартии = глСП.М_Тара));
	|";

	НетОш = 1; // нет ошибок при наложении фильтров
	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,);
		ВремЮрЛицо = ВыбРазделитель1.ЮрЛицо;
		НаимФирмы = СокрЛП(ВремЮрЛицо.ПолнНаименование);
		ФирмаОКПО = ВремЮрЛицо.ОКПО;
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,);
		НаимФирмы = СокрЛП(ВыбРазделитель2.ПолнНаименование);
		ФирмаОКПО = ВыбРазделитель2.ОКПО;
	КонецЕсли;
	
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "Мол", ВыбМОЛ,"ВыбМОЛ",ТекстЗапроса,);
	
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;

	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(ТекстЗапроса)=0 тогда
		Предупреждение("Запрос по Партиям не выполнился!!!", 60);
		Возврат;
	КонецЕсли;
	
 	глЧислоСтрок = 0;
	//  Создание Таблицы для выходного отчета
	Т.ВывестиСекцию("Кнопки");
	Т.ВывестиСекцию("Заголовок");
	Т.ВывестиСекцию("Шапка");
	
	НачПовт = Т.ВысотаСекции("Кнопки") + Т.ВысотаСекции("Заголовок");
    КонПовт = НачПовт + Т.ВысотаСекции("Шапка");
	
	Т.ПовторятьПриПечатиСТроки(НачПовт + 1, КонПовт);
	
	глОживить(5);
	Ном=0;

	СписокПрихода = СоздатьОбъект("ТаблицаЗначений");
	СписокПрихода.НоваяКолонка("Докум");
	СписокПрихода.НоваяКолонка("СуммаТовара");
	СписокПрихода.НоваяКолонка("СуммаТары");
	СписокПрихода.Выгрузить(СписокРасхода);

	Т.ВывестиСекцию("ОстатокНачала"); 
	
	Пока Запрос.Группировка("Докум") = 1 Цикл
	    
		Если Запрос.ПрихСтоимостьВсего <> 0 Тогда
			СписокПрихода.НоваяСтрока();
			СписокПрихода.Докум = Запрос.Докум;
			СписокПрихода.СуммаТары   = Запрос.ПрихСтоимостьТара;
			СписокПрихода.СуммаТовара = Запрос.ПрихСтоимостьВсего - Запрос.ПрихСтоимостьТара;
		КонецЕсли;
		
		Если Запрос.РасхСтоимостьВсего <> 0 Тогда
			СписокРасхода.НоваяСтрока();
			СписокРасхода.Докум = Запрос.Докум;
			СписокРасхода.СуммаТары   = Запрос.РасхСтоимостьТара;
			СписокРасхода.СуммаТовара = Запрос.РасхСтоимостьВсего - Запрос.РасхСтоимостьТара;
		КонецЕсли;
			
	КонецЦикла;

	Т.ВывестиСекцию("Приход");
	глОживить(3);
	
	СписокПрихода.ВыбратьСтроки();
	Пока СписокПрихода.ПолучитьСтроку() = 1 Цикл
		Докум       = СписокПрихода.Докум;
		СуммаТовара = СписокПрихода.СуммаТовара;
		СуммаТары   = СписокПрихода.СуммаТары;
		
		Если глЕстьРеквизитШапки("НомерДокВходящий", Докум.Вид()) = 1 Тогда
		    НомерДокумента = Докум.НомерДокВходящий;
		Иначе
			НомерДокумента = Докум.НомерДок;
		КонецЕсли;
		
		Если глЕстьРеквизитШапки("ДатаДокВходящий", Докум.Вид()) = 1 Тогда
		    ДатаДокумента = Докум.ДатаДокВходящий;
		Иначе
			ДатаДокумента = Докум.ДатаДок;
		КонецЕсли;

		Ном   = Ном + 1;
		Т.ВывестиСекцию("Строка");
		глОживить(1);
	КонецЦикла;

	// итого приход
	Т.ВывестиСекцию("ИтогоПриход");
	глОживить(1);

	// итого приход + остаток
	Т.ВывестиСекцию("ВсегоПриход");
	глОживить(1);
	
	// расходы
	
	Т.НоваяСтраница();
	//Т.ВывестиСекцию("Шапка");
	Т.ВывестиСекцию("Расход");
	глОживить(1);

	СписокРасхода.ВыбратьСтроки();
	Пока СписокРасхода.ПолучитьСтроку() = 1 Цикл
		Докум       = СписокРасхода.Докум;
		СуммаТовара = СписокРасхода.СуммаТовара;
		СуммаТары   = СписокРасхода.СуммаТары;
		
		НомерДокумента = Докум.НомерДок;
		ДатаДокумента = Докум.ДатаДок;
		
		Ном   = Ном + 1;
		Т.ВывестиСекцию("Строка");
		глОживить(1);
	КонецЦикла;

	Т.ВывестиСекцию("ИтогоРасход");
	глОживить(1);

	Т.ВывестиСекцию("ОстатокКонец");
	глОживить(1);

	Т.ВывестиСекцию("Подвал");
	глОживить(4);
	//Вызов выходного отчета в окно просмотра и редактирования.
	Т.Опции(0, 0, 2, 0, "ТоварныйОтчет", "ТоварныйОтчет");
	//Т.Защита(Константа.ФлагЗащитыТаблиц);
	Т.ТолькоПросмотр(1);
	Т.ОбластьПечати(2);
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Т.Защита(1);
	КонецЕсли;
	Т.Показать("Товарный отчет № " + Номер, "");

	Сформирован    = 1;
	Номер          = Номер + 1;
	ТекНомерОтчета = Номер;     
	
	Если (Обновить     = 2)
	 или (ЗакрытьЭкран = 1)
	Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

//******************************************************************************
// Предопределенная процедура
//
Процедура ВводНового()
	Номер = ТекНомерОтчета;
КонецПроцедуры  // ВводНового()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии(ФлагЧтенияНастройки)
	
	Если ФлагЧтенияНастройки = 0 Тогда
	    ДатаНачала      = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
		Если ПустоеЗначение(ДатаНачала) = 1 Тогда
			ДатаНачала      = НачМесяца(ДатаКонца);    
		КонецЕсли;
	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВидРазделителя	= глРасшифровка.Получить("ВидРазделителя");
		ВыбРазделитель1	= глРасшифровка.Получить("ВыбРазделитель1");
		ВыбРазделитель2	= глРасшифровка.Получить("ВыбРазделитель2");
		ВыбМол			= глРасшифровка.Получить("ВыбМол");
		
		Если Обновить <> 0 Тогда
			Т = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		
		Обновить = 0;
	КонецЕсли;
	
	Номер	= ВосстановитьЗначение("ТоварныйОтчет_Номер");
	
	Если (ФлагЧтенияНастройки = 0)
	 или (Обновить = 0) 
	Тогда
		Номер          = 1 + Номер;
		ТекНомерОтчета = Номер;
	КонецЕсли;
	
	Форма.Обновить(1);
	Сформирован = 0;
	
	Если ПустоеЗначение(ВыбРазделитель1) = 1 Тогда
		// установим фирму по умолчанию
		ВыбРазделитель1 = глЗначениеПоУмолчанию("ОсновнаяФирма");
	КонецЕсли;
	
	Если ПустоеЗначение(ВыбРазделитель2) = 1 Тогда
		// установим собственное юр.лицо по умолчанию
		ВыбРазделитель2 = ВыбРазделитель1.ЮрЛицо;
	КонецЕсли;
	
	Если ВидРазделителя = 0 Тогда
	    ВидРазделителя = 1;
	КонецЕсли;
	УправлениеДиалогом();
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
Процедура ПриЗакрытии()
	
	Если Сформирован = 1 Тогда
		СохранитьЗначение("ТоварныйОтчет_Номер", Номер);
	КонецЕсли;
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
ДатаКонца  = ПолучитьДатуТА();
