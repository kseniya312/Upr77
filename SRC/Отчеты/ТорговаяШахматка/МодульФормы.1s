////////////////////////////////////////////////////////////////////////////////
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
//                  
Перем ТекСтрокаВТаблице; // текущая строка в таблице значений  МФ

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 
Перем БылЗаданМФ; // применяем для ускорения расчетов в создании расшифровок
                        
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ      
//******************************************************************************
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

//******************************************************************************
// ПерерисовкаНазванийЗакладок
//
// Параметры:
// 	Нет
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Функция ПерерисовкаНазванийЗакладок()      
	
	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
	
КонецФункции // ПерерисовкаНазванийЗакладок	

//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда           
		Форма.ИспользоватьСлой("Шапка,Подвал,Основной1,Основной2,Разделитель"+СокрЛП(ВидРазделителя));
	Иначе     
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;
	
КонецПроцедуры //УправлениеДиалогом()   

//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции //РасшифровкаОбновить()                                            

//******************************************************************************
// ИмяПеременной(ПозицияПереключателя)
//
// Параметры:
//  ПозицияПереключателя - число от 1 до 6
//
// Возвращаемое значение:
//  Строка (название переменной группировки запроса)
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  По значению переключателя ("По горизонтали" и "По вертикали")
//  определяет название переменной группировки
//
Функция ИмяПеременной(ПозицияПереключателя)
	
	Если  ПозицияПереключателя = 1 Тогда
		Возврат "Номенклатура";
	ИначеЕсли ПозицияПереключателя = 2 Тогда
		Возврат "СвойствоТМЦ";
	ИначеЕсли ПозицияПереключателя = 3 Тогда
		Возврат "Поставщик";
	ИначеЕсли ПозицияПереключателя = 4 Тогда
		Возврат "СвойствоПост";
	ИначеЕсли ПозицияПереключателя = 5 Тогда
		Возврат "Покупатель";
	ИначеЕсли ПозицияПереключателя = 6 Тогда
		Возврат "СвойствоПок";
	Иначе
		Возврат "Номенклатура";
	КонецЕсли;
	
КонецФункции // ИмяПеременной()       

//******************************************************************************
// НазваниеАтрибута(ПозицияПереключателя)
//
// Параметры:
//  ПозицияПереключателя - число от 1 до 6
//
// Возвращаемое значение:
//  Строка ("пользовательское" название атрибута группировки запроса)
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  По значению переключателя ("По горизонтали" и "По вертикали")
//  определяет название группировки
//
Функция НазваниеАтрибута(ПозицияПереключателя)
	
	Если  ПозицияПереключателя = 1 Тогда
		Возврат "ТМЦ";
	ИначеЕсли ПозицияПереключателя = 2 Тогда
		Возврат "Св-во ТМЦ";
	ИначеЕсли ПозицияПереключателя = 3 Тогда
		Возврат "Поставщик";
	ИначеЕсли ПозицияПереключателя = 4 Тогда
		Возврат "Св-во поставщика";
	ИначеЕсли ПозицияПереключателя = 5 Тогда
		Возврат "Покупатель";
	ИначеЕсли ПозицияПереключателя = 6 Тогда
		Возврат "Св-во покупателя";
	Иначе
		Возврат "ТМЦ";
	КонецЕсли;
	
КонецФункции // НазваниеАтрибута()       

//******************************************************************************
// СоздатьРасшифровку         // понять как со свойством ТМЦ быть в расшифровке
//
// Параметры:
//  Расшифровка - список значений, который следует заполнить
//	АтрибутГоризонтали                                      
//	АтрибутВертикали
//
// Возвращаемое значение:
// 	Нет
//
// Описание:
//    формирует расшифровку для детализации чисел в отчете
//
Процедура СоздатьРасшифровку(Расшифровка, НазвАтрибута1, Атрибут1, НазвАтрибута2, Атрибут2)   
	
	Перем ПустойСписок; //пустой список значений
	                                    
	Расшифровка = СоздатьОбъект("СписокЗначений");
	
	Если (ПустоеЗначение(Атрибут1)=1) или (ПустоеЗначение(Атрибут2)=1) Тогда
		Расшифровка = "Расшифровка не доступна!";
		Возврат;
	КонецЕсли;
	
	// создаем стандартную расшифровку отчета "Анализ продаж"
	Группировки = СоздатьОбъект ("СписокЗначений");
	Группировки.ДобавитьЗначение("Фирма", 		"Фирма");
	Группировки.ДобавитьЗначение("Покупатель", 	"Покупатель");
	Группировки.ДобавитьЗначение("СвойствоПок", "Свойство покупателя");
	Группировки.ДобавитьЗначение("Поставщик", 	"Поставщик");           
	Группировки.ДобавитьЗначение("СвойствоПост", "Свойство поставщика");
	Группировки.ДобавитьЗначение("Номенклатура", 		"Номенклатура");
	Группировки.ДобавитьЗначение("СвойствоНоменклатуры", "Свойство номенклатуры");
	Группировки.ДобавитьЗначение("Документ", 	"Документы движения");
		
	Группировки. Пометка(8,1);
	
	Расшифровка.Установить("Отчет", "АнализПродаж");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1",ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2",ВыбРазделитель2);
	Расшифровка.Установить("ВыбРазделитель3",ВыбРазделитель3);
	Расшифровка.Установить("ВидРазделителя",ВидРазделителя);

	Расшифровка.Установить("ВыбТМЦ",		ВыбТМЦ);
	Расшифровка.Установить("ВыбПокупатель",	ВыбПокупатель);
	Расшифровка.Установить("ВыбПоставщик",	ВыбПоставщик);
	Расшифровка.Установить("Группировки",	Группировки);
	Расшифровка.Установить("ВидЕдиницы",	1);    
	
	Если БылЗаданМФ = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ",	ТаблицаМФ);
	КонецЕсли;
	
	Если  НазвАтрибута1 = "Номенклатура" Тогда            
		Расшифровка.Установить("ВыбТМЦ",Атрибут1);
		
	ИначеЕсли  НазвАтрибута1 = "СвойствоТМЦ" Тогда                  
		Расшифровка.Установить("ВыбСвойствоТМЦ",Атрибут1);
		
	ИначеЕсли  НазвАтрибута1 = "Поставщик" Тогда          
		Расшифровка.Установить("ВыбПоставщик",Атрибут1);
		
	ИначеЕсли  НазвАтрибута1 = "СвойствоПост" Тогда       
		Расшифровка.Установить("ВыбСвойствоПоставщика",Атрибут1);
		
	ИначеЕсли  НазвАтрибута1 = "Покупатель" Тогда         
		Расшифровка.Установить("ВыбПокупатель",Атрибут1);
		
	ИначеЕсли  НазвАтрибута1 = "СвойствоПок" Тогда        
		Расшифровка.Установить("ВыбСвойствоПокупателя",Атрибут1);
		
	КонецЕсли;	
	
	Если  НазвАтрибута2 = "Номенклатура" Тогда            
		Расшифровка.Установить("ВыбТМЦ",Атрибут2);
		
	ИначеЕсли  НазвАтрибута2 = "СвойствоТМЦ" Тогда                  
		Расшифровка.Установить("ВыбСвойствоТМЦ",Атрибут2);
		
	ИначеЕсли  НазвАтрибута2 = "Поставщик" Тогда          
		Расшифровка.Установить("ВыбПоставщик",Атрибут2);
		
	ИначеЕсли  НазвАтрибута2 = "СвойствоПост" Тогда       
		Расшифровка.Установить("ВыбСвойствоПоставщика",Атрибут2);
		
	ИначеЕсли  НазвАтрибута2 = "Покупатель" Тогда         
		Расшифровка.Установить("ВыбПокупатель",Атрибут2);
		
	ИначеЕсли  НазвАтрибута2 = "СвойствоПок" Тогда        
		Расшифровка.Установить("ВыбСвойствоПокупателя",Атрибут2);
		
	КонецЕсли;	
	                                                   
КонецПроцедуры // создать расшифровку
        
//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог	
//
// Возвращаемое значение: НЕТ
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   запускает отчет
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;
	Перем АтрибутГоризонтали, АтрибутВертикали; // названия атрибутов запроса по горизонтали и вертикали
	
	Перем СписокКолонок;// список названий колонок отчета
	Перем РезТаблица; // главная таблица значений с результатами
	
	Перем СчетчикСтолбцов; 
	Перем КлючСтроки;
	Перем РасчПоказатель; //расчитанный показатель анализа
	
	Перем ИтогВсего; //общий итог показателя  (для расчета %)
	Перем ИтогСтроки; //итог показателя по строке (для расчета %)
	Перем ИтогСтолбца; //итог показателя по столбцу (для расчета %)
	
	СписокКолонок	= СоздатьОбъект("СписокЗначений");
	РезТаблица 		= СоздатьОбъект("ТаблицаЗначений");
	
	Если глПроверкаДаты(ДатаНачала,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;      
	                                        
	Таб.ИсходнаяТаблица( "ТорговаяШахматка" );
	    
	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	Если глПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонца,Последовательность.ОсновнаяПоследовательность)=0 Тогда
		Возврат;
	КонецЕсли;	  
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ТорговаяШахматка");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1",ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2",ВыбРазделитель2);
	Расшифровка.Установить("ВыбРазделитель3",ВыбРазделитель3);
	Расшифровка.Установить("ВидРазделителя",ВидРазделителя);
    
	Расшифровка.Установить("ВыбТМЦ",		ВыбТМЦ);
	Расшифровка.Установить("ВыбПокупатель",	ВыбПокупатель);
	Расшифровка.Установить("ВыбПоставщик",	ВыбПоставщик);
	
	Расшифровка.Установить("ВыбГоризонталь",ВыбГоризонталь);
	Расшифровка.Установить("ВыбВертикаль",	ВыбВертикаль);
	                                                      
	Расшифровка.Установить("ЧтоАнализируем",ЧтоАнализируем.ТекущаяСтрока());
	Расшифровка.Установить("СортироватьСтроки",СортироватьСтроки);

	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были

	БылЗаданМФ = глМножественныйФильтрЗадан(ТаблицаМФ);
	// запомним МФ только если он задан
    Если  БылЗаданМФ = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ); 
	КонецЕсли;
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Фирма			= Регистр.Продажи.Фирма;
	|УпрАналитика	= Регистр.Продажи.Фирма.УпрАналитика;
	|ЮрЛицо 		= Регистр.Продажи.Фирма.ЮрЛицо;
	|Покупатель 	= Регистр.Продажи.Покупатель;
	|СвойствоПок 	= Регистр.Продажи.Покупатель.ОсновноеСвойство.ЗначениеСвойства;
	|Номенклатура	= Регистр.Продажи.Номенклатура;                
	|СвойствоТМЦ 	= Регистр.Продажи.Номенклатура.ОсновноеСвойство.ЗначениеСвойства;
	|Поставщик		= Регистр.Продажи.Поставщик;
	|СвойствоПост 	= Регистр.Продажи.Поставщик.ОсновноеСвойство.ЗначениеСвойства;";
	
	
	// Проверка на необходимость включения в запрос переменной "Автор"
	ЕстьАвтор = 0;
	ЕстьПроект = 0;                          
	НомСтроки = 0;
	НомКолонки = 0;
	
	// Сначала проверим, есть ли множественный фильтр по автору.
	Если ТаблицаМФ.НайтиЗначение("Автор", НомСтроки, НомКолонки) = 1 Тогда
		Если ТаблицаМФ.ПолучитьЗначение(НомСтроки, "ФлВкл") = 2  Тогда
		    ЕстьАвтор = 1;
		КонецЕсли;
	КонецЕсли;                                                  
	
	// Проверка на необходимость включения в запрос переменной "Проект"    
	НомСтроки = 0;
	НомКололонки = 0;
	Если ТаблицаМФ.НайтиЗначение("Проект", НомСтроки, НомКолонки) = 1 Тогда
		Если ТаблицаМФ.ПолучитьЗначение(НомСтроки, "ФлВкл") = 2  Тогда
		    ЕстьПроект = 1;
		КонецЕсли;
	КонецЕсли;            
	
	Если ЕстьАвтор=1 Тогда
		ТекстЗапроса = ТекстЗапроса +
		"
		|Автор      = Регистр.Продажи.ТекущийДокумент.Автор;";
	КонецЕсли;     
	
	Если ЕстьПроект=1 Тогда
		ТекстЗапроса = ТекстЗапроса +
		"
		|Проект  = Регистр.Продажи.ТекущийДокумент.Проект;";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"
	|Себест  = Регистр.Продажи.Себестоимость;
	|Функция СуммаСебест  = Сумма(Себест);
	|СебестВ = Регистр.Продажи.СебестоимостьВ;
	|Функция СуммаСебестВ = Сумма(СебестВ);";
	
	ТекстЗапроса = ТекстЗапроса +
	"
	|ПродСт  = Регистр.Продажи.ПродСтоимость;
	|Функция СуммаПродСт  = Сумма(ПродСт);
	|ПродСтВ = Регистр.Продажи.ПродСтоимостьВ;
	|Функция СуммаПродСтВ = Сумма(ПродСтВ);";
	                                             
	АтрибутГоризонтали 	= ИмяПеременной(ВыбГоризонталь);
	АтрибутВертикали 	= ИмяПеременной(ВыбВертикаль);
	ТекстЗапроса = ТекстЗапроса + "Группировка "+АтрибутГоризонтали+" без групп;";
	ТекстЗапроса = ТекстЗапроса + "Группировка "+АтрибутВертикали+" без групп;";
	
	Загол="";
	                  
	НетОш = 1; // нет ошибок при наложении фильтров
	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",ВыбРазделитель3,"ВыбРазделитель3",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
	КонецЕсли;
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Номенклатура",ВыбТМЦ,"ВыбТМЦ",ТекстЗапроса,Загол,"СвойстваНоменклатуры");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Поставщик",ВыбПоставщик,"ВыбПоставщик",ТекстЗапроса,Загол,"СвойстваКонтрагентов");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Покупатель",ВыбПокупатель,"ВыбПокупатель",ТекстЗапроса,Загол,"СвойстваКонтрагентов");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Автор",   ,        ,ТекстЗапроса,Загол,"Автор");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Проект",  ,        ,ТекстЗапроса,Загол,"Проект");
	
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПечЗаголовок = "Торговая шахматка ("+глДоллары.Наименование+")";
	                          
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;         
	
	глЧислоСтрок = 0;
	
 	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	// выводим шапку на каждой странице
	НачПовт = Таб.ВысотаСекции("Кнопки") + Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	
	РезТаблица.НоваяКолонка("КлючСтроки");
	
	// выводим шапку отчета и формируем колонки таблицы знач. с результатами
	Таб.ВывестиСекцию("ШапкаТаблицы|ПервыйСтолбец");
	Пока Запрос.Группировка(1)=1 цикл         
		
		ВремЗнач = Запрос.ПолучитьАтрибут(АтрибутГоризонтали);
		
		РезТаблица.НоваяКолонка(,"Число","15","2",ВремЗнач);
		СписокКолонок.ДобавитьЗначение(ВремЗнач);
		Расшифровка	= ВремЗнач;
		ПечСтолбец	= ?(ПустоеЗначение(ВремЗнач) = 1, глПредставлениеПустогоЗначения(""), ВремЗнач);
		Таб.ПрисоединитьСекцию("ШапкаТаблицы|Столбец");
	КонецЦикла;
	
	Таб.ПрисоединитьСекцию("ШапкаТаблицы|ИтогСтолбец");
	ВысотаШапки=Таб.ВысотаТаблицы();            
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 1, "ТорговаяШахматка", "ТорговаяШахматка"); // закрепим шапку
	глОживить(ВысотаШапки);            
	
	Состояние("Заполнение таблицы результатов...");
	
	// теперь заполняем таблицу результатов
	ИтогВсего=0;
	СчетчикСтолбцов=0;
	Запрос.ВНачалоВыборки();
	Пока Запрос.Группировка(1)=1 цикл 
		Состояние(Запрос.ПолучитьАтрибут(АтрибутГоризонтали));
		СчетчикСтолбцов=СчетчикСтолбцов+1;
		Пока Запрос.Группировка(2)=1 цикл
			КлючСтроки = Запрос.ПолучитьАтрибут(АтрибутВертикали); 
			НомСтр=0;                                                            
			// проверяем есть ли уже такая строка
			Если РезТаблица.НайтиЗначение(КлючСтроки,НомСтр,"КлючСтроки")=1 тогда
				РезТаблица.ПолучитьСтрокуПоНомеру(НомСтр);
			Иначе
				РезТаблица.НоваяСтрока();
				РезТаблица.КлючСтроки=КлючСтроки;
			КонецЕсли;        
			
			Если ЧтоАнализируем.ТекущаяСтрока()=1 тогда // выручка
				РасчПоказатель = Запрос.СуммаПродСт - Запрос.СуммаПродСтВ;
			Иначе     // прибыль
				РасчПоказатель = (Запрос.СуммаПродСт  - Запрос.СуммаСебест)
				               - (Запрос.СуммаПродСтВ - Запрос.СуммаСебестВ);
			КонецЕсли;
			//.. и запишем в таблицу результатов
			РезТаблица.УстановитьЗначение(РезТаблица.НомерСтроки,СчетчикСтолбцов+1,РасчПоказатель);
			ИтогВсего = ИтогВсего + РасчПоказатель;
		Конеццикла;
	КонецЦикла;                     
	
	Состояние("Расчет удельного веса строк ...");
	//расчет удельного веса каждой строки
	РезТаблица.НоваяКолонка("ВесСтроки","Число","7","3");
	РезТаблица.НоваяКолонка("ИтогСтроки","Число","17","2");
	РезТаблица.ВыбратьСтроки();
	Пока РезТаблица.ПолучитьСтроку()=1 цикл
		ИтогСтроки=0;
		Для Индекс=2 по РезТаблица.КоличествоКолонок()-1 цикл
			ИтогСтроки=ИтогСтроки+РезТаблица.ПолучитьЗначение(РезТаблица.НомерСтроки,Индекс);
		Конеццикла;                                
		РезТаблица.ВесСтроки=?(ИтогВсего=0,0,Окр(ИтогСтроки/ИтогВсего*100,3));
		РезТаблица.ИтогСтроки=ИтогСтроки;
	Конеццикла;  
	
	//Сортировка по весу строки
	Если СортироватьСтроки=1 тогда         
		Состояние("Сортировка...");
		РезТаблица.Сортировать("ВесСтроки-"); // сортируем по весу по убыванию
	КонецЕсли;
	
	// вывод результатов
	РезТаблица.ВыбратьСтроки();
	Пока РезТаблица.ПолучитьСтроку()=1 цикл
		
		Расшифровка		= РезТаблица.КлючСтроки;
		
		ПечКлючСтроки	= ?(ПустоеЗначение(РезТаблица.КлючСтроки) = 1, "<не выбран>" , РезТаблица.КлючСтроки);
		
		Таб.ВывестиСекцию("Строка|ПервыйСтолбец");
		Для Индекс=2 по РезТаблица.КоличествоКолонок()-2 цикл
			
			ПечПоказатель=глФРМ(РезТаблица.ПолучитьЗначение(РезТаблица.НомерСтроки,Индекс)); 
			// расшифровка текущего числа в отчете (при необходимости)
			Расшифровка="";                 
			Если (ПустоеЗначение(ПечПоказатель)=0) Тогда
				ЗначСтолбца=СписокКолонок.ПолучитьЗначение(Индекс-1);
				СоздатьРасшифровку(Расшифровка,АтрибутГоризонтали,ЗначСтолбца,АтрибутВертикали,РезТаблица.КлючСтроки);
			КонецЕсли;
			Таб.ПрисоединитьСекцию("Строка|Столбец");
			
		Конеццикла;                                
		
		ПечИтогЗнач	 = глФРМ(РезТаблица.ИтогСтроки);
		ПечВесСтроки = Формат (РезТаблица.ВесСтроки,"Ч07.3");
		Таб.ПрисоединитьСекцию("Строка|ИтогСтолбец");
		глОживить(1);
	Конеццикла;  
	        
	// выводим подвал
	Таб.ВывестиСекцию("Подвал|ПервыйСтолбец");	
	Для Индекс=2 по РезТаблица.КоличествоКолонок()-2 цикл
		
		ИтогСтолбца		= РезТаблица.Итог(Индекс);    
		ПечИтогСтолбца	= глФРМ(ИтогСтолбца);
		ПечВесСтолбца	=?(ИтогВсего=0,0,Окр(ИтогСтолбца/ИтогВсего*100,3));
		Таб.ПрисоединитьСекцию("Подвал|Столбец");
		
	Конеццикла;                     
	
	ПечИтогИтог=глФРМ(ИтогВсего);
	Таб.ПрисоединитьСекцию("Подвал|ИтогСтолбец");         
	
	Таб.ОбластьПечати(3);
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Торговая шахматка", "");
	
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	//                  тип          вид           переменная  название
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Номенклатура", "Номенклатура",  "По номенклатуре");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты", "Поставщик",  "По поставщикам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты", "Покупатель",  "По покупателям");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Номенклатура",  "По свойствам номенклатуры");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Поставщик",  "По свойствам поставщиков");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Покупатель",  "По свойствам покупателей");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Пользователи", "Автор",  "По авторам документов");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Проекты", "Проект",  "По проектам");               
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Фирмы", "Фирма",  "По фирмам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","СвоиЮрЛица", "ЮрЛицо",  "По юр. лицам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","УпрАналитика", "УпрАналитика",  "По упр. аналитике");

	Если ФлагВосстановленияНастройки = 0 Тогда     
		// заполнить по умолчанию	
		ВыбВертикаль   = 1; // товар
		ВыбГоризонталь = 5; // покупатель
		
		ВидРазделителя = 1;

	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВидРазделителя	= глРасшифровка.Получить("ВидРазделителя");
		ВыбРазделитель1	= глРасшифровка.Получить("ВыбРазделитель1");
		ВыбРазделитель2	= глРасшифровка.Получить("ВыбРазделитель2");
		ВыбРазделитель3	= глРасшифровка.Получить("ВыбРазделитель3");
		
		ВыбПокупатель 	= глРасшифровка.Получить("ВыбПокупатель");
		ВыбПоставщик 	= глРасшифровка.Получить("ВыбПоставщик");
		ВыбТМЦ 			= глРасшифровка.Получить("ВыбТМЦ");
		
		ВыбГоризонталь 	= глРасшифровка.Получить("ВыбГоризонталь");
		ВыбВертикаль 	= глРасшифровка.Получить("ВыбВертикаль");
		
		ЧтоАнализируем.ТекущаяСтрока(глРасшифровка.Получить("ЧтоАнализируем"));
		СортироватьСтроки = глРасшифровка.Получить("СортироватьСтроки");

		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;            
	
	ПерерисовкаНазванийЗакладок();

	ТаблицаМФ.ВидимостьКолонки("Тип",0);
	ТаблицаМФ.ВидимостьКолонки("Вид",0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");

	УправлениеДиалогом();
	
КонецПроцедуры		// ПриОткрытии()       

//******************************************************************************
// Предопределенная процедура.
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	ПерерисовкаНазванийЗакладок();
	УправлениеДиалогом();       
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	
КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореЗакладки(Номер,Значение)	
	// закладки
    Если Номер=1 Тогда
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	КонецЕсли;      
	УправлениеДиалогом();
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки
                                       
//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(Значение)  
	
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ЧтоАнализируем.УдалитьВсе();
ЧтоАнализируем.ДобавитьЗначение("выручка по отгрузке");
ЧтоАнализируем.ДобавитьЗначение("прибыль по отгрузке");
ЧтоАнализируем.ТекущаяСтрока(1);

ДатаКонца	= ПолучитьДатуТА();
ДатаНачала      = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
Если ПустоеЗначение(ДатаНачала) = 1 Тогда
	ДатаНачала      = НачМесяца(ДатаКонца);    
КонецЕсли;
    
// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
                                                                                
ТекСтрокаВТаблице="";

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);