////////////////////////////////////////////////////////////////////////////////
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ                          
//
// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 
Перем ДокПодч;
Перем СтарыйВидОтчета;        

// Таблица соответствия между розничными реализациями и ПКО по поступлению выручки.
// Служит для вывода номера кассовой ленты.
Перем СФРеализацияРозница;               

Перем Трассировка;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//                                                                                 
//******************************************************************************
// СообщениеТрассировки(ТекстСообщения, ТипСообщения = -1, Документ)
//
// Параметры: 
//  ТекстСообщения
//  ТипСообщения
//  Документ
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Выводит сообщение в таблицу.
//
Процедура СообщениеТрассировки(ТекстСообщения, ТипСообщения = -1, Документ)
	Если ТипЗначенияСтр(Трассировка) <> "СписокЗначений" Тогда
	    Трассировка = глСоздатьТрассировку("Вывод книги продаж по " + ВыбЮрЛицо);
	КонецЕсли;	
	глСообщениеТрассировки(Трассировка, ТекстСообщения, ТипСообщения,,,,,,,,,,Документ,Документ);
КонецПроцедуры // СообщениеТрассировки()()

//******************************************************************************
// ОбработкаДляРозницы(КнПрод)
//
// Параметры: 
//  КнПрод - таблица значений, содержащая данные книги продаж
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Обрабатывает записи книги продаж, сделанные по приходу розничной выручки, объединяя
// сторнирующие записи с обычными, так чтобы по каждому ПКО было не более одной записи.
//
Процедура ОбработкаДляРозницы(КнПрод)      
	
	СФРеализацияРозница = СоздатьОбъект("ТаблицаЗначений");
	СФРеализацияРозница.НоваяКолонка("Реализация", "Документ.РеализацияРозница");
	СФРеализацияРозница.НоваяКолонка("ПКО", "Документ.ПКО");
	
	// Создадим временную таблицу
	ВремТаб = СоздатьОбъект("ТаблицаЗначений");
	КнПрод.Выгрузить(ВремТаб);  
	
	// Удалим из временной таблицы все записи, не относящиеся к розничной выручке
	ВремТаб.ВыбратьСтроки();                                             
	Пока ВремТаб.ПолучитьСтроку() = 1  Цикл
		Пока ТипЗначенияСтр(ВремТаб.КредДокумент) = "Документ" Цикл
		    Если (ВремТаб.КредДокумент.Вид() <> "ПКО") или (ВремТаб.ВидДолга <> глВД.РозничнаяВыручка) Тогда      
		        ВремТаб.УдалитьСтроку();                                                                    
			Иначе
				Прервать;
	    	КонецЕсли;
		КонецЦикла;                                                                                         
	КонецЦикла;	
	
	
	// Теперь удалим все записи относящиеся к розничной выручке из книги продаж
	КнПрод.ВыбратьСтроки();                                             
	Пока КнПрод.ПолучитьСтроку() = 1  Цикл
		Пока ТипЗначенияСтр(КнПрод.КредДокумент) = "Документ" Цикл
		    Если (КнПрод.ТекДокумент.Вид() = "ПКО") и ((КнПрод.ВидДолга = глВД.РозничнаяВыручка) или (КнПрод.ВидДолга = глВД.ДолгЗаТоварыВРознице)
			 или (КнПрод.ВидДолга = глВД.ДолгЗаПродукциюВРознице) или (КнПрод.ВидДолга = глВД.ДолгЗаУслугиВРознице)) Тогда
			  Если КнПрод.КредДокумент.Вид() = "РеализацияРозница" Тогда
			      СФРеализацияРозница.НоваяСтрока();
				  СФРеализацияРозница.ПКО = КнПрод.ТекДокумент;
				  СФРеализацияРозница.Реализация = КнПрод.КредДокумент; 
			  КонецЕсли;
		  	КонецЕсли;
		    Если (КнПрод.КредДокумент.Вид() = "ПКО") и (КнПрод.ВидДолга = глВД.РозничнаяВыручка) Тогда
			  Если КнПрод.КредДокумент.Вид() = "РеализацияРозница" Тогда
			      СФРеализацияРозница.НоваяСтрока();
				  СФРеализацияРозница.ПКО = КнПрод.КредДокумент;
				  СФРеализацияРозница.Реализация = КнПрод.ДокументОплаты;
			  КонецЕсли;
	  	      КнПрод.УдалитьСтроку();        
		  	Иначе
		      	Прервать;
	  		КонецЕсли;
		КонецЦикла;
	КонецЦикла;	                                      

	// Если таблица пуста, то делать больше нечего
	Если ВремТаб.КоличествоСтрок() = 0 Тогда
	    Возврат;
	КонецЕсли;  
	
    // Свернем временную таблицу 
	ВремТаб.Свернуть("КредДокумент, ТипЗаписи, КредКонтрагент", "СуммаРуб20, СуммаНДС20, СуммаНП20, СуммаРуб10, СуммаНДС10, СуммаНП10, СуммаРуб0, СуммаНДС0, СуммаНП0");
	
	// Теперь проверим не осталось ли у нас непогашенной розничной выручки без указания ставки НДС   
	// Если осталась, то мы не сможем правильно сформировать книгу.
	ВремТаб.ВыбратьСтроки();     
	Пока ВремТаб.ПолучитьСтроку() = 1 Цикл
	    Пока ТипЗначенияСтр(ВремТаб.КредДокумент) = "Документ" Цикл
			Если (ВремТаб.КредДокумент.Вид() = "ПКО") Тогда
				Если (ВремТаб.СуммаРуб20 = 0) 
				   и (ВремТаб.СуммаНДС20 = 0) 
			   	   и (ВремТаб.СуммаНП20  = 0) 
				   и (ВремТаб.СуммаРуб10 = 0) 
				   и (ВремТаб.СуммаНДС10 = 0) 
				   и (ВремТаб.СуммаНП10  = 0) 
				   и (ВремТаб.СуммаРуб0  = 0) 
				   и (ВремТаб.СуммаНДС0  = 0) 
				   и (ВремТаб.СуммаНП0   = 0) Тогда
						Прервать;
				КонецЕсли;
				Если ПустоеЗначение(ВремТаб.КредДокумент.СтавкаНДС) = 1 Тогда
					СообщениеТрассировки("По " + глПредставлениеДокумента(ВремТаб.КредДокумент) + 
					" необходимо либо указать ставку НДС," + РазделительСтрок + 
					"либо провести Реализацию (продажу по принятой выручке)!"
					,,ВремТаб.КредДокумент);
					ВремТаб.УдалитьСтроку();     
				Иначе
					Прервать;
				КонецЕсли;                                 
			КонецЕсли;
		КонецЦикла;  
	КонецЦикла;     
	
	// Теперь добавим преобразованные розничные записи в книгу продаж
	ВремТаб.ВыбратьСтроки();
	Пока ВремТаб.ПолучитьСтроку()=1 Цикл 
		Если (ВремТаб.СуммаРуб20 = 0) 
		   и (ВремТаб.СуммаНДС20 = 0) 
	   	   и (ВремТаб.СуммаНП20  = 0) 
		   и (ВремТаб.СуммаРуб10 = 0) 
		   и (ВремТаб.СуммаНДС10 = 0) 
		   и (ВремТаб.СуммаНП10  = 0) 
		   и (ВремТаб.СуммаРуб0  = 0) 
		   и (ВремТаб.СуммаНДС0  = 0) 
		   и (ВремТаб.СуммаНП0   = 0) Тогда
			Продолжить;
		КонецЕсли;
		КнПрод.НоваяСтрока();
		КнПрод.КредДокумент   = ВремТаб.КредДокумент;                   
		КнПрод.ДокументОплаты = ВремТаб.КредДокумент;
		КнПрод.ТипЗаписи 	  = ВремТаб.ТипЗаписи;
		КнПрод.КредКонтрагент = ВремТаб.КредКонтрагент;
		КнПрод.СуммаРуб20     = ВремТаб.СуммаРуб20;
		КнПрод.СуммаНДС20	  = ВремТаб.СуммаНДС20;
		КнПрод.СуммаНП20	  = ВремТаб.СуммаНП20;
		КнПрод.СуммаРуб10  	  = ВремТаб.СуммаРуб10;
		КнПрод.СуммаНДС10	  = ВремТаб.СуммаНДС10;
		КнПрод.СуммаНП10 	  = ВремТаб.СуммаНП10;
		КнПрод.СуммаРуб0	  = ВремТаб.СуммаРуб0;
		КнПрод.СуммаНДС0	  = ВремТаб.СуммаНДС0;
		КнПрод.СуммаНП0		  = ВремТаб.СуммаНП0;    
	КонецЦикла; 
	КнПрод.Сортировать("КредДокумент", 1);
	
КонецПроцедуры // ОбработкаДляРозницы()                                          



//******************************************************************************
// НайтиСФДокумента(ДокПоставки, ВидСФ = "СчетФактураВыданный")
//
// Параметры:
//  ДокПоставки	- документ, для которого надо найти СФ
//  ВидСФ       - вид СФ, по умолчанию "СчетФактураВыданный" 
//
// Возвращаемое значение:
//  СФ 
//
// Описание:
//  У документа производится поиск счета фактуры. Если она есть то возвращаем.
//
Функция НайтиСФДокумента(ДокПоставки, ВидСФ = "СчетФактураВыданный")
	                                
	Если (ДокПоставки.Вид() = "ОтчетККМ") Тогда 
		Возврат ДокПоставки;                                                
	ИначеЕсли (ДокПоставки.Вид() = "РеализацияРозница") Тогда
		Если ДокПоставки.ВидОперации = Перечисление.ВидыОперацийРеализацияРозница.ОтчетПоПродажам Тогда
			Возврат ДокПоставки;
		КонецЕсли;
	ИначеЕсли ДокПоставки.Вид() = "ПКО" Тогда
		Если ДокПоставки.КодОперации = глКО.РозничнаяВыручка Тогда
		    Возврат ДокПоставки;
		КонецЕсли;
	ИначеЕсли ДокПоставки.Вид() = "ВозвратОтПокупателя"	Тогда
		Если ДокПоставки.ДокОснование.Выбран() = 1  Тогда
		    Возврат НайтиСФДокумента(ДокПоставки.ДокОснование)
		Иначе
			Возврат ДокПоставки;                                                                       
		КонецЕсли;   	
	ИначеЕсли ДокПоставки.Вид() = "ВозвратПоставщику" Тогда	
		Если ДокПоставки.ДокОснование.Выбран() = 1  Тогда
		    Возврат НайтиСФДокумента(ДокПоставки.ДокОснование, "СчетФактураПолученный")
		Иначе
			Возврат ДокПоставки;
		КонецЕсли;   	
		Возврат ДокПоставки;
	КонецЕсли;
	ДокПодч.ВыбратьПодчиненныеДокументы(,,ДокПоставки);
	Пока ДокПодч.ПолучитьДокумент()=1 Цикл
        Если Найти(ДокПодч.Вид(),ВидСФ)<>0 Тогда
		 	Если ДокПодч.Проведен()=1 Тогда
				Возврат ДокПодч.ТекущийДокумент();                        
			ИначеЕсли глЕстьРеквизитШапки("ФлагСвертки", ДокПоставки.Вид()) = 1 Тогда
				Если ДокПоставки.ФлагСвертки = 1 Тогда
					Возврат ДокПодч.ТекущийДокумент();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;   
	
	СообщениеТрассировки("На документ "+ДокПоставки+" не зарегистрирован счет - фактура!",, ДокПоставки);
	Возврат "";
	
КонецФункции // ПроверкаСФДокумента()

//******************************************************************************
// УправлениеДиалогом()
// Параметры:
//  нет
//
// Возвращаемое значение:
//  нет
//
// Вызывается из формул элементов диалога:
//  "СписокВидовОтчетов","ПоКлиентам","ОтборПоКлиентам"
//
// Описание: для управления формой диалога
Процедура УправлениеДиалогом()

	Если СтарыйВидОтчета <> СписокВидовОтчетов.ТекущаяСтрока() Тогда

		// Сброс флагов
		
		ПоКлиентам      = 0;
		ОтборПоКлиентам = 0;
		ИтогиПоТипам    = 0;

		СтарыйВидОтчета = СписокВидовОтчетов.ТекущаяСтрока();

	КонецЕсли;
	
	// управление флагами группировок
	Форма.ПоКлиентам.Доступность(?(СписокВидовОтчетов.ТекущаяСтрока()=1,1,0));
	Форма.ИтогиПоТипам.Доступность(?(СписокВидовОтчетов.ТекущаяСтрока()=1,1,0));

	Форма.ОтборПоКлиентам.Доступность(ПоКлиентам);
	ОтборПоКлиентам = ?(ПоКлиентам = 1,ОтборПоКлиентам,0);
	Форма.ВыбКонтрагент.Доступность(ОтборПоКлиентам);
    ВыбКонтрагент = ?(ОтборПоКлиентам = 1,ВыбКонтрагент,"");
	
КонецПроцедуры  // УправлениеДиалогом()

//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//   помещает значение параметра в список Расшифровка  и возвращает этот список
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции //РасшифровкаОбновить()                                            

//******************************************************************************
// ПроверкаАвансовыхСчетов(Режим="тест")
//
// Параметры:
//   Режим = "тест" - если только проверка
//   Режим = "коррекция" - если проверка и в случае необходимости исправление
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "ВыписатьСФНаАванс"
//
// Описание: просматриваем все "протянутые" авансы и проверяем наличие введенных
// на их основании с/ф на аванс

Процедура ПроверкаАвансовыхСчетов(ЗакрытьДиалог=0)

	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Фирма = Регистр.Покупатели.Фирма.ЮрЛицо.ТекущийЭлемент;
	|Контрагент = Регистр.Покупатели.Договор.Владелец.ТекущийЭлемент;
	|ДокАванса = Регистр.Покупатели.КредДокумент.ТекущийДокумент;
	|ВидДолга = Регистр.Покупатели.ВидДолга;
	|Остаток = Регистр.Покупатели.СуммаРуб;
	|Функция ОстАванса = КонОст(Остаток);
	|Группировка ДокАванса;
	|Условие(Фирма = ВыбЮрЛицо);
	|Условие(ВидДолга = Перечисление.ВидыДолга.Аванс);
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;
    
	ТабАвансов = СоздатьОбъект("ТаблицаЗначений");
	Запрос.Выгрузить(ТабАвансов,"Фирма,ДокАванса,Контрагент,ОстАванса",0);
	ТабАвансов.НоваяКолонка("СчФактАванс","Документ");
	
	ЕстьАвансы = 0;
	
	ТабАвансов.ВыбратьСтроки();
	Пока ТабАвансов.ПолучитьСтроку()=1 Цикл
		Если ТабАвансов.ДокАванса.Выбран()=1 Тогда            
			Если ТабАвансов.ОстАванса > 0 Тогда
			    Продолжить;
			КонецЕсли;
			
			//проверяем наличие подчиненных документов "СчетФактураВыданный", не помеченных на удаление
			ТабАвансов.СчФактАванс = глНайтиСчетФактуру(ТабАвансов.ДокАванса,"СчетФактураВыданный", 0);
			ЕстьАвансы = ?(ТабАвансов.СчФактАванс.Выбран()=0,1,ЕстьАвансы);
		КонецЕсли;    
	КонецЦикла;

	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "КнигаПродаж");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбЮрЛицо",     ВыбЮрЛицо);
	Расшифровка.Установить("ПоКлиентам",        ПоКлиентам);
	Расшифровка.Установить("ОтборПоКлиентам",   ОтборПоКлиентам);
	Расшифровка.Установить("ВыбКонтрагент",     ВыбКонтрагент);

	Расшифровка.Установить("СписокВидовОтчетов",СписокВидовОтчетов.ТекущаяСтрока());
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;                                                           
	
    Если ЕстьАвансы = 1 Тогда
		Таб.ИсходнаяТаблица("ПроверкаВыписки");
		Таб.ВывестиСекцию("Кнопки");
		Таб.ВывестиСекцию("Шапка");
	
		НомСтр = 1;
		ТабАвансов.ВыбратьСтроки();
		Пока ТабАвансов.ПолучитьСтроку()=1 Цикл
			Если (ТабАвансов.СчФактАванс.Выбран() = 0)
			   и (ТабАвансов.ДокАванса.Вид() <> "РКО") Тогда
				Если ТабАвансов.ОстАванса > 0 Тогда
				    Продолжить;
				КонецЕсли;
				Печнарушение = "Отсутствует счет-фактура на аванс!";
				Таб.ВывестиСекцию("Строка");
				НомСтр = НомСтр +1;
			КонецЕсли;
		КонецЦикла;
	
		Таб.ВывестиСекцию("Подвал");
	
		Таб.ОбластьПечати(2);
		Таб.Опции(0,0,2,0, "ПроверкаВыпискиСчетовФактур", "ПроверкаВыпискиСчетовФактур");
		Таб.ТолькоПросмотр(1);
		Таб.ОбластьПечати(2);
		Таб.Показать("Проверка выписки Счетов-фактур");         
	Иначе
		Таб.Показать("Проверка выписки Счетов-фактур",, -1);         
		Предупреждение("Непогашенных авансов, на которые отсутствуют счета-фактуры не обнаружено!");
	КонецЕсли;                                                            
	
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
		
КонецПроцедуры //ПроверкаАвансовыхСчетов()

//******************************************************************************
// СуммаСчетаФактуры(ДокСФ)
//
// Параметры: 
//  ДокСФ - документ счет-фактура
//
// Возвращаемое значение:
//  Сумма СФ без НП
//
// Описание:
//  Вычисляет сумму СФ как суммарный приход по регистру Книга продаж
//
Функция СуммаСчетаФактуры(ДокСФ)
	СуммаВсего = 0;                                 
	СуммаНП    = 0;
	
	Если (ДокСФ.ДокОснование.Выбран() = 1)
	и    (ДокСФ.КоличествоСтрок() = 0)
	Тогда
		ДокОснование = ДокСФ.ДокОснование;
	Иначе
		ДокОснование = ДокСФ;
	КонецЕсли;
	
	РегКнигаПродаж = СоздатьОбъект("Регистр.КнигаПродаж");
	РегКнигаПродаж.ВыбратьДвиженияДокумента(ДокОснование);
	Пока РегКнигаПродаж.ПолучитьДвижение() = 1 Цикл
		Если РегКнигаПродаж.Приход = 1 Тогда
			СуммаНП      = СуммаНП    + РегКнигаПродаж.СуммаНП;
			СуммаВсего   = СуммаВсего + РегКнигаПродаж.СуммаРуб;
		КонецЕсли;
	КонецЦикла;
	
	Возврат (СуммаВсего - СуммаНП);
	
КонецФункции // СуммаСчетаФактуры()

//******************************************************************************
// СформироватьКнигу(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   запускает отчет

Процедура СформироватьКнигу(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;   
	Перем ДатаНачалаЗ, ДатаКонцаЗ;
	Перем ПНДС, СтарыйКлиент;           

	// инициализация переменных
	ИтогВсего	         = 0;
	ИтогСуммаБезНДС20	 = 0;
	ИтогНДС20            = 0;
	ИтогСуммаБезНДС10    = 0;
	ИтогНДС10            = 0;
	ИтогСуммаНДС0        = 0; 	
	ИтогСуммаСовсемБезНДС= 0;
	
	ИтогКлиентВсего         = 0;
	ИтогКлиентСуммаБезНДС20 = 0;
	ИтогКлиентНДС20         = 0;
	ИтогКлиентСуммаБезНДС10 = 0;
	ИтогКлиентНДС10         = 0;
	ИтогКлиентСуммаНДС0     = 0;
	ИтогКлиентСуммаСовсемБезНДС = 0;

	ПНДС = Перечисление.СтавкиНДС;
	                              
	ДатаНачалаЗ = НачМесяца(ДатаНачала);
	Если глПроверкаДаты(ДатаНачалаЗ,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	
	ДатаКонцаЗ = Мин(КонМесяца(ДатаКонца),ПолучитьДатуТА());
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
		Таб = СоздатьОбъект("Таблица");
	Иначе
		Таб.Очистить();
	КонецЕсли;      
	                     
	Таб.ИсходнаяТаблица( "КнигаПродаж" ); 
	
	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	Если глПроверкаАктуальностиОтчета(ДатаНачалаЗ,ДатаКонцаЗ,Последовательность.КнигаПродаж)=0 Тогда
		Возврат;
	КонецЕсли;	       
	
	Если ВыбЮрЛицо.Выбран()=0 Тогда
		Предупреждение("Не выбрано юридическое лицо!",60);
		Возврат;
	КонецЕсли;

	Если (ПоКлиентам = 1) И (ОтборПоКлиентам = 1) Тогда
		Если ВыбКонтрагент.Выбран()=0 Тогда
			Предупреждение("Не выбран контрагент для отбора!",60);
			Возврат;
		КонецЕсли; 
	КонецЕсли;
	
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "КнигаПродаж");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбЮрЛицо",     ВыбЮрЛицо);
	Расшифровка.Установить("СтарыйВидОтчета",   СтарыйВидОтчета);
	Расшифровка.Установить("ПоКлиентам",        ПоКлиентам);
	Расшифровка.Установить("ОтборПоКлиентам",   ОтборПоКлиентам);
	Расшифровка.Установить("ВыбКонтрагент",     ВыбКонтрагент);
	Расшифровка.Установить("ИтогиПоТипам",      ИтогиПоТипам);
	
	Расшифровка.Установить("СписокВидовОтчетов",СписокВидовОтчетов.ТекущаяСтрока());
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"Период с ДатаНачалаЗ по ДатаКонцаЗ;
	|ЮрЛицо = Регистр.КнигаПродаж.КредДокумент.Фирма.ЮрЛицо;
	|КредДокумент = Регистр.КнигаПродаж.КредДокумент;
	|СтавкаНДС = Регистр.КнигаПродаж.СтавкаНДС;
	|СуммаРуб  = Регистр.КнигаПродаж.СуммаРуб;
	|СуммаНДС  = Регистр.КнигаПродаж.СуммаНДС;
	|СуммаНП   = Регистр.КнигаПродаж.СуммаНП;
	|ДокументОплаты = Регистр.КнигаПродаж.ДокументОплаты;
	|ВидДолга       = Регистр.КнигаПродаж.ВидДолга;
	|ТекДокумент    = Регистр.КнигаПродаж.ТекущийДокумент;
	|Функция СуммаРуб20 = Расход(СуммаРуб) Когда ((СтавкаНДС = ПНДС.НДС20) или (СтавкаНДС = ПНДС.НДС16_67));
	|Функция СуммаНДС20 = Расход(СуммаНДС) Когда ((СтавкаНДС = ПНДС.НДС20) или (СтавкаНДС = ПНДС.НДС16_67));
	|Функция СуммаНП20  = Расход(СуммаНП)  Когда ((СтавкаНДС = ПНДС.НДС20) или (СтавкаНДС = ПНДС.НДС16_67));
	|Функция СуммаРуб10 = Расход(СуммаРуб) Когда ((СтавкаНДС = ПНДС.НДС10) или (СтавкаНДС = ПНДС.НДС9_09));
	|Функция СуммаНДС10 = Расход(СуммаНДС) Когда ((СтавкаНДС = ПНДС.НДС10) или (СтавкаНДС = ПНДС.НДС9_09));
	|Функция СуммаНП10  = Расход(СуммаНП)  Когда ((СтавкаНДС = ПНДС.НДС10) или (СтавкаНДС = ПНДС.НДС9_09));
	|Функция СуммаРуб20_120 = Расход(СуммаРуб) Когда (СтавкаНДС = ПНДС.НДС20_120);
	|Функция СуммаНДС20_120 = Расход(СуммаНДС) Когда (СтавкаНДС = ПНДС.НДС20_120);
	|Функция СуммаНП20_120  = Расход(СуммаНП)  Когда (СтавкаНДС = ПНДС.НДС20_120);
	|Функция СуммаРуб10_110 = Расход(СуммаРуб) Когда (СтавкаНДС = ПНДС.НДС10_110);
	|Функция СуммаНДС10_110 = Расход(СуммаНДС) Когда (СтавкаНДС = ПНДС.НДС10_110);
	|Функция СуммаНП10_110  = Расход(СуммаНП)  Когда (СтавкаНДС = ПНДС.НДС10_110);
	|Функция СуммаРуб0 = Расход(СуммаРуб)  Когда ((СтавкаНДС = ПНДС.БезНДС)или (ПустоеЗначение(СтавкаНДС)=1));
	|Функция СуммаНДС0 = Расход(СуммаНДС)  Когда ((СтавкаНДС = ПНДС.БезНДС)или (ПустоеЗначение(СтавкаНДС)=1));
	|Функция СуммаНП0  = Расход(СуммаНП)   Когда ((СтавкаНДС = ПНДС.БезНДС)или (ПустоеЗначение(СтавкаНДС)=1));
	|Группировка КредДокумент;
	|Группировка ДокументОплаты;
	|Группировка ТекДокумент;"//}}ЗАПРОС
	;                     

	Загол="";
	НетОш = 1; // нет ошибок при наложении фильтров

	НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "ЮрЛицо",ВыбЮрЛицо,"ВыбЮрЛицо",ТекстЗапроса,Загол);
	                
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;                                             

	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;

	КнПрод = СоздатьОбъект("ТаблицаЗначений");
	Запрос.Выгрузить(КнПрод,1,0);
	
	КнПрод.НоваяКолонка("ТипЗаписи","Строка",60);
	КнПрод.НоваяКолонка("КредКонтрагент","Справочник");
	
	Если ПоКлиентам = 1 Тогда

		// Если нужна группировка по контрагентам - требуется дополнительный проход по таблице
		КнПрод.ВыбратьСтроки();
		Пока КнПрод.ПолучитьСтроку()=1 Цикл
			            
			КнПрод.КредКонтрагент = ПолучитьПустоеЗначение("Справочник.Контрагенты");  // начальная инициализация
			Если КнПрод.КредДокумент.Вид() = "СчетФактураВыданный" Тогда
				КнПрод.КредКонтрагент = КнПрод.КредДокумент.Контрагент;
			Иначе     
				СФ 	= НайтиСФДокумента(КнПрод.КредДокумент);
				Если ТипЗначенияСтр(СФ)="Документ" Тогда
					Если глЕстьРеквизитШапки("Контрагент", СФ.Вид() ) = 1 Тогда
						КнПрод.КредКонтрагент = СФ.Контрагент;
					КонецЕсли;
				Иначе
					Если глЕстьРеквизитШапки("Контрагент", КнПрод.КредДокумент.Вид() ) = 1 Тогда
						КнПрод.КредКонтрагент = КнПрод.КредДокумент.Контрагент;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ОбработкаДляРозницы(КнПрод);
		КнПрод.Сортировать("КредКонтрагент,КредДокумент,ДокументОплаты",1);
	Иначе

		ОбработкаДляРозницы(КнПрод);
		КнПрод.Сортировать("КредДокумент,ДокументОплаты",1);
	КонецЕсли;
	
	глЧислоСтрок = 0;
	
	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	Таб.ВывестиСекцию("ШапкаТаблицы");

	НачПовт = Таб.ВысотаСекции("Кнопки") + Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);

	глОживить(1);
	                         
	Таб.Опции(0, 0, 2, 0, "КнигаПродаж", "КнигаПродаж");
	Таб.ОбластьПечати(3);
	     
	// Инициализация цикла
	Ном = 0;
    Если (ПоКлиентам = 1) 
	   и (КнПрод.КоличествоСтрок() > 0) Тогда
		КнПрод.ПолучитьСтрокуПоНомеру(1);
		СтарыйКлиент = КнПрод.КредКонтрагент;
    КонецЕсли;

	КнПрод.ВыбратьСтроки();
	Пока КнПрод.ПолучитьСтроку()=1 Цикл

		КредДокумент   = КнПрод.КредДокумент;
		ДокументОплаты = КнПрод.ДокументОплаты;
		ТекДокумент    = КнПрод.ТекДокумент;
		Если ТекДокумент.Вид() = "ВозвратОтПокупателя"  Тогда
			ДатаЗаписи = ТекДокумент.ДатаДок;
		ИначеЕсли ПустоеЗначение(ДокументОплаты) = 1 Тогда
			ДатаЗаписи = КредДокумент.ДатаДок;
		Иначе
			ДатаЗаписи = Макс(КредДокумент.ДатаДок,ДокументОплаты.ДатаДок);
		КонецЕсли;
		
		Если (ДатаЗаписи<ДатаНачала) или (ДатаЗаписи>ДатаКонца) Тогда
			КнПрод.ТипЗаписи = "В итогах не участвует";
			Продолжить; // запись не входит в период
		КонецЕсли;

		Ном = Ном+1;  
		ДокПоставки = КредДокумент;

		Если КнПрод.КредДокумент.Вид() = "СчетФактураВыданный" Тогда
			СФ = КнПрод.КредДокумент;
		Иначе     
			СФ 	= НайтиСФДокумента(КнПрод.КредДокумент);
		КонецЕсли;

		Если ПустоеЗначение(СФ)=1 Тогда
			КнПрод.ТипЗаписи = "В итогах не участвует";
			Продолжить; // нет счета-фактуры
		КонецЕсли;

		Если (ПоКлиентам = 1)И(ОтборПоКлиентам = 1) Тогда
			Если СФ.Контрагент<>ВыбКонтрагент Тогда
				КнПрод.ТипЗаписи = "В итогах не участвует";
				Продолжить;  // Фильтруем по клиенту    
			КонецЕсли;    
		КонецЕсли;

		Если СФ.Вид() = "ОтчетККМ" Тогда
			НомерДата = СФ.НомерЛентыККМ + " от "+ СФ.ДатаДок;
		ИначеЕсли СФ.Вид() = "ПКО" Тогда
			НомерДата = СокрЛП(СФ.Основание) + " от "+ СФ.ДатаДок; 
		ИначеЕсли СФ.Вид() = "РеализацияРозница" Тогда  
			НомерЛентыНайден = 0;
			Если (ДокументОплаты.Выбран() = 1) Тогда
			    Если ДокументОплаты.Вид() = "ПКО" Тогда
					НомерДата = СокрЛП(ДокументОплаты.Основание) + " от "+ ДокументОплаты.ДатаДок; 
					НомерЛентыНайден = 1;
				КонецЕсли;	
			КонецЕсли;                   
			Если НомерЛентыНайден = 0 Тогда   
				Стр = 0;
			    СФРеализацияРозница.НайтиЗначение(СФ, Стр, 1);  
				Если Стр > 0 Тогда
					СФРеализацияРозница.ПолучитьСтрокуПоНомеру(Стр);				
					НомерДата = СокрЛП(СФРеализацияРозница.ПКО.Основание) + " от "+ СФРеализацияРозница.ПКО.ДатаДок; 
					НомерЛентыНайден = 1;
				КонецЕсли;
			КонецЕсли;      
			Если НомерЛентыНайден = 0 Тогда
			    СообщениеТрассировки("По " + глПредставлениеДокумента(СФ) + " не проведен прием розничной выручки!",, СФ);
			КонецЕсли;
		Иначе
			НомерДата = глПредставлениеСФ(СФ,1);
		КонецЕсли; 
		
		// Дата оплаты - в записи книги продаж указана пользователем.
		ДатаОплаты = ?(ДокументОплаты.Вид() = "ЗаписьКнигиПродаж",
				   ДокументОплаты.ДатаОплаты,
				   Формат(ДокументОплаты.ДатаДок,"ДДДММГГГГ"));
	              
		// При учетной политике "по отгрузке" дата оплаты имеет смысл только при авансовых платежах				   
		Если (ВыбЮрЛицо.МетодОпределенияВыручки.Получить(ДатаКонца) = Перечисление.МетодыОпределенияВыручки.ПоОтгрузке) Тогда
			Если СФ.Вид() <> "СчетФактураВыданный" Тогда
				ДатаОплаты = "";
			ИначеЕсли СФ.СФНаАванс = 0 Тогда
				ДатаОплаты = "";      
			КонецЕсли;
		КонецЕсли;
			
		ЮрФизЛицо	= СФ.Контрагент.ЮрФизЛицо;
		ПечКлиент   = ?(ПустоеЗначение(ЮрФизЛицо) = 1, "", ЮрФизЛицо.ПолнНаименование);
		ПечИНН		= ?(ПустоеЗначение(ЮрФизЛицо) = 1, "", ЮрФизЛицо.ИНН);
		
		ПечСуммаВсего    = (КнПрод.СуммаРуб20 + КнПрод.СуммаРуб20_120 + КнПрод.СуммаРуб10  + КнПрод.СуммаРуб10_110 + КнПрод.СуммаРуб0) - 
						   (КнПрод.СуммаНП20 + КнПрод.СуммаНП20_120  + КнПрод.СуммаНП10 + КнПрод.СуммаНП10_110  + КнПрод.СуммаНП0 );
		ПечСуммаБезНДС20 = 	КнПрод.СуммаРуб20 - КнПрод.СуммаНДС20 - КнПрод.СуммаНП20 + КнПрод.СуммаРуб20_120 - КнПрод.СуммаНП20_120;
		ПечНДС20	     =	КнПрод.СуммаНДС20 + КнПрод.СуммаНДС20_120;
		ПечСуммаБезНДС10 =	КнПрод.СуммаРуб10 + КнПрод.СуммаРуб10_110 - КнПрод.СуммаНДС10 - КнПрод.СуммаНП10 - КнПрод.СуммаНП10_110;
		ПечНДС10	     =  КнПрод.СуммаНДС10 + КнПрод.СуммаНДС10_110;
		ПечСуммаНДС0	 =  0;
		ПечСуммаСовсемБезНДС = КнПрод.СуммаРуб0- КнПрод.СуммаНП0;           
		                 
		ИтогВсего	         = ИтогВсего			+ПечСуммаВсего;
		ИтогСуммаБезНДС20	 = ИтогСуммаБезНДС20	+ПечСуммаБезНДС20;
		ИтогНДС20            = ИтогНДС20            +ПечНДС20;
		ИтогСуммаБезНДС10    = ИтогСуммаБезНДС10    +ПечСуммаБезНДС10;
		ИтогНДС10            = ИтогНДС10            +ПечНДС10;
		ИтогСуммаНДС0        = ИтогСуммаНДС0        +ПечСуммаНДС0; 	
		ИтогСуммаСовсемБезНДС= ИтогСуммаСовсемБезНДС+ПечСуммаСовсемБезНДС;
		
		Если ДокументОплаты.Вид() = "ЗаписьКнигиПродаж" Тогда
			НомерДата = НомерДата + " " + ДокументОплаты.ДополнительнаяИнформация;
		Иначе                                
			Если ТекДокумент.Вид() = "ВозвратОтПокупателя" Тогда
			    Если ТекДокумент.ДокОснование.Выбран() = 0 Тогда
					НомерДата = ТекДокумент.НомерДатаСФ;
				КонецЕсли;
			ИначеЕсли ТекДокумент.Вид() = "ВозвратПоставщику" Тогда 
				
				// В книгу продаж попадает возврат поставщику оплаченного товара
			    Если ТекДокумент.ДокОснование.Выбран() = 0 Тогда
					НомерДата = ТекДокумент.НомерДатаСФ;
				Иначе
					НомерДата = глПредставлениеСФ(СФ,1);
				КонецЕсли;
				НомерДата = НомерДата + РазделительСтрок + 
				            "восстановление НДС в связи с возвратом товара поставщику";
			Иначе
				// Сумму СФ надо определять по движениям регистра (например, продажа товаров принятых в сумму входить не должна).			
				СуммаСФбезНП = СуммаСчетаФактуры(СФ);
				Если (ПечСуммаВсего > 0)
				и    (СуммаСФбезНП > ПечСуммаВсего) 
				Тогда
					НомерДата = НомерДата + " ;част. оплата";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// создаем расшифровку текущей строки
		СписДоков=СоздатьОбъект("СписокЗначений");
		СписДоков.ДобавитьЗначение(СФ,		      "Счет-Фактура: "           +глПредставлениеДокумента(СФ));
        Если (ДокПоставки.Вид() = "ВозвратОтПокупателя") 
		 или (ДокПоставки.Вид() = "ВозвратПоставщику")  Тогда // Попытаемся вывести в расшифровке основании возврата, если есть
			Если ПустоеЗначение(ДокПоставки.ДокОснование) = 0  Тогда
				СписДоков.ДобавитьЗначение(ДокПоставки.ДокОснование,   "Документ - основание СФ: "+глПредставлениеДокумента(ДокПоставки.ДокОснование));
			КонецЕсли;
		Иначе
			СписДоков.ДобавитьЗначение(ДокПоставки,   "Документ - основание СФ: "+глПредставлениеДокумента(ДокПоставки));
		КонецЕсли;
		
		Если  ПустоеЗначение(ДокументОплаты) =  0 Тогда
			СписДоков.ДобавитьЗначение(ДокументОплаты,"Запись на основании: "+глПредставлениеДокумента(ДокументОплаты));
		КонецЕсли;
		
		Расшифровка.Установить("Меню",СписДоков);

		Если ПоКлиентам = 1 Тогда
			Если СтарыйКлиент <> КнПрод.КредКонтрагент Тогда

				// нужно вывести итоговую секцию
				ПечКлиентИтог = ?(ПустоеЗначение(СтарыйКлиент.ЮрФизЛицо) = 1, "", СтарыйКлиент.ЮрФизЛицо.ПолнНаименование);;

				// Итоги по последнему выводятся после цикла
				Если ОтборПоКлиентам = 0 Тогда
					Таб.ВывестиСекцию("ВсегоКлиент");
				КонецЕсли;
				
				// Сброс промежуточных итогов
				СтарыйКлиент = КнПрод.КредКонтрагент;
				ИтогКлиентВсего         = 0;
				ИтогКлиентСуммаБезНДС20 = 0;
				ИтогКлиентНДС20         = 0;
				ИтогКлиентСуммаБезНДС10 = 0;
				ИтогКлиентНДС10         = 0;
				ИтогКлиентСуммаНДС0     = 0;
				ИтогКлиентСуммаСовсемБезНДС = 0;

			КонецЕсли;

			ИтогКлиентВсего         = ИтогКлиентВсего + ПечСуммаВсего;
			ИтогКлиентСуммаБезНДС20 = ИтогКлиентСуммаБезНДС20 + ПечСуммаБезНДС20;
			ИтогКлиентНДС20         = ИтогКлиентНДС20 + ПечНДС20;
			ИтогКлиентСуммаБезНДС10 = ИтогКлиентСуммаБезНДС10 + ПечСуммаБезНДС10;
			ИтогКлиентНДС10         = ИтогКлиентНДС10 + ПечНДС10;
			ИтогКлиентСуммаНДС0     = ИтогКлиентСуммаНДС0 + ПечСуммаНДС0;
			ИтогКлиентСуммаСовсемБезНДС = ИтогКлиентСуммаСовсемБезНДС + ПечСуммаСовсемБезНДС;

		КонецЕсли;

		Если ИтогиПоТипам = 1 Тогда

			// Определение типа записи

			Если КнПрод.ВидДолга = Перечисление.ВидыДолга.Аванс Тогда

				// это авансы
				
				Если ПечСуммаВсего >0  Тогда
					КнПрод.ТипЗаписи = "Авансы полученные";
				Иначе
					КнПрод.ТипЗаписи = "Авансы зачтенные и возвращенные из бюджета";
				КонецЕсли;    
			ИначеЕсли ((КнПрод.ВидДолга = Перечисление.ВидыДолга.ДолгЗаТовары) 	   ИЛИ
						(КнПрод.ВидДолга = Перечисление.ВидыДолга.ДолгЗаПродукцию) ИЛИ
						(КнПрод.ВидДолга = Перечисление.ВидыДолга.ДолгЗаМатериалы) ИЛИ
						(КнПрод.ВидДолга = Перечисление.ВидыДолга.ДолгЗаУслуги)	   ИЛИ
						(КнПрод.ВидДолга = Перечисление.ВидыДолга.ДолгЗаОС)		   ИЛИ
						(КнПрод.ВидДолга = Перечисление.ВидыДолга.ДолгЗаНМА)	   ИЛИ
						(КнПрод.ВидДолга = Перечисление.ВидыДолга.ДолгЗаТоварыВРознице)    ИЛИ
						(КнПрод.ВидДолга = Перечисление.ВидыДолга.ДолгЗаПродукциюВРознице) ИЛИ
						(КнПрод.ВидДолга = Перечисление.ВидыДолга.ДолгЗаУслугиВРознице)
						) И
					  (ПечСуммаВсего < 0) Тогда

            	// это возвраты от покупателей
				
				КнПрод.ТипЗаписи = "Возвраты от покупателей";
			Иначе
				
				// это обычные продажи
				
				КнПрод.ТипЗаписи = "Продажа";
			КонецЕсли;			
		КонецЕсли;

		Таб.ВывестиСекцию("Строка");
		глОживить(1);
		
 	КонецЦикла;
	
 	// Выводим итоги по последнему клиенту
	Если (ПоКлиентам = 1) 
	   и (ТипЗначенияСтр(СтарыйКлиент) = "Справочник") Тогда
	   	Если СтарыйКлиент.Вид() = "Контрагенты" Тогда
		   	ПечКлиентИтог = ?(ПустоеЗначение(СтарыйКлиент.ЮрФизЛицо) = 1, "", СтарыйКлиент.ЮрФизЛицо.ПолнНаименование);;
			Если ОтборПоКлиентам = 1 Тогда
				Если СтарыйКлиент = ВыбКонтрагент Тогда
					Таб.ВывестиСекцию("ВсегоКлиент");
				КонецЕсли;
			Иначе
				Таб.ВывестиСекцию("ВсегоКлиент");
			КонецЕсли;
	   	КонецЕсли;
	КонецЕсли;
	
	ГлБух = ВыбЮрЛицо.ГлБухгалтер.Получить(ДатаКонца);
	Таб.ВывестиСекцию("Всего");
	глОживить(1);
	
	Если ИтогиПоТипам = 1 Тогда
	    
		//проанализировать итоги по типам записей
		КнПрод.Свернуть("ТипЗаписи","СуммаРуб0,СуммаРуб10,СуммаРуб20,СуммаНДС10,СуммаНДС20,СуммаНП0,СуммаНП10,СуммаНП20, СуммаРуб20_120, СуммаНДС20_120, СуммаНП20_120, СуммаРуб10_110, СуммаНДС10_110, СуммаНП10_110");
        
		// Надо удалить строку, которую не надо отбражать
		НомерСтроки = "";
		КнПрод.НайтиЗначение(Формат("В итогах не участвует","С60"), НомерСтроки, "ТипЗаписи");
		Если НомерСтроки > 0 Тогда
			КнПрод.УдалитьСтроку(НомерСтроки);
		КонецЕсли; 
		
		Таб.ВывестиСекцию("ШапкаВсегоТип");

		КнПрод.ВыбратьСтроки();
		Пока КнПрод.ПолучитьСтроку()=1 Цикл
			ПечТип = СокрЛП(КнПрод.ТипЗаписи);
			ИтогТипВсего = (КнПрод.СуммаРуб20 + КнПрод.СуммаРуб10 + КнПрод.СуммаРуб0 + КнПрод.СуммаРуб20_120 + 
							КнПрод.СуммаРуб10_110) - (КнПрод.СуммаНП20  + КнПрод.СуммаНП10  + КнПрод.СуммаНП0 
							+ КнПрод.СуммаНП20_120 + КнПрод.СуммаНП10_110);
			ИтогТипСуммаБезНДС20 = КнПрод.СуммаРуб20 + КнПрод.СуммаРуб20_120 - КнПрод.СуммаНДС20 - КнПрод.СуммаНП20 
							- КнПрод.СуммаНП20_120;
			ИтогТипНДС20 = КнПрод.СуммаНДС20 + КнПрод.СуммаНДС20_120;
			ИтогТипСуммаБезНДС10 = КнПрод.СуммаРуб10 + КнПрод.СуммаРуб10_110 - КнПрод.СуммаНДС10 - КнПрод.СуммаНП10 
							- КнПрод.СуммаНП10_110;
			ИтогТипНДС10 = КнПрод.СуммаНДС10 + КнПрод.СуммаНДС10_110;
			ИтогТипСуммаНДС0 = 0;
			ИтогТипСуммаСовсемБезНДС = КнПрод.СуммаРуб0 - КнПрод.СуммаНП0;
			
			Таб.ВывестиСекцию("ВсегоТип");    
		КонецЦикла;		
	КонецЕсли;
	
	
	Таб.ВывестиСекцию("Подвал");
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);            
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Книга продаж", ""); 
	
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // СформироватьКнигу()

//******************************************************************************
// СформироватьРеестр(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   запускает отчет
Процедура СформироватьРеестр(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;   
	Перем ПНДС;
	
	ПНДС = Перечисление.СтавкиНДС;
	
	ИтогВсего	         = 0;
	ИтогСуммаБезНДС20	 = 0;
	ИтогНДС20            = 0;
	ИтогСуммаБезНДС10    = 0;
	ИтогНДС10            = 0;
	ИтогСуммаНДС0        = 0; 	
	ИтогСуммаСовсемБезНДС= 0;
	
	Если глПроверкаДаты(ДатаНачала,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбЮрЛицо.Выбран()=0 Тогда
		Предупреждение("Не выбрано юридическое лицо!",60);
		Возврат;
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;      
	                     
	Таб.ИсходнаяТаблица( "РеестрСФ" );
	
	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	Если глПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонца,Последовательность.КнигаПродаж)=0 Тогда
		Возврат;
	КонецЕсли;	  
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "КнигаПродаж");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбЮрЛицо",     ВыбЮрЛицо);
	Расшифровка.Установить("ПоКлиентам",        ПоКлиентам);
	Расшифровка.Установить("ОтборПоКлиентам",   ОтборПоКлиентам);
	Расшифровка.Установить("ВыбКонтрагент",     ВыбКонтрагент);
	Расшифровка.Установить("СписокВидовОтчетов",СписокВидовОтчетов.ТекущаяСтрока());
	
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"Период с ДатаКонца по ДатаКонца;
	|ЮрЛицо = Регистр.КнигаПродаж.КредДокумент.Фирма.ЮрЛицо;
	|КредДокумент = Регистр.КнигаПродаж.КредДокумент;
	|СтавкаНДС = Регистр.КнигаПродаж.СтавкаНДС;
	|СуммаРуб  = Регистр.КнигаПродаж.СуммаРуб;
	|СуммаНДС  = Регистр.КнигаПродаж.СуммаНДС;
	|СуммаНП   = Регистр.КнигаПродаж.СуммаНП;
	|Функция СуммаРуб20 = КонОст(СуммаРуб) Когда ((СтавкаНДС = ПНДС.НДС20) или (СтавкаНДС = ПНДС.НДС16_67));
	|Функция СуммаНДС20 = КонОст(СуммаНДС) Когда ((СтавкаНДС = ПНДС.НДС20) или (СтавкаНДС = ПНДС.НДС16_67));
	|Функция СуммаНП20  = КонОст(СуммаНП)  Когда ((СтавкаНДС = ПНДС.НДС20) или (СтавкаНДС = ПНДС.НДС16_67));
	|Функция СуммаРуб10 = КонОст(СуммаРуб) Когда ((СтавкаНДС = ПНДС.НДС10) или (СтавкаНДС = ПНДС.НДС9_09));
	|Функция СуммаНДС10 = КонОст(СуммаНДС) Когда ((СтавкаНДС = ПНДС.НДС10) или (СтавкаНДС = ПНДС.НДС9_09));
	|Функция СуммаНП10  = КонОст(СуммаНП)  Когда ((СтавкаНДС = ПНДС.НДС10) или (СтавкаНДС = ПНДС.НДС9_09));
	|Функция СуммаРуб0 = КонОст(СуммаРуб)  Когда ((СтавкаНДС = ПНДС.БезНДС)или (ПустоеЗначение(СтавкаНДС)=1));
	|Функция СуммаНДС0 = КонОст(СуммаНДС)  Когда ((СтавкаНДС = ПНДС.БезНДС)или (ПустоеЗначение(СтавкаНДС)=1));
	|Функция СуммаНП0  = КонОст(СуммаНП)   Когда ((СтавкаНДС = ПНДС.БезНДС)или (ПустоеЗначение(СтавкаНДС)=1));
	|Группировка КредДокумент;"
	;                     
	
	Загол="";
	
	НетОш = 1; // нет ошибок при наложении фильтров
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "ЮрЛицо",ВыбЮрЛицо,"ВыбЮрЛицо",ТекстЗапроса,Загол);
	                
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;

	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;
                 
	глЧислоСтрок  = 0;
	
	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	Таб.ВывестиСекцию("ШапкаТаблицы");

	НачПовт = Таб.ВысотаСекции("Кнопки") + Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);

	глОживить(1);
	                         
	Таб.Опции(0, 0, 2, 0, "РеестрСФ", "РеестрСФ");
	Таб.ОбластьПечати(3);
	     
	Ном = 0;
	Пока Запрос.Группировка("КредДокумент") = 1 Цикл
		КредДокумент   = Запрос.КредДокумент;
		
		Ном = Ном+1;  
		ДокПоставки = КредДокумент;
		Если КредДокумент.Вид() = "СчетФактураВыданный" Тогда
			СФ 	= КредДокумент;
		Иначе     
			СФ 	= НайтиСФДокумента(КредДокумент);
		КонецЕсли;
		
		Если (ПустоеЗначение(СФ)=1) Тогда
			Продолжить;
		ИначеЕсли (СФ.Вид() = "ПКО") или (СФ.Вид() = "РеализацияРозница") Тогда
			Продолжить;
		КонецЕсли;
		
		Если СФ.Вид() = "ОтчетККМ" Тогда
			НомерДата = СФ.НомерЛентыККМ + " от "+ СФ.ДатаДок;
		Иначе	
			НомерДата = глПредставлениеСФ(СФ,1);
		КонецЕсли;
		
		ДатаПрихода = Формат(ДокПоставки.ДатаДок,"ДДДММГГГГ");
		ЮрФизЛицо	= СФ.Контрагент.ЮрФизЛицо;
		ПечКлиент   = ?(ПустоеЗначение(ЮрФизЛицо)=1,"",ЮрФизЛицо.ПолнНаименование);
		ПечИНН		= ?(ПустоеЗначение(ЮрФизЛицо)=1,"",ЮрФизЛицо.ИНН);
		
		ПечСуммаВсего    =  ((Запрос.СуммаРуб20 + Запрос.СуммаРуб10 + Запрос.СуммаРуб0) - 
		                     (Запрос.СуммаНП20  + Запрос.СуммаНП10  + Запрос.СуммаНП0 ));
		ПечСуммаБезНДС20 = 	(Запрос.СуммаРуб20 - Запрос.СуммаНДС20 - Запрос.СуммаНП20);
		ПечНДС20	     =	(Запрос.СуммаНДС20);
		ПечСуммаБезНДС10 =	(Запрос.СуммаРуб10 - Запрос.СуммаНДС10 - Запрос.СуммаНП10);
		ПечНДС10	     =  (Запрос.СуммаНДС10);
		ПечСуммаНДС0	 =  0;
		ПечСуммаСовсемБезНДС = (Запрос.СуммаРуб0 - Запрос.СуммаНП0);
		
		ИтогВсего	         = ИтогВсего			+ПечСуммаВсего;
		ИтогСуммаБезНДС20	 = ИтогСуммаБезНДС20	+ПечСуммаБезНДС20;
		ИтогНДС20            = ИтогНДС20            +ПечНДС20;
		ИтогСуммаБезНДС10    = ИтогСуммаБезНДС10    +ПечСуммаБезНДС10;
		ИтогНДС10            = ИтогНДС10            +ПечНДС10;
		ИтогСуммаНДС0        = ИтогСуммаНДС0        +ПечСуммаНДС0; 	
		ИтогСуммаСовсемБезНДС= ИтогСуммаСовсемБезНДС+ПечСуммаСовсемБезНДС;
		
		// создаем расшифровку текущей строки
		СписДоков=СоздатьОбъект("СписокЗначений");
		СписДоков.ДобавитьЗначение(СФ,		      "Счет-Фактура: "           +глПредставлениеДокумента(СФ));
		СписДоков.ДобавитьЗначение(ДокПоставки,   "Документ - основание СФ: "+глПредставлениеДокумента(ДокПоставки));
		
		Расшифровка.Установить("Меню",СписДоков);
		
		Таб.ВывестиСекцию("Строка");
		глОживить(1);
		
	КонецЦикла;
	
	Таб.ВывестиСекцию("Всего");
	Таб.ВывестиСекцию("Подвал");
	глОживить(1);
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Реестр счетов-фактур", ""); 
	
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // СформироватьРеестр()

//******************************************************************************
// Сформировать(ЗакрытьДиалог)
//
// Параметры:
//  ЗакрытьДиалог
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Здесь можно перечислить элементы диалога.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура Сформировать(ЗакрытьДиалог = 0)
	
	Если СписокВидовОтчетов.ТекущаяСтрока()=1 Тогда
		СформироватьКнигу(ЗакрытьДиалог);
	ИначеЕсли СписокВидовОтчетов.ТекущаяСтрока()=2 Тогда     
		СформироватьРеестр(ЗакрытьДиалог);
	ИначеЕсли СписокВидовОтчетов.ТекущаяСтрока()=3 Тогда     
		ПроверкаАвансовыхСчетов(ЗакрытьДиалог)
	КонецЕсли;   
	
	Если ТипЗначенияСтр(Трассировка) = "СписокЗначений" Тогда
	    глПоказатьТрассировку(Трассировка); 
		Трассировка = 0;
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

//******************************************************************************
// ПоКнопкеПроверитьНаличиеСФ()
//
// Параметры:
//  Нет
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "ПроверитьНаличиеСФ"
//
// Описание: формирование таблицы просроченных авансов
// 

Процедура ПоКнопкеПроверитьНаличиеСФ()
	СписокВидовОтчетов.ТекущаяСтрока(3);
	Сформировать();
КонецПроцедуры // ПоКнопкеПроверитьНаличиеСФ()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ

//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	Если ФлагВосстановленияНастройки = 0 Тогда
		
		Фирма      = глЗначениеПоУмолчанию("ОсновнаяФирма");
		ВыбЮрЛицо  = ?(ПустоеЗначение(Фирма)=1,"",Фирма.ЮрЛицо);
		ДатаНачала      = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
		Если ПустоеЗначение(ДатаНачала) = 1 Тогда
			ДатаНачала      = НачМесяца(ДатаКонца);    
		КонецЕсли;

	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВыбЮрЛицо	    = глРасшифровка.Получить("ВыбЮрЛицо");
		СтарыйВидОтчета = глРасшифровка.Получить("СтарыйВидОтчета");
		ПоКлиентам      = глРасшифровка.Получить("ПоКлиентам");
		ОтборПоКлиентам = глРасшифровка.Получить("ОтборПоКлиентам");
		ВыбКонтрагент   = глРасшифровка.Получить("ВыбКонтрагент");
		ИтогиПоТипам    = глРасшифровка.Получить("ИтогиПоТипам");
		
		СписокВидовОтчетов.ТекущаяСтрока(глРасшифровка.Получить("СписокВидовОтчетов"));
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		СтарыйВидОтчета = СписокВидовОтчетов.ТекущаяСтрока();
		Обновить = 0;
	КонецЕсли;                      

    УправлениеДиалогом();
	
КонецПроцедуры		// ПриОткрытии()       

//******************************************************************************
// Предопределенная процедура.
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	
КонецПроцедуры // ВводНового()  
                                                                                
////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ДатаКонца  = ПолучитьДатуТА();

ДокПодч    = СоздатьОбъект("Документ");
СписокВидовОтчетов.ДобавитьЗначение("Книга продаж");
СписокВидовОтчетов.ДобавитьЗначение("Реестр непогашенных счетов - фактур");
СписокВидовОтчетов.ДобавитьЗначение("Перечень непогашеных авансов");
СписокВидовОтчетов.ТекущаяСтрока(1);