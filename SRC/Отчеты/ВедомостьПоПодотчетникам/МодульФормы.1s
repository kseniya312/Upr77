////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//    
Перем ТекСтрокаВТаблице; // текущая строка в таблице значений  МФ

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 

Перем СписокГруппировок;
Перем КоличествоГруппировок;
Перем НомерГруппировкиПоВалюте;

// списки значений, в которых определены возможные типы операций
Перем СписокОперДебет, СписокОперКредит;

Перем ВалютаОтчета;

Процедура ВывестиГруппировку(Запрос,Ном) Далее

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ  
//******************************************************************************
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

//******************************************************************************
// ПерерисовкаНазванийЗакладок
//
// Параметры:
// 	Нет
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Функция ПерерисовкаНазванийЗакладок()      
	
	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
	
КонецФункции // ПерерисовкаНазванийЗакладок	

//******************************************************************************   
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление видимостью элементов формы.
//
Процедура УправлениеДиалогом()
	
	Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда           
		Форма.ИспользоватьСлой("Шапка,Подвал,Основной1,Основной2,Разделитель"+СокрЛП(ВидРазделителя));
	Иначе     
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;
	                              
КонецПроцедуры //УправлениеДиалогом()

//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)
	
	Расшифровка.Установить("Обновить", Обновить);
	
	Возврат Расшифровка;
	
КонецФункции //РасшифровкаОбновить()                                            

//******************************************************************************
// ИзменениеПорядкаГрупп(НаправлениеСдвига)
//
// Параметры:
//  НаправлениеСдвига = 	-1 - вниз
//							 1 - вверх
// Возвращаемое значение: 
//	Нет
//
// Вызывается из формул элементов диалога:
//  кнопки "вверх" и "вниз" рядом со списком группировок
//
// Описание:  
//	Процедура производит сдвиг текущей группировки в общем
// 	списке группировок на "НаправлениеСдвига" позиций
//
Процедура ИзменениеПорядкаГрупп(НаправлениеСдвига)
	ТекСтр = Группировки.ТекущаяСтрока();
	ПослСдвигСтр = Группировки.РазмерСписка() - 1;
	Если ТекСтр <= ПослСдвигСтр Тогда
		Если не((НаправлениеСдвига = 1) и (ТекСтр = ПослСдвигСтр)) Тогда
			Группировки.СдвинутьЗначение(НаправлениеСдвига, ТекСтр);
		КонецЕсли;
	Иначе
		Предупреждение("Группировка ""По документам движения"" всегда находится
		               |в конце списка и не перемещается.",60);
	КонецЕсли;
КонецПроцедуры // ИзменениеПорядкаГрупп()

//******************************************************************************
// УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
//
// Параметры:  ТекстЗапроса - переданный по ссылке текст запроса
// 			   ТекстЗагол   - переданный по ссылке текст заголовка
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Дополняет строку запроса и заголовка в соответствии с выбранными группировками.
//    
Процедура УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
	СписокГруппировок = СоздатьОбъект("СписокЗначений");
	
	Для Сч=1 По Группировки.РазмерСписка() Цикл
		
		Если Группировки.Пометка(Сч)=1 Тогда
			ПредставлениеГрупп="";
			ТекстГрупп=Группировки.ПолучитьЗначение(Сч,ПредставлениеГрупп);
			Если  (ТекстГрупп = "ФизЛицо") Тогда
				ТекстБезГрупп = " без групп";
			Иначе
				ТекстБезГрупп = "";
			КонецЕсли;
			ТекстЗапроса 	= ТекстЗапроса 	+ "Группировка "+ТекстГрупп+ТекстБезГрупп+";";
			ТекстЗагол 		= ТекстЗагол 	+ ?(ТекстЗагол="",""," / ")+ПредставлениеГрупп;
			СписокГруппировок.ДобавитьЗначение(ТекстГрупп,ПредставлениеГрупп);
			Если ТекстГрупп = "Валюта" Тогда
				НомерГруппировкиПоВалюте = СписокГруппировок.РазмерСписка();
			КонецЕсли;    
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры  //УстановитьГруппировкиЗапроса()

//******************************************************************************
// ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка)
//
// Параметры:
//  Запрос - объект "Запрос", на основании которого строится отчет
//	Ном - Номер группировки запроса (Число)
//	НазваниеСекции - название секции, которую следует использовать (Строка)
//	ПечТекстСтроки - текстовое представление текущей строки
//	ТекРасшифровка - расшифровка текущей строки
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Производит вывод в печатную форму одной строки запроса.
//                                           
Процедура ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка)
	
	Если (Ном >= НомерГруппировкиПоВалюте) Тогда
		ПечВал	= Запрос.Валюта;
	Иначе     
		ПечВал 	= "";
	КонецЕсли; 
	ПечТип = ВалютаОтчета;
	
	//начальный остаток
	ПечНачОст	= глФРМ(Запрос.НачОст);
	ПечНачОстВ 	= глФРМ(Запрос.НачОстВ);
	Таб.ВывестиСекцию(НазваниеСекции+"|Начало");
	
	// приходы (по операциям)
	Приход		= Запрос.Приход;
	ПриходВ		= Запрос.ПриходВ;
	ВсегоСтрок 	= СписокОперДебет.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации 	= СписокОперДебет.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		ТекПриход 		= Запрос.ПолучитьАтрибут(НазвОперации+"Приход");
		ТекПриходВ 		= Запрос.ПолучитьАтрибут(НазвОперации+"ПриходВ");
		Приход			= Приход 	- ТекПриход;
		ПриходВ			= ПриходВ 	- ТекПриходВ;
		ПечПриход 		= глФРМ(ТекПриход);
		ПечПриходВ 		= глФРМ(ТекПриходВ);
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|Приход");
	КонецЦикла;                                                                 
	ПечПриход 	= глФРМ(Приход);
	ПечПриходВ 	= глФРМ(ПриходВ);
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|Приход");
	
	// расходы (по операциям)   
	Расход		= Запрос.Расход;
	РасходВ 	= Запрос.РасходВ;
	ВсегоСтрок 	= СписокОперКредит.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации 	= СписокОперКредит.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		ТекРасход 		= Запрос.ПолучитьАтрибут(НазвОперации+"Расход");
		ТекРасходВ		= Запрос.ПолучитьАтрибут(НазвОперации+"РасходВ");
		Расход			= Расход 	- ТекРасход; 
		РасходВ			= РасходВ 	- ТекРасходВ; 
		ПечРасход 		= глФРМ(ТекРасход);
		ПечРасходВ 		= глФРМ(ТекРасходВ);
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|Расход");
	КонецЦикла;
	ПечРасход 	= глФРМ(Расход);
	ПечРасходВ 	= глФРМ(РасходВ);
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|Расход");
	
	// конечный остаток
	ПечКонДолгНаш 		= ?(Запрос.КонОст  <0, глФРМ(-Запрос.КонОст  ),"");
	ПечКонДолгНашВ 		= ?(Запрос.КонОстВ <0, глФРМ(-Запрос.КонОстВ ),"");
	ПечКонДолгКлиента 	= ?(Запрос.КонОст  >0, глФРМ(Запрос.КонОст  ),"");
	ПечКонДолгКлиентаВ 	= ?(Запрос.КонОстВ >0, глФРМ(Запрос.КонОстВ ),"");
	
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|КонечныйОстаток");
	глОживить(1);

КонецПроцедуры // ПечатьСтроки()

//******************************************************************************
// ПечатьСтрокиДокумента(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка, ТекОст,ТекОстВ)
//
// Параметры:
//  Запрос - объект "Запрос", на основании которого строится отчет
//	Ном - Номер группировки запроса (Число)
//	НазваниеСекции - название секции, которую следует использовать (Строка)
//	ПечТекстСтроки - текстовое представление текущей строки
//	ТекРасшифровка - расшифровка текущей строки
//	ТекОст - 
//	ТекОстВ - 
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Производит вывод в печатную форму одной строки запроса по группировке "Документ".
//                                           
Процедура ПечатьСтрокиДокумента(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка, ТекОст,ТекОстВ)
	     
	Если (Ном >= НомерГруппировкиПоВалюте) Тогда
		ПечВал	= Запрос.Валюта;
	Иначе     
		ПечВал 	= "";
	КонецЕсли; 
	ПечТип = ВалютаОтчета;
	
	//начальный остаток
	Таб.ВывестиСекцию(НазваниеСекции+"|Начало");
	
	// приходы (по операциям)
	Приход		= Запрос.Приход;
	ПриходВ		= Запрос.ПриходВ;
	ТекОст 		= ТекОст 	+ Приход;
	ТекОстВ 	= ТекОстВ 	+ ПриходВ;
	ВсегоСтрок 	= СписокОперДебет.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации 	= СписокОперДебет.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		ТекПриход 		= Запрос.ПолучитьАтрибут(НазвОперации+"Приход");
		ТекПриходВ 		= Запрос.ПолучитьАтрибут(НазвОперации+"ПриходВ");
		Приход			= Приход 	- ТекПриход;
		ПриходВ			= ПриходВ 	- ТекПриходВ;
		ПечПриход 		= глФРМ(ТекПриход);
		ПечПриходВ 		= глФРМ(ТекПриходВ);
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|Приход");
	КонецЦикла;                                                                 
	ПечПриход 	= глФРМ(Приход);
	ПечПриходВ 	= глФРМ(ПриходВ);
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|Приход");
	
	// расходы (по операциям)   
	Расход		= Запрос.Расход;
	РасходВ 	= Запрос.РасходВ; 
	ТекОст 		= ТекОст 	- Расход;
	ТекОстВ 	= ТекОстВ 	- РасходВ;
	ВсегоСтрок 	= СписокОперКредит.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации 	= СписокОперКредит.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		ТекРасход 		= Запрос.ПолучитьАтрибут(НазвОперации+"Расход");
		ТекРасходВ		= Запрос.ПолучитьАтрибут(НазвОперации+"РасходВ");
		Расход			= Расход 	- ТекРасход; 
		РасходВ			= РасходВ 	- ТекРасходВ; 
		ПечРасход 		= глФРМ(ТекРасход);
		ПечРасходВ 		= глФРМ(ТекРасходВ);
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|Расход");
	КонецЦикла;
	ПечРасход 	= глФРМ(Расход);
	ПечРасходВ 	= глФРМ(РасходВ);
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|Расход");
	
	// конечный остаток
	ПечКонДолгНаш 		= ?(ТекОст  <0, глФРМ(-ТекОст  ),"");
	ПечКонДолгНашВ 		= ?(ТекОстВ <0, глФРМ(-ТекОстВ ),"");
	ПечКонДолгКлиента 	= ?(ТекОст  >0, глФРМ( ТекОст  ),"");
	ПечКонДолгКлиентаВ 	= ?(ТекОстВ >0, глФРМ( ТекОстВ ),"");
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|КонечныйОстаток");
	глОживить(1);

КонецПроцедуры // ПечатьСтрокиДокумента()

//******************************************************************************
// ВывестиГруппировку(Запрос,Ном)
//
// Параметры:
//  Запрос - объект "Запрос"
//	Ном - номер выводимой группировки
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Выводит в печатную форму одну группировку запроса. Если
//	Есть нижележащие группировки, они выводятся также с использованием рекурсивного
//	вызова этой же процедуры.
//
Процедура ВывестиГруппировку(Запрос,Ном)
	  
	Если Ном <= КоличествоГруппировок Тогда    
		
		НазваниеГруппировки = СписокГруппировок.ПолучитьЗначение(Ном);
		Если НазваниеГруппировки = "Документ" Тогда
			ТекОст 	= Запрос.НачОст;
			ТекОстВ = Запрос.НачОстВ;
		КонецЕсли;  
		
		Пока Запрос.Группировка(Ном) = 1 Цикл
		    
			Если НазваниеГруппировки = "Документ" Тогда
				НазваниеСекции="Документ";   
			Иначе                           
				НазваниеСекции="Строка"+Ном;
			КонецЕсли;       
			
			Если (Ном >= НомерГруппировкиПоВалюте) Тогда
				Если Запрос.Валюта<>ВалютаОтчета Тогда
					НазваниеСекции = НазваниеСекции + "В";
				КонецЕсли;
			КонецЕсли;       
			
			ПечТекстСтроки = Запрос.ПолучитьАтрибут(НазваниеГруппировки);
			Если (ТипЗначенияСтр(ПечТекстСтроки) = "Документ") или 
				 (ТипЗначенияСтр(ПечТекстСтроки) = "Справочник") Тогда
				ТекРасшифровка = ПечТекстСтроки;
			Иначе
				ТекРасшифровка = "";
			КонецЕсли;
			ПечТекстСтроки = ?(ПустоеЗначение(ПечТекстСтроки)=1,глПредставлениеПустогоЗначения(НазваниеГруппировки),ПечТекстСтроки);
		    
			Если НазваниеГруппировки = "Документ" Тогда
				ПечТекстСтроки = ""+глНазваниеДокументаВЖурнале(ПечТекстСтроки)+" № "+ПечТекстСтроки.НомерДок;
			ИначеЕсли НазваниеГруппировки = "КредДокумент" Тогда
				ПечТекстСтроки = ""+глПредставлениеДокумента(ПечТекстСтроки);
			КонецЕсли;
			
			Если НазваниеГруппировки = "Документ" Тогда
				ПечатьСтрокиДокумента(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка,ТекОст,ТекОстВ);
			Иначе
				ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка);
			КонецЕсли;
		
			// если есть более детальная группировка - выведем ее
			Если КоличествоГруппировок > Ном Тогда
				ВывестиГруппировку(Запрос,Ном+1);
			КонецЕсли;          
		
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ВывестиГруппировку()                                          

//******************************************************************************
// ЗаполнитьСпискиОпераций()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  В этой процедуры в списки операций прописываются значения кодов операций,
//	движения по которым мы хотели бы показать в отдельных колонках.
//	Операции задаются раздельно по дебету и кредиту
//
Процедура ЗаполнитьСпискиОпераций()

	СписокОперДебет = СоздатьОбъект("СписокЗначений");
	СписокОперКредит= СоздатьОбъект("СписокЗначений");
	Если ПоОперациям = 1 Тогда
		СписокОперДебет.ДобавитьЗначение(глКО.ВыдачаПодотчет);  
		СписокОперДебет.ДобавитьЗначение(глКО.КурсоваяРазница);
		
		СписокОперКредит.ДобавитьЗначение(глКО.АвансовыйОтчет);  
		СписокОперКредит.ДобавитьЗначение(глКО.ВозвратПодотчетныхСумм); 
		СписокОперКредит.ДобавитьЗначение(глКО.КурсоваяРазница);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСпискиОпераций()

//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог	
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   
//	Запускает отчет.
//
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;
	
	Если глПроверкаДаты(ДатаНачала,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;      
	
	ВалютаОтчета = ?(ВыбВидВалюты = 1, глРубли, глДоллары);
	
	Таб.ИсходнаяТаблица( "ВедомостьПоПодотчетникам" );
	
	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	Если глПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонца,Последовательность.ОсновнаяПоследовательность)=0 Тогда
		Возврат;
	КонецЕсли;	  
	
	ЗаполнитьСпискиОпераций();
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ВедомостьПоПодотчетникам");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1",ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2",ВыбРазделитель2);
	Расшифровка.Установить("ВыбРазделитель3",ВыбРазделитель3);
	Расшифровка.Установить("ВидРазделителя",ВидРазделителя);

	Расшифровка.Установить("ВыбФизЛицо", 	ВыбФизЛицо);
	Расшифровка.Установить("ПоОперациям", 	ПоОперациям);
	Расшифровка.Установить("ВыбВидВалюты", 	ВыбВидВалюты);
	
	Расшифровка.Установить("Группировки",	Группировки);

	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	// запомним МФ только если он задан
    Если глМножественныйФильтрЗадан(ТаблицаМФ) = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ); 
	КонецЕсли;
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Фирма			= Регистр.ПодотчетныеЛица.Фирма;
	|УпрАналитика 	= Регистр.ПодотчетныеЛица.Фирма.УпрАналитика;
	|ЮрЛицо 		= Регистр.ПодотчетныеЛица.Фирма.ЮрЛицо;
	|ФизЛицо 		= Регистр.ПодотчетныеЛица.ФизЛицо;
	|Валюта 		= Регистр.ПодотчетныеЛица.Валюта;
	|КодОперации 	= Регистр.ПодотчетныеЛица.КодОперации;
	|КредДокумент 	= Регистр.ПодотчетныеЛица.КредДокумент;
	|СуммаВ 		= Регистр.ПодотчетныеЛица.СуммаВал;";
	
	Если ВыбВидВалюты = 1 Тогда
		ТекстЗапроса = ТекстЗапроса + "Сумма = Регистр.ПодотчетныеЛица.СуммаРуб;";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "Сумма = Регистр.ПодотчетныеЛица.СуммаУпр;";
	КонецЕсли;
		
	ТекстЗапроса = ТекстЗапроса +
	"Функция НачОст = НачОст(Сумма);
	|Функция Приход = Приход(Сумма);
	|Функция Расход = Расход(Сумма);
	|Функция КонОст = КонОст(Сумма);";
	
	ТекстЗапроса = ТекстЗапроса +
	"Функция НачОстВ = НачОст(СуммаВ);
	|Функция ПриходВ = Приход(СуммаВ);
	|Функция РасходВ = Расход(СуммаВ);
	|Функция КонОстВ = КонОст(СуммаВ);";       
	
	Для СчЦикла=1 по СписокОперДебет.РазмерСписка() Цикл
		НазвОперации = СписокОперДебет.ПолучитьЗначение(СчЦикла).Идентификатор();
		ТекстЗапроса = ТекстЗапроса + РазделительСтрок+
		"Функция "+НазвОперации+"Приход  = Приход(Сумма)  когда (КодОперации = глКО."+НазвОперации+");"+
		"Функция "+НазвОперации+"ПриходВ = Приход(СуммаВ) когда (КодОперации = глКО."+НазвОперации+");";
	КонецЦикла;
	Для СчЦикла=1 по СписокОперКредит.РазмерСписка() Цикл
		НазвОперации = СписокОперКредит.ПолучитьЗначение(СчЦикла).Идентификатор();
		ТекстЗапроса = ТекстЗапроса + РазделительСтрок+
		"Функция "+НазвОперации+"Расход  = Расход(Сумма)  когда (КодОперации = глКО."+НазвОперации+");"+
		"Функция "+НазвОперации+"РасходВ = Расход(СуммаВ) когда (КодОперации = глКО."+НазвОперации+");";
	КонецЦикла;
	
	Загол="";
	
	НетОш = 1; // нет ошибок при наложении фильтров
	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",ВыбРазделитель3,"ВыбРазделитель3",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
	КонецЕсли;
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ФизЛицо",ВыбФизЛицо,"ВыбФизЛицо",ТекстЗапроса,Загол);
	
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;
	                           
	НомерГруппировкиПоВалюте	= 9999; // невозможно большое значение
	
	ПечЗаголовокСтолбца = "";  
	ПечЗаголовок = "Ведомость по подотчетным лицам: "+ВалютаОтчета;
	УстановитьГруппировкиЗапроса(ТекстЗапроса, ПечЗаголовокСтолбца);
	КоличествоГруппировок = СписокГруппировок.РазмерСписка();
	
	Если КоличествоГруппировок > 5 Тогда
		Предупреждение("Нельзя сделать больше 5 группировок!",60);
		Возврат;
	КонецЕсли;
	
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;       
	
	глЧислоСтрок = 0;

	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	Таб.ВывестиСекцию("ШапкаТаблицы|Начало");

	// выводим шапку на каждой странице
	НачПовт = Таб.ВысотаСекции("Кнопки") + Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	// сформируем шапку, исходя из выбранной формы представления данных
	ВсегоСтрок = СписокОперДебет.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации = СписокОперДебет.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		Если (Запрос.ПолучитьАтрибут(НазвОперации+"Приход")=0) и 
			 (Запрос.ПолучитьАтрибут(НазвОперации+"ПриходВ")=0) Тогда
			// уберем лишние горизонтальные группировки (по которым нет итогов)
			СписокОперДебет.УдалитьЗначение(ВсегоСтрок - СчЦикла + 1);
		Иначе
			НазвОперации = СписокОперДебет.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1);
			Таб.ПрисоединитьСекцию("ШапкаТаблицы|Приход");
		КонецЕсли;
	КонецЦикла;                                                                 
	НазвОперации = ?(ПоОперациям=1,"Увеличение долга - прочее","Увеличение долга");
	Таб.ПрисоединитьСекцию("ШапкаТаблицы|Приход");
	
	ВсегоСтрок = СписокОперКредит.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации = СписокОперКредит.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		Если (Запрос.ПолучитьАтрибут(НазвОперации+"Расход")=0) И
			 (Запрос.ПолучитьАтрибут(НазвОперации+"РасходВ")=0) Тогда             
			// уберем лишние горизонтальные группировки (по которым нет итогов)
			СписокОперКредит.УдалитьЗначение(ВсегоСтрок - СчЦикла + 1);
		Иначе
			НазвОперации = СписокОперКредит.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1);
			Таб.ПрисоединитьСекцию("ШапкаТаблицы|Расход");
		КонецЕсли;
	КонецЦикла;
	НазвОперации = ?(ПоОперациям=1,"Уменьшение долга - прочее","Уменьшение долга");
	Таб.ПрисоединитьСекцию("ШапкаТаблицы|Расход");
	
	Таб.ПрисоединитьСекцию("ШапкаТаблицы|КонечныйОстаток");
	глОживить(1);
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 3, "ВедомостьПоПодотчетникам", "ВедомостьПоПодотчетникам");
	
	// ВЫВОД ГРУППИРОВОК ЗАПРОСА
	ВывестиГруппировку(Запрос,1);
	
	// Заполнение полей "Итого"
	ПечатьСтроки(Запрос,0,"Итого","","");                  
	
	Таб.ОбластьПечати(3);
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Ведомость по подотчетникам", "");
	                                     
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры  // Сформировать()     

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура       
//
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	//                  тип          вид           переменная  название
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ФизЛица", "ФизЛицо",  "По физ. лицам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Фирмы", "Фирма",  "По фирмам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","СвоиЮрЛица", "ЮрЛицо",  "По юр. лицам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","УпрАналитика", "УпрАналитика",  "По упр. аналитике");

	Если ФлагВосстановленияНастройки = 0 Тогда

		ВидРазделителя = 1;
		ВыбВидВалюты   = 1;		
		ДатаНачала      = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
		Если ПустоеЗначение(ДатаНачала) = 1 Тогда
			ДатаНачала      = НачМесяца(ДатаКонца);    
		КонецЕсли;
	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВидРазделителя	= глРасшифровка.Получить("ВидРазделителя");
		ВыбРазделитель1	= глРасшифровка.Получить("ВыбРазделитель1");
		ВыбРазделитель2	= глРасшифровка.Получить("ВыбРазделитель2");
		ВыбРазделитель3	= глРасшифровка.Получить("ВыбРазделитель3");

		ВыбФизЛицо 		= глРасшифровка.Получить("ВыбФизЛицо");
		
		ПоОперациям 	= глРасшифровка.Получить("ПоОперациям");
		
		ВыбВидВалюты 	= глРасшифровка.Получить("ВыбВидВалюты");
		
		глРасшифровка.Получить("Группировки").Выгрузить(Группировки);        

		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;
		
	 	Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;                      
	
	ПерерисовкаНазванийЗакладок();

	ТаблицаМФ.ВидимостьКолонки("Тип",0);
	ТаблицаМФ.ВидимостьКолонки("Вид",0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");

	УправлениеДиалогом();
	
КонецПроцедуры		// ПриОткрытии()       

//******************************************************************************
// Предопределенная процедура.
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	ПерерисовкаНазванийЗакладок();
	УправлениеДиалогом();       
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	
КонецПроцедуры // ВводНового()
 
//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореЗакладки(Номер,Значение)	
	// закладки
    Если Номер=1 Тогда
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	КонецЕсли;      
	УправлениеДиалогом();
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки
                                       
//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(Значение)  
	
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ДатаКонца = ПолучитьДатуТА();

// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
                                                                                
ТекСтрокаВТаблице="";

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);

Группировки.ДобавитьЗначение("Фирма", 		"Фирма");
Группировки.ДобавитьЗначение("Валюта", 		"Валюта");
Группировки.ДобавитьЗначение("ФизЛицо", 	"Физ. лицо");
Группировки.ДобавитьЗначение("КредДокумент","Кредитный документ");
Группировки.ДобавитьЗначение("Документ", 	"Документы движения");

Группировки.Пометка(2, 1);
Группировки.Пометка(3, 1);
