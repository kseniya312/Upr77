////////////////////////////////////////////////////////////////////////////////
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 

Перем ТаблицаАктивов, ТаблицаПассивов;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Форма.ИспользоватьСлой("Основной1,Основной2,Разделитель"+СокрЛП(ВидРазделителя));
	
КонецПроцедуры //УправлениеДиалогом() 

//******************************************************************************
// АбсЗнач(Числ)
//
// Параметры: Числ - числовое значение
//
// Возвращаемое значение: Абсолютное значение переданного числа
//
// Описание: вычисление абсолютного значения числа

Функция АбсЗнач(Числ)
	Если Числ<0 Тогда
		Возврат (Числ * (-1));
	Иначе     
		Возврат (Числ);
	КонецЕсли;
КонецФункции                                                                    

//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//   помещает значение параметра в список Расшифровка  и возвращает этот список
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции //РасшифровкаОбновить()                                            

//******************************************************************************
// ВывестиПериод()
//
// Параметры:
//
// Возвращаемое значение: НЕТ
//
// Описание:

Процедура ВывестиПериод (Запрос,Диаграмма,ВидДиаграммы,ПериодСтрокой,НомерПериода,Суффикс)
	
	Всего = 0;
	Показатель = Запрос.ПолучитьАтрибут("СуммаПостУпр"+Суффикс);
	Если ((ВидДиаграммы = "Активы") и (Показатель>0))  или
	((ВидДиаграммы = "Пассивы") и (Показатель<0))Тогда
		Показатель = АбсЗнач(Показатель);
		Расшифровка = "Поставщики на "+ПериодСтрокой+": "+глФРМ(Показатель,глДоллары,1);
		Диаграмма.УстановитьЗначение(НомерПериода,2,Показатель,Расшифровка);	// точка, серия, знач, расш	
		Всего = Всего + Показатель;
	КонецЕсли;                      
	
	Показатель = Запрос.ПолучитьАтрибут("СуммаПокупУпр"+Суффикс);
	Если ((ВидДиаграммы = "Активы") и (Показатель>0))  или
	((ВидДиаграммы = "Пассивы") и (Показатель<0))Тогда
		Показатель = АбсЗнач(Показатель);
		Расшифровка = "Покупатели на "+ПериодСтрокой+": "+глФРМ(Показатель,глДоллары,1);
		Диаграмма.УстановитьЗначение(НомерПериода,3,Показатель,Расшифровка);	// точка, серия, знач, расш	
		Всего = Всего + Показатель;
	КонецЕсли;                      
	
	Показатель = Запрос.ПолучитьАтрибут("СуммаТМЦУпр"+Суффикс);
	Если ((ВидДиаграммы = "Активы") и (Показатель>0))  или
	((ВидДиаграммы = "Пассивы") и (Показатель<0))Тогда
		Показатель = АбсЗнач(Показатель);
		Расшифровка = "ТМЦ на балансе на "+ПериодСтрокой+": "+глФРМ(Показатель,глДоллары,1);
		Диаграмма.УстановитьЗначение(НомерПериода,4,Показатель,Расшифровка);	// точка, серия, знач, расш	
		Всего = Всего + Показатель;
	КонецЕсли;                      
	
	Показатель = Запрос.ПолучитьАтрибут("СуммаОтданоУпр"+Суффикс);
	Если ((ВидДиаграммы = "Активы") и (Показатель>0))  или
	((ВидДиаграммы = "Пассивы") и (Показатель<0))Тогда
		Показатель = АбсЗнач(Показатель);
		Расшифровка = "ТМЦ  (отданы) на "+ПериодСтрокой+": "+глФРМ(Показатель,глДоллары,1);
		Диаграмма.УстановитьЗначение(НомерПериода,5,Показатель,Расшифровка);	// точка, серия, знач, расш	
		Всего = Всего + Показатель;
	КонецЕсли;                      
	
	Показатель = Запрос.ПолучитьАтрибут("СуммаКассаУпр"+Суффикс);
	Если ((ВидДиаграммы = "Активы") и (Показатель>0))  или
	((ВидДиаграммы = "Пассивы") и (Показатель<0))Тогда
		Показатель = АбсЗнач(Показатель);
		Расшифровка = "Деньги в кассе на "+ПериодСтрокой+": "+глФРМ(Показатель,глДоллары,1);
		Диаграмма.УстановитьЗначение(НомерПериода,6,Показатель,Расшифровка);	// точка, серия, знач, расш	
		Всего = Всего + Показатель;
	КонецЕсли;                      
	
	Показатель = Запрос.ПолучитьАтрибут("СуммаБанкУпр"+Суффикс);
	Если ((ВидДиаграммы = "Активы") и (Показатель>0))  или
	((ВидДиаграммы = "Пассивы") и (Показатель<0))Тогда
		Показатель = АбсЗнач(Показатель);
		Расшифровка = "Деньги в банке на "+ПериодСтрокой+": "+глФРМ(Показатель,глДоллары,1);
		Диаграмма.УстановитьЗначение(НомерПериода,7,Показатель,Расшифровка);	// точка, серия, знач, расш	
		Всего = Всего + Показатель;
	КонецЕсли;                      
	
	Показатель = Запрос.ПолучитьАтрибут("СуммаПодотчУпр"+Суффикс);
	Если ((ВидДиаграммы = "Активы") и (Показатель>0))  или
	((ВидДиаграммы = "Пассивы") и (Показатель<0))Тогда
		Показатель = АбсЗнач(Показатель);
		Расшифровка = "Подотчетные лица на "+ПериодСтрокой+": "+глФРМ(Показатель,глДоллары,1);
		Диаграмма.УстановитьЗначение(НомерПериода,8,Показатель,Расшифровка);	// точка, серия, знач, расш	
		Всего = Всего + Показатель;
	КонецЕсли;                      
	
	Расшифровка = "Всего на "+ПериодСтрокой+": "+глФРМ(Всего,глДоллары,1);
	Диаграмма.УстановитьЗначение(НомерПериода,1,Всего,Расшифровка);	// точка, серия, знач, расш	
	

КонецПроцедуры

//******************************************************************************
// ПостроитьДиаграмму(Диаграмма,Запрос)
// 
// Параметры: 	
//			Диаграмма - ссылка на объект "Диаграмма"
// 			Запрос - ссылка на объект "Запрос"
//			ВидДиаграммы - строка ("Прибыли" / "Убытки")
// 
// Возвращаемое значение: НЕТ
//
// Описание: Содержит алгоритм построения диаграммы на основании данных запроса

Процедура ПостроитьДиаграмму(Диаграмма,Запрос, ВидДиаграммы)
	
	Диаграмма.Обновление(0);  
	ЧислоПериодов = 0;  
	
	Диаграмма.КоличествоСерий(8); // (в одном столбце)
	Диаграмма.УстановитьИмяСерии(1,"<Всего>");
	Диаграмма.УстановитьИмяСерии(2,"Поставщики");
	Диаграмма.УстановитьИмяСерии(3,"Покупатели");
	Диаграмма.УстановитьИмяСерии(4,"ТМЦ на балансе");
	Диаграмма.УстановитьИмяСерии(5,"ТМЦ (отданы)");
	Диаграмма.УстановитьИмяСерии(6,"Деньги в кассе");
	Диаграмма.УстановитьИмяСерии(7,"Деньги в банке");
	Диаграмма.УстановитьИмяСерии(8,"Подотчетные лица");
	   
	Пока Запрос.Группировка(1) = 1 Цикл // по периодам
		
		Если ЧислоПериодов=0 Тогда
			// для первого периода надо вывести состояние на начало.
			ПериодСтрокой = Запрос.НачалоПериода();
			ЧислоПериодов = ЧислоПериодов+1;
			Диаграмма.УстановитьИмяТочки(ЧислоПериодов,ПериодСтрокой);	
			ВывестиПериод (Запрос,Диаграмма,ВидДиаграммы,ПериодСтрокой,ЧислоПериодов,"НачОст");
		КонецЕсли;
		
		ПериодСтрокой = Запрос.КонецПериода();
		ЧислоПериодов = ЧислоПериодов+1;
		Диаграмма.УстановитьИмяТочки(ЧислоПериодов,ПериодСтрокой);	                
		ВывестиПериод (Запрос,Диаграмма,ВидДиаграммы,ПериодСтрокой,ЧислоПериодов,"КонОст");
	КонецЦикла;
	
	Диаграмма.Обновление(1);    
	 
КонецПроцедуры // ПостроитьДиаграмму()  
    
//******************************************************************************
// ВывестиПериодГрафика(Запрос,Диаграмма,ПериодСтрокой,НомерПериода,Суффикс)
//
// Параметры:
//
// Возвращаемое значение:
//
// Описание:

Процедура ВывестиПериодГрафика(Запрос,Диаграмма,ПериодСтрокой,НомерПериода,Суффикс)
        
	Активы = 0;
	Пассивы = 0;
		
	Показатель = Запрос.ПолучитьАтрибут("СуммаПостУпр"+Суффикс);
	Если (Показатель<0) Тогда
		Пассивы = Пассивы + АбсЗнач(Показатель);
	Иначе
		Активы = Активы + АбсЗнач(Показатель);
	КонецЕсли;                      
	Показатель = Запрос.ПолучитьАтрибут("СуммаПокупУпр"+Суффикс);
	Если (Показатель<0) Тогда
		Пассивы = Пассивы + АбсЗнач(Показатель);
	Иначе
		Активы = Активы + АбсЗнач(Показатель);
	КонецЕсли;                      
	Показатель = Запрос.ПолучитьАтрибут("СуммаТМЦУпр"+Суффикс);
	Если (Показатель<0) Тогда
		Пассивы = Пассивы + АбсЗнач(Показатель);
	Иначе
		Активы = Активы + АбсЗнач(Показатель);
	КонецЕсли;                      
	Показатель = Запрос.ПолучитьАтрибут("СуммаОтданоУпр"+Суффикс);
	Если (Показатель<0) Тогда
		Пассивы = Пассивы + АбсЗнач(Показатель);
	Иначе
		Активы = Активы + АбсЗнач(Показатель);
	КонецЕсли;                      
	Показатель = Запрос.ПолучитьАтрибут("СуммаКассаУпр"+Суффикс);
	Если (Показатель<0) Тогда
		Пассивы = Пассивы + АбсЗнач(Показатель);
	Иначе
		Активы = Активы + АбсЗнач(Показатель);
	КонецЕсли;                      
	Показатель = Запрос.ПолучитьАтрибут("СуммаБанкУпр"+Суффикс);
	Если (Показатель<0) Тогда
		Пассивы = Пассивы + АбсЗнач(Показатель);
	Иначе
		Активы = Активы + АбсЗнач(Показатель);
	КонецЕсли;                      
	Показатель = Запрос.ПолучитьАтрибут("СуммаПодотчУпр"+Суффикс);
	Если (Показатель<0) Тогда
		Пассивы = Пассивы + АбсЗнач(Показатель);
	Иначе
		Активы = Активы + АбсЗнач(Показатель);
	КонецЕсли;                      
	
	Коэфф = ?(Пассивы=0,0,Активы/Пассивы);
	Расшифровка = "На "+ПериодСтрокой+": "+глФРМ(Коэфф,,0);
	Диаграмма.УстановитьЗначение(НомерПериода,1,Коэфф,Расшифровка);	// точка, серия, знач, расш	
	
КонецПроцедуры //ВывестиПериодГрафика()

//******************************************************************************
// ПостроитьГрафик(Диаграмма,Запрос)
//
// Параметры: 	Диаграмма - ссылка на объект "Диаграмма"
// 				Запрос - ссылка на объект "Запрос"
//
// Возвращаемое значение: НЕТ
//
// Описание: Содержит алгоритм построения графика коэфф. на основании запроса

Процедура ПостроитьГрафик(Диаграмма,Запрос)
	
	
	Диаграмма.Обновление(0);  
	ЧислоПериодов = 0;  
	
	Пока Запрос.Группировка(1) = 1 Цикл // по периодам
		          
		Если ЧислоПериодов = 0 Тогда
			ПериодСтрокой = Запрос.НачалоПериода();
			ЧислоПериодов = ЧислоПериодов+1;
			Диаграмма.УстановитьИмяТочки(ЧислоПериодов,ПериодСтрокой);	
			ВывестиПериодГрафика(Запрос,Диаграмма,ПериодСтрокой,ЧислоПериодов,"НачОст");
		КонецЕсли;
		
		ПериодСтрокой = Запрос.КонецПериода();
		ЧислоПериодов = ЧислоПериодов+1;
		Диаграмма.УстановитьИмяТочки(ЧислоПериодов,ПериодСтрокой);	
		ВывестиПериодГрафика(Запрос,Диаграмма,ПериодСтрокой,ЧислоПериодов,"КонОст");
	    
	КонецЦикла;
	
	Диаграмма.Обновление(1);    
	
КонецПроцедуры // ПостроитьГрафик()
                      
//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог
//
// Возвращаемое значение: НЕТ
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   запускает отчет
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;
	
	Если глПроверкаДаты(ДатаНачала,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;      
	                                        
	Если СписВидОтчета.ТекущаяСтрока()=1 Тогда //гистограмма
		Таб.ИсходнаяТаблица( "ДинамикаОборотныхСредствГи" );
	ИначеЕсли СписВидОтчета.ТекущаяСтрока()=2 Тогда // график
		Таб.ИсходнаяТаблица( "ДинамикаОборотныхСредствГра" );
	Иначе // с областями
		Таб.ИсходнаяТаблица( "ДинамикаОборотныхСредствОбл" );
	КонецЕсли; 
	
	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	Если глПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонца,Последовательность.ОсновнаяПоследовательность)=0 Тогда
		Возврат;
	КонецЕсли;	  
	
	    
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ДинамикаОборотныхСредств");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1",ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2",ВыбРазделитель2);
	Расшифровка.Установить("ВыбРазделитель3",ВыбРазделитель3);
	Расшифровка.Установить("ВидРазделителя",ВидРазделителя);

	Расшифровка.Установить("СписПериоды",	СписПериоды.ТекущаяСтрока());
	Расшифровка.Установить("СписВидОтчета",	СписВидОтчета.ТекущаяСтрока());
	               
	СтрПериодичность= СписПериоды.ПолучитьЗначение(СписПериоды.ТекущаяСтрока());
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Фирма = 	Регистр.Поставщики.Фирма, 
	|			Регистр.Покупатели.Фирма, 
	|			Регистр.ПартииНаличие.Фирма, 
	|			Регистр.ПартииОтданные.Фирма, 
	|			Регистр.Касса.Фирма, 
	|			Регистр.Банк.Фирма, 
	|			Регистр.ПодотчетныеЛица.Фирма;
	|ЮрЛицо = 	Регистр.Поставщики.Фирма.ЮрЛицо, 
	|			Регистр.Покупатели.Фирма.ЮрЛицо, 
	|			Регистр.ПартииНаличие.Фирма.ЮрЛицо, 
	|			Регистр.ПартииОтданные.Фирма.ЮрЛицо, 
	|			Регистр.Касса.Фирма.ЮрЛицо, 
	|			Регистр.Банк.Фирма.ЮрЛицо, 
	|			Регистр.ПодотчетныеЛица.Фирма.ЮрЛицо;
	|УпрАналитика = Регистр.Поставщики.Фирма.УпрАналитика, 
	|				Регистр.Покупатели.Фирма.УпрАналитика, 
	|				Регистр.ПартииНаличие.Фирма.УпрАналитика, 
	|				Регистр.ПартииОтданные.Фирма.УпрАналитика, 
	|				Регистр.Касса.Фирма.УпрАналитика, 
	|				Регистр.Банк.Фирма.УпрАналитика, 
	|				Регистр.ПодотчетныеЛица.Фирма.УпрАналитика;
	|СуммаПостУпр = Регистр.Поставщики.СуммаУпр;
	|СуммаПокупУпр = Регистр.Покупатели.СуммаУпр;
	|СуммаТМЦУпр = Регистр.ПартииНаличие.СуммаУпр;
	|СтатусПартии = Регистр.ПартииНаличие.СтатусПартии,Регистр.ПартииОтданные.СтатусПартии;
	|СуммаОтданоУпр = Регистр.ПартииОтданные.СуммаУпр;
	|СуммаКассаУпр = Регистр.Касса.СуммаУпр;
	|СуммаБанкУпр = Регистр.Банк.СуммаУпр;
	|СуммаПодотчУпр = Регистр.ПодотчетныеЛица.СуммаУпр;
	|
	|Функция СуммаПостУпрКонОст 	= КонОст(СуммаПостУпр);
	|Функция СуммаПокупУпрКонОст 	= КонОст(СуммаПокупУпр);
	|Функция СуммаТМЦУпрКонОст 		= КонОст(СуммаТМЦУпр);
	|Функция СуммаОтданоУпрКонОст 	= КонОст(СуммаОтданоУпр);
	|Функция СуммаКассаУпрКонОст 	= КонОст(СуммаКассаУпр);
	|Функция СуммаБанкУпрКонОст 	= КонОст(СуммаБанкУпр);
	|Функция СуммаПодотчУпрКонОст 	= КонОст(СуммаПодотчУпр);
	|
	|Функция СуммаПостУпрНачОст 	= НачОст(СуммаПостУпр);
	|Функция СуммаПокупУпрНачОст 	= НачОст(СуммаПокупУпр);
	|Функция СуммаТМЦУпрНачОст 		= НачОст(СуммаТМЦУпр);
	|Функция СуммаОтданоУпрНачОст 	= НачОст(СуммаОтданоУпр);
	|Функция СуммаКассаУпрНачОст 	= НачОст(СуммаКассаУпр);
	|Функция СуммаБанкУпрНачОст 	= НачОст(СуммаБанкУпр);
	|Функция СуммаПодотчУпрНачОст 	= НачОст(СуммаПодотчУпр);
	|Условие (СтатусПартии<>глСП.Т_Принятый);
	|"//}}ЗАПРОС
	;                     
	ТекстЗапроса = ТекстЗапроса +
	"
	|Группировка "+СтрПериодичность+" все;";
	
	Загол="";
	НетОш = 1; // нет ошибок при наложении фильтров
	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "УпрАналитика",ВыбРазделитель3,"ВыбРазделитель3",ТекстЗапроса,Загол);
	КонецЕсли;    

	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ПечЗаголовок = "Динамика оборотных средств";
	
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;

	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");      
	Таб.ВывестиСекцию("Диаграмма");      
	                         
	Таб.Опции(0, 0, 2, 0, "ДинамикаОборотныхСредств", "ДинамикаОборотныхСредств");
	             
	Таб.ОбластьПечати(3);
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Динамика оборотных средств", ""); 
	
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ

//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	Если ФлагВосстановленияНастройки = 0 Тогда
		
		ВидОбъекта     = 1;
		ВыбПоказатель  = 1;
		ВидРазделителя = 1;
         
		ДатаНачала      = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
		Если ПустоеЗначение(ДатаНачала) = 1 Тогда
			ДатаНачала      = НачМесяца(ДатаКонца);    
		КонецЕсли;
	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВидРазделителя	= глРасшифровка.Получить("ВидРазделителя");
		ВыбРазделитель1	= глРасшифровка.Получить("ВыбРазделитель1");
		ВыбРазделитель2	= глРасшифровка.Получить("ВыбРазделитель2");
		ВыбРазделитель3	= глРасшифровка.Получить("ВыбРазделитель3");
		
		СписПериоды.ТекущаяСтрока(	глРасшифровка.Получить("СписПериоды"));
		СписВидОтчета.ТекущаяСтрока(глРасшифровка.Получить("СписВидОтчета"));
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;                      

	УправлениеДиалогом();
	
КонецПроцедуры		// ПриОткрытии()       

//******************************************************************************
// Предопределенная процедура.
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	УправлениеДиалогом();

КонецПроцедуры // ВводНового()  
                                                                                
////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
		СписПериоды.ДобавитьЗначение("Год");
		СписПериоды.ДобавитьЗначение("Квартал");
		СписПериоды.ДобавитьЗначение("Месяц");
		СписПериоды.ДобавитьЗначение("Неделя");
		СписПериоды.ДобавитьЗначение("День");
		СписПериоды.ТекущаяСтрока(3);
		
		СписВидОтчета.ДобавитьЗначение("Гистограмма");
		СписВидОтчета.ДобавитьЗначение("График");
		СписВидОтчета.ДобавитьЗначение("С областями");
		СписВидОтчета.ТекущаяСтрока(1);

ДатаКонца=ПолучитьДатуТА();