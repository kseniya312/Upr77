Перем ТекСтрокаВТаблице; // текущая строка в таблице значений  МФ
Перем Таб;                       
Перем ДатаПериода,ДатаПериода1,ДатаПериода2;                       
//---------------------------------------------------
Процедура ПроверитьПериод(ДатаНачала,ДатаКонца) Экспорт
	Если ДатаКонца>ПолучитьДатуТА() Тогда
		Предупреждение ("Дата конца отчета не может быть больше ТА итогов!!!");
		ДатаКонца=ПолучитьДатуТА();
		Возврат;
	КонецЕсли;
	Если ДатаНачала>ДатаКонца Тогда
		Предупреждение ("Дата начала отчета не может быть позже даты конца!!!");
		ДатаНачала=ДатаКонца;
		Возврат;
	КонецЕсли;
КонецПроцедуры
//---------------------------------------------
Функция ПоказПериодаОтчета() 
	ДатаНачала=НачМесяца(ДатаПериода);
	ДатаКонца=КонМесяца(ДатаПериода);	
	ПериодОтчета=ПериодСтр(ДатаНачала, ДатаКонца);
	ДатаНачала=НачМесяца(ДатаПериода1);
	ДатаКонца=КонМесяца(ДатаПериода1);	
	ПериодОтчета1=ПериодСтр(ДатаНачала, ДатаКонца);
	ДатаНачала=НачМесяца(ДатаПериода2);
	ДатаКонца=КонМесяца(ДатаПериода2);	
	ПериодОтчета2=ПериодСтр(ДатаНачала, ДатаКонца);
	Возврат "";
КонецФункции
//---------------------------------------------
Процедура ПлюсПериод(Реквизит)
	Если Реквизит=0 Тогда
		Если КонМесяца(ДатаПериода)+1>ПолучитьДатуТА() Тогда
			Предупреждение("Период вне зоны ТА!",3);
		Иначе
			ДатаПериода=КонМесяца(ДатаПериода)+1;
		КонецЕсли;
	ИначеЕсли Реквизит=1 Тогда
		Если КонМесяца(ДатаПериода1)+1>ПолучитьДатуТА() Тогда
			Предупреждение("Период вне зоны ТА!",3);
		Иначе
			ДатаПериода1=КонМесяца(ДатаПериода1)+1;
		КонецЕсли;
	Иначе
		Если КонМесяца(ДатаПериода2)+1>ПолучитьДатуТА() Тогда
			Предупреждение("Период вне зоны ТА!",3);
		Иначе
			ДатаПериода2=КонМесяца(ДатаПериода2)+1;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры
//---------------------------------------------
Процедура МинусПериод(Реквизит)
	Если Реквизит=0 Тогда
		ДатаПериода=НачМесяца(ДатаПериода)-1;
	ИначеЕсли Реквизит=1 Тогда
		ДатаПериода1=НачМесяца(ДатаПериода1)-1;
	Иначе
		ДатаПериода2=НачМесяца(ДатаПериода2)-1;
	КонецЕсли;	
КонецПроцедуры
//---------------------------------------------
Процедура ПриЗакрытии()
	СохранитьЗначение("ОтчетОборачиваемостьТовара_ВидОтчета",ВидОтчета);
	СохранитьЗначение("ОтчетОборачиваемостьТовара_ТипРасчета",ТипРасчета);
КонецПроцедуры
//-------------------------
Процедура ПриОткрытии()
	ВидОтчета=ВосстановитьЗначение("ОтчетОборачиваемостьТовара_ВидОтчета");
	Если ПустоеЗначение(ВидОтчета)=1 Тогда
		ВидОтчета=1;
	КонецЕсли;	
	ТипРасчета=ВосстановитьЗначение("ОтчетОборачиваемостьТовара_ТипРасчета");
	Если ПустоеЗначение(ТипРасчета)=1 Тогда
		ТипРасчета=1;
	КонецЕсли;	
	Условие=ВосстановитьЗначение("ОтчетОборачиваемостьТовара_Условие");
	Если ПустоеЗначение(Условие)=1 Тогда
		Условие=1;
	КонецЕсли;
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Номенклатура", 	"Номенклатура",  "По номенклатуре");          
КонецПроцедуры
//------------------------ 
Функция ДоступностьРеквизитов()
	Если ВидОтчета=1 Тогда
		Форма.Минус.Доступность(1);
		Форма.Плюс.Доступность(1);
		Форма.Минус1.Доступность(0);
		Форма.Плюс1.Доступность(0);
		Форма.Минус2.Доступность(0);
		Форма.Плюс2.Доступность(0);
	Иначе	        
		Форма.Минус.Доступность(0);
		Форма.Плюс.Доступность(0);
		Форма.Минус1.Доступность(1);
		Форма.Плюс1.Доступность(1);
		Форма.Минус2.Доступность(1);
		Форма.Плюс2.Доступность(1);
	КонецЕсли;
	Возврат "";
КонецФункции
//----------------------
Процедура Сформировать()
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Оборачиваемость");
	Если ВидОтчета=1 Тогда
		Месяц="";
		Шапка="Оборачиваемость товаров за "+ПериодОтчета;
		Таб.ВывестиСекцию("Шапка");
		ДатаНачала=НачМесяца(ДатаПериода);
		ДатаКонца=КонМесяца(ДатаПериода);	
		Запрос=СоздатьОбъект("Запрос");
		ТекстЗапроса="Период С ДатаНачала";
		Если ДатаКонца>=ПолучитьДатуТА() Тогда
			ТекстЗапроса=ТекстЗапроса+";";
		Иначе
			ТекстЗапроса=ТекстЗапроса+" По ДатаКонца;";
		КонецЕсли;     
		ТекстЗапроса=ТекстЗапроса+"
		|Номенклатура=Регистр.ПартииНаличие.Номенклатура;";
		Если ТипРасчета=1 Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|Кол=Регистр.ПартииНаличие.Количество;
			|Функция Нач=НачОст(Кол);
			|Функция Кон=КонОст(Кол);
			|Функция Расх=Расход(Кол);";
		Иначе
			ТекстЗапроса=ТекстЗапроса+"
			|СуммаРублевая=Регистр.ПартииНаличие.СуммаРуб;
			|Функция Нач=НачОст(СуммаРублевая);
			|Функция Кон=КонОст(СуммаРублевая);
			|Функция Расх=Расход(СуммаРублевая);";
		КонецЕсли;
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка Номенклатура без групп;
		|Группировка День Все ВошедшиеВЗапрос;";
		Если СписокЭлементовМФ.РазмерСписка()>0 Тогда
			Если ТипМФ.ТекущаяСтрока()=1 Тогда // принадлежит списку
				ТекстЗапроса = ТекстЗапроса+" Условие (Номенклатура в СписокЭлементовМФ);";
			Иначе     
				ТекстЗапроса = ТекстЗапроса+" Условие (НЕ(Номенклатура в СписокЭлементовМФ));";
			КонецЕсли;
		КонецЕсли;	
		Флаг=Запрос.Выполнить(ТекстЗапроса);
		Если Флаг=0 Тогда
			Предупреждение ("Запрос не выполнился !!!");
			Возврат;
		КонецЕсли;        
		Пока Запрос.Группировка(1) = 1 Цикл
			Товар=Запрос.Номенклатура;
			ПечТовар="" + Товар.Наименование + ?(Константа.ПоказыватьАртикул = 1, " " + СокрЛП(Товар.Артикул), "");
			ПечКод=Товар.Код;
			Дней=0;
			СредниеЗапасы=0;
			Расход=0;          
			Пока Запрос.Группировка(2) = 1 Цикл
				НачальныйОстаток=Запрос.Нач;
				КонечныйОстаток=Запрос.Кон;
				Если (НачальныйОстаток>0) И (КонечныйОстаток>0) Тогда
					СредниеЗапасы=СредниеЗапасы+(НачальныйОстаток+КонечныйОстаток)/2;
					Дней=Дней+1;
					Расход=Расход+Запрос.Расх;          
				ИначеЕсли (НачальныйОстаток<=0) И (КонечныйОстаток>0) Тогда
					СредниеЗапасы=СредниеЗапасы+КонечныйОстаток;
					Дней=Дней+1;
					Расход=Расход+Запрос.Расх;          
				ИначеЕсли (НачальныйОстаток>0) И (КонечныйОстаток<=0) Тогда
					СредниеЗапасы=СредниеЗапасы+НачальныйОстаток;
					Дней=Дней+1;
					Расход=Расход+Запрос.Расх;          
				ИначеЕсли (НачальныйОстаток=0) И (КонечныйОстаток=0) И (Запрос.Расх>0) Тогда
					СредниеЗапасы=СредниеЗапасы+Запрос.Расх;
					Дней=Дней+1;
					Расход=Расход+Запрос.Расх;          
				КонецЕсли;	
			КонецЦикла;	                
			Если Дней=0 Тогда
				Продолжить;
			КонецЕсли;	
			СредниеЗапасы=СредниеЗапасы/Дней;
			Если (СредниеЗапасы=0) Или (Расход=0) Тогда
				ОборачиваемостьРазы=0;
				ОборачиваемостьДни=0;
			Иначе	
				ОборачиваемостьРазы=Расход/СредниеЗапасы;
				ОборачиваемостьДни=Окр((ДатаКонца-ДатаНачала+1)/ОборачиваемостьРазы,2);
				ОборачиваемостьРазы=Окр(ОборачиваемостьРазы,2);
			КонецЕсли;
			Если (ОборачиваемостьДни<=0) Или (ОборачиваемостьРазы<=0) Тогда
				Продолжить;
			КонецЕсли;	
			Если Значение>0 Тогда   
				Если ((Условие=1) И (ОборачиваемостьДни=Значение)) Или
					((Условие=2) И (ОборачиваемостьДни>Значение)) Или
	                ((Условие=3) И (ОборачиваемостьДни<Значение)) Тогда
				Иначе		
					Продолжить;
				КонецЕсли;	
			КонецЕсли;	
			Таб.ВывестиСекцию("Товар");
		КонецЦикла;	
	Иначе	
		ДатаНачала=НачМесяца(ДатаПериода1);
		ДатаКонца=КонМесяца(ДатаПериода2);	
		Запрос=СоздатьОбъект("Запрос");
		ТекстЗапроса="Период С ДатаНачала";
		Если ДатаКонца>=ПолучитьДатуТА() Тогда
			ТекстЗапроса=ТекстЗапроса+";";
		Иначе
			ТекстЗапроса=ТекстЗапроса+" По ДатаКонца;";
		КонецЕсли;     
		ТекстЗапроса=ТекстЗапроса+"
		|Номенклатура=Регистр.ПартииНаличие.Номенклатура;";
		Если ТипРасчета=1 Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|Кол=Регистр.ПартииНаличие.Количество;
			|Функция Нач=НачОст(Кол);
			|Функция Кон=КонОст(Кол);
			|Функция Расх=Расход(Кол);";
		Иначе
			ТекстЗапроса=ТекстЗапроса+"
			|СуммаРублевая=Регистр.ПартииНаличие.СуммаРуб;
			|Функция Нач=НачОст(СуммаРублевая);
			|Функция Кон=КонОст(СуммаРублевая);
			|Функция Расх=Расход(СуммаРублевая);";
		КонецЕсли;
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка Номенклатура без групп;
		|Группировка Месяц Все ВошедшиеВЗапрос;";
		Если СписокЭлементовМФ.РазмерСписка()>0 Тогда
			Если ТипМФ.ТекущаяСтрока()=1 Тогда // принадлежит списку
				ТекстЗапроса = ТекстЗапроса+" Условие (Номенклатура в СписокЭлементовМФ);";
			Иначе     
				ТекстЗапроса = ТекстЗапроса+" Условие (НЕ(Номенклатура в СписокЭлементовМФ));";
			КонецЕсли;
		КонецЕсли;	
		Флаг=Запрос.Выполнить(ТекстЗапроса);
		Если Флаг=0 Тогда
			Предупреждение ("Запрос не выполнился !!!");
			Возврат;
		КонецЕсли;        
		Шапка="Оборачиваемость товаров за период с "+ПериодОтчета1+" по "+ПериодОтчета2;
		Таб.ВывестиСекцию("Шапка|ТоварСтолбец");
		ТаблицаТоваров=СоздатьОбъект("ТаблицаЗначений");
		ТаблицаТоваров.НоваяКолонка("Товар");
		Пока Запрос.Группировка(1) = 1 Цикл
			Пока Запрос.Группировка(2) = 1 Цикл
				Месяц=Запрос.Месяц;
				Таб.ПрисоединитьСекцию("Шапка|Месяц");
				ПромКолонка=СтрЗаменить(Месяц," ","");       
				ТаблицаТоваров.НоваяКолонка(ПромКолонка);
			КонецЦикла;
			Прервать;
		КонецЦикла;
		Запрос.вНачалоВыборки();
		
		Пока Запрос.Группировка(1) = 1 Цикл
			Товар=Запрос.Номенклатура;
			ТаблицаТоваров.НоваяСтрока();
			ТаблицаТоваров.Товар=Товар;
			Пока Запрос.Группировка(2) = 1 Цикл
				Расход=Запрос.Расх;          
				НачальныйОстаток=Запрос.Нач;
				КонечныйОстаток=Запрос.Кон; 
				СредниеЗапасы=(НачальныйОстаток+КонечныйОстаток)/2;
				Если (СредниеЗапасы=0) Или (Расход=0) Тогда
					ОборачиваемостьРазы=0;
				Иначе	
					ОборачиваемостьРазы=Расход/СредниеЗапасы;
				КонецЕсли;
				ПромКолонка=СтрЗаменить(Запрос.Месяц," ","");       
				ТаблицаТоваров.УстановитьЗначение(ТаблицаТоваров.НомерСтроки,ПромКолонка,ОборачиваемостьРазы);
			КонецЦикла;	
		КонецЦикла;	
		Для I=1 По ТаблицаТоваров.КоличествоСтрок() Цикл
			Товар=ТаблицаТоваров.ПолучитьЗначение(I,"Товар");
			ПечТовар="" + Товар.Наименование + ?(Константа.ПоказыватьАртикул = 1, " " + СокрЛП(Товар.Артикул), "");
			ПечКод=Товар.Код;
			ЕстьОборачиваемость=0;
			Для R=2 По ТаблицаТоваров.КоличествоКолонок() Цикл
				ОборачиваемостьРазы=ТаблицаТоваров.ПолучитьЗначение(I,R);
				Если ОборачиваемостьРазы=0 Тогда
					ОборачиваемостьДни=0;
				Иначе	
					ОборачиваемостьДни=Окр(30/ОборачиваемостьРазы,2);
					ОборачиваемостьРазы=Окр(ОборачиваемостьРазы,2);
				КонецЕсли;
				Если (ОборачиваемостьДни<=0) Или (ОборачиваемостьРазы<=0) Тогда
					Продолжить;
				КонецЕсли;
				Если Значение>0 Тогда   
					Если ((Условие=1) И (ОборачиваемостьДни=Значение)) Или
						((Условие=2) И (ОборачиваемостьДни>Значение)) Или
		                ((Условие=3) И (ОборачиваемостьДни<Значение)) Тогда
					Иначе		
						Продолжить;
					КонецЕсли;	
				КонецЕсли;	
				ЕстьОборачиваемость=1;
				Прервать;
			КонецЦикла;	
            Если ЕстьОборачиваемость=1 Тогда
				Таб.ВывестиСекцию("Товар|ТоварСтолбец");
				Для R=2 По ТаблицаТоваров.КоличествоКолонок() Цикл
					ОборачиваемостьРазы=ТаблицаТоваров.ПолучитьЗначение(I,R);
					Если ОборачиваемостьРазы=0 Тогда
						ОборачиваемостьДни=0;
					Иначе	
						ОборачиваемостьДни=Окр(30/ОборачиваемостьРазы,2);
						ОборачиваемостьРазы=Окр(ОборачиваемостьРазы,2);
					КонецЕсли;
					ОборачиваемостьДни=?(ОборачиваемостьДни=0,"",ОборачиваемостьДни);
					ОборачиваемостьРазы=?(ОборачиваемостьРазы=0,"",ОборачиваемостьРазы);
					Таб.ПрисоединитьСекцию("Товар|Месяц");
				КонецЦикла;	
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
	Таб.ВывестиСекцию("Примечание");
	Таб.Опции(0,0,4,2,"ОпцииОборачиваемостьТовара","ОкноОборачиваемостьТовара");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Оборачиваемость товара","");
КонецПроцедуры
//-------------------------
ДатаПериода=РабочаяДата();
ДатаПериода1=РабочаяДата();
ДатаПериода2=РабочаяДата();
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
                                                                                
ТекСтрокаВТаблице="";
