////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//       
Перем ТекСтрокаВТаблице; // текущая строка в таблице значений  МФ

// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ                                            
//******************************************************************************
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

//******************************************************************************
// ПерерисовкаНазванийЗакладок
//
// Параметры:
// 	Нет
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Функция ПерерисовкаНазванийЗакладок()      
	
	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
	
КонецФункции // ПерерисовкаНазванийЗакладок	

//******************************************************************************
// СформироватьИнформациюОПартии(Партия)
//
// Параметры: 
//		Партия - элемент справочника "Партии"
//
// Возвращаемое значение: 
//	строка с описанием партии
//
// Описание: 
//	формирует строку описания партии со всеми ее реквизитами
//
Функция СформироватьИнформациюОПартии(Партия)
	Перем Результат;
	               
	Результат="";
	
	Если ПустоеЗначение(Партия)=1 Тогда
		Возврат(Результат);
	КонецЕсли; 
	          
	Если ПустоеЗначение(Партия.Поставщик)=0 Тогда
		Результат=?(Результат="","",Результат+РазделительСтрок);
		Результат=Результат+" Поставщик: "+СокрЛП(Партия.Поставщик);
	КонецЕсли;
	
	Если ПустоеЗначение(Партия.ПриходныйДокумент)=0 Тогда
		Результат=?(Результат="","",Результат+РазделительСтрок);
		Результат=Результат+" Поставка: "+СокрЛП(Партия.ПриходныйДокумент);
	КонецЕсли;
	
	Если ПустоеЗначение(Партия.ДоговорПоставщика)=0 Тогда
		Результат=?(Результат="","",Результат+РазделительСтрок);
		Результат=Результат+" Договор: "+СокрЛП(Партия.ДоговорПоставщика);
	КонецЕсли;
	
	Если ПустоеЗначение(Партия.ГТД)=0 Тогда        
		Результат=?(Результат="","",Результат+РазделительСтрок);
		Результат=Результат+" ГТД: "+СокрЛП(Партия.ГТД);
	КонецЕсли;
	
	Если ПустоеЗначение(Партия.СтранаПроисхождения)=0 Тогда
		Результат=?(Результат="","",Результат+РазделительСтрок);
		Результат=Результат+" Страна: "+СокрЛП(Партия.СтранаПроисхождения);
	КонецЕсли;
	
	Если ПустоеЗначение(Партия.Свойство)=0 Тогда   
		Результат=?(Результат="","",Результат+РазделительСтрок);
		Результат=Результат+" "+Партия.Свойство.Владелец+": "+СокрЛП(Партия.Свойство);
	КонецЕсли;
	
	Возврат(Результат);
	
КонецФункции //СформироватьИнформациюОПартии()

//******************************************************************************   
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление видимостью элементов формы.
//
Процедура УправлениеДиалогом()

	Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда           
		Форма.ИспользоватьСлой("Шапка,Подвал,Основной1,Основной2,Разделитель"+СокрЛП(ВидРазделителя));
	Иначе     
		Форма.ИспользоватьСлой("Шапка,МФ,Подвал");
	КонецЕсли;
	
КонецПроцедуры //УправлениеДиалогом()

//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)
	
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
	
КонецФункции //РасшифровкаОбновить()                                            

//******************************************************************************
// ВывестиДокументы(Запрос)
//
// Параметры: 
//  Запрос - значение типа "Запрос"
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Выводит документы движения по номенклатуре.
//
Процедура ВывестиДокументы(Запрос,ПечЕд)
	                
	Цена     = Запрос.Цена;
	ТекОстК	 = Запрос.НачОстОстаток;
	ТекОстНК = Запрос.НачОстПродажи;
	
	Пока Запрос.Группировка("Документ") = 1 Цикл
		
		Документ = Запрос.Документ;
		ПечТекстСтроки = "" + глПредставлениеДокумента(Документ) + 
				         " (" + СокрЛП(глИнформацияПоДокументуВЖурнале(Документ)) + ")";
		ТекРасшифровка = Документ;
		
		ТекОстК  = ТекОстК  + Запрос.ПриходОстаток - Запрос.РасходОстаток;
		ТекОстНК = ТекОстНК + Запрос.ПриходПродажи - Запрос.РасходПродажи;
		
		ПечПриходК	= глФРМКоличество(Запрос.ПриходОстаток,ПечЕд);
		ПечРасходК	= глФРМКоличество(Запрос.РасходОстаток,ПечЕд);
		ПечКонОстК	= глФРМКоличество(ТекОстК, ПечЕд);
		ПечКонОстНК = глФРМКоличество(ТекОстНК,ПечЕд);
		
		ПриходС	 = Запрос.ПриходОстаток * Цена;
		РасходС	 = Запрос.РасходОстаток * Цена;
		КонОстС	 = ТекОстК  * Цена;
		КонОстНС = ТекОстНК * Цена;
		
		ПечПриходС	= глФРМ(ПриходС);
		ПечРасходС	= глФРМ(РасходС);
		ПечКонОстС	= глФРМ(КонОстС);
		ПечКонОстНС = глФРМ(КонОстНС);
		
		Таб.ВывестиСекцию ("Документ");
		
	КонецЦикла;
	
КонецПроцедуры // ВывестиДокументы()

//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог	
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   
//	Запускает отчет.
//
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;
	
	Если глПроверкаДаты(ДатаНачала,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;      
	 
	Таб.ИсходнаяТаблица( "ВедомостьПоКомитентам" );
	
	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	Если глПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонца,Последовательность.ОсновнаяПоследовательность)=0 Тогда
		Возврат;
	КонецЕсли;	  
	
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ВедомостьПоКомитентам");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1",ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2",ВыбРазделитель2);
	Расшифровка.Установить("ВыбРазделитель3",ВыбРазделитель3);
	Расшифровка.Установить("ВидРазделителя",ВидРазделителя);
	
	Расшифровка.Установить("ВыбТМЦ",		ВыбТМЦ);
	Расшифровка.Установить("ВыбКомитент",   ВыбКомитент);
	Расшифровка.Установить("ВыбДоговор",	ВыбДоговор);
	
	Расшифровка.Установить("ВидЕдиницы",	ВидЕдиницы);
	Расшифровка.Установить("ПоДокументам",	ПоДокументам);
	
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	
	// запомним МФ только если он задан
    Если глМножественныйФильтрЗадан(ТаблицаМФ) = 1 Тогда
		Расшифровка.Установить("ТаблицаМФ", ТаблицаМФ); 
	КонецЕсли;
	             
	СтатусПринятый = глСП.Т_Принятый;
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Фирма			= Регистр.ПартииНаличие.     Фирма,
	|				  Регистр.ПартииОтданные.    Фирма,
	|				  Регистр.РеализованныйТовар.Фирма;
	|УпрАналитика	= Регистр.ПартииНаличие.     Фирма.УпрАналитика,
	|				  Регистр.ПартииОтданные.    Фирма.УпрАналитика,
	|				  Регистр.РеализованныйТовар.Фирма.УпрАналитика;
	|ЮрЛицо 		= Регистр.ПартииНаличие.     Фирма.ЮрЛицо,
	|				  Регистр.ПартииОтданные.    Фирма.ЮрЛицо,
	|				  Регистр.РеализованныйТовар.Фирма.ЮрЛицо;
	|Номенклатура	= Регистр.ПартииНаличие.     Номенклатура,
	|				  Регистр.ПартииОтданные.    Номенклатура,
	|				  Регистр.РеализованныйТовар.Номенклатура;
	|Комитент	    = Регистр.ПартииНаличие.     Партия.Поставщик,
	|                 Регистр.ПартииОтданные.    Партия.Поставщик,
	|				  Регистр.РеализованныйТовар.Договор.Владелец;
	|СтатусПартии   = Регистр.ПартииНаличие.     СтатусПартии,
	|                 Регистр.ПартииОтданные.    СтатусПартии;
	|Договор		= Регистр.ПартииНаличие.     Партия.ДоговорПоставщика,
	|                 Регистр.ПартииОтданные.    Партия.ДоговорПоставщика,
	|				  Регистр.РеализованныйТовар.Договор;
	|Цена			= Регистр.ПартииНаличие.     Партия.ЗакупочнаяЦена,
	|                 Регистр.ПартииОтданные.    Партия.ЗакупочнаяЦена,
	|				  Регистр.РеализованныйТовар.Партия.ЗакупочнаяЦена;
	|КодОперации	= Регистр.ПартииНаличие.     КодОперации,
	|                 Регистр.ПартииОтданные.    КодОперации;";
	
	ТекстЗапроса = ТекстЗапроса + 
	"КолОстаток     = Регистр.ПартииНаличие.     Количество,
	|                 Регистр.ПартииОтданные.    Количество;
	|КолПродажи		= Регистр.РеализованныйТовар.Количество;";
	
	ТекстЗапроса = ТекстЗапроса +
	"Функция НачОстОстаток = НачОст(КолОстаток) Когда (СтатусПартии = СтатусПринятый);
	|Функция ПриходОстаток = Приход(КолОстаток) Когда (СтатусПартии = СтатусПринятый);
	|Функция РасходОстаток = Расход(КолОстаток) Когда (СтатусПартии = СтатусПринятый);
	|Функция КонОстОстаток = КонОст(КолОстаток) Когда (СтатусПартии = СтатусПринятый);
	|Условие (КодОперации <> глКО.ПередачаНаРеализацию);
	|Условие (КодОперации <> глКО.Перемещение);
	|Условие (КодОперации <> глКО.ПередачаВРозницу);
	|Условие (КодОперации <> глКО.ВозвратИзРозницы);
	|Условие (КодОперации <> глКО.ВозвратОтРеализатора);";
	
	ТекстЗапроса = ТекстЗапроса +
	"Функция КонОстПродажи = КонОст(КолПродажи);
	|Функция НачОстПродажи = НачОст(КолПродажи);
	|Функция ПриходПродажи = Приход(КолПродажи);
	|Функция РасходПродажи = Расход(КолПродажи);";
                                 
	ТекстЗапроса = ТекстЗапроса +
	"Группировка Комитент;
	|Группировка Договор;
	|Группировка Номенклатура;
	|Группировка Цена;";
	
	ПечЗаголовокСтолбца = "Договор / Номенклатура + Цена";  
	
	Если ПоДокументам  = 1 Тогда
	    ТекстЗапроса = ТекстЗапроса +
		"Группировка Документ;";     
		ПечЗаголовокСтолбца = ПечЗаголовокСтолбца + " / Документы движения";
	КонецЕсли;
	
    Загол = "";
	
	НетОш = 1; // нет ошибок при наложении фильтров
	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",,,ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "УпрАналитика",ВыбРазделитель3,"ВыбРазделитель3",ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Фирма",,,ТекстЗапроса,Загол);
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "ЮрЛицо",,,ТекстЗапроса,Загол);
	КонецЕсли;
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Номенклатура",ВыбТМЦ,     "ВыбТМЦ",     ТекстЗапроса,Загол,"СвойстваНоменклатуры");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Комитент",    ВыбКомитент,"ВыбКомитент",ТекстЗапроса,Загол,"СвойстваКонтрагентов");
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Договор",     ВыбДоговор, "ВыбДоговор", ТекстЗапроса,Загол);
	
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;
    
	ПечЗаголовок = "Ведомость по принятым на реализацию ТМЦ (по учетным ценам)";
	                   
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;  
	
	глЧислоСтрок = 0;

	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	Таб.ВывестиСекцию("ШапкаТаблицы");

	// выводим шапку на каждой странице
	НачПовт = Таб.ВысотаСекции("Кнопки") + Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	глОживить(1);
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 3, "ВедомостьПоКомитентам", "ВедомостьПоКомитентам");
	Таб.ОбластьПечати(3,,,);        
	
	Пока Запрос.Группировка("Комитент") = 1 Цикл
	    Пока Запрос.Группировка("Договор") = 1 Цикл
	        ПечТекстСтроки = "" + Запрос.Комитент + "(" + Запрос.Договор + ")";
			ТекРасшифровка = Запрос.Договор;
			Вал            = Запрос.Договор.ВалютаВзаиморасчетов;
			Таб.ВывестиСекцию("Договор");
			
			ИтогНачОстС  = 0;
			ИтогПриходС  = 0;
			ИтогРасходС  = 0;
			ИтогКонОстС  = 0;
			ИтогКонОстНС = 0;
			
			Пока Запрос.Группировка("Номенклатура") = 1 Цикл
				Номенклатура = Запрос.Номенклатура;
				ПечЕд = ?(ВидЕдиницы=1,Номенклатура.ОсновнаяЕдиница,Номенклатура.БазоваяЕдиница);
			    Пока Запрос.Группировка("Цена") = 1 Цикл
			        Цена           = Запрос.Цена;
					ПечТекстСтроки = "" + Номенклатура + " ("+глФРМ(Цена,Вал)+")";
					ТекРасшифровка = Запрос.Номенклатура;
					                             
					ПечНачОстК	= глФРМКоличество(Запрос.НачОстОстаток,ПечЕд);
					ПечПриходК	= глФРМКоличество(Запрос.ПриходОстаток,ПечЕд);
					ПечРасходК	= глФРМКоличество(Запрос.РасходОстаток,ПечЕд);
					ПечКонОстК	= глФРМКоличество(Запрос.КонОстОстаток,ПечЕд);
					ПечКонОстНК = глФРМКоличество(Запрос.КонОстПродажи,ПечЕд);
					
					НачОстС  = Запрос.НачОстОстаток * Цена;
					ПриходС	 = Запрос.ПриходОстаток * Цена;
					РасходС	 = Запрос.РасходОстаток * Цена;
					КонОстС	 = Запрос.КонОстОстаток * Цена;
					КонОстНС = Запрос.КонОстПродажи * Цена;
					
					ИтогНачОстС  = ИтогНачОстС  + НачОстС ;
					ИтогПриходС	 = ИтогПриходС	+ ПриходС ;
					ИтогРасходС	 = ИтогРасходС	+ РасходС ;
					ИтогКонОстС	 = ИтогКонОстС	+ КонОстС ;
					ИтогКонОстНС = ИтогКонОстНС + КонОстНС;

					ПечНачОстС  = глФРМ(НачОстС);
					ПечПриходС	= глФРМ(ПриходС);
					ПечРасходС	= глФРМ(РасходС);
					ПечКонОстС	= глФРМ(КонОстС);
					ПечКонОстНС = глФРМ(КонОстНС);
					                                      
					Таб.ВывестиСекцию ("Номенклатура");
					
					Если ПоДокументам = 1 Тогда
					    ВывестиДокументы(Запрос, ПечЕд);
					КонецЕсли;
			    КонецЦикла;
			КонецЦикла;
			
			// выводим итоги по договору.
			
			ПечТекстСтроки = "Итого по " + Запрос.Комитент + "(" + Запрос.Договор + ")";
			ТекРасшифровка = Запрос.Договор;
			
			ПечНачОстС  = глФРМ(ИтогНачОстС );
			ПечПриходС  = глФРМ(ИтогПриходС );
			ПечРасходС	= глФРМ(ИтогРасходС );
			ПечКонОстС	= глФРМ(ИтогКонОстС );
			ПечКонОстНС = глФРМ(ИтогКонОстНС);
			
			Таб.ВывестиСекцию("ДоговорИтог");
	    КонецЦикла;
	КонецЦикла;
	
	Таб.ВывестиСекцию("ИтогоПусто");
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Ведомость по комитентам", ""); 
	
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	//                  тип          вид           переменная  название
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Номенклатура",    "Номенклатура",  "По номенклатуре");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Контрагенты",     "Комитент",      "По комитентам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Номенклатура",  "По свойствам номенклатуры");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","ЗначенияСвойств", "Комитент",      "По свойствам комитентов");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","Фирмы", "Фирма",  "По фирмам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","СвоиЮрЛица", "ЮрЛицо",  "По юр. лицам");
	глДобавитьВТаблицуМФ(ТаблицаМФ,"Справочник","УпрАналитика", "УпрАналитика",  "По упр. аналитике");        
	
	Если ФлагВосстановленияНастройки = 0 Тогда
		
		ВидЕдиницы     = 1;
		ВидРазделителя = 1;
		ДатаНачала      = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
		Если ПустоеЗначение(ДатаНачала) = 1 Тогда
			ДатаНачала      = НачМесяца(ДатаКонца);    
		КонецЕсли;

	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		
		ВидРазделителя	= глРасшифровка.Получить("ВидРазделителя");
		ВыбРазделитель1	= глРасшифровка.Получить("ВыбРазделитель1");
		ВыбРазделитель2	= глРасшифровка.Получить("ВыбРазделитель2");
		ВыбРазделитель3	= глРасшифровка.Получить("ВыбРазделитель3");
		
		ВыбТМЦ 			= глРасшифровка.Получить("ВыбТМЦ");
		ВыбКомитент     = глРасшифровка.Получить("ВыбКомитент");
		ВыбДоговор 		= глРасшифровка.Получить("ВыбДоговор");
		                                 
		ВидЕдиницы		= глРасшифровка.Получить("ВидЕдиницы");
		ПоДокументам    = глРасшифровка.Получить("ПоДокументам");
		
		Если ТипЗначенияСтр(глРасшифровка.Получить("ТаблицаМФ"))="ТаблицаЗначений" Тогда
			ТаблицаМФ.Загрузить(глРасшифровка.Получить("ТаблицаМФ"));
		КонецЕсли;
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;                      
	
	ПерерисовкаНазванийЗакладок();

	ТаблицаМФ.ВидимостьКолонки("Тип",0);
	ТаблицаМФ.ВидимостьКолонки("Вид",0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",0);
	
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");

	УправлениеДиалогом();
	
КонецПроцедуры		// ПриОткрытии()       

//******************************************************************************
// Предопределенная процедура.
Процедура ВводНового()

	// эта предопределенная процедура выполняется при восстановлении настройки
	ПерерисовкаНазванийЗакладок();
	УправлениеДиалогом();       
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	
КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореЗакладки(Номер,Значение)	
	// закладки
    Если Номер=1 Тогда
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	КонецЕсли;      
	УправлениеДиалогом();
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки
                                       
//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(Значение)  
	
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ДатаКонца = ПолучитьДатуТА();

// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов"); // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ"); // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,); // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
                                                                                
ТекСтрокаВТаблице="";

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);