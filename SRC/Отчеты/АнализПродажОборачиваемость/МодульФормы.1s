//*******************************************
Процедура Сформировать()
	
	Перем Запрос, ТекстЗапроса;
	
	Если глПроверкаДаты(ДатаНачала, ДатаКонца) = 0 Тогда
		Возврат;
	КонецЕсли;      
	
	СпКодыПродаж = СоздатьОбъект("СписокЗначений");
	СпКодыПродаж.ДобавитьЗначение(глКО.Продажа);
	СпКодыПродаж.ДобавитьЗначение(глКО.РозничнаяПродажа);
	СпКодыПродаж.ДобавитьЗначение(глКО.РозничнаяПродажаЕНВД);
	
	СпКодыВозвратовПокупателя = СоздатьОбъект("СписокЗначений");
	СпКодыВозвратовПокупателя.ДобавитьЗначение(глКО.ВозвратОтПокупателя);
	СпКодыВозвратовПокупателя.ДобавитьЗначение(глКО.ВозвратОтПокупателяЕНВД);
	
	Запрос = СоздатьОбъект("Запрос");
	
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Фирма = Регистр.ПартииНаличие.Фирма;
	|МОЛ = Регистр.ПартииНаличие.МОЛ;
	|Номенклатура = Регистр.ПартииНаличие.Номенклатура;
	|Поставщик = Регистр.ПартииНаличие.Номенклатура.ОсновноеСвойство.ЗначениеСвойства;
	|Покупатель = Регистр.ПартииНаличие.ТекущийДокумент.Реализация.Контрагент,
	|			Регистр.ПартииНаличие.ТекущийДокумент.РеализацияРозница.Контрагент,
	|			Регистр.ПартииНаличие.ТекущийДокумент.ВозвратОтПокупателя.Контрагент;
	|Количество = Регистр.ПартииНаличие.Количество;
	|Себестоимость = Регистр.ПартииНаличие.СуммаРуб;
	|Выручка = Регистр.ПартииНаличие.Выручка;
	|КодОперации = Регистр.ПартииНаличие.КодОперации;
	
	|Функция КоличествоВ = Приход(Количество) Когда ((КодОперации В СпКодыВозвратовПокупателя) И (Покупатель в ВыбПокупатель));
	|Функция СебестоимостьВ = Приход(Себестоимость) Когда ((КодОперации В СпКодыВозвратовПокупателя) И (Покупатель в ВыбПокупатель));
	|Функция ВыручкаСуммаВ = Сумма(Выручка) Когда ((КодОперации В СпКодыВозвратовПокупателя) И (Покупатель в ВыбПокупатель));
	|Функция ПрибыльСуммаВ = Сумма(Выручка - Себестоимость) Когда ((КодОперации В СпКодыВозвратовПокупателя) И (Покупатель в ВыбПокупатель));
	
	|Функция КоличествоНачОст = НачОст(Количество);
	|Функция КоличествоПриход = Приход(Количество);
	|Функция КоличествоРасход = Расход(Количество) Когда ((КодОперации В СпКодыПродаж) И (Покупатель в ВыбПокупатель));
	|Функция СебестоимостьНачОст = НачОст(Себестоимость);
	|Функция СебестоимостьКонОст = КонОст(Себестоимость);
	|Функция СебестоимостьРасход = Расход(Себестоимость) Когда ((КодОперации В СпКодыПродаж) И (Покупатель в ВыбПокупатель));
	|Функция ВыручкаСумма = Сумма(Выручка) Когда ((КодОперации В СпКодыПродаж) И (Покупатель в ВыбПокупатель));
	|Функция ПрибыльСумма = Сумма(Выручка - Себестоимость) Когда ((КодОперации В СпКодыПродаж) И (Покупатель в ВыбПокупатель));
	
	|Группировка Номенклатура Без Групп;
	|Группировка День;
	|Условие(Фирма в ВыбФирма);
	|Условие(МОЛ в ВыбМОЛ);
	|Условие(Номенклатура в ВыбНоменклатура);
	|Условие(Поставщик в ВыбПоставщик);
	|"//}}ЗАПРОС
	;
	
	// если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;

   	Таб = СоздатьОбъект("Таблица");

	глЧислоСтрок = 0;
	
	ПечФильтр = "";
	Если ПустоеЗначение(ВыбФирма) = 0 Тогда
		ПечФильтр = ПечФильтр + ?(ПечФильтр = "", "", ", ");
		ПечФильтр = ПечФильтр + ВыбФирма;
	КонецЕсли;
	Если ПустоеЗначение(ВыбМОЛ) = 0 Тогда
		ПечФильтр = ПечФильтр + ?(ПечФильтр = "", "", ", ");
		ПечФильтр = ПечФильтр + ВыбМОЛ;
	КонецЕсли;
	Если ПустоеЗначение(ВыбНоменклатура) = 0 Тогда
		ПечФильтр = ПечФильтр + ?(ПечФильтр = "", "", ", ");
		ПечФильтр = ПечФильтр + ВыбНоменклатура;
	КонецЕсли;
	Если ПустоеЗначение(ВыбПоставщик) = 0 Тогда
		ПечФильтр = ПечФильтр + ?(ПечФильтр = "", "", ", ");
		ПечФильтр = ПечФильтр + ВыбПоставщик;
	КонецЕсли;
	Если ПустоеЗначение(ВыбПокупатель) = 0 Тогда
		ПечФильтр = ПечФильтр + ?(ПечФильтр = "", "", ", ");
		ПечФильтр = ПечФильтр + ВыбПокупатель;
	КонецЕсли;
	ПечФильтр = ?(ПечФильтр = "", "[без фильтра]", ПечФильтр);
	
	Таб.ВывестиСекцию("Шапка");
	Таб.ВывестиСекцию("ШапкаТаблицы");
	
	НачПовт = Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 2, "АнализПродаж1П", "АнализПродаж1П");
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("Номенклатура", "Справочник.Номенклатура");
	ТЗ.НоваяКолонка("Количество", "Число");
	ТЗ.НоваяКолонка("Себестоимость", "Число");
	ТЗ.НоваяКолонка("Выручка", "Число");
	ТЗ.НоваяКолонка("Прибыль", "Число");
	ТЗ.НоваяКолонка("ДнейВПродаже", "Число");
	ТЗ.НоваяКолонка("ДнейПродаж", "Число");
	ТЗ.НоваяКолонка("УсредПрибыль", "Число");
	ТЗ.НоваяКолонка("УсредКоличество", "Число");
	ТЗ.НоваяКолонка("УсредКоличествоПоДнямПродаж", "Число");
	ТЗ.НоваяКолонка("КоэфОтдачи", "Число");
	ТЗ.НоваяКолонка("КоэфОборачиваемости", "Число");

	КоличествоКалендарныхДнейПериодаАнализа = ДатаКонца - ДатаНачала + 1;
	
	Пока Запрос.Группировка("Номенклатура") = 1 Цикл

		// считаем количество дней, когда товар был в продаже, дни продаж
		// и средний остаток товара (по себестоимости)
		ДнейВПродаже = 0;
		ДнейПродаж = 0;
		СуммаСреднихОстатков = 0;
		
		Пока Запрос.Группировка("День") = 1 Цикл
			
			// дней в продаже
			Если (Запрос.КоличествоНачОст > 0) или (Запрос.КоличествоПриход > 0) Тогда
				ДнейВПродаже = ДнейВПродаже + 1;
			КонецЕсли;
			
			// дней продаж
			Если (Запрос.КоличествоРасход - Запрос.КоличествоВ) > 0 Тогда
				ДнейПродаж = ДнейПродаж + 1;
			КонецЕсли;
			
			// средний остаток
			СуммаСреднихОстатков = СуммаСреднихОстатков + (Запрос.СебестоимостьНачОст + Запрос.СебестоимостьКонОст) / 2;
			
		КонецЦикла;
		
		СреднийОстатокСебестоимости = СуммаСреднихОстатков / КоличествоКалендарныхДнейПериодаАнализа;
		
		// фильтр по дням в продаже
		Если ДнейВПродаже <= ВыбДнейВПродажеБольше Тогда
			Продолжить;
		КонецЕсли;
		
		// теперь запоминаем в таблице, чтобы потом отсортировать как нужно
		ТЗ.НоваяСтрока();
		
		ТЗ.Номенклатура = Запрос.Номенклатура;
		ТЗ.Количество = Запрос.КоличествоРасход - Запрос.КоличествоВ;
		ТЗ.Себестоимость = Запрос.СебестоимостьРасход - Запрос.СебестоимостьВ;
		ТЗ.Выручка = Запрос.ВыручкаСумма - Запрос.ВыручкаСуммаВ;
		ТЗ.Прибыль = Запрос.ПрибыльСумма - Запрос.ПрибыльСуммаВ;
		ТЗ.ДнейВПродаже = ДнейВПродаже;
		ТЗ.ДнейПродаж = ДнейПродаж;
		ТЗ.УсредПрибыль = ?(ДнейВПродаже > 0, ТЗ.Прибыль / ДнейВПродаже, 0);
		ТЗ.УсредКоличество = ?(ДнейВПродаже > 0, ТЗ.Количество / ДнейВПродаже, 0);
		ТЗ.УсредКоличествоПоДнямПродаж = ?(ДнейПродаж > 0, ТЗ.Количество / ДнейПродаж, 0);
		ТЗ.КоэфОтдачи = ?(ТЗ.Себестоимость > 0, ТЗ.Прибыль / ТЗ.Себестоимость, 0); // отношение прибыли к себестоимости
		ТЗ.КоэфОтдачи = ?(ДнейВПродаже > 0, ТЗ.КоэфОтдачи / ДнейВПродаже, 0); // делим на кол-во дней в продаже
		ТЗ.КоэфОтдачи = ТЗ.КоэфОтдачи * 100; // превращаем в проценты
		ТЗ.КоэфОборачиваемости = ?(ТЗ.Себестоимость > 0, СреднийОстатокСебестоимости / ТЗ.Себестоимость, 0) * КоличествоКалендарныхДнейПериодаАнализа;
		
	КонецЦикла;
	
	// сортируем
	Состояние("Сортировка...");
	Если ВыбСортировка = 1 Тогда
		ТЗ.Сортировать("-УсредПрибыль,Номенклатура");
	ИначеЕсли ВыбСортировка = 2 Тогда
		ТЗ.Сортировать("-КоэфОтдачи,Номенклатура");
	ИначеЕсли ВыбСортировка = 3 Тогда
		ТЗ.Сортировать("-Количество,Номенклатура");
	ИначеЕсли ВыбСортировка = 4 Тогда
		ТЗ.Сортировать("-КоэфОборачиваемости,Номенклатура");
	ИначеЕсли ВыбСортировка = 5 Тогда
		ТЗ.Сортировать("-УсредКоличество,Номенклатура");
	ИначеЕсли ВыбСортировка = 6 Тогда
		ТЗ.Сортировать("-УсредКоличествоПоДнямПродаж,Номенклатура");
	КонецЕсли;
	
	// выводим
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		
		Расшифровка = ТЗ.Номенклатура;
		
		ПечАртикул = СокрЛП(ТЗ.Номенклатура.Артикул);
		ПечНоменклатура = СокрЛП(ТЗ.Номенклатура.Наименование);
		ПечКоличество = глФРМКоличество(ТЗ.Количество);
		ПечВыручка = глФРМ(ТЗ.Выручка);
		ПечПрибыль = глФРМ(ТЗ.Прибыль);
		ПечДнейВПродаже = СокрЛ(Формат(ТЗ.ДнейВПродаже, "Ч015.0.'"));
		ПечДнейПродаж = СокрЛ(Формат(ТЗ.ДнейПродаж, "Ч015.0.'"));
		ПечУсредПрибыль = глФРМ(ТЗ.УсредПрибыль);
		ПечУсредКоличество = глФРМКоличество(ТЗ.УсредКоличество);
		ПечУсредКоличествоПоДнямПродаж = глФРМКоличество(ТЗ.УсредКоличествоПоДнямПродаж);
		ПечКоэфОтдачи = СокрЛ(Формат(ТЗ.КоэфОтдачи,"Ч015.3. "));
		ПечКоэфОборачиваемости = СокрЛ(Формат(ТЗ.КоэфОборачиваемости,"Ч015.3. "));
		
		Таб.ВывестиСекцию("Строка");
		
		глОживить(1);
	КонецЦикла;
		
	ПечВыручка = глФРМ(ТЗ.Итог("Выручка"));
	ПечПрибыль = глФРМ(ТЗ.Итог("Прибыль"));
	
	Таб.ВывестиСекцию("Итог");

	Таб.ТолькоПросмотр(1);
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Анализ продаж", "");
	
КонецПроцедуры  

Процедура ПриОткрытии()
	
	ВыбСортировка = ?(ВыбСортировка = 0, 1, ВыбСортировка);
	
КонецПроцедуры  
