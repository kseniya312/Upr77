////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 
// группировки в отчете
Перем СписокГруппировок;
Перем КоличествоГруппировок;  
Перем НомерГруппировкиПоВалюте;
Перем НомерГруппировкиПоДню;


// списки значений, в которых определены возможные типы операций
Перем СписокОперДебет, СписокОперКредит; // по дебету и кредиту соответственно
                        
Перем ВалютаОтчета;

Процедура ВывестиГруппировку(Запрос,Ном) Далее

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Управление доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Форма.ИспользоватьСлой("Основной1,Основной2,Разделитель"+СокрЛП(ВидРазделителя));
	
КонецПроцедуры //УправлениеДиалогом()   

//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)
	
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
	
КонецФункции //РасшифровкаОбновить()                                            

//******************************************************************************
// ИзменениеПорядкаГрупп(НаправлениеСдвига)
//
// Параметры:
//  НаправлениеСдвига = 	-1 - вниз
//							 1 - вверх
// Возвращаемое значение: 
//	Нет
//
// Вызывается из формул элементов диалога:
//  кнопки "вверх" и "вниз" рядом со списком группировок
//
// Описание:  
//	Процедура производит сдвиг текущей группировки в общем
// 	списке группировок на "НаправлениеСдвига" позиций
//
Процедура ИзменениеПорядкаГрупп(НаправлениеСдвига)
	ТекСтр = Группировки.ТекущаяСтрока();
	ПослСдвигСтр = Группировки.РазмерСписка() - 1;
	Если ТекСтр <= ПослСдвигСтр Тогда
		Если не((НаправлениеСдвига = 1) и (ТекСтр = ПослСдвигСтр)) Тогда
			Группировки.СдвинутьЗначение(НаправлениеСдвига, ТекСтр);
		КонецЕсли;
	Иначе
		Предупреждение("Группировка ""По документам движения"" всегда находится
		               |в конце списка и не перемещается.",60);
	КонецЕсли;
КонецПроцедуры // ИзменениеПорядкаГрупп()

//******************************************************************************
// УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
//
// Параметры:  ТекстЗапроса - переданный по ссылке текст запроса
// 			   ТекстЗагол   - переданный по ссылке текст заголовка
//
// Возвращаемое значение: 
//	Нет
//
// Описание: 
//	Дополняет строку запроса и заголовка в соответствии с выбранными группировками.
//    
Процедура УстановитьГруппировкиЗапроса(ТекстЗапроса, ТекстЗагол)
	СписокГруппировок = СоздатьОбъект("СписокЗначений");
	
	Для Сч=1 По Группировки.РазмерСписка() Цикл
		Если Группировки.Пометка(Сч)=1 Тогда
			ПредставлениеГрупп="";
			ТекстГрупп=Группировки.ПолучитьЗначение(Сч,ПредставлениеГрупп);
			Если ТекстГрупп = "Валюта" Тогда
				НомерГруппировкиПоВалюте = СписокГруппировок.РазмерСписка()+1;
			ИначеЕсли ТекстГрупп = "День" Тогда
				НомерГруппировкиПоДню 	 = СписокГруппировок.РазмерСписка()+1;
			КонецЕсли;    
			ТекстЗапроса 	= ТекстЗапроса 	+ "Группировка "+ТекстГрупп+";";
			ТекстЗагол 		= ТекстЗагол 	+ ?(ТекстЗагол="",""," / ")+ПредставлениеГрупп;
			СписокГруппировок.ДобавитьЗначение(ТекстГрупп,ПредставлениеГрупп);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры                                                                  

//******************************************************************************
// ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка)
//
// Параметры:
//  Запрос - объект "Запрос", на основании которого строится отчет
//	Ном - Номер группировки запроса (Число)
//	НазваниеСекции - название секции, которую следует использовать (Строка)
//	ПечТекстСтроки - текстовое представление текущей строки
//	ТекРасшифровка - расшифровка текущей строки
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Производит вывод в печатную форму одной строки запроса.
//                                                                                  
Процедура ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка)
	            
	Если (НомерГруппировкиПоВалюте <= Ном) Тогда
		ПечВал	= Запрос.Валюта;
	Иначе     
		ПечВал 	= "";
	КонецЕсли;            
	
	ПечТип = ВалютаОтчета;
	
	//начальный остаток
	ПечНачОст	= глФРМ(Запрос.НачОст);
	ПечНачОстВ 	= глФРМ(Запрос.НачОстВ);
	Таб.ВывестиСекцию(НазваниеСекции+"|Начало");
	
	// приходы (по операциям)
	Приход		= Запрос.Приход;
	ПриходВ		= Запрос.ПриходВ;
	ВсегоСтрок 	= СписокОперДебет.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации 	= СписокОперДебет.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		ТекПриход 		= Запрос.ПолучитьАтрибут(НазвОперации+"Приход");
		ТекПриходВ 		= Запрос.ПолучитьАтрибут(НазвОперации+"ПриходВ");
		Приход			= Приход 	- ТекПриход;
		ПриходВ			= ПриходВ 	- ТекПриходВ;
		ПечПриход 		= глФРМ(ТекПриход);
		ПечПриходВ 		= глФРМ(ТекПриходВ);
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|Приход");
	КонецЦикла;                                                                 
	ПечПриход 	= глФРМ(Приход);
	ПечПриходВ 	= глФРМ(ПриходВ);
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|Приход");
	
	// расходы (по операциям)   
	Расход		= Запрос.Расход;
	РасходВ 	= Запрос.РасходВ;
	ВсегоСтрок 	= СписокОперКредит.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации 	= СписокОперКредит.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		ТекРасход 		= Запрос.ПолучитьАтрибут(НазвОперации+"Расход");
		ТекРасходВ		= Запрос.ПолучитьАтрибут(НазвОперации+"РасходВ");
		Расход			= Расход 	- ТекРасход; 
		РасходВ			= РасходВ 	- ТекРасходВ; 
		ПечРасход 		= глФРМ(ТекРасход);
		ПечРасходВ 		= глФРМ(ТекРасходВ);
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|Расход");
	КонецЦикла;
	ПечРасход 	= глФРМ(Расход);
	ПечРасходВ 	= глФРМ(РасходВ);
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|Расход");
	
	// конечный остаток
	ПечКонОст 	= глФРМ(Запрос.КонОст);      
	ПечКонОстВ 	= глФРМ(Запрос.КонОстВ);      
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|КонечныйОстаток");
	глОживить(1);
	
КонецПроцедуры  // ПечатьСтроки()
                                       
//******************************************************************************
// ПечатьСтрокиДокумента(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка, ТекОст,ТекОстВ)
//
// Параметры:
//  Запрос - объект "Запрос", на основании которого строится отчет
//	Ном - Номер группировки запроса (Число)
//	НазваниеСекции - название секции, которую следует использовать (Строка)
//	ПечТекстСтроки - текстовое представление текущей строки
//	ТекРасшифровка - расшифровка текущей строки
//	ТекОст - 
//	ТекОстВ - 
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Производит вывод в печатную форму одной строки запроса по группировке "Документ".
//                                           
Процедура ПечатьСтрокиДокумента(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка, ТекОст,ТекОстВ)
	     
	Если (НомерГруппировкиПоВалюте <= Ном) Тогда
		ПечВал	= Запрос.Валюта;
	Иначе     
		ПечВал 	= "";
	КонецЕсли;            
	
	ПечТип = ВалютаОтчета;
	
	//начальный остаток
	Таб.ВывестиСекцию(НазваниеСекции+"|Начало");
	
	// приходы (по операциям)
	Приход		= Запрос.Приход;
	ПриходВ		= Запрос.ПриходВ;
	ТекОст 		= ТекОст 	+ Приход;
	ТекОстВ 	= ТекОстВ 	+ ПриходВ;
	ВсегоСтрок 	= СписокОперДебет.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации 	= СписокОперДебет.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		ТекПриход 		= Запрос.ПолучитьАтрибут(НазвОперации+"Приход");
		ТекПриходВ 		= Запрос.ПолучитьАтрибут(НазвОперации+"ПриходВ");
		Приход			= Приход 	- ТекПриход;
		ПриходВ			= ПриходВ 	- ТекПриходВ;
		ПечПриход 		= глФРМ(ТекПриход);
		ПечПриходВ 		= глФРМ(ТекПриходВ);
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|Приход");
	КонецЦикла;                                                                 
	ПечПриход 	= глФРМ(Приход);
	ПечПриходВ 	= глФРМ(ПриходВ);
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|Приход");
	
	// расходы (по операциям)   
	Расход		= Запрос.Расход;
	РасходВ 	= Запрос.РасходВ; 
	ТекОст 		= ТекОст 	- Расход;
	ТекОстВ 	= ТекОстВ 	- РасходВ;
	ВсегоСтрок 	= СписокОперКредит.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации 	= СписокОперКредит.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		ТекРасход 		= Запрос.ПолучитьАтрибут(НазвОперации+"Расход");
		ТекРасходВ		= Запрос.ПолучитьАтрибут(НазвОперации+"РасходВ");
		Расход			= Расход 	- ТекРасход; 
		РасходВ			= РасходВ 	- ТекРасходВ; 
		ПечРасход 		= глФРМ(ТекРасход);
		ПечРасходВ 		= глФРМ(ТекРасходВ);
		Таб.ПрисоединитьСекцию(НазваниеСекции+"|Расход");
	КонецЦикла;
	ПечРасход 	= глФРМ(Расход);
	ПечРасходВ 	= глФРМ(РасходВ);
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|Расход");
	
	// конечный остаток
	ПечКонОст	= глФРМ( ТекОст  );
	ПечКонОстВ	= глФРМ( ТекОстВ );
	Таб.ПрисоединитьСекцию(НазваниеСекции+"|КонечныйОстаток");
	глОживить(1);

КонецПроцедуры // ПечатьСтрокиДокумента()

//******************************************************************************
// ВывестиГруппировку(Запрос,Ном)
//
// Параметры:
//  Запрос - объект "Запрос"
//	Ном - номер выводимой группировки
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Выводит в печатную форму одну группировку запроса. Если
//	Есть нижележащие группировки, они выводятся также с использованием рекурсивного
//	вызова этой же процедуры.
//
Процедура ВывестиГруппировку(Запрос,Ном)
	  
	Если Ном <= КоличествоГруппировок Тогда  
		
		НазваниеГруппировки = СписокГруппировок.ПолучитьЗначение(Ном);
		Если НазваниеГруппировки = "Документ" Тогда
			ТекОст 	= Запрос.НачОст;
			ТекОстВ = Запрос.НачОстВ;
		КонецЕсли;  
		
		Пока Запрос.Группировка(Ном) = 1 Цикл
		
			Если НазваниеГруппировки = "Документ" Тогда
				НазваниеСекции="Документ";   
			Иначе                           
				НазваниеСекции="Строка"+Ном;
			КонецЕсли;       
			
			Если (Ном >= НомерГруппировкиПоВалюте) Тогда
				Если Запрос.Валюта<>ВалютаОтчета Тогда
					НазваниеСекции = НазваниеСекции + "В";
				КонецЕсли;
			КонецЕсли;  
			
			ПечТекстСтроки = Запрос.ПолучитьАтрибут(СписокГруппировок.ПолучитьЗначение(Ном));
			ПечТекстСтроки = ?(ПустоеЗначение(ПечТекстСтроки)=1,глПредставлениеПустогоЗначения(НазваниеГруппировки),ПечТекстСтроки);
			
			Если (ТипЗначенияСтр(ПечТекстСтроки) = "Документ") или 
				 (ТипЗначенияСтр(ПечТекстСтроки) = "Справочник") Тогда
				ТекРасшифровка = ПечТекстСтроки;
			Иначе
				ТекРасшифровка = "";
			КонецЕсли;
			                  
			Если НазваниеГруппировки = "Документ" Тогда
				ПечТекстСтроки = ""+глНазваниеДокументаВЖурнале(ПечТекстСтроки)+" № "+ПечТекстСтроки.НомерДок+ 
				                  " (" + СокрЛП(глИнформацияПоДокументуВЖурнале(ПечТекстСтроки)) + ")";
			КонецЕсли;
			
			Если НазваниеГруппировки = "Документ" Тогда
				ПечатьСтрокиДокумента(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка,ТекОст,ТекОстВ);
			Иначе
				ПечатьСтроки(Запрос,Ном,НазваниеСекции,ПечТекстСтроки,ТекРасшифровка);
			КонецЕсли;
		
			// если есть более детальная группировка - выведем ее
			Если КоличествоГруппировок > Ном Тогда
				ВывестиГруппировку(Запрос,Ном+1);
			КонецЕсли;          
		
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ВывестиГруппировку()                                          

//******************************************************************************
// ЗаполнитьСпискиОпераций()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  В этой процедуры в списки операций прописываются значения кодов операций,
//	движения по которым мы хотели бы показать в отдельных колонках.
//	Операции задаются раздельно по дебету и кредиту
//
Процедура ЗаполнитьСпискиОпераций()

	СписокОперДебет = СоздатьОбъект("СписокЗначений");
	СписокОперКредит= СоздатьОбъект("СписокЗначений");
	Если ПоОперациям = 1 Тогда
		СписокОперДебет.ДобавитьЗначение(глКО.НаличныеВМагазине);  
		СписокОперДебет.ДобавитьЗначение(глКО.КлиентскийКредит);  
		СписокОперДебет.ДобавитьЗначение(глКО.Проценты);
		СписокОперДебет.ДобавитьЗначение(глКО.ОплатаОтПокупателя);
		СписокОперДебет.ДобавитьЗначение(глКО.ВозвратОплатыОтПоставщика);  
		СписокОперДебет.ДобавитьЗначение(глКО.ВозвратПодотчетныхСумм);  
		СписокОперДебет.ДобавитьЗначение(глКО.КурсоваяРазница);  
		СписокОперДебет.ДобавитьЗначение(глКО.СтатьиДР); 
		СписокОперДебет.ДобавитьЗначение(глКО.Залог);
		
		
		СписокОперКредит.ДобавитьЗначение(глКО.НаличныеВМагазине);  
		СписокОперКредит.ДобавитьЗначение(глКО.КлиентскийКредит);  
		СписокОперКредит.ДобавитьЗначение(глКО.Проценты);  
		СписокОперКредит.ДобавитьЗначение(глКО.ОплатаПоставщику);
		СписокОперКредит.ДобавитьЗначение(глКО.ВозвратОплатыПокупателю);
		СписокОперКредит.ДобавитьЗначение(глКО.ВыдачаПодотчет);
		СписокОперКредит.ДобавитьЗначение(глКО.КурсоваяРазница);  
		СписокОперКредит.ДобавитьЗначение(глКО.СтатьиДР); 
		СписокОперКредит.ДобавитьЗначение(глКО.Залог);
	Иначе
		// списки будут пустыми, т.е. мы не разворачиваем
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСпискиОпераций()

//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог	
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   
//	Запускает отчет.
//
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;
	
	Если глПроверкаДаты(ДатаНачала,ДатаКонца)=0 Тогда
		Возврат;
	КонецЕсли;       
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;    
	                           
	ВалютаОтчета = ?(ВыбВидВалюты = 1, глРубли, глДоллары);
	
	НомерГруппировкиПоВалюте = 9999; // невозможно большое значение
	НомерГруппировкиПоДню    = 9999; // невозможно большое значение
	 
	Таб.ИсходнаяТаблица( "ВедомостьПоКассе" ); 
	
	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	Если глПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонца,Последовательность.ОсновнаяПоследовательность)=0 Тогда
		Возврат;
	КонецЕсли;	  
	
	ЗаполнитьСпискиОпераций();
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ВедомостьПоКассе");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаНачала", 	ДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1",ВыбРазделитель1);
	Расшифровка.Установить("ВыбРазделитель2",ВыбРазделитель2);
	Расшифровка.Установить("ВыбРазделитель3",ВыбРазделитель3);
	Расшифровка.Установить("ВидРазделителя",ВидРазделителя);

	Расшифровка.Установить("ВыбКасса",		ВыбКасса);
	Расшифровка.Установить("ВыбВалюта",		ВыбВалюта);
	Расшифровка.Установить("ВыбВидВалюты",	ВыбВидВалюты);
	Расшифровка.Установить("ПоОперациям",	ПоОперациям);
	
	Расшифровка.Установить("Группировки",	Группировки);
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Фирма			= Регистр.Касса.Фирма;
	|УпрАналитика 	= Регистр.Касса.Фирма.УпрАналитика;
	|ЮрЛицо 		= Регистр.Касса.Фирма.ЮрЛицо;
	|Касса			= Регистр.Касса.Касса;
	|КодОперации	= Регистр.Касса.КодОперации;
	|Валюта			= Регистр.Касса.Валюта;
	|СуммаВ			= Регистр.Касса.СуммаВал;";
	Если ВыбВидВалюты = 1 Тогда // рублевый эквивалент
		ТекстЗапроса = ТекстЗапроса + РазделительСтрок+
		"Сумма 		= Регистр.Касса.СуммаРуб;";
	Иначе //  упр. эквивалент
		ТекстЗапроса = ТекстЗапроса + РазделительСтрок+
		"Сумма 		= Регистр.Касса.СуммаУпр;";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"Функция НачОст = НачОст(Сумма);
	|Функция Приход = Приход(Сумма);
	|Функция Расход = Расход(Сумма);
	|Функция КонОст = КонОст(Сумма);";
	
	ТекстЗапроса = ТекстЗапроса +
	"Функция НачОстВ = НачОст(СуммаВ);
	|Функция ПриходВ = Приход(СуммаВ);
	|Функция РасходВ = Расход(СуммаВ);
	|Функция КонОстВ = КонОст(СуммаВ);";

    Для СчЦикла=1 по СписокОперДебет.РазмерСписка() Цикл
		НазвОперации = СписокОперДебет.ПолучитьЗначение(СчЦикла).Идентификатор();
		ТекстЗапроса = ТекстЗапроса + РазделительСтрок+
		"Функция "+НазвОперации+"Приход = Приход(Сумма) когда (КодОперации = глКО."+НазвОперации+");"+
		"Функция "+НазвОперации+"ПриходВ = Приход(СуммаВ) когда (КодОперации = глКО."+НазвОперации+");";
	КонецЦикла;
	Для СчЦикла=1 по СписокОперКредит.РазмерСписка() Цикл
		НазвОперации = СписокОперКредит.ПолучитьЗначение(СчЦикла).Идентификатор();
		ТекстЗапроса = ТекстЗапроса + РазделительСтрок+
		"Функция "+НазвОперации+"Расход = Расход(Сумма) когда (КодОперации = глКО."+НазвОперации+");"+
		"Функция "+НазвОперации+"РасходВ = Расход(СуммаВ) когда (КодОперации = глКО."+НазвОперации+");";
	КонецЦикла;
	
	Загол="";        
	ПечЗаголовок = "Ведомость по кассе: "+ВалютаОтчета;
	
	НетОш = 1; // нет ошибок при наложении фильтров
	Если ВидРазделителя = 1 Тогда
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "Фирма",ВыбРазделитель1,"ВыбРазделитель1",ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 2 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "ЮрЛицо",ВыбРазделитель2,"ВыбРазделитель2",ТекстЗапроса,Загол);
	ИначеЕсли ВидРазделителя = 3 Тогда                                                   
		НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "УпрАналитика",ВыбРазделитель3,"ВыбРазделитель3",ТекстЗапроса,Загол);
	КонецЕсли;    

	НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "Касса",   ВыбКасса,    "ВыбКасса",   ТекстЗапроса,Загол);
	НетОш = НетОш * глФильтрПоПеременнойЗапроса(, "Валюта",  ВыбВалюта,   "ВыбВалюта",  ТекстЗапроса,Загол);

	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПечЗаголовокСтолбца = "";  
	УстановитьГруппировкиЗапроса(ТекстЗапроса, ПечЗаголовокСтолбца);
	КоличествоГруппировок = СписокГруппировок.РазмерСписка();
	
	Если КоличествоГруппировок > 5 Тогда
		Предупреждение("Нельзя сделать больше 5 группировок!",60);
		Возврат;
	КонецЕсли;              
	
	Если (НомерГруппировкиПоДню <> 9999) и (КоличествоГруппировок > 3) Тогда
		Предупреждение("При установленной группировке по дню нельзя использовать больше трех группировок!");
		Возврат;
	КонецЕсли;

	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;         
	
	глЧислоСтрок = 0;
	
	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	Таб.ВывестиСекцию("ШапкаТаблицы|Начало");

	// выводим шапку на каждой странице
	НачПовт = Таб.ВысотаСекции("Кнопки") + Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	// сформируем шапку, исходя из выбранной формы представления данных
	ВсегоСтрок = СписокОперДебет.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации = СписокОперДебет.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		Если (Запрос.ПолучитьАтрибут(НазвОперации+"Приход")=0) и
			 (Запрос.ПолучитьАтрибут(НазвОперации+"ПриходВ")=0) Тогда
			// уберем лишние горизонтальные группировки (по которым нет итогов)
			СписокОперДебет.УдалитьЗначение(ВсегоСтрок - СчЦикла + 1);
		Иначе
			НазвОперации = СписокОперДебет.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1);
			Таб.ПрисоединитьСекцию("ШапкаТаблицы|Приход");
		КонецЕсли;
	КонецЦикла;                                                                 
	НазвОперации = ?(ПоОперациям=1,"Приход - прочее","Приход");
	Таб.ПрисоединитьСекцию("ШапкаТаблицы|Приход");
	ВсегоСтрок = СписокОперКредит.РазмерСписка();
	Для СчЦикла=1 по ВсегоСтрок Цикл
		НазвОперации = СписокОперКредит.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1).Идентификатор();
		Если (Запрос.ПолучитьАтрибут(НазвОперации+"Расход")=0) и 
		  	 (Запрос.ПолучитьАтрибут(НазвОперации+"РасходВ")=0)Тогда
			// уберем лишние горизонтальные группировки (по которым нет итогов)
			СписокОперКредит.УдалитьЗначение(ВсегоСтрок - СчЦикла + 1);
		Иначе
			НазвОперации = СписокОперКредит.ПолучитьЗначение(ВсегоСтрок - СчЦикла + 1);
			Таб.ПрисоединитьСекцию("ШапкаТаблицы|Расход");
		КонецЕсли;
	КонецЦикла;
	НазвОперации = ?(ПоОперациям=1,"Расход - прочее","Расход");
	Таб.ПрисоединитьСекцию("ШапкаТаблицы|Расход");
	
	Таб.ПрисоединитьСекцию("ШапкаТаблицы|КонечныйОстаток");
	глОживить(1);
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 3, "ВедомостьПоКассе", "ВедомостьПоКассе");
	
	// ВЫВОД ГРУППИРОВОК ЗАПРОСА
	ВывестиГруппировку(Запрос,1);  
	
	// Заполнение полей "Итого"
	ПечатьСтроки(Запрос,0,"Итого","","");   
	   
	Таб.ОбластьПечати(3);
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);    
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;

	Таб.Показать("Ведомость по кассе", "");   
	       
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	Если ФлагВосстановленияНастройки = 0 Тогда
	                                                      
		ВыбВидВалюты   = 1;
		ВидРазделителя = 1;
        ДатаНачала      = глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов");
		Если ПустоеЗначение(ДатаНачала) = 1 Тогда
			ДатаНачала      = НачМесяца(ДатаКонца);    
		КонецЕсли;
		
	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаНачала 		= глРасшифровка.Получить("ДатаНачала");
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВидРазделителя	= глРасшифровка.Получить("ВидРазделителя");
		ВыбРазделитель1	= глРасшифровка.Получить("ВыбРазделитель1");
		ВыбРазделитель2	= глРасшифровка.Получить("ВыбРазделитель2");
		ВыбРазделитель3	= глРасшифровка.Получить("ВыбРазделитель3");

		ВыбКасса		= глРасшифровка.Получить("ВыбКасса");
		ВыбВалюта		= глРасшифровка.Получить("ВыбВалюта");
		ВыбВидВалюты	= глРасшифровка.Получить("ВыбВидВалюты");
		ПоОперациям		= глРасшифровка.Получить("ПоОперациям");
		
		глРасшифровка.Получить("Группировки").Выгрузить(Группировки);
		
	
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;                      
	
	УправлениеДиалогом();
	
КонецПроцедуры		// ПриОткрытии()       

//******************************************************************************
// Предопределенная процедура.
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	УправлениеДиалогом();
	
КонецПроцедуры // ВводНового()
                                                                                
////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//

ДатаКонца = ПолучитьДатуТА();

Группировки.ДобавитьЗначение("Фирма", 	"Фирма");
Группировки.ДобавитьЗначение("Касса", 	"Касса");
Группировки.ДобавитьЗначение("Валюта", 	"Валюта остатка");
Группировки.ДобавитьЗначение("День", 	"День");
Группировки.ДобавитьЗначение("Документ","Документы движения");

Группировки.Пометка(2, 1);
Группировки.Пометка(3, 1);
Группировки.Пометка(4, 1);
