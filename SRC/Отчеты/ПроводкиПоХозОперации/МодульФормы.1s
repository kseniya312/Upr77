////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)
	
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
	
КонецФункции //РасшифровкаОбновить()                                            
    
//******************************************************************************
// ЭтоНоменклатура(Элемент)
//
// Параметры: 
//  Элемент - проверяемый элемент.
//
// Возвращаемое значение:
//  1 - это номенклатура, 0 - нет.
//
// Описание:
//  Проверяет, является ли переданное значение элементом справочника "Номенклатура"
//
Функция ЭтоНоменклатура(Элемент)
	
	Если ТипЗначенияСтр(Элемент) = "Справочник" Тогда
		Если Элемент.Вид()="Номенклатура" Тогда
			Возврат 1;
		КонецЕсли;		
	КонецЕсли;
	                 
	Возврат 0;
	
КонецФункции // ЭтоНоменклатура()


//**************************************************************************************************
// ПопробоватьУзнатьЕдиницу(Проводка)
//
// Параметры:
//  Проводка  - спозиционированная на текущую строку таблица проводок
//
// Описание:
//	Попытка узнать единицу измерения для текущей проводки.
//
Функция ПопробоватьУзнатьЕдиницу(Проводка)
	
	Если ЭтоНоменклатура(Проводка.СубконтоД1) = 1 Тогда
		Возврат Проводка.СубконтоД1.БазоваяЕдиница;
	КонецЕсли;
	Если ЭтоНоменклатура(Проводка.СубконтоД2) = 1 Тогда
		Возврат Проводка.СубконтоД2.БазоваяЕдиница;
	КонецЕсли;
	Если ЭтоНоменклатура(Проводка.СубконтоД3) = 1 Тогда
		Возврат Проводка.СубконтоД3.БазоваяЕдиница;
	КонецЕсли;                    
	Если ЭтоНоменклатура(Проводка.СубконтоК1) = 1 Тогда
		Возврат Проводка.СубконтоК1.БазоваяЕдиница;
	КонецЕсли;
	Если ЭтоНоменклатура(Проводка.СубконтоК2) = 1 Тогда
		Возврат Проводка.СубконтоК2.БазоваяЕдиница;
	КонецЕсли;
	Если ЭтоНоменклатура(Проводка.СубконтоК3) = 1 Тогда
		Возврат Проводка.СубконтоК3.БазоваяЕдиница;
	КонецЕсли;
	
	Возврат("");
	
КонецФункции // ПопробоватьУзнатьЕдиницу()

//**************************************************************************************************
// ПечатьПроводки()
//
// Описание:
//	Печать одной строки проводки
//
Функция ПечатьПроводки(Проводка,Сводная = 0)
	                                   
	НомерПроводки = Проводка.НомерСтроки;
	РазделительУчета = "";

	СчетДт = Проводка.СчетД;
	СчетКт = Проводка.СчетК;
	                      
	Если Сводная = 0 Тогда
		СубконтоДт1 = Проводка.СубконтоД1;
		СубконтоДт2 = Проводка.СубконтоД2;
		СубконтоДт3 = Проводка.СубконтоД3;
		СубконтоКт1 = Проводка.СубконтоК1;
		СубконтоКт2 = Проводка.СубконтоК2;
		СубконтоКт3 = Проводка.СубконтоК3;
		
		Содержание = Проводка.Содержание;
    КонецЕсли;
	
	ЕдКоличество="";
	
	Если (Проводка.Количество=0) или (Сводная = 1) Тогда
		ЕдКоличество="";
	Иначе
		ЕдКоличество=ПопробоватьУзнатьЕдиницу(Проводка);
	КонецЕсли;
	
	ЕдВалСумма   = Проводка.Валюта;
	ВалСумма     = Формат(Проводка.ВалСумма, "Ч 015.2");
	
	Количество   = Формат(Проводка.Количество, "Ч 015.3.,");
	Сумма        = Формат(Проводка.Сумма, "Ч 015.2");
	ЕдСумма      = глРубли;
	
	Если Сводная = 0 Тогда
		Таб.ВывестиСекцию("Проводка");
	Иначе     
		Таб.ВывестиСекцию("СводнаяПроводка");
	КонецЕсли;                                          
	
КонецФункции // ПечатьПроводки()

//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   
//	Запускает отчет.
//
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;
	
	Если ВыбДокумент.Выбран()=0 Тогда
		Предупреждение("Не выбран документ для построения отчета!",60);
		Возврат;
	КонецЕсли;
	
	Если ВыбДокумент.Проведен() = 0 Тогда
		Предупреждение("Выбранный документ не проведен!",60);
		Возврат;
	КонецЕсли;            
	                    
	Загол = "Проводки по хоз. операции документа: " + глПредставлениеДокумента(ВыбДокумент);
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;    
	 
	Таб.ИсходнаяТаблица( "ПроводкиПоХозОперации" );
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ПроводкиПоХозОперации");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ВыбДокумент", ВыбДокумент);
	
	глЧислоСтрок = 0;
	
	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Шапка");
	глОживить(1);
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 0, "ПроводкиПоХозОперации", "ПроводкиПоХозОперации");
	
	Таб.ОбластьПечати(3);
	
	ТабПров = глПолучитьТаблицуПроводок(ВыбДокумент, ВыбДокумент);
	
	ТабПров.ВыбратьСтроки();
	Пока ТабПров.ПолучитьСтроку()=1 Цикл
		ПечатьПроводки(ТабПров);
	КонецЦикла;                
	
	Таб.ВывестиСекцию("ШапкаСводные");
	
	ТабПров.Свернуть("СчетД,СчетК,Валюта","Сумма,ВалСумма,Количество");
	                           
	ТабПров.ВыбратьСтроки();
	Пока ТабПров.ПолучитьСтроку()=1 Цикл
		ПечатьПроводки(ТабПров,1);
	КонецЦикла;                
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Проводки по хоз. операции документа", "");   
	       
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ВыбДокумент = глРасшифровка.Получить("ВыбДокумент");
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;                      
КонецПроцедуры		// ПриОткрытии()       