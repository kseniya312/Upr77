////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;
Перем Расшифровка; 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)
	
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
	
КонецФункции //РасшифровкаОбновить()                                            


//******************************************************************************
// ПоДокументам()
//
// Параметры:
//	Нет.
//
// Возвращаемое значение: 
//	Нет
//
// Описание:  
//	Выводит документы движения для определенного заказа по определенной номенклатуре
//
Процедура ПоДокументам()
	Запрос = СоздатьОбъект("Запрос");
	ДатаНач = ВыбДокумент.ДатаДок;
	ДатаКон = ПолучитьДатуТА();
	ТекстЗапроса = "     
	|Период с ДатаНач по ДатаКон;
	|Заказ	 		= Регистр.Заказы.ЗаказПоставщику;
	|Номенклатура   = Регистр.Заказы.Номенклатура;
	|Договор 		= Регистр.Заказы.ДоговорПоставщика;
	|Количество 	= Регистр.Заказы.КоличествоПриход;
	|Сумма			= Регистр.Заказы.СтоимостьПриход;
	|Функция КолНачОст	= НачОст(Количество);
	|Функция КолПриход 	= Приход(Количество);
	|Функция КолРасход 	= Расход(Количество);
	|Функция КолКонОст 	= КонОст(Количество);
	|Функция СуммаНачОст= НачОст(Сумма);
	|Функция СуммаПриход= Приход(Сумма);
	|Функция СуммаРасход= Расход(Сумма);
	|Функция СуммаКонОст= КонОст(Сумма);
	|Группировка Документ;
	|Условие (Номенклатура 	= ВыбНоменклатура);
	|Условие (Заказ 		= ВыбДокумент);";
	ТабДок = СоздатьОбъект("Таблица");        
		
	ТабДок.ИсходнаяТаблица("ДокументыДвижения");
	ПечЗаголовок = "Документы движений по заказу";
	ТабДок.ВывестиСекцию("Шапка");        

	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;       

	ТабДок.Опции(0, 0, ТабДок.ВысотаТаблицы(), 0, "СостояниеЗаказаПоДокументам", "СостояниеЗаказаПоДокументам");

	Пока Запрос.Группировка("Документ") = 1 Цикл     
		
		Вал = Запрос.Договор.ВалютаВзаиморасчетов;
		Ед  = Запрос.Номенклатура.БазоваяЕдиница;
		ПечТекстСтроки = глПредставлениеДокумента(Запрос.Документ);

		ПечНачОст 	= глФРМ(Запрос.СуммаНачОст);
		ПечПриход 	= глФРМ(Запрос.СуммаПриход);
		ПечРасход 	= глФРМ(Запрос.СуммаРасход);
		ПечКонОст 	= глФРМ(Запрос.СуммаКонОст);
		
		ПечКолНачОст= глФРМКоличество(Запрос.КолНачОст);
		ПечКолПриход= глФРМКоличество(Запрос.КолПриход);
		ПечКолРасход= глФРМКоличество(Запрос.КолРасход);
		ПечКолКонОст= глФРМКоличество(Запрос.КолКонОст);
		
		ТабДок.ВывестиСекцию("Док");  
	КонецЦикла;                
    
	ТабДок.ВывестиСекцию("Подвал");
	ТабДок.ТолькоПросмотр(1);
	ТабДок.Показать("Документы движения заказа " + глПредставлениеДокумента(ВыбДокумент)
					+ " по номенклатуре " + ВыбНоменклатура);
КонецПроцедуры // ПоДокуметам()
	
//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   
//	Запускает отчет.
//
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;    
	Перем ИмяГоризСекции;     
	Перем ДоговорЗаявки, ФирмаЗаказа;
	
	Если ВыбДокумент.Выбран()=0 Тогда
		Предупреждение("Не выбран документ для построения отчета!",60);
		Возврат;
	КонецЕсли;
	
	Если (ВыбДокумент.Вид()<> "ЗаказПоставщику") Тогда
		Предупреждение("Неверный вид документа!",60);
		Возврат;
	КонецЕсли;            
	
	Если ВыбДокумент.Проведен() = 0 Тогда
		Предупреждение("Выбранный документ не проведен!",60);
		Возврат;
	КонецЕсли;          
	
	// формирование заголовка отчета.
	Загол = глПредставлениеДокумента(ВыбДокумент) + "; Поставщик: " + ВыбДокумент.Контрагент+"";
	Если ПустоеЗначение(ВыбНоменклатура) = 0 Тогда
		Загол = Загол+";"+РазделительСтрок+"По номенклатуре: "+ВыбНоменклатура;
	КонецЕсли;
	
	Если ВыбДокумент.СравнитьТА() = 1 Тогда
		Предупреждение("Выбранный документ находится за точкой актуальности итогов!",60);
		Возврат;
	КонецЕсли;   
	
	ДатаНачала    = ВыбДокумент;   
	ДоговорЗаказа = ВыбДокумент.Договор;
	ФирмаЗаказа   = ВыбДокумент.Фирма;
	    
	СписокНоменклатуры = СоздатьОбъект("СписокЗначений");
	// запрос будем делать по товарам, которые есть в таблице заказа
	Если ПустоеЗначение(ВыбНоменклатура)=0 Тогда
	    СписокНоменклатуры.ДобавитьЗначение(ВыбНоменклатура);
	Иначе                                 
		ТаблицаНоменклатуры = СоздатьОбъект("ТаблицаЗначений");
		ВыбДокумент.ВыгрузитьТабличнуюЧасть(ТаблицаНоменклатуры,"Номенклатура");
		ТаблицаНоменклатуры.Выгрузить(СписокНоменклатуры);
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;    
	 
	Таб.ИсходнаяТаблица( "СостояниеЗаказа" );
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "СостояниеЗаказа");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаКонца", ДатаКонца);
	Расшифровка.Установить("ВыбДокумент",  ВыбДокумент);
	Расшифровка.Установить("ВыбНоменклатура", ВыбНоменклатура);
    
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Заказ	      = Регистр.Заказы.      ЗаказПоставщику,
	|               Регистр.ЗаказыЗаявки.ЗаказПоставщику;
	|ТекДок       = Регистр.Заказы.      ТекущийДокумент,
	|               Регистр.ЗаказыЗаявки.ТекущийДокумент;
	|Фирма        = Регистр.Заказы.                      Фирма,
	|               Регистр.ЗаказыЗаявки.ЗаказПоставщику.Фирма;
	|КолЗаказа    = Регистр.Заказы.      КоличествоПриход;
	|КолЗаказаПок = Регистр.ЗаказыЗаявки.Количество;
	|Покупатель   = Регистр.ЗаказыЗаявки.ЗаявкаПокупателя.Контрагент;
	|Номенклатура = Регистр.Заказы.      Номенклатура, 
	|               Регистр.ЗаказыЗаявки.Номенклатура;
	|Функция ВыписанЗаказ	    = Приход(КолЗаказа);
	|Функция СнятЗаказ          = Расход(КолЗаказа) Когда (ТекДок.Вид()  = ""ЗаказПоставщику"");
	|Функция ОстатокЗаказа      = КонОст(КолЗаказа);
	|Функция Получено	        = Расход(КолЗаказа) Когда (ТекДок.Вид() <> ""ЗаказПоставщику"");
	|Функция ВыписанЗаказПок    = Приход(КолЗаказаПок);
	|Функция СнятЗаказПок       = Расход(КолЗаказаПок) Когда ((ТекДок.Вид() = ""ЗаказПоставщику"")
	|                                                    или  (ТекДок.Вид() = ""ЗаявкаПокупателя""));
	|Функция ОстатокЗаказаПок   = КонОст(КолЗаказаПок);
	|Функция ПолученоПок        = Расход(КолЗаказаПок) Когда ((ТекДок.Вид() <> ""ЗаказПоставщику"")
	|                                                      и  (ТекДок.Вид() <> ""ЗаявкаПокупателя""));
	|Группировка Номенклатура Без Групп;
	|Группировка Покупатель   Без Групп;
	|Условие (Номенклатура в СписокНоменклатуры);                                
	|Условие (Заказ = ВыбДокумент);
	|Условие (Фирма = ФирмаЗаказа);
	|"//}}ЗАПРОС    
	;
	
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;       
	                
	ПечЗаголовок = "Состояние заказа поставщику";
	глЧислоСтрок = 0;
	
	Таб.ВывестиСекцию("Кнопки|"+ИмяГоризСекции);
	Таб.ВывестиСекцию("Шапка|"+ИмяГоризСекции);
	Таб.ВывестиСекцию("ШапкаТаблицы|"+ИмяГоризСекции);
	
	// выводим шапку на каждой странице
	НачПовт = Таб.ВысотаСекции("Кнопки") + Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	глОживить(1);
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 0, "СостояниеЗаказа", "СостояниеЗаказа");
	
	// ВЫВОД ГРУППИРОВОК ЗАПРОСА  
	Пока Запрос.Группировка("Номенклатура") = 1 Цикл
		// Заполнение полей Номенклатура
		ТекНом          = Запрос.Номенклатура;
        ПечЕд           = ТекНом.ОсновнаяЕдиница;
		ПечТекстСтроки 	= ТекНом;
		ТекРасшифровка 	= ТекНом;
		Расшифровка.Установить("ВыбНоменклатура", ТекНом);
		Расшифровка.Установить("Режим", "По документам");
		ПечВыписаноК 	= глФРМКоличество(Запрос.ВыписанЗаказ , ПечЕд);
		ПечСнятоК 	    = глФРМКоличество(Запрос.СнятЗаказ    , ПечЕд);
		ПечПолучитьК    = глФРМКоличество(Запрос.ОстатокЗаказа, ПечЕд);
		ПечПолученоК 	= глФРМКоличество(Запрос.Получено     , ПечЕд);
		                          
		Если Запрос.ОстатокЗаказа = 0 Тогда
			Таб.ВывестиСекцию("СтрокаТМЦОК");
		Иначе     
			Таб.ВывестиСекцию("СтрокаТМЦ");
		КонецЕсли;
		глОживить(1);
		
		// ВЫВОД ГРУППИРОВОК ЗАПРОСА  
		Пока Запрос.Группировка("Покупатель") = 1 Цикл
			// Заполнение полей Номенклатура
			ТекПок          = Запрос.Покупатель;
			Если ПустоеЗначение(ТекПок) = 1 Тогда
			    Продолжить;
			КонецЕсли;
			ПечТекстСтроки 	= ТекПок;
			ТекРасшифровка 	= ТекПок;
			ПечВыписаноК 	= глФРМКоличество(Запрос.ВыписанЗаказПок , ПечЕд);
			ПечСнятоК 	    = глФРМКоличество(Запрос.СнятЗаказПок    , ПечЕд);
			ПечПолучитьК    = глФРМКоличество(Запрос.ОстатокЗаказаПок, ПечЕд);
			ПечПолученоК 	= глФРМКоличество(Запрос.ПолученоПок     , ПечЕд);
			
			Если Запрос.ОстатокЗаказаПок = 0 Тогда
				Таб.ВывестиСекцию("СтрокаПокОК");
			Иначе     
				Таб.ВывестиСекцию("СтрокаПок");
			КонецЕсли;
			глОживить(1);
			
		КонецЦикла;       
	КонецЦикла;       
	
	Таб.ВывестиСекцию("Подвал|"+ИмяГоризСекции);
	   
	Таб.ОбластьПечати(3);
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Состояние заказа поставщику", "");
	       
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВыбДокумент		= глРасшифровка.Получить("ВыбДокумент");
		ВыбНоменклатура = глРасшифровка.Получить("ВыбНоменклатура");
		Режим 			= глРасшифровка.Получить("Режим");

		Если Режим = "По документам" Тогда
			ПоДокументам();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;                      
КонецПроцедуры		// ПриОткрытии()       

ДатаКонца = ПолучитьДатуТА();