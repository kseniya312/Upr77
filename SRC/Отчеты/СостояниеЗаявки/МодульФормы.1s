////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
// используются для стандартного механизма кнопок "Обновить" и "Настройка"
Перем Таб;		
Перем Обновить;                                                                    
Перем Расшифровка;                                                                 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//******************************************************************************
// РасшифровкаОбновить(Обновить)
//
// Параметры:
//  Обновить = 	1 - нажата кнопка "Обновить"
//				2 - нажата кнопка "Настройка"	
//
// Возвращаемое значение: 
//	Расшифровка (список значений)
//
// Вызывается из формул элементов диалога:
//  из таблицы, кнопки "Обновить" и "Настройка"
//
// Описание:  
//	функция для стандартного механизма кнопок ""Обновить" и "Настройка""
//  помещает значение параметра в список Расшифровка  и возвращает этот список
//
Функция РасшифровкаОбновить(Обновить)
	
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
	
КонецФункции //РасшифровкаОбновить()                                            

//******************************************************************************
// ПоДокументам()
//
// Параметры:
//	Нет.
//
// Возвращаемое значение: 
//	Нет
//
// Описание:  
//	Выводит документы движения для определенной заявки по определенной номенклатуре
//
Процедура ПоДокументам()
	Запрос = СоздатьОбъект("Запрос");
	ДатаНач = ВыбДокумент.ДатаДок;
	ДатаКон = ПолучитьДатуТА();
	ТекстЗапроса = "     
	|Период с ДатаНач по ДатаКон;
	|Заявка 		= 	Регистр.Заявки.ЗаявкаПокупателя,
	|					Регистр.РезервыТМЦ.ЗаявкаПокупателя;
	|Номенклатура   = 	Регистр.Заявки.Номенклатура,
	|					Регистр.РезервыТМЦ.Номенклатура;
	|Договор 		= 	Регистр.Заявки.ДоговорПокупателя,
	|					Регистр.РезервыТМЦ.ДоговорПокупателя;
	|Количество 	= 	Регистр.Заявки.КоличествоРасход;
	|Резерв		 	= 	Регистр.РезервыТМЦ.Количество;
	|Сумма			= Регистр.Заявки.СтоимостьРасход;
	|Функция КолНачОст	= НачОст(Количество);
	|Функция КолПриход 	= Приход(Количество);
	|Функция КолРасход 	= Расход(Количество);
	|Функция КолКонОст 	= КонОст(Количество);
	|Функция СуммаНачОст= НачОст(Сумма);
	|Функция СуммаПриход= Приход(Сумма);
	|Функция СуммаРасход= Расход(Сумма);
	|Функция СуммаКонОст= КонОст(Сумма);
	|Функция РезервНачОст = НачОст(Резерв);
	|Функция РезервПриход = Приход(Резерв);
	|Функция РезервРасход = Расход(Резерв);
	|Группировка Документ;
	|Условие (Номенклатура = ВыбНоменклатура);
	|Условие (Заявка = ВыбДокумент);";
	ТабДок = СоздатьОбъект("Таблица");        
		
	ТабДок.ИсходнаяТаблица("ДокументыДвижения");
	ПечЗаголовок = "Документы движений по заявке";
	ТабДок.ВывестиСекцию("Шапка");        

	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;       

	ТабДок.Опции(0, 0, ТабДок.ВысотаТаблицы(), 0, "СостояниеЗаявкиПоДокументам", "СостояниеЗаявкиПоДокументам");
	ТекРезерв = Запрос.РезервНачОст;

	Пока Запрос.Группировка("Документ") = 1 Цикл     
		
		Вал = Запрос.Договор.ВалютаВзаиморасчетов;
		Ед  = Запрос.Номенклатура.БазоваяЕдиница;
		ПечТекстСтроки = глПредставлениеДокумента(Запрос.Документ);

		ПечНачОст 	= глФРМ(Запрос.СуммаНачОст);
		ПечПриход 	= глФРМ(Запрос.СуммаПриход);
		ПечРасход 	= глФРМ(Запрос.СуммаРасход);
		ПечКонОст 	= глФРМ(Запрос.СуммаКонОст);
		
		ПечКолНачОст= глФРМКоличество(Запрос.КолНачОст);
		ПечКолПриход= глФРМКоличество(Запрос.КолПриход);
		ПечКолРасход= глФРМКоличество(Запрос.КолРасход);    
		                            
		ТекРезерв   = ТекРезерв + Запрос.РезервПриход-Запрос.РезервРасход; 
		ПечРезерв	= глФРМКоличество(ТекРезерв);
		
		ТабДок.ВывестиСекцию("Док");  
	КонецЦикла;                

	ТабДок.ВывестиСекцию("Подвал");
	ТабДок.ТолькоПросмотр(1);
	ТабДок.Показать("Документы движения заявки " + глПредставлениеДокумента(ВыбДокумент)
					+ " по номенклатуре " + ВыбНоменклатура);
КонецПроцедуры // ПоДокуметам()
	
//******************************************************************************
// Сформировать(ЗакрытьДиалог=0)
//
// Параметры:
//   ЗакрытьДиалог - флаг того, что после формирования отчета надо закрыть диалог
//
// Возвращаемое значение: 
//	Нет
// 
// Вызывается из формул элементов диалога:
//   кнопки "Сформировать" и "ОК"
//
// Описание:   
//	Запускает отчет.
//
Процедура Сформировать(ЗакрытьДиалог=0)
	
	Перем Запрос, ТекстЗапроса;    
	Перем ИмяГоризСекции;     
	Перем ДоговорЗаявки, ФирмаЗаявки;
	
	Если ВыбДокумент.Выбран()=0 Тогда
		Предупреждение("Не выбран документ для построения отчета!",60);
		Возврат;
	КонецЕсли;
	
	Если (ВыбДокумент.Вид()<> "ЗаявкаПокупателя") Тогда
		Предупреждение("Неверный вид документа!",60);
		Возврат;
	КонецЕсли;            
	
	Если ВыбДокумент.Проведен() = 0 Тогда
		Предупреждение("Выбранный документ не проведен!",60);
		Возврат;
	КонецЕсли;          
	
	Если ВыбДокумент.ВидОперации = Перечисление.ВидыОперацийЗаявок.Неподтвержденная Тогда
		ИмяГоризСекции = "БезРезервов";
	Иначе
		ИмяГоризСекции = "СРезервами";
	КонецЕсли;
	                    
	// формирование заголовка отчета.
	Загол = глПредставлениеДокумента(ВыбДокумент) + "; Клиент: " + ВыбДокумент.Контрагент+"";
	Если ПустоеЗначение(ВыбНоменклатура) = 0 Тогда
		Загол = Загол+";"+РазделительСтрок+"По номенклатуре: "+ВыбНоменклатура;
	КонецЕсли;
	
	Если ВыбДокумент.СравнитьТА() = 1 Тогда
		Предупреждение("Выбранный документ находится за точкой актуальности итогов!",60);
		Возврат;
	КонецЕсли;   
	
	ДатаНачала    = ВыбДокумент;   
	ДоговорЗаявки = ВыбДокумент.Договор;
	ФирмаЗаявки   = ВыбДокумент.Фирма;
	    
	СписокНоменклатуры = СоздатьОбъект("СписокЗначений");
	// запрос будем делать по товарам, которые есть в таблице заявки
	Если ПустоеЗначение(ВыбНоменклатура)=0 Тогда
	    СписокНоменклатуры.ДобавитьЗначение(ВыбНоменклатура);
	Иначе                                 
		ТаблицаНоменклатуры = СоздатьОбъект("ТаблицаЗначений");
		ВыбДокумент.ВыгрузитьТабличнуюЧасть(ТаблицаНоменклатуры,"Номенклатура");
		ТаблицаНоменклатуры.Выгрузить(СписокНоменклатуры);
		Если СписокНоменклатуры.РазмерСписка() = 0 Тогда
			Предупреждение("Выбранная заявка не содержит ни одной строки!",60);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;    
	 
	Таб.ИсходнаяТаблица( "СостояниеЗаявки" );
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "СостояниеЗаявки");
	
	// все настройки помещаем в список
	Расшифровка.Установить("ДатаКонца", ДатаКонца);
	Расшифровка.Установить("ВыбДокумент", ВыбДокумент);
	Расшифровка.Установить("ВыбНоменклатура", ВыбНоменклатура);
    
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Заявка	      = Регистр.Заявки.      ЗаявкаПокупателя,
	|               Регистр.ЗаказыЗаявки.ЗаявкаПокупателя,
	|               Регистр.РезервыТМЦ.  ЗаявкаПокупателя;
	|ТекДок       = Регистр.Заявки.ТекущийДокумент;
	|Договор      = Регистр.РезервыТМЦ.ДоговорПокупателя;
	|Фирма        = Регистр.РезервыТМЦ.Фирма,
	|               Регистр.ОстаткиТМЦ.Фирма,
	|               Регистр.Заявки.    Фирма,
	|               Регистр.ЗаказыЗаявки.ЗаявкаПокупателя.Фирма;
	|КолЗаявки    = Регистр.Заявки.      КоличествоРасход;
	|КолЗаказа    = Регистр.ЗаказыЗаявки.Количество;
	|КолРезерва   = Регистр.РезервыТМЦ.  Количество;
	|КолОстатка   = Регистр.ОстаткиТМЦ.  Количество;      
	|Номенклатура = Регистр.Заявки.      Номенклатура, 
	|               Регистр.ЗаказыЗаявки.Номенклатура, 
	|               Регистр.РезервыТМЦ.  Номенклатура,
	|               Регистр.ОстаткиТМЦ.  Номенклатура;
	|Функция ВыписанаЗаявка	    = Приход(КолЗаявки)  Когда (Заявка = ВыбДокумент);
	|Функция ОстатокНаСкладах   = КонОст(КолОстатка);
	|Функция ЧужиеРезервы       = КонОст(КолРезерва) Когда (Заявка <> ВыбДокумент);
	|Функция Зарезервировано    = КонОст(КолРезерва) Когда (Заявка = ВыбДокумент);
	|Функция СнятаЗаявка 	    = Расход(КолЗаявки)  Когда((Заявка = ВыбДокумент) и ((ТекДок.Вид() = ""ЗаявкаПокупателя"") или (ТекДок.Вид() = ""ОтменаЗаявок"")));
	|Функция ОстатокЗаявки      = КонОст(КолЗаявки)  Когда (Заявка = ВыбДокумент);
	|Функция ЗаказаноПоставщику = КонОст(КолЗаказа)  Когда (Заявка = ВыбДокумент);
	|Функция Отпущено	        = Расход(КолЗаявки)  Когда((Заявка = ВыбДокумент) и (ТекДок.Вид() <> ""ЗаявкаПокупателя"") и (ТекДок.Вид() <> ""ОтменаЗаявок""));
	|Группировка Номенклатура Без Групп;
	|Условие (Номенклатура в СписокНоменклатуры);                                
	|Условие (Фирма = ФирмаЗаявки);
	|"//}}ЗАПРОС    
	;
	
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;       
	                
	ПечЗаголовок = "Состояние заявки покупателя";
	глЧислоСтрок = 0;
	
	Таб.ВывестиСекцию("Кнопки|"+ИмяГоризСекции);
	Таб.ВывестиСекцию("Шапка|"+ИмяГоризСекции);
	Таб.ВывестиСекцию("ШапкаТаблицы|"+ИмяГоризСекции);
	
	// выводим шапку на каждой странице
	НачПовт = Таб.ВысотаСекции("Кнопки") + Таб.ВысотаСекции("Шапка");
	КонПовт = НачПовт + Таб.ВысотаСекции("ШапкаТаблицы");
	Таб.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	глОживить(1);
	
	Таб.Опции(0, 0, Таб.ВысотаТаблицы(), 0, "СостояниеЗаявки", "СостояниеЗаявки");
	
	// ВЫВОД ГРУППИРОВОК ЗАПРОСА  
	Пока Запрос.Группировка(1) = 1 Цикл
		// Заполнение полей Номенклатура
		ТекНом          = Запрос.Номенклатура;
        ПечЕд           = ТекНом.ОсновнаяЕдиница;
		ПечТекстСтроки 	= ТекНом;
		Расшифровка.Установить("ВыбНоменклатура", ТекНом);
		Расшифровка.Установить("Режим", "По документам");
		Отпустить       = Запрос.ВыписанаЗаявка - Запрос.СнятаЗаявка - Запрос.Отпущено;
		Если ТекНом.ВидНоменклатуры = Перечисление.ВидыНоменклатуры.Услуга Тогда
			// услуги считаем что оказать нам ничто не мешает
			МожноОтпустить  = Отпустить;
		Иначе
			МожноОтпустить  = Мин(Отпустить,Макс(Запрос.ОстатокНаСкладах - Запрос.ЧужиеРезервы,0));
		КонецЕсли;
		
		ПечВыписаноК 	= глФРМКоличество(Запрос.ВыписанаЗаявка    ,   ПечЕд);
		ПечСнятоК 	    = глФРМКоличество(Запрос.СнятаЗаявка       ,   ПечЕд);
		ПечЗаказаноК 	= глФРМКоличество(Запрос.ЗаказаноПоставщику,   ПечЕд);	
		ПечОтпуститьК   = глФРМКоличество(Отпустить                ,   ПечЕд);
		ПечОтпущеноК 	= глФРМКоличество(Запрос.Отпущено          ,   ПечЕд);
		ПечОстатокК 	= глФРМКоличество(МожноОтпустить           ,   ПечЕд);
		ПечЗарезервировано= глФРМКоличество(Запрос.Зарезервировано ,   ПечЕд);
		              
		Если МожноОтпустить < Отпустить Тогда
			Таб.ВывестиСекцию("Строка|"+ИмяГоризСекции);
		Иначе     
			Таб.ВывестиСекцию("СтрокаОК|"+ИмяГоризСекции);
		КонецЕсли;
		глОживить(1);
		
	КонецЦикла;       
	
	Таб.ВывестиСекцию("Подвал|"+ИмяГоризСекции);
	   
	Таб.ОбластьПечати(3);
	
	// Вывод заполненной формы
	Таб.ТолькоПросмотр(1);
	Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
		Таб.Защита(1);
	КонецЕсли;
	Таб.Показать("Состояние заявки покупателя", "");   
	       
	Если (Обновить = 2)ИЛИ(ЗакрытьДиалог=1) Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
КонецПроцедуры // Сформировать()                          


////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
Процедура ПриОткрытии(ФлагВосстановленияНастройки)	
	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		
		// восстанавливаем настройки из списка
		ДатаКонца 		= глРасшифровка.Получить("ДатаКонца");
		ВыбДокумент		= глРасшифровка.Получить("ВыбДокумент");
		ВыбНоменклатура = глРасшифровка.Получить("ВыбНоменклатура");
		Режим 			= глРасшифровка.Получить("Режим");

		Если Режим = "По документам" Тогда
			ПоДокументам();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;                      
КонецПроцедуры		// ПриОткрытии()       
	
ДатаКонца = ПолучитьДатуТА();