////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// ПечатьСправкиОСчетах(ВидСправки)
//
// Параметры:          
//  ВидСправки - "число" - 1 - рублевая, иначе валютная.
//
// Вызывается из формул элементов диалога:
//  
//
// Описание:
//  Процедура выводит на печать справки о рублевых и валютных счетах.
//
Процедура ПечатьСправкиОСчетах(ВидСправки)
	
	Перем СвоеЮрЛицо;
	
	ПолучитьОтбор("ЮрФизЛицо", СвоеЮрЛицо);
	ЮридическийАдрес        = СвоеЮрЛицо.ЮрАдрес;
	ОфициальноеНаименование = СвоеЮрЛицо.ОфициальноеНаименование;
	Телефоны                = СвоеЮрЛицо.Телефоны;
	ИНН                     = СвоеЮрЛицо.ИНН;
	ИМНС                    = СвоеЮрЛицо.НаименованиеИМНС;
	
	ДатаНачалаКвартала      = НачКвартала( РабочаяДата() );
	ДатаНачалаСледПериода   = КонКвартала( РабочаяДата() )+1;

	Справка=СоздатьОбъект("Таблица");
	Если ВидСправки = 1 Тогда
	    Справка.ИсходнаяТаблица("Рублевые");
	Иначе
		Справка.ИсходнаяТаблица("Валютные");
	КонецЕсли;

	Справка.ВывестиСекцию("Шапка");

	Спр=СоздатьОбъект("Справочник.БанковскиеСчета");
	Спр.ПорядокКодов();
	Спр.ВыбратьЭлементыПоРеквизиту("ЮрФизЛицо", СвоеЮрЛицо, 0, 1);
	
	СчетчикСтрок=0;
	Пока Спр.ПолучитьЭлемент()=1  Цикл
		Если Спр.ПометкаУдаления()=1 Тогда
		    Продолжить;
		КонецЕсли;
		Если ВидСправки = 1 Тогда
			Если Спр.ВалютаСчета <> глРубли Тогда
	        	Продолжить;
			КонецЕсли;
	    Иначе 
			Если Спр.ВалютаСчета = глРубли Тогда
	        	Продолжить;
			КонецЕсли;
	    КонецЕсли;

		// В отчет должны попадать счета, у которых дата закрытия счета пустая или
		// лежит в текущем квартале
		Если Число( Спр.ДатаЗакрытияСчета ) > 0 Тогда
			Если (Спр.ДатаЗакрытияСчета < ДатаНачалаКвартала ) Тогда
			    Продолжить;
			КонецЕсли;
		КонецЕсли;

		СчетчикСтрок=СчетчикСтрок+1;
		Справка.ВывестиСекцию( "Строка" );

	КонецЦикла;

	Справка.ВывестиСекцию( "Подвал" );
	Справка.ТолькоПросмотр(1);
	Справка.Опции(0,0,0,0, "СправкаОСчетах", "СправкаОСчетах");
	
	Если ВидСправки = 1 Тогда
		Справка.Показать("Справка о рублевых счетах");
	Иначе
		Справка.Показать("Справка о валютных счетах");
	КонецЕсли;

КонецПроцедуры // ПечатьСправкиОСчетах()

//******************************************************************************
// ВключитьОтбор(ЗначениеОтбора = "")
//
// Параметры:
//  ЗначениеОтбора - "Справочник" - юридическое или физическое лицо.
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Здесь перечисляются элементы диалога.
//
// Описание:
//  Включает / отключает отбор по юр./физ. лицу
//  Делает видимыми кнопки печати "Справки о счетах" для своих юр. лиц.
//
Процедура ВключитьОтбор(ЗначениеОтбора = "")
	
	Видимость = 0;
	
	Если ПустоеЗначение(ЗначениеОтбора) = 1 Тогда 
		УстановитьОтбор("", );
		
	Иначе
		Если ЗначениеОтбора.Вид() = "Контрагенты" Тогда
			ЗначениеОтбора = ЗначениеОтбора.ЮрФизЛицо;
		КонецЕсли;
		
	    УстановитьОтбор("ЮрФизЛицо", ЗначениеОтбора);
		
		Если (ЗначениеОтбора.Вид() = "СвоиЮрЛица") и (Форма.МодальныйРежим() = 0) Тогда
			Видимость = 1;
		КонецЕсли;
	КонецЕсли;
	
	Форма.ИспользоватьСлой("Кнопки", Видимость);
	
КонецПроцедуры // ВключитьОтбор()

//******************************************************************************
// ВыбратьЗначениеОтбора()
//
// Параметры:
//  
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "Отбор...".
//
// Описание:
//  Организует назначение отбора по юр./физ. лицу.
//
Процедура ВыбратьЗначениеОтбора()
	
	Перем ИмяОтбора, ЗначениеОтбора, ВидЗначения;
	
	Список = СоздатьОбъект("СписокЗначений");
	
	Список.ДобавитьЗначение("СвоиЮрЛица", "Собственные юр. лица");
	Список.ДобавитьЗначение("ЮрЛица", "Сторонние юр. лица");
	Список.ДобавитьЗначение("ФизЛица", "Физические лица");
	Список.ДобавитьЗначение("Контрагенты", "Контрагенты");
	
	ПолучитьОтбор(ИмяОтбора, ЗначениеОтбора);
	
	Если ПустаяСтрока(ИмяОтбора) = 0 Тогда
		Список.ДобавитьЗначение("", "Отключить отбор");
	КонецЕсли;
	
	Если Список.ВыбратьЗначение(ВидЗначения, "", 0, 60, 1) <> 1 Тогда
		Возврат;
	
	ИначеЕсли ПустаяСтрока(ВидЗначения) = 1 Тогда
		ВключитьОтбор();
		Возврат;
	
	КонецЕсли;
		
	Объект = СоздатьОбъект("Справочник."+ВидЗначения);
	Объект.НайтиЭлемент(ЗначениеОтбора);
	
	Если Объект.Выбрать("Значение отбора", "ФормаСписка") <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	ВключитьОтбор(Объект.ТекущийЭлемент());
	
КонецПроцедуры // ВыбратьЗначениеОтбора()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()	
	
	Если ТипЗначенияСтр(Форма.Параметр) = "Справочник" Тогда
		ВключитьОтбор(Форма.Параметр);
	Иначе
		ВключитьОтбор();
	КонецЕсли;

КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриПовторномОткрытии()
	
	Если ТипЗначенияСтр(Форма.Параметр) = "Справочник" Тогда
		ВключитьОтбор(Форма.Параметр);
	КонецЕсли;
	
КонецПроцедуры // ПриПовторномОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриУстановкеОтбора(ТипОтбора, ЗначениеОтбора)
	
	ВключитьОтбор(ЗначениеОтбора);
	
КонецПроцедуры // ПриУстановкеОтбора()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриВводеСтроки()
	
	Перем ВидОтбора, ЗначениеОтбора;
	
	ПолучитьОтбор(ВидОтбора, ЗначениеОтбора);
	ОткрытьФорму("Элемент.БанковскиеСчета", ЗначениеОтбора);
	СтатусВозврата(0);
	
КонецПроцедуры // ПриВводеСтроки()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеРедактированияСтроки()
	
	ОткрытьФорму(ТекущийЭлемент());
	СтатусВозврата(0);
	
КонецПроцедуры // ПриНачалеРедактированияСтроки()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриВыбореСтроки()
	
	// Форма может быть открыта для выбора из процедур ПриНачалеВыбораЗначения()
	// в модальном режиме. В этом случае результат выбора передается передается
	// через параметр формы.
	
	Форма.Параметр = ТекущийЭлемент();
	
КонецПроцедуры // ПриВыбореСтроки()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
 
РедактироватьВДиалоге(0, 0);