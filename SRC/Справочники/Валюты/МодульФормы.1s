////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ВыбратьФайл()
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//	Открывает диалог выбора файла прописи. 
// Выбранный файл запоминается в реквизите "ИмяФайлаПрописи".
//
Процедура ВыбратьФайл()
	
	ИмяФайла	= ИмяФайлаПрописи;
	Каталог		= КаталогИБ();
	
	Пока 1=1 Цикл
		ФС.ВыбратьФайл(0, ИмяФайла, Каталог, "Выберите файл прописи для валюты """ +
		                                      Наименование + """", "Файлы прописи(*.spl)|*.spl");
		
		Если СокрП(Нрег(Каталог)) <> СокрП(Нрег(КаталогИБ())) Тогда
			Предупреждение("Выберите файл из каталога """ + КаталогИБ() + """ !", 60);
			Каталог = КаталогИБ();
		Иначе
			ИмяФайлаПрописи	= ИмяФайла;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ВыбратьФайл()

//******************************************************************************
// ПоКнопкеВыбораДаты()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка выбора даты.
//
// Описание:
//  Выбирается дата просмотра и редактирования значений периодических реквизитов 
// выбирается по кнопке, чтобы не менялся признак модифицированности формы.
//
Процедура ПоКнопкеВыбораДаты()
	
	глВвестиДатуПериодическихРеквизитов(Контекст, 1);
	
КонецПроцедуры // ПоКнопкеВыбораДаты()

//******************************************************************************
// ПоКнопкеИстория()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "История".
//
// Описание:
//  Вызывается обработка "ЗначенияПериодическихРеквизитов" для просмотра истории 
// значений периодических реквизитов.
//
Процедура ПоКнопкеИстория()
	
	глЗначенияПериодическихРеквизитов(Контекст);
	
КонецПроцедуры // ПоКнопкеИстория()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(ПризнКопирования)	
	
	Если  ПризнКопирования = 0 Тогда
		Кратность = 1;
		Курс      = 1;
	КонецЕсли;
	
КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнЗаписать.Доступность(0);
		Форма.кнОк.Доступность(0);
		
		Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кнОк");
	КонецЕсли;

	// Форме может быть передана дата просмотра и редактирования значений
	// периодических реквизитов, например, когда форма открыта из обработки
	// "ЗначенияПериодическихРеквизитов".
	глПолучитьДатуПериодическихРеквизитов(Контекст);
	
	// Системно не надо контролировать изменение периодических реквизитов.
	СохранениеПериодическихРеквизитов(0);
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриПовторномОткрытии()
	
	// Форме может быть передана дата просмотра и редактирования значений
	// периодических реквизитов, например, когда форма открыта из обработки
	// "ЗначенияПериодическихРеквизитов".
	глПолучитьДатуПериодическихРеквизитов(Контекст);
	
КонецПроцедуры // ПриПовторномОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, ФлагПродолжения)
	
	Если ЭлементДиалога = "ИмяФайлаПрописи" Тогда
		ВыбратьФайл();
		ФлагПродолжения = 0;
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗаписи()
	
	Если ПустоеЗначение(Наименование) = 1 Тогда
		Предупреждение("Не заполнено поле ""Обозначение""!", 60);
		Активизировать("Наименование");
		СтатусВозврата(0);
		Возврат;
	
	ИначеЕсли (ПустаяСтрока(ИмяФайлаПрописи) = 1) ИЛИ // для NT, W2k
	          (ФС.СуществуетФайл(КаталогИБ() + ИмяФайлаПрописи) = 0) Тогда
		Если Вопрос("Неверно указан файл прописей! Продолжить?", "Да+Нет", 60) <> "Да" Тогда
			Активизировать("ИмяФайлаПрописи");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;

	// Обработке ЗаписьПериодическихРеквизитов необходимо передать
	// значения периодических реквизитов, введенные в форме диалога.
	Список = СоздатьОбъект("СписокЗначений");
	Список.Установить("Курс", Курс);
	Список.Установить("Кратность", Кратность);
	
	// При записи вызывается обработка ЗаписьПериодическихРеквизитов 
	// для управления записью значений периодических реквизитов.
	Если глЗаписьПериодическихРеквизитов(Контекст, Список) = 0 Тогда
		СтатусВозврата(0);
	КонецЕсли;

КонецПроцедуры // ПриЗаписи()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
