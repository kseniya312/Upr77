////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ

//******************************************************************************
// ПоКнопкеОстатокПоКассе()
//
// Параметры:
//  
// Возвращаемое значение:
//  нет.
// Описание:

Процедура ПоКнопкеОстатокПоКассе()
	
	Если ПустоеЗначение(ТекущийЭлемент()) = 0 Тогда
	
		Расшифровка = СоздатьОбъект("СписокЗначений");
	    Расшифровка.Установить("Отчет", "ВедомостьПоКассе");
		
		// все настройки помещаем в список
		ВыбДатаКонца  = Мин(РабочаяДата(),ПолучитьДатуТА());
		ВыбДатаНачала = Мин(глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов"),ВыбДатаКонца);
		ВыбДатаНачала = ?(ПустоеЗначение(ВыбДатаНачала) = 1,ВыбДатаКонца,ВыбДатаНачала);
		
		Расшифровка.Установить("ДатаНачала", 	ВыбДатаНачала);
		Расшифровка.Установить("ДатаКонца", 	ВыбДатаКонца);		
		Расшифровка.Установить("ВыбРазделитель1","");
		Расшифровка.Установить("ВыбРазделитель2","");
		Расшифровка.Установить("ВыбРазделитель3","");
		Расшифровка.Установить("ВидРазделителя",1);  
		
		Расшифровка.Установить("ВыбКасса",		0);
		Расшифровка.Установить("ВыбВалюта",		ТекущийЭлемент());
		Расшифровка.Установить("ВыбВидВалюты",	1);
		Расшифровка.Установить("ПоОперациям",	0);
		
		Группировки = СоздатьОбъект("СписокЗначений");
		
		Группировки.ДобавитьЗначение("Фирма", "Фирма");
		Группировки.ДобавитьЗначение("Касса", "Касса");
		Группировки.ДобавитьЗначение("Валюта", "Валюта");
		Группировки.ДобавитьЗначение("День", "День");
		Группировки.ДобавитьЗначение("Документ", "Документы движения");
		
//		Группировки.Пометка(3, 1);
		Группировки.Пометка(3, 1);
//		Группировки.Пометка(5, 1);
		Расшифровка.Установить("Группировки",	Группировки);
		
		глОбработкаРасшифровки(Расшифровка, 0, 0);
	Иначе
		Предупреждение("Не выбрана вылюта!" , 60);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеОстатокПоКассе()

//******************************************************************************
// ПоКнопкеОстаткиНаРасчетныхСчетах()
//
// Параметры:
//  
// Возвращаемое значение:
//  нет.
// Описание:

Процедура ПоКнопкеОстаткиНаРасчетныхСчетах()
	
	Если ПустоеЗначение(ТекущийЭлемент()) = 0 Тогда
	
		Расшифровка = СоздатьОбъект("СписокЗначений");
	    Расшифровка.Установить("Отчет", "ВедомостьПоБанку");
		
		// все настройки помещаем в список
		ВыбДатаКонца  = Мин(РабочаяДата(),ПолучитьДатуТА());
		ВыбДатаНачала = Мин(глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов"),ВыбДатаКонца);
		ВыбДатаНачала = ?(ПустоеЗначение(ВыбДатаНачала) = 1,ВыбДатаКонца,ВыбДатаНачала);
		
		Расшифровка.Установить("ДатаНачала", 	ВыбДатаНачала);
		Расшифровка.Установить("ДатаКонца", 	ВыбДатаКонца);
		Расшифровка.Установить("ВыбРазделитель1","");
		Расшифровка.Установить("ВыбРазделитель2","");
		Расшифровка.Установить("ВыбРазделитель3","");
		Расшифровка.Установить("ВидРазделителя",1);  
		
		Расшифровка.Установить("ВыбРасчСчет",	0);
		Расшифровка.Установить("ВыбВалюта",		ТекущийЭлемент());
		Расшифровка.Установить("ВыбВидВалюты",	1);
		Расшифровка.Установить("ПоОперациям",	0);
		
		Группировки = СоздатьОбъект("СписокЗначений");
		
		Группировки.ДобавитьЗначение("Фирма", 		"Фирма");
		Группировки.ДобавитьЗначение("Валюта", 		"Валюта");
		Группировки.ДобавитьЗначение("РасчСчет", 	"Расчетный счет");
		Группировки.ДобавитьЗначение("День", 		"День");
		Группировки.ДобавитьЗначение("Документ", 	"Документы движения");
		
		Группировки.Пометка(2, 1);
		Группировки.Пометка(3, 1);
		//Группировки.Пометка(4, 1);
		
		Расшифровка.Установить("Группировки",	Группировки);
		
		глОбработкаРасшифровки(Расшифровка, 0, 0);
	Иначе
		Предупреждение("Не выбрана вылюта!" , 60);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеОстаткиНаРасчетныхСчетах()

//******************************************************************************
// ПоКнопкеДобавитьВалюты()
//
// Вызывается из формул элементов диалога:
//  Кнопка "Добавить...".
//
// Описание:
//  Открывает обработку для добавления валют из внешнего классификатора.

Процедура ПоКнопкеДобавитьВалюты()
	
	Параметр=СоздатьОбъект("СписокЗначений");
	Параметр.ДобавитьЗначение("Валюты", "Вид справочника");
	Параметр.ДобавитьЗначение(1, "Способ выбора");
	
	ОткрытьФорму("Обработка.ВыборИзКлассификатора#Валюты", Параметр);
	
КонецПроцедуры // ПоКнопкеДобавитьВалюты()

//******************************************************************************
// ПоКнопкеЗагрузитьКурсы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "Загрузить...".
//
// Описание:
//  Открывает обработку для загрузки курсов валют с сайта РБК.
//
Процедура ПоКнопкеЗагрузитьКурсы()
	
	ОткрытьФорму("Обработка.ИППКурсыВалютРБК");
	
КонецПроцедуры // ПоКнопкеЗагрузитьКурсы()

//******************************************************************************
// ПоКнопкеВыбораДаты()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка выбора даты.
//
// Описание:
//  Выбирается дата просмотра значений периодических реквизитов.
//
Процедура ПоКнопкеВыбораДаты()
	
	глВвестиДатуПериодическихРеквизитов(Контекст, 0);
	
КонецПроцедуры // ПоКнопкеВыбораДаты()

//******************************************************************************
// ПоКнопкеИстория()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "История".
//
// Описание:
//  Вызывается обработка "ЗначенияПериодическихРеквизитов" для просмотра истории 
// значений периодических реквизитов.
//
Процедура ПоКнопкеИстория()
	
	глЗначенияПериодическихРеквизитов(Контекст);
	
КонецПроцедуры // ПоКнопкеИстория()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	Если Форма.МодальныйРежим() = 1 Тогда
		Форма.кнОстатокНаСчетах.Видимость(0);
		Форма.кнОстатокВКассе.Видимость(0);
	КонецЕсли;
	
	// Системно не надо контролировать изменение периодических реквизитов.
	СохранениеПериодическихРеквизитов(0);
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриРедактированииНовойСтроки()

	Кратность = 1;
	Курс      = 1;
	
КонецПроцедуры // ПриРедактированииНовойСтроки()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗаписи()
	
	Если ПустоеЗначение(Наименование) = 1 Тогда
		Предупреждение("Не заполнено поле ""Обозначение""!", 60);
		Активизировать("Наименование");
		СтатусВозврата(0);
		Возврат;
	
	КонецЕсли;

	// Записью периодических реквизитов невозможно управлять при вводе 
	// новой строки в форме списка справочника. Система автоматически 
	// запишет значения периодических реквизитов на текущую дату.
	Если Выбран() = 1 Тогда
		// Обработке ЗаписьПериодическихРеквизитов необходимо передать
		// значения периодических реквизитов, введенные в форме диалога.
		Список = СоздатьОбъект("СписокЗначений");
		Список.Установить("Курс", Курс);
		Список.Установить("Кратность", Кратность);
		
		// При записи вызывается обработка ЗаписьПериодическихРеквизитов 
		// для управления записью значений периодических реквизитов.
		Если глЗаписьПериодическихРеквизитов(Контекст, Список) = 0 Тогда
			СтатусВозврата(0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

//******************************************************************************

