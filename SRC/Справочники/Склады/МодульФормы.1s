Перем СтарыйМОЛ, СтарыйРознСклад;
Перем НачМОЛ, НачРознСклад;

Перем ТекстВопроса;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//******************************************************************************

//++Ерошенко 07.05.2007
Процедура ОтобразитьEMAIL()
	
	Если СокрЛП(EMAIL) = "" Тогда
		ТM = "e-mail не указан";
	Иначе
		ТM = СокрЛП(EMAIL);
	КонецЕсли;
	Форма.ТекMail.Заголовок(ТM);
	
КонецПроцедуры
//--Ерошенко

// УправлениеДиалогом()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  
//
// Описание:
//  Управляет доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Если РозничныйСклад = 1 Тогда
		Форма.ОблагаетсяЕНВД.Доступность(1);
		
		Форма.ТекстНомерСекции.Видимость(1);
		Форма.НомерСекции.Видимость(1);
	Иначе     
		Форма.ОблагаетсяЕНВД.Доступность(0);	
		
		Форма.ТекстНомерСекции.Видимость(0);
		Форма.НомерСекции.Видимость(0);
	КонецЕсли;   
	
	Если Форма.МодальныйРежим() = 1 Тогда
	    Форма.кнОстатки.Видимость(0);
	КонецЕсли;
	
КонецПроцедуры // УправлениеДиалогом()
                              
//******************************************************************************
// НайтиПервыйДокумент(Док()
//
// Параметры:
//  Док					- через это параметр возвращается документ
//  РегистрУчета		- имя регистра учета, в котором ищем движения
//  Реквизит			- реквизит регитсра учета
//  ЗначениеРеквизита	- значение реквизита регистра учета
//  
//
// Возвращаемое значение:
//  0 - документ не найден
//  1 - документ найден
//
//	Описание:
//
Функция НайтиПервыйДокумент(Док, РегистрУчета, Реквизит, ЗначениеРеквизита)
	Перем Рез;
	
	РегДвижения = СоздатьОбъект("Регистр." + РегистрУчета);
	РегДвижения.УстановитьЗначениеФильтра(Реквизит, ЗначениеРеквизита, 1);
	РегДвижения.ВыбратьДвижения();
	
	Рез = 0;
	Если РегДвижения.ПолучитьДвижение() <> 0 Тогда
		Док = РегДвижения.ТекущийДокумент();
		Рез = 1;
	Иначе
		Рез = 0;
	КонецЕсли;
	
	Возврат Рез;	
	
КонецФункции // НайтиПервыйДокумент(Док()   

//******************************************************************************
// ПриИзмененииФлагаОблагаетсяЕНВД()
//
// Параметры:
//   Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Флаг "Облагается ЕНВД".
//
// Описание:
//  Выдаем предупреждение о флаге.
//
Процедура ПриИзмененииФлагаОблагаетсяЕНВД()
	
	Если Выбран() <> 0 Тогда
		Предупреждение("Этот реквизит устанавливается в качестве значения по умолчанию.
		|После его изменения существующие документы по складу не будут автоматически переоформлены.", 60);
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииФлагаОблагаетсяЕНВД()
    
//******************************************************************************
// ПриИзмененииФлагаРозничныйСклад()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Флаг "Розничный склад".
//
// Описание:
//  При изменении флага розничный надо установить доступность элементов формы.
//
Процедура ПриИзмененииФлагаРозничныйСклад()
	                    
	Если (Выбран() = 1) И (РозничныйСклад <> СтарыйРознСклад) Тогда
		Если  Вопрос(ТекстВопроса,"Да+Нет", 60) <> "Да" Тогда
	 	// нажали кнопку нет
	 		РозничныйСклад = СтарыйРознСклад;
		КонецЕсли;
	КонецЕсли;
	 
	СтарыйРознСклад = РозничныйСклад;
	УправлениеДиалогом();
	
КонецПроцедуры // ПриИзмененииФлагаРозничныйСклад()

//******************************************************************************
// ПриИзмененииМОЛ()
//
// Параметры:
//  нет.
//
// Возвращаемое значение:
//  нет.
//
// Описание:
//  прецедура вызывается при изменении значения в реквизите МОЛ сохраненного
//  элемента. Процедура требует от пользователя подтверждения изменения.
//
Процедура ПриИзмененииМОЛ()
	
    Если (Выбран() = 1) И (МОЛ <> СтарыйМОЛ) Тогда
		Если  Вопрос(ТекстВопроса,"Да+Нет", 60) <> "Да" Тогда
	 		// нажали кнопку нет
	 		МОЛ=СтарыйМОЛ;
	 	КонецЕсли;
	 КонецЕсли;
	 
	 СтарыйМОЛ = МОЛ;
	 
КонецПроцедуры	// ПриИзмененииМОЛ()

//******************************************************************************
// ПоКнопкеОстатки()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ПоКнопкеОстатки()
	
	Если Выбран() = 0 Тогда
		Предупреждение("Отчет можно посмотреть только для существующего склада", 60);
	Иначе
		// создаем стандартную расшифровку отчета               
		Расшифровка = СоздатьОбъект("СписокЗначений");
		Расшифровка.Установить("Отчет", "ОстаткиТМЦ");
		
		// все настройки помещаем в список
		Расшифровка.Установить("ДатаКонца", 	Мин(РабочаяДата(),ПолучитьДатуТА()));
		Расшифровка.Установить("ВыбРазделитель1","");
		Расшифровка.Установить("ВыбРазделитель2","");
		Расшифровка.Установить("ВыбРазделитель3","");
		Расшифровка.Установить("ВидРазделителя",  1);
		
		Расшифровка.Установить("ВыбТМЦ",	     "");
		Расшифровка.Установить("ВыбСклад",		ТекущийЭлемент());
		
		Расшифровка.Установить("ВидЕдиницы",	  1);
		Расшифровка.Установить("ПоказатьЦену",	РозничныйСклад);
		Расшифровка.Установить("ПоказатьСумму",	РозничныйСклад);
		Расшифровка.Установить("ПоказатьОстатки", 1);
		Расшифровка.Установить("Показатель",      1);
		
		глОбработкаРасшифровки(Расшифровка,"","");
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеОстатки()
 

// Добавлено: Валерий МЭТР
Процедура ЗагрузитьКоэффициенты()
	
	СпрКоэффициентыСезонности = СоздатьОбъект("Справочник.КоэффициентыСезонностиСкладов");
	СпрКоэффициентыСезонности.ИспользоватьВладельца(ТекущийЭлемент());
	
	СпрКоэффициентыСезонности.ВыбратьЭлементы();
	Пока СпрКоэффициентыСезонности.ПолучитьЭлемент() = 1 Цикл
		Если СпрКоэффициентыСезонности.ТекущийЭлемент().ПометкаУдаления() = 0 Тогда
			Январь = СпрКоэффициентыСезонности.ТекущийЭлемент().Январь;
			Февраль = СпрКоэффициентыСезонности.ТекущийЭлемент().Февраль;
			Март = СпрКоэффициентыСезонности.ТекущийЭлемент().Март;
			Апрель = СпрКоэффициентыСезонности.ТекущийЭлемент().Апрель;
			Май = СпрКоэффициентыСезонности.ТекущийЭлемент().Май;
			Июнь = СпрКоэффициентыСезонности.ТекущийЭлемент().Июнь;
			Июль = СпрКоэффициентыСезонности.ТекущийЭлемент().Июль;
			Август = СпрКоэффициентыСезонности.ТекущийЭлемент().Август;
			Сентябрь = СпрКоэффициентыСезонности.ТекущийЭлемент().Сентябрь;
			Октябрь = СпрКоэффициентыСезонности.ТекущийЭлемент().Октябрь;
			Ноябрь = СпрКоэффициентыСезонности.ТекущийЭлемент().Ноябрь;
			Декабрь = СпрКоэффициентыСезонности.ТекущийЭлемент().Декабрь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Добавлено: Валерий МЭТР
Процедура СохранитьКоэффициенты()
	
	СпрКоэффициентыСезонности = СоздатьОбъект("Справочник.КоэффициентыСезонностиСкладов");
	СпрКоэффициентыСезонности.ИспользоватьВладельца(ТекущийЭлемент());
	
	ЭтотЭлемент = СоздатьОбъект("Справочник.КоэффициентыСезонностиСкладов");
	ЭтотЭлемент.ИспользоватьВладельца(ТекущийЭлемент());
	
	СпрКоэффициентыСезонности.ВыбратьЭлементы();
	ЭлементНайден = 0;
	Пока СпрКоэффициентыСезонности.ПолучитьЭлемент() = 1 Цикл
		Если СпрКоэффициентыСезонности.ТекущийЭлемент().ПометкаУдаления() = 0 Тогда
			ЭтотЭлемент.НайтиЭлемент(СпрКоэффициентыСезонности.ТекущийЭлемент());
			ЭтотЭлемент.Январь = Январь;
			ЭтотЭлемент.Февраль = Февраль;
			ЭтотЭлемент.Март = Март;
			ЭтотЭлемент.Апрель = Апрель;
			ЭтотЭлемент.Май = Май;
			ЭтотЭлемент.Июнь = Июнь;
			ЭтотЭлемент.Июль = Июль;
			ЭтотЭлемент.Август = Август;
			ЭтотЭлемент.Сентябрь = Сентябрь;
			ЭтотЭлемент.Октябрь = Октябрь;
			ЭтотЭлемент.Ноябрь = Ноябрь;
			ЭтотЭлемент.Декабрь = Декабрь;
			ЭтотЭлемент.Записать();
			ЭлементНайден = 1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если (ЭлементНайден = 0) И (ТекущийЭлемент().Выбран() = 1) Тогда
		ЭтотЭлемент.Новый();
		ЭтотЭлемент.Наименование = ТекущийЭлемент().Наименование;
		ЭтотЭлемент.Январь = Январь;
		ЭтотЭлемент.Февраль = Февраль;
		ЭтотЭлемент.Март = Март;
		ЭтотЭлемент.Апрель = Апрель;
		ЭтотЭлемент.Май = Май;
		ЭтотЭлемент.Июнь = Июнь;
		ЭтотЭлемент.Июль = Июль;
		ЭтотЭлемент.Август = Август;
		ЭтотЭлемент.Сентябрь = Сентябрь;
		ЭтотЭлемент.Октябрь = Октябрь;
		ЭтотЭлемент.Ноябрь = Ноябрь;
		ЭтотЭлемент.Декабрь = Декабрь;		
		ЭтотЭлемент.Записать();
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнЗаписать.Доступность(0);
		Форма.кнОК.Доступность(0);
		
		Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
	    Форма.КнопкаПоУмолчанию("кнОк");
	КонецЕсли;
	
	НачМОЛ			= МОЛ;
	СтарыйМОЛ		= МОЛ;
	НачРознСклад	= РозничныйСклад;
	СтарыйРознСклад	= РозничныйСклад;
	УправлениеДиалогом(); 
	
	//++Ерошенко 07.05.2007
	ОтобразитьEMAIL();
	//--Ерошенко
	
	// Валерий МЭТР
	ЗагрузитьКоэффициенты();
	
КонецПроцедуры	// ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	Перем Док;
	
	Если ПустоеЗначение(Наименование) = 1 Тогда
		СтатусВозврата(0);
		Предупреждение("Не заполнено поле ""Наименование""", 60);
		Активизировать("Наименование");
		Возврат;
	КонецЕсли;
	
	Если Выбран() = 1 Тогда
		
		МинПоз	= Последовательность.ОсновнаяПоследовательность.ПолучитьПозицию();
		
		// проверка изменения МОЛ
		Если НачМОЛ <> МОЛ Тогда
			Если НайтиПервыйДокумент(Док, "ПартииНаличие", "МОЛ", НачМОЛ) = 1 Тогда
				МинПоз	= Мин(Док.ПолучитьПозицию(), МинПоз);
				НачМОЛ = МОЛ;
			КонецЕсли;
		КонецЕсли;
		
		Если НачРознСклад <> РозничныйСклад Тогда
			Если НайтиПервыйДокумент(Док, "ОстаткиТМЦ", "Склад", ТекущийЭлемент()) = 1 Тогда
				МинПоз		= Мин(Док.ПолучитьПозицию(), МинПоз);
				НачРозСклад	= РозничныйСклад;
			КонецЕсли;
		КонецЕсли;

		Если МинПоз < ПолучитьПозициюТА() Тогда
			Последовательность.ОсновнаяПоследовательность.Установить(МинПоз);
		КонецЕсли;
		
	КонецЕсли;
	
	// Валерий МЭТР
	СохранитьКоэффициенты();
	
КонецПроцедуры	// ПриЗаписи()

Процедура ПоКнопкеВводаАдреса(Адрес1)
	глВводАдреса(Адрес1);
КонецПроцедуры // ПоКнопкеВводаАдреса()

//++Ерошенко 07.05.2007  
Процедура ВыборEMAIL()
	
	Стр = "@san-sanych.ru";
	Если ВвестиСтроку(Стр,"Укажите электронный адрес склада",50,0,0) = 1 Тогда  
		EMAIL = Стр;
	КонецЕсли;
	ОтобразитьEMAIL();
	
КонецПроцедуры
//--Ерошенко

ТекстВопроса = 
	"Этот реквизит устанавливается только для нового склада.
	|После его изменения потребуется перепровести все документы по складу!
	|Вы уверены, что хотите изменить этот реквизит?";
