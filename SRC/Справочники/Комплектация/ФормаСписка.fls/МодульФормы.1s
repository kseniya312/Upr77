////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// ПоКнопкеУстановитьЦеновойКоэффициент()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Устанавливает ценой коэффициент у выбранного комплектующего
//
Процедура ПоКнопкеУстановитьЦеновойКоэффициент()
	
	ТекВладелец	= ИспользоватьВладельца();
	
	Если ПустоеЗначение(ТекВладелец) = 1 Тогда
	    Предупреждение("Не выбран набор, владелец комплектующих", 60);
		Возврат;
	КонецЕсли;
	
	СпрКомплектующие = СоздатьОбъект("Справочник.Комплектация");
	СпрКомплектующие.ИспользоватьВладельца(Владелец);
	Если СпрКомплектующие.ВыбратьЭлементы() = 0 Тогда
	    Предупреждение("У номенклатуры """ + ТекВладелец + """ отсутствуют комплектующие", 60);
	Иначе
		СпрТипыЦен	= СоздатьОбъект("Справочник.ТипыЦен");
			
		Если СпрТипыЦен.Выбрать("Выберите тип цен", ) = 1 Тогда
			
			НачатьТранзакцию();
			
			Пока СпрКомплектующие.ПолучитьЭлемент() = 1 Цикл
				СпрКомплектующие.ЦеновойКоэфф = глПолучитьЦену(СпрКомплектующие.Номенклатура, СпрТипыЦен.ТекущийЭлемент(), 
				                                 РабочаяДата(), СпрКомплектующие.Номенклатура.БазоваяЕдиница);
				Попытка
					СпрКомплектующие.Записать();
				Исключение
					ОтменитьТранзакцию();
					Предупреждение(ОписаниеОшибки(), 60);
					Возврат;
				КонецПопытки;
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеУстановитьЦеновойКоэффициент()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	ПараметрВызова = Форма.Параметр;
	
	Если ТипЗначенияСтр(ПараметрВызова) = "Справочник" Тогда
		Если (ПараметрВызова.Вид() = "Номенклатура") И (ПараметрВызова.ЭтоГруппа() = 0) Тогда
			ИспользоватьВладельца(ПараметрВызова);
			ИерархическийСписок(1, 1);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	// ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	// проверка заполнения обязательных полей
	Если Номенклатура.Выбран()=0 Тогда
	    Предупреждение("Не заполнено поле ""Номенклатурная позиция""!", 60);
		Активизировать("Номенклатура");
	    СтатусВозврата(0);
	    Возврат;
		
	ИначеЕсли ПустоеЗначение(Количество)=1 Тогда
	    Предупреждение("Не заполнено поле ""Количество""!", 60);
		Активизировать("Количество");
	    СтатусВозврата(0);
	    Возврат;
	КонецЕсли;
	
	// проверим, а есть ли такое комплектующее в наборе
	// если есть то отменим запись
	Комплектующие = СоздатьОбъект("Справочник.Комплектация");
	Комплектующие.ИспользоватьВладельца(Владелец);
	Комплектующие.ВыбратьЭлементы();
	
	Пока Комплектующие.ПолучитьЭлемент()=1 Цикл
		Если Комплектующие.Номенклатура = Номенклатура Тогда
			Если Комплектующие.ТекущийЭлемент() <> ТекущийЭлемент() Тогда
			    Предупреждение("Номенклатурная позиция """+Номенклатура+""" уже указана составе комплекта" +
					              ?(Комплектующие.ПометкаУдаления() = 1,", помечена на удаление.","."), 60);
				Активизировать("Номенклатура");
				СтатусВозврата(0);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
