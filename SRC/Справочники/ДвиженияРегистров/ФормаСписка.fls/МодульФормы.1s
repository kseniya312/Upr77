////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем КонтекстФормы;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
//	ВверхВниз(Сдвиг)
//
//	Параметры:
//	 Сдвиг - -1 или +1 - направление.
//
//	Вызывается из формул элементов диалога:
//	 Кнопка "Вверх"
//	 Кнопка "Вниз"
//
//	Описание:
//	 Устанавливает у элементов значение реквизита НомерСтроки, 
//	 по которому выполняется сортировка элементов в списке.

Процедура ВверхВниз(Сдвиг)
	
	Если ТекущийЭлемент().Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Объект = СоздатьОбъект("Справочник."+Вид());
	
	Объект.ИспользоватьРодителя(ТекущийЭлемент().Родитель);
	Объект.ИспользоватьВладельца(ТекущийЭлемент().Владелец);
	
	Объект.ПорядокКодов();
	Объект.ОбратныйПорядок(?(Сдвиг < 0, 0, 1));
	
	ПредыдущийЭлемент = Объект.ТекущийЭлемент();
		
	Объект.ВыбратьЭлементы(1);
	Пока Объект.ПолучитьЭлемент(0) = 1 Цикл
		Если Объект.ТекущийЭлемент() = ТекущийЭлемент() Тогда
			Прервать;
		КонецЕсли;
		
		ПредыдущийЭлемент = Объект.ТекущийЭлемент();
	КонецЦикла;
	
	Если ПредыдущийЭлемент.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийКод = ТекущийЭлемент().Код;
	ПредыдущийКод = ПредыдущийЭлемент.Код;
	
	Попытка
		НачатьТранзакцию();
		
		Объект.НайтиЭлемент(ТекущийЭлемент());
		Объект.УстановитьНовыйКод();
	    Объект.Записать();
		
		Объект.НайтиЭлемент(ПредыдущийЭлемент);
		Объект.Код = ТекущийКод;
	    Объект.Записать();
		
		Объект.НайтиЭлемент(ТекущийЭлемент());
		Объект.Код = ПредыдущийКод;
	    Объект.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		Сообщить(ОписаниеОшибки(), "!");
	КонецПопытки;

КонецПроцедуры // ВверхВниз()

//******************************************************************************
// УстановитьОтбор()
//
// Вызывается из формул элементов диалога:
//  Текст <<УстановитьОтбор()>>.
//
// Описание:
//  Вызывается при переходе по строкам справочника. Переназначает отбор в 
// справочнике проводок, открытом по кнопке "Бухгалтерские проводки".
//
Функция УстановитьОтбор()
	
	Если ТекущийЭлемент().Выбран() = 1 Тогда
		Если ТипЗначенияСтр(КонтекстФормы) = "ГрупповойКонтекст" Тогда
			КонтекстФормы.УстановитьОтбор("Движение", ТекущийЭлемент());
			КонтекстФормы.ИспользоватьВладельца(ТекущийЭлемент().Владелец);
		КонецЕсли;
	КонецЕсли;
		
КонецФункции // УстановитьОтбор()

//******************************************************************************
// Проводки()
//
// Вызывается из формул элементов диалога:
//  Кнопка "Бухгалтерские проводки".
//
// Описание:
//  Открывает справочник проводок с отбором по текущему движению регистра.
//
Процедура Проводки()
	
	ОткрытьФорму("Справочник.ПроводкиХозОпераций.ФормаСписка#ПроводкиПоДвижению", КонтекстФормы);
	УстановитьОтбор();
	
КонецПроцедуры // Проводки()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, ФлагПродолжения)
	
	Список = СоздатьОбъект("СписокЗначений");
	
	Если ЭлементДиалога = "ВидРегистра" Тогда
		Для Номер = 1 По Метаданные.Регистр() Цикл
			Если Метаданные.Регистр(Номер).Реквизит("КодОперации").Выбран() = 1 Тогда
				Список.ДобавитьЗначение(Метаданные.Регистр(Номер).Идентификатор);
			КонецЕсли;
		КонецЦикла;
		
		Вид = СокрП(ВидРегистра);
		Если Список.ВыбратьЗначение(Вид, , , 60, 2) = 1 Тогда
			ВидРегистра  = Вид;
			
			Наименование = Метаданные.Регистр(Вид).Представление();
			
			Если Метаданные.Регистр(Вид).Измерение("ВидДолга").Выбран() = 1 Тогда
				мдИзмерение  = Метаданные.Регистр(Вид).Измерение("ВидДолга");
				ТипВид = мдИзмерение.Тип+"."+мдИзмерение.Вид;
			
			ИначеЕсли Метаданные.Регистр(Вид).Измерение("СтатусПартии").Выбран() = 1 Тогда
				мдИзмерение  = Метаданные.Регистр(Вид).Измерение("СтатусПартии");
				ТипВид = мдИзмерение.Тип+"."+мдИзмерение.Вид;
			
			Иначе
				ТипВид = "";
			КонецЕсли;
			
			НазначитьТип("ВидСтатус", ТипВид);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
// Если нет права корректировки справочника, то прячем служебные кнопки
Если ПравоДоступа("Корректировка","Справочник.ДвиженияРегистров")=0 Тогда
	Сортировка("Код", 1);
	//Форма.ТолькоПросмотр(1);
	Форма.ВидРегистра.Видимость(0);
	Форма.кнВверх.Видимость(0);
	Форма.кнВниз.Видимость(0);
КонецЕсли;
