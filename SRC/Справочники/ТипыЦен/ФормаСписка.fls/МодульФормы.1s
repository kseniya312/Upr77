Перем ТаблицаПечФорм;
Перем НомерТекущейФормы;

//******************************************************************************
// ПоКнопкеЦены()
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//  нет.
//
// Вызывается из формул элементов диалога:
//  по ккнопке "Цены"
//
// Описание:
//  для текущего в списке типа цены открывает форму списка справочника цены
//
Процедура ПоКнопкеЦены()
	
	Если ПустоеЗначение(ТекущийЭлемент())=0 Тогда 
		ОткрытьФорму("Справочник.Цены#"+Наименование,ТекущийЭлемент()); 
	Иначе
		Предупреждение("Не выбран тип цен.", 60); 
	КонецЕсли;

КонецПроцедуры // ПоКнопкеЦены()

//******************************************************************************
// ПоКнопкеПечать()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ПоКнопкеПечать()
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
	КонецЕсли;
		
	Если НомерТекущейФормы = 1  Тогда
		Параметр = ТекущийЭлемент();
		ОткрытьФорму("Обработка.ПечатьПрайса", Параметр);
	Иначе
		// внешняя обработка
		Параметры = глВзятьКонтекст(Контекст);
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм + ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Параметры:
//  нет.    
//
// Возвращаемое значение:
//  нет.
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Справочник." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормыСпис()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	// Заполним таблицу для выбора печатной формы
	Если Форма.РежимВыбора() <> 0 Тогда
	    Форма.Заголовок("Выберите тип цены", 0);
		Форма.кнВыбПечать.Видимость(0);
		Форма.кнПечать.Видимость(0);
	Иначе
		НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Справочник." + Вид(), ТаблицаПечФорм);
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// ТЕЛО МОДУЛЯ

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");

ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название	= "Прайс-лист";
ТаблицаПечФорм.Кнопка	= "Прайс-лист";

