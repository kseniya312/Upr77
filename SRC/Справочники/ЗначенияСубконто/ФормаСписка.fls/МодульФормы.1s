//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриВыбореВладельца(ЭлементВладелец)
                                                               
	ЗаголовокКолонкиЗначение	=	СокрЛП(ЭлементВладелец.ИдРеквизита);
	КолонкаЗначений				=	Форма.ПолучитьАтрибут("Значение");
	КолонкаЗначений.Видимость(1);
	
	Если  		ЗаголовокКолонкиЗначение = "Наименование"					Тогда
		КолонкаЗначений.Видимость(0);
		
	ИначеЕсли	Найти(ЭлементВладелец.ТипЗначенияБух, "Перечисление.") > 0	Тогда
		КолонкаЗначений.Видимость(0);
		
	ИначеЕсли	ПустоеЗначение(ЗаголовокКолонкиЗначение) = 0 				Тогда
		КолонкаЗначений.Заголовок(ЗаголовокКолонкиЗначение);
		
	Иначе
		КолонкаЗначений.Заголовок("Значение");
		
	КонецЕсли;
	
КонецПроцедуры

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриСменеИерархии(Способ)
	                      
	Если Способ = 0 Тогда	//	Все
		КолонкаЗначений	=	Форма.ПолучитьАтрибут("Значение");
		КолонкаЗначений.Заголовок("Значение");
		КолонкаЗначений.Видимость(1);
	Иначе
		ПриВыбореВладельца(	ИспользоватьВладельца()	);
	КонецЕсли;
	
КонецПроцедуры

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриВводеСтроки()
    
	Если Найти(ИспользоватьВладельца().ТипЗначенияБух, "Перечисление.") > 0 Тогда
		Предупреждение("Во избежание ошибок, значения субконто, имеющие в
						|бухгалтерской конфигурации тип 'перечисление',
						|заполняются исключительно обработками экспорта / импорта!", 60);
		СтатусВозврата(0);
	ИначеЕсли	ПустоеЗначение(ИспользоватьВладельца().ТипЗначенияТорг) = 0 Тогда
		Предупреждение("Не имеет смысла вводить значения субконто, для видов субконто,
						|у которых указан соответствующий объект торгового учета!", 60);
		СтатусВозврата(0);
	КонецЕсли;
	
КонецПроцедуры


//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	// проверка заполнения обязательных полей
	Если ПустоеЗначение(Наименование) = 1 Тогда
	    Предупреждение("Не заполнено поле ""Наименование""!", 60);
		Активизировать("Наименование");
	    СтатусВозврата(0);
	    Возврат;
	КонецЕсли;
	
	ИдРеквизита	=	СокрЛП(Владелец.ИдРеквизита);
	
	Если ИдРеквизита = "Наименование" Тогда
		Значение	=	Наименование;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	Если ТипЗначенияСтр(Форма.Параметр) = "Справочник" Тогда
		Если Форма.Параметр.Вид() = "ВидыСубконто" Тогда
			ИспользоватьВладельца(Форма.Параметр);
		КонецЕсли;
	КонецЕсли;
	
	Если 0 < Форма.РежимВыбора() Тогда
		ИерархическийСписок(1, 0);
	КонецЕсли;
	
	ПриВыбореВладельца(	ИспользоватьВладельца()	);
	
КонецПроцедуры // ПриОткрытии()

