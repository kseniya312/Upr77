Перем Номенклатура;
Перем внКонтекст;
Перем ОбщРег, РегПартии;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//******************************************************************************
// ПолучитьСвойство()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - название свойства и вида свойства.
//
// Вызывается из формул элементов диалога:
//  Колонка "свойство".
//
// Описание:
//  Получение свойства текущего элемента с названием вида свойства.
//
Функция ПолучитьСвойство(Партия)
	
	Если ПустоеЗначение(Партия)=1 Тогда
		Возврат "";
	КонецЕсли;     
	
	Если ПустоеЗначение(Партия.Свойство)=0 Тогда   
		Возврат ""+Партия.Свойство.Владелец+": "+СокрЛП(Партия.Свойство);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции // ПолучитьСвойство()

//******************************************************************************
// РасшифровкаРеквизита(Рекв)
//
// Параметры:
//  Рекв - реквизит, форму которого необходимо открыть
// Возвращаемое значение:
//  нет.
// Описание:
//  открывает форму дополнительного реквизита справочника, переданного через
//  параметр.

Процедура РасшифровкаРеквизита(Рекв)
	
	Тек = ТекущийЭлемент();
	
	Если ПустоеЗначение(Тек) = 0 Тогда
		ОткрытьФорму(Рекв,,1);
	Иначе
		Предупреждение("Не выбрана партия!", 60);
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаРеквизита()        

          
//******************************************************************************
// ПроизвестиВыбор(Партия)
//
// Параметры:
//  Партия - элемент справочника "Партии"
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "<Автоподбор партий>".
//
// Описание:
//  Производит запись в список возврата выбранную партию
//
Процедура ПроизвестиВыбор(Партия)
	
	Форма.Параметр.Установить("Партия",Партия);
	Форма.Параметр.Установить("СтатусВозврата", 1);
	
КонецПроцедуры // ПроизвестиВыбор()

//******************************************************************************
// ПолучитьОстатокПартии(Партия)
//
// Параметры:
//  Партия - элемент справочника "Партии"
//
// Возвращаемое значение:
//  Остаток количественный (число).
//
// Вызывается из формул элементов диалога:
//  
//
// Описание:
//  Вычисляет остаток партии в соответствии с текущими установками фильтра.
//
Функция ПолучитьОстатокПартии(Партия)
	
	Если ПустоеЗначение(Партия) = 1 Тогда
		Возврат "";
	КонецЕсли;
	
	НайденныйОстаток = РегПартии.СводныйОстаток(Фирма,МОЛ, Партия.Владелец, ,Партия, , , "Количество");
	                                             
	Если НайденныйОстаток = 0 Тогда
		Возврат "";
	Иначе
		Возврат ""+глФРМКоличество(НайденныйОстаток)+" "+Партия.Владелец.БазоваяЕдиница;
	КонецЕсли;
	
КонецФункции // ПолучитьОстатокПартии()
                                                   
//******************************************************************************
// ПересчетРегистров()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
// 	Производит пересчет регистров в соответствии с выбранными значениями
//	фильтра.
//
Процедура ПересчетРегистров()
	
	ОбщРег	= СоздатьОбъект("Регистры");
	РегПартии	= ОбщРег.ПартииНаличие;
	
	Если ( внКонтекст.СравнитьТА()=-1) Тогда
		                                          
		РегПартии.УстановитьЗначениеФильтра("МОЛ",МОЛ,1);

		Если ПустоеЗначение(Владелец) = 0 Тогда
			РегПартии.УстановитьЗначениеФильтра("Номенклатура",Номенклатура,1);
		Иначе     
			РегПартии.УстановитьЗначениеФильтра("Номенклатура",,0);
		КонецЕсли;
		Если ПустоеЗначение(Фирма) = 0 Тогда
			РегПартии.УстановитьЗначениеФильтра("Фирма",Фирма,1);
		Иначе     
			РегПартии.УстановитьЗначениеФильтра("Фирма",,0);
		КонецЕсли;
		РегПартии.ВременныйРасчет();
		ОбщРег.РассчитатьРегистрыНа(внКонтекст.ТекущийДокумент());
	Иначе
	    // сброс временного расчета
		ОбщРег		= СоздатьОбъект("Регистры");
		РегПартии 	= ОбщРег.ПартииНаличие;
	КонецЕсли;

КонецПроцедуры // ПересчетРегистров()

//******************************************************************************
// ПолучитьСебестоимостьПартии()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка с информацией об остатке и стоимости партии
//
// Вызывается из формул элементов диалога:
//  Информационная строка.
//
// Описание:
//  Возвращает информацию о текущем остатке партии.
//
Функция ПолучитьСебестоимостьПартии()
	
	Партия = ТекущийЭлемент();
	Если ПустоеЗначение(Партия)=1 Тогда
		Возврат "";
	КонецЕсли;     
	
	Количество= РегПартии.СводныйОстаток(Фирма,МОЛ, Партия.Владелец, ,Партия,, , "Количество"); 
	Стоимость = РегПартии.СводныйОстаток(Фирма,МОЛ, Партия.Владелец, ,Партия,, , "СуммаУпр");   
	                                             
	Если (Количество = 0) Тогда
		Возврат "< остаток отсутствует > ";
	Иначе
		Возврат ""+глФРМ(Стоимость/Количество)+" "+глДоллары.Наименование+" / "
				+Партия.Владелец.БазоваяЕдиница;
	КонецЕсли;
	    
КонецФункции // ПолучитьСебестоимостьПартии()
                
////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если ТипЗначенияСтр(Форма.Параметр) <> "СписокЗначений" Тогда
		Предупреждение("Форма вызывается только для целей подбора партий в документе!", 60);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	 
	внКонтекст	= Форма.Параметр.Получить("Контекст");
	Фирма		= Форма.Параметр.Получить("Фирма");
	Номенклатура= Форма.Параметр.Получить("Номенклатура");
	Склад		= Форма.Параметр.Получить("Склад");
	МОЛ 		= ?(ПустоеЗначение(Склад)=1,ПолучитьПустоеЗначение("Справочник.Склады"),Склад.МОЛ);
	Партия		= Форма.Параметр.Получить("Партия");
	
	Форма.Параметр.Установить("СтатусВозврата"	, 0);
	
	ИерархическийСписок(1,0); // не даем отключить иерархию, чтобы чужую партию не выбрал!
	ИспользоватьВладельца(Номенклатура);
	
	Если ПустоеЗначение(Партия) = 0 Тогда
		АктивизироватьОбъект(Партия);
	КонецЕсли;
	
	ПересчетРегистров();
	
	Форма.ОбработкаВыбораСтроки(1);
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореСтроки()
	
	Если ПустоеЗначение(ТекущийЭлемент()) = 0 Тогда
		Если ПриходныйДокумент.Выбран() = 1 Тогда
			Если Фирма.ЮрЛицо = ПриходныйДокумент.Фирма.ЮрЛицо Тогда
				ПроизвестиВыбор(ТекущийЭлемент());
				Форма.Закрыть();
				Возврат;
			КонецЕсли;
		КонецЕсли;
	    Предупреждение("Можно выбирать партии только юридического лица (" +СокрЛП(Фирма.ЮрЛицо)+"), от которого оформляется документ.",60); 
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры // ПриВыбореСтроки()
