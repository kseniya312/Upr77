////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
//	ВверхВниз(Сдвиг)
//
//	Параметры:
//	 Сдвиг - -1 или +1 - направление.
//
//	Вызывается из формул элементов диалога:
//	 Кнопка "Вверх"
//	 Кнопка "Вниз"
//
//	Описание:
//	 Устанавливает у элементов значение реквизита НомерСтроки, 
//	 по которому выполняется сортировка элементов в списке.

Процедура ВверхВниз(Сдвиг)
	
	Если ТекущийЭлемент().Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Объект = СоздатьОбъект("Справочник."+Вид());
	
	Объект.ИспользоватьРодителя(ТекущийЭлемент().Родитель);
	Объект.ИспользоватьВладельца(ТекущийЭлемент().Владелец);
	
	Объект.ПорядокКодов();
	Объект.ОбратныйПорядок(?(Сдвиг < 0, 0, 1));
	
	ПредыдущийЭлемент = Объект.ТекущийЭлемент();
		
	Объект.ВыбратьЭлементы(1);
	Пока Объект.ПолучитьЭлемент(0) = 1 Цикл
		Если Объект.ТекущийЭлемент() = ТекущийЭлемент() Тогда
			Прервать;
		КонецЕсли;
		
		ПредыдущийЭлемент = Объект.ТекущийЭлемент();
	КонецЦикла;
	
	Если ПредыдущийЭлемент.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийКод = ТекущийЭлемент().Код;
	ПредыдущийКод = ПредыдущийЭлемент.Код;
	
	Попытка
		НачатьТранзакцию();
		
		Объект.НайтиЭлемент(ТекущийЭлемент());
		Объект.УстановитьНовыйКод();
	    Объект.Записать();
		
		Объект.НайтиЭлемент(ПредыдущийЭлемент);
		Объект.Код = ТекущийКод;
	    Объект.Записать();
		
		Объект.НайтиЭлемент(ТекущийЭлемент());
		Объект.Код = ПредыдущийКод;
	    Объект.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		Сообщить(ОписаниеОшибки(), "!");
	КонецПопытки;

КонецПроцедуры // ВверхВниз()

//******************************************************************************
// ЗаполнитьОписаниеСубконто(ДК)
//
// Параметры:
//  ДК          - "Д", если счет дебета, "К" если счет кредита.
//  ДокНазначен - число, 1 - в случае если счет и аналитика определяются документом.
//                       0 - определяются данным элементом справочника.
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Формирует текстовые поля - описание аналитики счетов проводки.
//
Процедура ЗаполнитьОписаниеСубконто(ДК, ДокНазначен)
	
	Счет = ПолучитьАтрибут("Счет"+ДК);
	
	Для Номер = 1 По 3 Цикл
		
		Форма.ПолучитьАтрибут("ТекстНазвание"+ДК+Номер).Видимость(1 - ДокНазначен);
		Форма.ПолучитьАтрибут("ТекстЗначение"+ДК+Номер).Видимость(1 - ДокНазначен);
		
		Форма.ПолучитьАтрибут("ТекстНазвание"+ДК+Номер).Заголовок(глПредставлениеВидаСубконто(Счет, Номер));
		Форма.ПолучитьАтрибут("ТекстЗначение"+ДК+Номер).Заголовок(Строка(ПолучитьАтрибут("Субконто"+ДК+Номер)));
		
	КонецЦикла;
	
	Форма.ПолучитьАтрибут("ТекстДокНазначен"+ДК).Видимость(ДокНазначен);
	
КонецПроцедуры // ЗаполнитьОписаниеСубконто()

//******************************************************************************
// ДвижениеРегистра()
//
// Параметры:
//
// Возвращаемое значение:
//  Строка - описание движения регистра, по которму формируется проводка.
//
// Вызывается из формул элементов диалога:
//  Текст "Движение регистра".
//
// Описание:
//  Функция формирует информационную строку о движении регистра.
//  Формирует текстовые поля - описание аналитики счетов проводки.
//  Управляет видимостью полей, описывающих атрибуты проводки.
//
Функция ДвижениеРегистра()
	
	Стр = "";
//	Количественный = 0; 
//	Валютный = 0;
	Если ПустоеЗначение(Движение) = 0 Тогда
		Стр = Движение.Наименование+
		      ?(ПустаяСтрока(Движение.ЗнакДвижения) = 1, ", " ," ("+?(Движение.ЗнакДвижения = "+", "приход", "расход")+"), ") +
		      Движение.ВидСтатус+", "+Движение.КодОперации;
		
//		Если (СчетД.Количественный = 1) ИЛИ (СчетК.Количественный = 1) Тогда
//			Количественный = 1;
//		КонецЕсли;
//		
//		Если (СчетД.Валютный = 1) ИЛИ (СчетК.Валютный = 1) Тогда
//			Валютный = 1;
//		КонецЕсли;

	ИначеЕсли (ПустоеЗначение(ТекущийЭлемент()) = 0) И (ЭтоГруппа() = 0) Тогда
		Стр = "НЕ ВЫБРАНО ДВИЖЕНИЕ РЕГИСТРА!";
		
	КонецЕсли;
	
//	Форма.ПодпКоличество.Видимость(Количественный);
//	Форма.ТекстКоличество.Видимость(Количественный);
//	
//	Форма.ПодпВалюта.Видимость(Валютный);
//	Форма.ТекстВалюта.Видимость(Валютный);
//	Форма.ПодпВалСумма.Видимость(Валютный);
//	Форма.ТекстВалСумма.Видимость(Валютный);
	
	ЗаполнитьОписаниеСубконто("Д", ДокНазначенД);
	ЗаполнитьОписаниеСубконто("К", ДокНазначенК);

	Возврат Стр;
	
КонецФункции // ДвижениеРегистра()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

Сортировка("Код", 1);