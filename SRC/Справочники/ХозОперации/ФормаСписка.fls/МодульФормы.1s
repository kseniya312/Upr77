////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
//	ВверхВниз(Сдвиг)
//
//	Параметры:
//	 Сдвиг - -1 или +1 - направление.
//
//	Вызывается из формул элементов диалога:
//	 Кнопка "Вверх"
//	 Кнопка "Вниз"
//
//	Описание:
//	 Устанавливает у элементов значение реквизита НомерСтроки, 
//	 по которому выполняется сортировка элементов в списке.

Процедура ВверхВниз(Сдвиг)
	
	Если ТекущийЭлемент().Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Объект = СоздатьОбъект("Справочник."+Вид());
	
	Объект.ИспользоватьРодителя(ТекущийЭлемент().Родитель);
	
	Объект.ПорядокКодов();
	Объект.ОбратныйПорядок(?(Сдвиг < 0, 0, 1));
	
	ПредыдущийЭлемент = Объект.ТекущийЭлемент();
		
	Объект.ВыбратьЭлементы(1);
	Пока Объект.ПолучитьЭлемент(0) = 1 Цикл
		Если Объект.ТекущийЭлемент() = ТекущийЭлемент() Тогда
			Прервать;
		КонецЕсли;
		
		ПредыдущийЭлемент = Объект.ТекущийЭлемент();
	КонецЦикла;
	
	Если ПредыдущийЭлемент.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийКод = ТекущийЭлемент().Код;
	ПредыдущийКод = ПредыдущийЭлемент.Код;
	
	Попытка
		НачатьТранзакцию();
		
		Объект.НайтиЭлемент(ТекущийЭлемент());
		Объект.УстановитьНовыйКод();
	    Объект.Записать();
		
		Объект.НайтиЭлемент(ПредыдущийЭлемент);
		Объект.Код = ТекущийКод;
	    Объект.Записать();
		
		Объект.НайтиЭлемент(ТекущийЭлемент());
		Объект.Код = ПредыдущийКод;
	    Объект.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		Сообщить(ОписаниеОшибки(), "!");
	КонецПопытки;

КонецПроцедуры // ВверхВниз()

//******************************************************************************
// ПредставлениеДокумента()
//
// Возвращаемое значение:
//  Нет.
//  строка - представление документа 
//  
// Вызывается из формул элементов диалога:
//  "Вид документа".
//
// Описание:
//  Определяет пользовательское представление документа.
//
Функция ПредставлениеДокумента()
	
	Если Метаданные.Документ(СокрЛП(ВидДокумента)).Выбран() = 1 Тогда
		Возврат Метаданные.Документ(СокрЛП(ВидДокумента)).Представление();
	КонецЕсли;
	
КонецФункции // ПредставлениеДокумента()

//******************************************************************************
// Движения()
//
// Вызывается из формул элементов диалога:
//  Кнопка "Бухгалтерские Движения".
//
// Описание:
//  Открывает справочник проводок по текущей хозяйственной операции.
//
Процедура Движения()
	
	Перем КонтекстФормы;
	
	ОткрытьФорму("Справочник.ДвиженияРегистров.ФормаСписка#ДвиженияПоОперации", КонтекстФормы);
	КонтекстФормы.ИерархическийСписок(1, 1);
	
КонецПроцедуры // Движения()

//******************************************************************************
// Проводки()
//
// Вызывается из формул элементов диалога:
//  Кнопка "Бухгалтерские проводки".
//
// Описание:
//  Открывает справочник проводок по текущей хозяйственной операции.
//
Процедура Проводки()
	
	Перем КонтекстФормы;
	
	ОткрытьФорму("Справочник.ПроводкиХозОпераций.ФормаСписка#ПроводкиПоОперации", КонтекстФормы);
	КонтекстФормы.ИерархическийСписок(1, 1);
	
КонецПроцедуры // Проводки()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, ФлагПродолжения)
	
	Список = СоздатьОбъект("СписокЗначений");
	
	Если ЭлементДиалога = "ВидДокумента" Тогда
		Для Номер = 1 По Метаданные.Документ() Цикл
			Список.ДобавитьЗначение(Метаданные.Документ(Номер).Идентификатор);
		КонецЦикла;
		
		Вид = СокрП(ВидДокумента);
		Если Список.ВыбратьЗначение(Вид, , , 60, 2) = 1 Тогда
			ВидДокумента = Вид;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//******************************************************************************

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

// Если нет права корректировки справочника, то прячем служебные кнопки
Если ПравоДоступа("Корректировка","Справочник.ХозОперации")=0 Тогда
	Сортировка("Код", 1);
	//Форма.ТолькоПросмотр(1);
	Форма.ВидДокумента.Видимость(0);
	Форма.кнВверх.Видимость(0);
	Форма.кнВниз.Видимость(0);
КонецЕсли;
