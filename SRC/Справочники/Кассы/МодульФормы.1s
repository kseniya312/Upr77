////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// УправлениеДоступностью()
//
// Параметры:
//  
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Флаг ЭтоККМ.
//
// Описание:
//  Управляет доступностью реквизитов для настройки взаимодействия с ККМ.
//
Процедура УправлениеДоступностью()
	
	Форма.ПодпРежимККМ.Доступность(ЭтоККМ);
	Форма.ПодпЗаводскойНомерККМ.Доступность(ЭтоККМ);
	Форма.РежимККМ.Доступность(ЭтоККМ);
	Форма.ЗаводскойНомерККМ.Доступность(ЭтоККМ);
	
	Если ЭтоККМ = 0 Тогда
		РежимККМ			= 0;
		//ЗаводскойНомерККМ	= 0;   //Это для возвратов
	КонецЕсли;
	
КонецПроцедуры // УправлениеДоступностью()

//******************************************************************************
// ПоКнопкеОстатки()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ПоКнопкеОстатки()
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ВедомостьПоКассе");
	
	// все настройки помещаем в список
	ВыбДатаКонца  = Мин(РабочаяДата(),ПолучитьДатуТА());
	ВыбДатаНачала = Мин(глЗначениеПоУмолчанию("ОсновнаяДатаНачалаОтчетов"),ВыбДатаКонца);
	ВыбДатаНачала = ?(ПустоеЗначение(ВыбДатаНачала) = 1,ВыбДатаКонца,ВыбДатаНачала);
	
	Расшифровка.Установить("ДатаНачала", 	ВыбДатаНачала);
    Расшифровка.Установить("ДатаКонца", 	ВыбДатаКонца);
	Расшифровка.Установить("ВыбРазделитель1","");
	Расшифровка.Установить("ВыбРазделитель2","");
	Расшифровка.Установить("ВыбРазделитель3","");
	Расшифровка.Установить("ВидРазделителя",1);

	Расшифровка.Установить("ВыбКасса",		ТекущийЭлемент());
	Расшифровка.Установить("ВыбВалюта",		0);
	Расшифровка.Установить("ВыбВидВалюты",	1);
	Расшифровка.Установить("ПоОперациям",	0);
	
	Группировки = СоздатьОбъект("СписокЗначений");
	
	Группировки.ДобавитьЗначение("Фирма", "Фирма");
	Группировки.ДобавитьЗначение("Касса", "Касса");
	Группировки.ДобавитьЗначение("Валюта", "Валюта");
	Группировки.ДобавитьЗначение("День", "День");
	Группировки.ДобавитьЗначение("Документ", "Документы движения");
	
	Группировки.Пометка(2, 1);
	Группировки.Пометка(3, 1);
	Расшифровка.Установить("Группировки",	Группировки);
	
	глОбработкаРасшифровки(Расшифровка, 0, 0);
	
КонецПроцедуры // ПоКнопкеОстатки()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнЗаписать.Доступность(0);
		Форма.кнОК.Доступность(0);
		
		Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
	    Форма.КнопкаПоУмолчанию("кнОк");
	КонецЕсли;                
	
	ЭтоККМ = ?(ПустоеЗначение(РежимККМ) = 1, 0,1);
	
	УправлениеДоступностью();
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	Если ПустоеЗначение(Наименование)=1 Тогда
		Предупреждение("Не заполнено поле ""Наименование""!", 60);
		Активизировать("Наименование");
		СтатусВозврата(0);

	ИначеЕсли (ЭтоККМ = 1) и (ПустоеЗначение(РежимККМ)=1) Тогда
		Предупреждение("Не заполнено поле ""Режим работы ККМ""!", 60);
		Активизировать("РежимККМ");
		СтатусВозврата(0);
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()
                                 
//******************************************************************************
