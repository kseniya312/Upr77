////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// ПоКнопкеЗначения()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "Значения".
//
// Описание:
//  Открывает справочник значений свойств
//
Процедура ПоКнопкеЗначения()
	
	Параметр = ТекущийЭлемент();
	
	Если Параметр.Выбран() = 0 Тогда
		Предупреждение("Не выбран вид свойства", 60);
	Иначе
		ОткрытьФорму("Справочник.ЗначенияСвойств", Параметр);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеЗначения()

//******************************************************************************
// ПоКнопкеОбработка()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
//  по кнопке "Обработка"
//
// Описание:
//  Открывает форму обработки ПодборОбъектов.
//
Процедура ПоКнопкеОбработка()
                                            
	СписДействий = СоздатьОбъект("СписокЗначений");
	СписДействий.ДобавитьЗначение("Перенести в группу");
	СписДействий.ДобавитьЗначение("Изменить каталог вида свойства");
	
	СписокПараметровВызова = СоздатьОбъект("СписокЗначений");
	СписокПараметровВызова.ДобавитьЗначение("Справочник",				"Тип");
	СписокПараметровВызова.ДобавитьЗначение("ВидыСвойств",				"Вид");
	СписокПараметровВызова.ДобавитьЗначение("Справочник.ВидыСвойств",	"ИмяВызвавшейФормы");
	СписокПараметровВызова.ДобавитьЗначение(СписДействий,				"Действия");
	
	ОткрытьФорму("Отчет.ПодборОбъектов", СписокПараметровВызова);
	
КонецПроцедуры // ПоКнопкеОбработка()

//******************************************************************************
// ВыполнитьДействие(СписЭлементов, Действие)
//
// Параметры:
//  СписЭлементов - список значений, содержащий значения элементов
//  Действие - действие, которое нужно выполнить над списком. 
//			   Возможные значения:
//			   "Перенести в группу"            
//			   "Изменить каталог вида свойства"
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Вызывается при повторном открытии формы. В зависимости от значения 
//  параметра Действие либо изменяет родителя, либо изменяет реквизит
//  Каталог у переданного списка элментов
//
Процедура ВыполнитьДействие(СписЭлементов, Действие)
	Эл	= СоздатьОбъект("Справочник.ВидыСвойств");
	
	Если Действие = "Перенести в группу" Тогда
            
		ЭлРодитель = СоздатьОбъект("Справочник.ВидыСвойств");
		ЭлРодитель.ВыборГруппы(1);
		
		Если ЭлРодитель.Выбрать("Выберите группу", ) = 0 Тогда
			ЭлРодитель = ПолучитьПустоеЗначение("Справочник.ВидыСвойств");
		ИначеЕсли ЭлРодитель.ЭтоГруппа() = 0 Тогда
			ЭлРодитель = ЭлРодитель.Родитель;
		КонецЕсли;
		
		Для Сч = 1 По СписЭлементов.РазмерСписка() Цикл
		    Эл.НайтиЭлемент(СписЭлементов.ПолучитьЗначение(Сч));
			Эл.Родитель = ЭлРодитель.ТекущийЭлемент();
			Эл.Записать();
		КонецЦикла;
		
	ИначеЕсли Действие = "Изменить каталог вида свойства" Тогда
                                                      
		ЭлКаталог	= СоздатьОбъект("Справочник.Каталоги");
		
		Если ЭлКаталог.Выбрать("Выберите каталог", ) = 0 Тогда
			Возврат
		КонецЕсли;
		
		Для Сч = 1 По СписЭлементов.РазмерСписка() Цикл
		    Эл.НайтиЭлемент(СписЭлементов.ПолучитьЗначение(Сч));
			Эл.Каталог = ЭлКаталог.ТекущийЭлемент();
			Эл.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьДействие()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	ИерархическийСписок(1, 1);
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриПовторномОткрытии()
	
	СписПараметров = Форма.Параметр;
	Если ПустоеЗначение(СписПараметров) = 1 Тогда Возврат КонецЕсли;

	СписЭлементов	= СписПараметров.Получить("Объекты");
	Действие		= СписПараметров.Получить("ВыбранноеДействие");
            
	Если ТипЗначенияСтр(СписЭлементов) <> "СписокЗначений" Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьДействие(СписЭлементов, Действие);
	
КонецПроцедуры // ПриПовторномОткрытии()

