//******************************************************************************
// СформироватьСписок(ТипОбъекта)
//
// Параметры:
//  ТипОбъекта - строка, Тип объекта метаданных "Справочник", "Документ", "Перечисление".
//
// Возвращаемое значение:
//  список значений – список видов объектов метаданных по типу.
//  
// Описание:
//  Формирует список видов объектов метаданных по типу "Справочник", "Документ", "Перечисление".
//
Функция СформироватьСписок(ТипОбъекта)
	
	Список = СоздатьОбъект("СписокЗначений");
	
	Если ТипОбъекта = "Справочник" Тогда
		КолОбъектов = Метаданные.Справочник();
	ИначеЕсли ТипОбъекта = "Документ" Тогда
		КолОбъектов = Метаданные.Документ();
	Иначе
		КолОбъектов = Метаданные.Перечисление();
	КонецЕсли;
	
	Для Номер = 1 По КолОбъектов Цикл
		
		Если ТипОбъекта = "Справочник" Тогда
			Объект = Метаданные.Справочник(Номер);
		ИначеЕсли ТипОбъекта = "Документ" Тогда
			Объект = Метаданные.Документ(Номер);
		Иначе
			Объект = Метаданные.Перечисление(Номер);
		КонецЕсли;
		
		Список.ДобавитьЗначение(Объект.Идентификатор, Объект.Представление());
		
	КонецЦикла;
	
	Возврат Список;
	
КонецФункции // СформироватьСписок()

//******************************************************************************
// ПредставлениеОбъекта()
//
// Возвращаемое значение:
//  строка - представление объекта конфигурации
//  
// Описание:
//  Функция определяет пользовательское представление реквизита "ТипЗначенияТорг".
//
Функция ПредставлениеОбъекта()
	
	Представление = ТипЗначенияТорг;
	Поз = Найти(ТипЗначенияТорг, ".");
	
	Если Поз > 0 Тогда
		
		ВидОбъекта = Сред(ТипЗначенияТорг, Поз + 1);
		СписокВидов = СформироватьСписок(Сред(ТипЗначенияТорг, 1, Поз - 1));
		
		Если СписокВидов.НайтиЗначение(ВидОбъекта) > 0 Тогда
			СписокВидов.ПолучитьЗначение(СписокВидов.НайтиЗначение(ВидОбъекта), Представление);
			Представление = Сред(ТипЗначенияТорг, 1, Поз - 1) + " """ + Представление + """";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции // ПредставлениеОбъекта()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнОчиститьТипЗначенияТорг.Доступность(0);
		Форма.кнЗаписать.Доступность(0);
		Форма.кнОК.Доступность(0);
		
		Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
	    Форма.КнопкаПоУмолчанию("кнОк");
	КонецЕсли;
	
	ТипЗначенияТорг1 = ПредставлениеОбъекта();
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеВыбораЗначения(Элемент, ФлагПродолжения)
	
	ФлагПродолжения = 0;
	
	Если Элемент = "ТипЗначенияТорг1" Тогда
		
		Поз = Найти(ТипЗначенияТорг, ".");
		Если Поз > 0 Тогда
			ТипОбъекта = Сред(ТипЗначенияТорг, 1, Поз - 1);
			ВидОбъекта = Сред(ТипЗначенияТорг, Поз + 1);
		Иначе
			ТипОбъекта = ТипЗначенияТорг;
			ВидОбъекта = "";
		КонецЕсли;
		
		Список = СоздатьОбъект("СписокЗначений");
		Список.ДобавитьЗначение("Справочник");
		Список.ДобавитьЗначение("Документ");
		Список.ДобавитьЗначение("Перечисление");
		
		Если Список.ВыбратьЗначение(ТипОбъекта, , , , 2) = 1 Тогда
			
			Если ПустаяСтрока(ТипОбъекта) = 1 Тогда
				ТипЗначенияТорг = "";
				ТипЗначенияТорг1 = ПредставлениеОбъекта();
				Возврат;
			КонецЕсли;
				
			Если СформироватьСписок(ТипОбъекта).ВыбратьЗначение(ВидОбъекта, , , , 2) = 1 Тогда
				ТипЗначенияТорг = ТипОбъекта + "." + ВидОбъекта;
				ТипЗначенияТорг1 = ПредставлениеОбъекта();
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Элемент = "" Тогда
		
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	// проверка заполнения обязательных полей
	Если ПустоеЗначение(Наименование) = 1 Тогда
	    Предупреждение("Не заполнено поле ""Вид субконто""!", 60);
		Активизировать("Наименование");
	    СтатусВозврата(0);
	    Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
