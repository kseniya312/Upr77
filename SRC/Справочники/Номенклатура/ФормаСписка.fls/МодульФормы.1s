////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем ТаблицаПечФорм;
Перем НомерТекущейФормы;
Перем ФормаСпискаЦен;
Перем НС,Рег1, Рег2, Склад_темп, ТЗ, ТЗ2;	//Работа с остатками

Перем ФормаТаблицыОстатков;  // контекст формы с таблицей остатков
Перем Склад;

Процедура ПриИзменениифлСкрыватьИзВосьмерки() Далее
   
Функция ПолучитьГруппуНоменклатуры()   

	ТекНоменклатура = ТекущийЭлемент();
	Возврат глПолучитьГруппуНоменклатуры(ТекНоменклатура, Склад);
	 
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// СформироватьШтрихкод(Весовой, Код)
//
// Параметры:
//  Весовой - 1 - позиция номенклатуры является весовым товаром
//            0 - позиция номенклатуры является штучным товаром
//  Код     - код позиции номенклатуры, для которой формируется штрих-код
//
// Возвращаемое значение:
//  строка - сформированный штрихкод
//
// Описание:
//  Формирует уникальный штрихкод позиции номенклатуры 
//
Функция СформироватьШтрихкод(Весовой, Код)
	
	СпрЕдиницы = СоздатьОбъект("Справочник.Единицы");
	СформировалиУникальныйШтрихкод = 0;
	
	Если Весовой = 0 Тогда
		
		// Сформируем штрих-код штучного товара
		
		КодТовара = глДополнитьСтрокуЛ(Число(СокрЛ(Прав(Код, 6))), "0", 7);
		
		Для Сч = 1 По 999 Цикл
			
			ДопКод = глДополнитьСтрокуЛ(Строка(Сч), "0", 3);
			ВремШтрихкод = глСформироватьШтрихкод(глПрефиксШтучногоШтрихкода, КодТовара + ДопКод);
			
			Если СпрЕдиницы.НайтиПоРеквизиту("Штрихкод", ВремШтрихкод, 1) = 0 Тогда
				СформировалиУникальныйШтрихкод = 1;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СформировалиУникальныйШтрихкод = 0 Тогда
			
			// Сформируем штрих-код независимо от кода номенклатуры
			
			Для Сч = 1 По 9999999999 Цикл // 10 разрядов
				
				ДопКод = глДополнитьСтрокуЛ(Строка(Сч), "0", 10);
				ВремШтрихкод = глСформироватьШтрихкод(глПрефиксШтучногоШтрихкода, ДопКод);
				
				Если СпрЕдиницы.НайтиПоРеквизиту("Штрихкод", ВремШтрихкод, 1) = 0 Тогда
					СформировалиУникальныйШтрихкод = 1;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	Иначе
		
		// Сформируем штрих-код весового товара
		
		КодТовараЧисло = Число(СокрЛ(Прав(Код, 5)));
		
		Для Сч = КодТовараЧисло По 99999 Цикл // 5 разрядов
			
			ДопКод = глДополнитьСтрокуЛ(Строка(Сч), "0", 5);
			ВремШтрихкод = глСформироватьШтрихкод(глПрефиксВесовогоШтрихкода, ДопКод + "00000");
			
			Если СпрЕдиницы.НайтиПоРеквизиту("Штрихкод", ВремШтрихкод, 1) = 0 Тогда
				СформировалиУникальныйШтрихкод = 1;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СформировалиУникальныйШтрихкод = 0 Тогда
			Для Сч = 1 По КодТовараЧисло Цикл
				
				ДопКод = глДополнитьСтрокуЛ(Строка(Сч), "0", 5);
				ВремШтрихкод = глСформироватьШтрихкод(глПрефиксВесовогоШтрихкода, ДопКод + "00000");
				
				Если СпрЕдиницы.НайтиПоРеквизиту("Штрихкод", ВремШтрихкод, 1) = 0 Тогда
					СформировалиУникальныйШтрихкод = 1;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	    
	КонецЕсли; // Если Весовой = 0 Тогда
	
	Если СформировалиУникальныйШтрихкод = 0 Тогда
		ВремШтрихкод = "";
	КонецЕсли;
	
	Возврат ВремШтрихкод;

КонецФункции // СформироватьШтрихкод()

//******************************************************************************
// СкопироватьЕдиницы(ЭлементИсточник, ЭлементПриемник, СписЕдиниц)
//
// Параметры: 
//  ЭлементИсточник, ЭлементПриемник, СписЕдиниц
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура СкопироватьЕдиницы(ЭлементИсточник, ЭлементПриемник, СписЕдиниц)
	СписЕдиниц	= СоздатьОбъект("ТаблицаЗначений");
	
	СписЕдиниц.НоваяКолонка("Единица"	, "Справочник.Единицы");
	СписЕдиниц.НоваяКолонка("ОКЕИ"		, "Справочник.ОКЕИ");
	
	СпрЕдиницы	= СоздатьОбъект("Справочник.Единицы");
	СпрЕдиницы.ИспользоватьВладельца(ЭлементПриемник.ТекущийЭлемент());
	
	СпрЕдИст	= СоздатьОбъект("Справочник.Единицы");
	СпрЕдИст.ИспользоватьВладельца(ЭлементИсточник);
	СпрЕдИст.ВыбратьЭлементы();
	Пока СпрЕдИст.ПолучитьЭлемент() = 1 Цикл
		СпрЕдиницы.Новый();
		
		СпрЕдиницы.ОКЕИ			= СпрЕдИст.ОКЕИ;
		СпрЕдиницы.Вес			= СпрЕдИст.Вес;
		СпрЕдиницы.Коэффициент	= СпрЕдИст.Коэффициент;
		
		// Сформируем новый штрихкод, если он был задан в источнике
		Если ПустоеЗначение(СпрЕдИст.ШтрихКод) = 0 Тогда
			СпрЕдиницы.ШтрихКод = СформироватьШтрихкод(ЭлементПриемник.Весовой, ЭлементПриемник.Код);
		КонецЕсли;
		
		СпрЕдиницы.Записать();
		
		СписЕдиниц.НоваяСтрока();
		ТекЕдиница			= СпрЕдиницы.ТекущийЭлемент();
		СписЕдиниц.Единица	= ТекЕдиница;
		СписЕдиниц.ОКЕИ		= ТекЕдиница.ОКЕИ;
		
		// устанавливаем базовую единицу
		Если СпрЕдиницы.ОКЕИ = ЭлементИсточник.БазоваяЕдиница.ОКЕИ Тогда
		    ЭлементПриемник.БазоваяЕдиница	= СпрЕдиницы.ТекущийЭлемент();
		КонецЕсли;
		
		// устанавливаем основную единицу
		Если СпрЕдиницы.ОКЕИ = ЭлементИсточник.ОсновнаяЕдиница.ОКЕИ Тогда
		    ЭлементПриемник.ОсновнаяЕдиница	= СпрЕдиницы.ТекущийЭлемент();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // СкопироватьЕдиницы()

//******************************************************************************
// СкопироватьЦены(ЭлементИсточник, ЭлементПриемник, СписЕдиниц)
//
// Параметры: 
//  ЭлементИсточник, ЭлементПриемник, СписЕдиниц
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура СкопироватьЦены(ЭлементИсточник, ЭлементПриемник, СписЕдиниц)
	
	СпрЦены	= СоздатьОбъект("Справочник.Цены");
	СпрЦены.ИспользоватьВладельца(ЭлементПриемник.ТекущийЭлемент());
	СпрЦены.ИспользоватьДату('01.01.1980');
	
	СпрЦеныИст	= СоздатьОбъект("Справочник.Цены");
	СпрЦеныИст.ИспользоватьВладельца(ЭлементИсточник);
	СпрЦеныИст.ИспользоватьДату(РабочаяДата());
	СпрЦеныИст.ВыбратьЭлементы();
	Пока СпрЦеныИст.ПолучитьЭлемент() = 1 Цикл
		СпрЦены.Новый();
		СпрЦены.ТипЦен	= СпрЦеныИст.ТипЦен;
		СпрЦены.Цена	= СпрЦеныИст.Цена;
		СпрЦены.Валюта	= СпрЦеныИст.Валюта;
		СпрЦены.Процент	= СпрЦеныИст.Процент;
		
		// в цене источнике указана единица другого товара
		// найдем нужную единицу текущего товара по ОКЕИ
		Поз	= 0;
		Если СписЕдиниц.НайтиЗначение(СпрЦеныИст.Единица.ОКЕИ, Поз, "ОКЕИ") =1 Тогда
		    СпрЦены.Единица	= СписЕдиниц.ПолучитьЗначение(Поз, "Единица");
			СпрЦены.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // СкопироватьЦены()

//******************************************************************************
// СкопироватьСвойства(ЭлементИсточник, ЭлементПриемник)
//
// Параметры: 
//  ЭлементИсточник, ЭлементПриемник
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура СкопироватьСвойства(ЭлементИсточник, ЭлементПриемник)
	
	ОснСвойство	= ЭлементИсточник.ОсновноеСвойство;

	СпрСвойства	= СоздатьОбъект("Справочник.СвойстваНоменклатуры");
	СпрСвойства.ИспользоватьВладельца(ЭлементПриемник.ТекущийЭлемент());
	
	СпрСвойстваИст	= СоздатьОбъект("Справочник.СвойстваНоменклатуры");
	СпрСвойстваИст.ИспользоватьВладельца(ЭлементИсточник);
	
	СпрСвойстваИст.ВыбратьЭлементы();
	Пока СпрСвойстваИст.ПолучитьЭлемент() = 1 Цикл
		СпрСвойства.Новый();
		
		СпрСвойства.ВидСвойства			= СпрСвойстваИст.ВидСвойства;
		СпрСвойства.ЗначениеСвойства	= СпрСвойстваИст.ЗначениеСвойства;
		
		СпрСвойства.Записать();
		
		Если (ОснСвойство.ВидСвойства = СпрСвойства.ВидСвойства) И (ОснСвойство.ЗначениеСвойства = СпрСвойства.ЗначениеСвойства) Тогда
			ЭлементПриемник.ОсновноеСвойство	= СпрСвойства.ТекущийЭлемент();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // СкопироватьСвойства()

//******************************************************************************
// СкопироватьАналоги(ЭлементИсточник, ЭлементПриемник)
//
// Параметры: 
//  ЭлементИсточник, ЭлементПриемник
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура СкопироватьАналоги(ЭлементИсточник, ЭлементПриемник)
	
	СпрАналоги	= СоздатьОбъект("Справочник.Аналоги");
	СпрАналоги.ИспользоватьВладельца(ЭлементПриемник.ТекущийЭлемент());
	
	СпрАналогиИст	= СоздатьОбъект("Справочник.Аналоги");
	СпрАналогиИст.ИспользоватьВладельца(ЭлементИсточник);
	
	СпрАналогиИст.ВыбратьЭлементы();
	Пока СпрАналогиИст.ПолучитьЭлемент() = 1 Цикл
		СпрАналоги.Новый();
		
		СпрАналоги.Наименование				= СпрАналогиИст.Наименование;
		СпрАналоги.Каталог					= СпрАналогиИст.Каталог;
		СпрАналоги.ИдентификаторВКаталоге	= СпрАналогиИст.ИдентификаторВКаталоге;
		
		СпрАналоги.Записать();
	КонецЦикла;

КонецПроцедуры // СкопироватьАналоги()

//******************************************************************************
// СкопироватьКомплектующие(ЭлементИсточник, ЭлементПриемник)
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура СкопироватьКомплектующие(ЭлементИсточник, ЭлементПриемник)
	
	СпрКомплектация	= СоздатьОбъект("Справочник.Комплектация");
	СпрКомплектация.ИспользоватьВладельца(ЭлементПриемник.ТекущийЭлемент());
	
	СпрКомплектацияИст	= СоздатьОбъект("Справочник.Комплектация");
	СпрКомплектацияИст.ИспользоватьВладельца(ЭлементИсточник);
	
	СпрКомплектацияИст.ВыбратьЭлементы();
	Пока СпрКомплектацияИст.ПолучитьЭлемент() = 1 Цикл
		СпрКомплектация.Новый();
		
		СпрКомплектация.Номенклатура	= СпрКомплектацияИст.Номенклатура;
		СпрКомплектация.Количество		= СпрКомплектацияИст.Количество;
		СпрКомплектация.ЦеновойКоэфф	= СпрКомплектацияИст.ЦеновойКоэфф;
		
		СпрКомплектация.Записать();
	КонецЦикла;
	
КонецПроцедуры // СкопироватьКомплектующие()

//******************************************************************************
// СкопироватьЭлемент()
//
// Параметры: 
//  
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура ПоКнопкеКопировать()
	Перем СписЕдиниц;
	
	ТекЭлемент	= ТекущийЭлемент();
	
	Если ПустоеЗначение(ТекЭлемент) = 1 Тогда
	    Предупреждение("Не выбрана номенклатура для копирования!", 60);
	Иначе
		Если ТекЭлемент.ЭтоГруппа() = 1 Тогда
		    Предупреждение("Выберите элемент, а не группу!", 60);
		Иначе
			СпрНоменклатура	= СоздатьОбъект("Справочник.Номенклатура");
			СпрНоменклатура.ИспользоватьРодителя(ТекЭлемент.Родитель);
			СпрНоменклатура.Новый();
			
			// копируем реквизиты
			СпрНоменклатура.Наименование		= ТекЭлемент.Наименование;
			СпрНоменклатура.Артикул				= ТекЭлемент.Артикул;
			СпрНоменклатура.Весовой				= ТекЭлемент.Весовой;
			СпрНоменклатура.ВидНоменклатуры		= ТекЭлемент.ВидНоменклатуры;
			СпрНоменклатура.Комментарий			= ТекЭлемент.Комментарий;
			СпрНоменклатура.МинОстаток			= ТекЭлемент.МинОстаток;
			СпрНоменклатура.НеВключатьВпрайс	= ТекЭлемент.НеВключатьВпрайс;
			СпрНоменклатура.НомерГТД			= ТекЭлемент.НомерГТД;
			СпрНоменклатура.ПолнНаименование	= ТекЭлемент.ПолнНаименование;
			СпрНоменклатура.СтавкаНДС			= ТекЭлемент.СтавкаНДС;
			СпрНоменклатура.СтавкаНП			= ТекЭлемент.СтавкаНП;
			СпрНоменклатура.СтранаПроисхождения	= ТекЭлемент.СтранаПроисхождения;
			
			НачатьТранзакцию();
			
				Попытка
					СпрНоменклатура.Записать();
					
					СкопироватьЕдиницы(ТекЭлемент, СпрНоменклатура, СписЕдиниц);
					СкопироватьЦены(ТекЭлемент, СпрНоменклатура, СписЕдиниц);
					СкопироватьСвойства(ТекЭлемент, СпрНоменклатура);
					СкопироватьАналоги(ТекЭлемент, СпрНоменклатура);
					СкопироватьКомплектующие(ТекЭлемент, СпрНоменклатура);
					
					// запишем элемент после заполнения реквизитов
					// БазоваяЕдиница, ОсновнаяЕдиница и ОсновноеСвойство
					СпрНоменклатура.Записать();
				Исключение
					ОтменитьТранзакцию();
					Предупреждение(ОписаниеОшибки(), 60);
					Возврат;
				КонецПопытки;
			
			ЗафиксироватьТранзакцию();
			
			АктивизироватьОбъект(СпрНоменклатура.ТекущийЭлемент());
			
			// откроем форму созданного элемента 
			ОткрытьФорму(СпрНоменклатура.ТекущийЭлемент());
				
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СкопироватьЭлемент()

//******************************************************************************
// ОсновноеСвойство()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Значение основного свойства
//
// Вызывается из формул реквизитов диалога
// колонка основное свойство
//
// Описание:
//  Формирует и возращает строковое представление основного свойства
//
Функция ОсновноеСвойство()
	
	Если ЭтоГруппа() = 1 Тогда
		Возврат "";
	КонецЕсли;
	
	Если ПустоеЗначение(ТекущийЭлемент()) = 1  Тогда
		Возврат "";
	Иначе  
		Если ПустоеЗначение(ТекущийЭлемент().ОсновноеСвойство) = 1  Тогда
			Возврат "";
		Иначе
			Возврат ТекущийЭлемент().ОсновноеСвойство.ЗначениеСвойства;
		КонецЕсли;
	КонецЕсли;
КонецФункции // ОсновноеСвойство()

//******************************************************************************
// ДобавитьВтаблицу(Табл, Товар, Тип, ИспТип)
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ДобавитьВтаблицу(Табл, Товар, Тип)
	Перем ВремЦена, ВремЕдиница, ВремВалюта;
	
	Если глВернутьЦену(Товар, Тип, РабочаяДата(), ВремЦена, ВремЕдиница, ВремВалюта) = 1 Тогда
		Табл.НоваяСтрока(); 
		Табл.Артикул   = СокрЛП(Товар.Артикул);
		Табл.Товар   = СокрЛП(Товар.ПолнНаименование);
		Табл.Цена    = ВремЦена;
		Табл.Валюта  = ВремВалюта;
		Табл.Единица = ВремЕдиница;
	Иначе
		Сообщить("У товара """ + Товар.ПолнНаименование + """ отсутствует цена """ + Тип + """");
	КонецЕсли;	
	
КонецПроцедуры // ДобавитьВтаблицу()

//******************************************************************************
// ПечатьЦенника(Товар, ТипЦены)
//
// Параметры:
//  Товар - товар, для которого нужно распечатать ценник
//  ТипЦены - тип цены.
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ПечатьЦенника() Экспорт
	Перем ПечЕдиница, ПечНаименование, ПечРозн;
	Перем Таб;
 	Перем Столбик;
 	Перем Ряд;
 	Перем Процент, ПорядокОкр;
 	
 	Перем ТабТоваров, ДанныеДляПечати;
 	
	ВыбТовар = ТекущийЭлемент();
		
	Если ПустоеЗначение(ВыбТовар) = 1 Тогда
		Предупреждение("Не выбрана товар или группа товаров!", 60);
		Возврат;
	КонецЕсли;
		
    
	ТипЦены	=	СоздатьОбъект("Справочник.ТипыЦен");
	Если ТипЦены.Выбрать("Типы цен",) = 0 Тогда Возврат КонецЕсли;
 	ТипЦены	=	ТипЦены.ТекущийЭлемент();
	
	ТабТоваров = СоздатьОбъект("ТаблицаЗначений");
	ТабТоваров.НоваяКолонка("Артикул");
	ТабТоваров.НоваяКолонка("Товар");
	ТабТоваров.НоваяКолонка("Цена","Число");
	ТабТоваров.НоваяКолонка("Единица");
	ТабТоваров.НоваяКолонка("Валюта");
	
	Если ВыбТовар.ЭтоГруппа() = 0 Тогда
		ДобавитьВтаблицу(ТабТоваров, ВыбТовар, ТипЦены);
	Иначе
		СтрТов = СоздатьОбъект("Справочник.Номенклатура");
		СтрТов.ИспользоватьРодителя(ВыбТовар);
		СтрТов.ВключатьПодчиненные(1);
		СтрТов.ВыбратьЭлементы();
		
		Пока СтрТов.ПолучитьЭлемент() = 1 Цикл
			ТекТов = СтрТов.ТекущийЭлемент();
			Если ТекТов.ЭтоГруппа() = 0 Тогда
				ДобавитьВтаблицу(ТабТоваров, ТекТов, ТипЦены);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// упакуем данные в список
	ДанныеДляПечати = СоздатьОбъект("СписокЗначений");
 	
	ДанныеДляПечати.ДобавитьЗначение(ТипЦены.Валюта , "Валюта");
	ДанныеДляПечати.ДобавитьЗначение(ТабТоваров     , "Таблица");
	
	ОткрытьФормуМодально("Обработка.ПечатьЦенников", ДанныеДляПечати);
	
КонецПроцедуры // глПечатьЦенника(Товар, ТипЦены)

//******************************************************************************
// ПечатьЭтикетки()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Формирует этикетку. Перед формированием
//  этикетки проверяет, чтобы текущий в многострочной части был выбран и 
//  он не являлся группой.
//
Процедура ПечатьЭтикетки()
	Перем Парам;
	ТекЭлем = ТекущийЭлемент();
	Если ПустоеЗначение(ТекЭлем) = 1 Тогда
		Предупреждение("Не выбрана номенклатурная позиция!", 60);
		Возврат;
	КонецЕсли;
	
	Если ТекЭлем.ЭтоГруппа() = 1 Тогда
		Предупреждение("Этикетка для группы номенклатуры не печатается!", 60);
		Возврат;
	КонецЕсли;
	
	Если ТекЭлем.Весовой = 0 Тогда
		Баркод = ТекЭлем.БазоваяЕдиница.Штрихкод;
		Табл = СоздатьОбъект("ТаблицаЗначений");
		Табл.НоваяКолонка("Товар", "Строка");
		Табл.НоваяКолонка("Штрихкод", "Строка");
		
		Табл.НоваяСтрока();
		Табл.Товар    = СокрЛП(ТекЭлем.Наименование);
		Табл.Штрихкод = Баркод;
		
		
		Парам = СоздатьОбъект("СписокЗначений");
		Парам.ДобавитьЗначение(Табл, "ТаблицаТоваров");
		
		ОткрытьФормуМодально("Обработка.ПечатьЭтикетки", Парам);
	Иначе
		// в штрихкоде храниться 5 правых цифр кода товара
		// перед печатью нужно сформировать
		
		// получаем вес товара
		Парам = СоздатьОбъект("СписокЗначений");
		Парам.ДобавитьЗначение(ТекЭлем					, "Номенклатура");
		Парам.ДобавитьЗначение(ТекЭлем.БазоваяЕдиница	, "Единица");
		
		ОткрытьФорму("Обработка.Этикетирование#", Парам);
	КонецЕсли;


КонецПроцедуры // ПечатьЭтикетки()

//******************************************************************************
// ПоКнопкеПечать()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ПоКнопкеПечать()
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
	КонецЕсли;
		
	Если НомерТекущейФормы = 1  Тогда
		// ЭТИКЕТКА
		ПечатьЭтикетки();
	ИначеЕсли НомерТекущейФормы = 2  Тогда
		// ЦЕННИК
	 	ПечатьЦенника();
	Иначе
		// внешняя обработка
		Параметры = глВзятьКонтекст(Контекст);
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм + ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Параметры:
//  нет.    
//
// Возвращаемое значение:
//  нет.
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Справочник." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормыСпис()

//******************************************************************************
// ПоКнопкеОстатки()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "Остатки".
//
// Описание:
//  Вызывает форму показа остатков по текущему ТМЦ в таблице значений.
//
Процедура ПоКнопкеОстатки()
	
	Параметр = глВзятьКонтекст(Контекст);
	ОткрытьФорму("Обработка.ПоказатьОстаткиТМЦ", Параметр);
	ФормаТаблицыОстатков = Параметр; // через параметр возвращается контекст открытой формы обработки
	
КонецПроцедуры // ПоКнопкеОстатки()

//******************************************************************************
// ПоКнопкеСвойства()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "Свойства".
//
// Описание:
//  Открывает справочник свойств номенклатуры.
//
Процедура ПоКнопкеСвойства()
	
	Параметр = ТекущийЭлемент();
	
	Если Параметр.Выбран() = 0 Тогда
		Предупреждение("Не выбрана номенклатура", 60);
	Иначе
		КонецЕсли;
	ОткрытьФорму("Справочник.СвойстваНоменклатуры", Параметр); 
	
КонецПроцедуры // ПоКнопкеСвойства()

//******************************************************************************
// ПоКнопкеАналоги()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "Аналоги".
//
// Описание:
//  Открывает справочник аналогов номенклатуры.
//
Процедура ПоКнопкеАналоги()
	
	Параметр = ТекущийЭлемент();
	
	Если Параметр.Выбран() = 0 Тогда
		Предупреждение("Не выбрана номенклатура", 60);
	Иначе
		ОткрытьФорму("Справочник.Аналоги", Параметр);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеАналоги()

//******************************************************************************
// ПоКнопкеЕдиницы()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "Единицы".
//
// Описание:
//  Открывает справочник единиц номенклатуры.
//
Процедура ПоКнопкеЕдиницы()
	
	Параметр = ТекущийЭлемент();
	
	Если Параметр.Выбран() = 0 Тогда
		Предупреждение("Не выбрана номенклатура", 60);
	Иначе
		ОткрытьФорму("Справочник.Единицы", Параметр);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеЕдиницы()                          

//******************************************************************************
// ПоКнопкеЦены()
//
// Параметры: 
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "Цены".
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Открывает справочник цен.
//
Процедура ПоКнопкеЦены()
	                                            
	Параметр = ТекущийЭлемент();
	
	Если Параметр.Выбран() = 0 Тогда
		Предупреждение("Не выбрана номенклатура", 60);
	Иначе
		ОткрытьФорму("Справочник.Цены", Параметр);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеЦены()

//******************************************************************************
// ПоКнопкеСправочники()
//
// Параметры: 
//  
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  ТекстОписания
//
Процедура ПоКнопкеСправочники()
	Перем ВидСпр;
	
	СписСправ	= СоздатьОбъект("СписокЗначений");
	СписСправ.ДобавитьЗначение("Единицы"				, "Единицы");
	СписСправ.ДобавитьЗначение("Цены"					, "Цены");
	СписСправ.ДобавитьЗначение("СвойстваНоменклатуры"	, "Свойства");
	СписСправ.ДобавитьЗначение("Аналоги"				, "Аналоги");
	СписСправ.ДобавитьЗначение("ПоставщикиТоваров"		, "Поставщики");
	
	Если СписСправ.ВыбратьЗначение(ВидСпр, , , 60, 1) = 1 Тогда
		Параметр = ТекущийЭлемент();
		
		Если Параметр.Выбран() = 0 Тогда
			Предупреждение("Не выбрана номенклатура", 60);
		Иначе
			ОткрытьФорму("Справочник." + ВидСпр, Параметр);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеСправочники()


//******************************************************************************
// СформироватьИнформационнуюСтроку()
//
// Параметры:
//  Нет
//
//	Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Текст информационной строки.
//
// Описание:
//  Формирует текст информационной строки формы. 
//
Функция СформироватьИнформационнуюСтроку()
	Перем ИнфСтрока;
	
	Если ПустоеЗначение(ТекущийЭлемент()) = 1 Тогда
		// не выбран элемент
		ИнфСтрока = "";
	Иначе
		
		Если ТекущийЭлемент().ЭтоГруппа() = 1 Тогда
			// текущий элемент - группа
			ИнфСтрока =  ТекущийЭлемент().ПолноеНаименование();
		Иначе
			
			ТекЭлем = ТекущийЭлемент();
			
			ИнфСтрока = СокрЛП(ТекЭлем.ПолнНаименование) +
			            ?(ПустоеЗначение(ТекЭлем.Артикул) = 0, "  Артикул: " + СокрЛП(ТекЭлем.Артикул), "") +
			            ?(ПустоеЗначение(ТекЭлем.Комментарий) = 0, "  Комментарий: " + СокрЛП(ТекЭлем.Комментарий),	"");
		
			Если ТипЗначения(ФормаТаблицыОстатков) = 100 Тогда
				
				// если открыта форма с таблицей остатков, то обновим в ней текущий элемент
				ФормаТаблицыОстатков.Форма.Обновить(0);
			    
			КонецЕсли;			
		КонецЕсли;

	КонецЕсли;
	
	Возврат ИнфСтрока;
	
КонецФункции // СформироватьИнформационнуюСтроку()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Справочник." + Вид(), ТаблицаПечФорм);
	
	//Если ПустоеЗначение(Константа.ВидСвойстваНоменклатуры) = 0  Тогда
	//	Форма.Свойство.Заголовок(Строка(Константа.ВидСвойстваНоменклатуры));
	//Иначе
	//	Форма.Свойство.Видимость(0);
	//КонецЕсли;
	
	ТЗ_Остаток.НоваяКолонка("Склад","Справочник.Склады",,,,11);
	ТЗ_Остаток.НоваяКолонка("Остаток","число",15,2,,4);
	ТЗ_Остаток.НоваяКолонка("Резерв","число",15,2,,4);

	Сотр=глПользователь.ТипСотрудника;
	Пер	=Перечисление.ТипСотрудника;
	НС=ТекущийЭлемент();
	флСкрыватьИзВосьмерки = ВосстановитьЗначение("Список_флСкрытьИзВосьмерки");
	ПриИзменениифлСкрыватьИзВосьмерки();
КонецПроцедуры // ПриОткрытии()          

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗакрытии()
	
	// если открыта форма с таблицей остатков, то закроем и ее
	Если ТипЗначения(ФормаТаблицыОстатков) = 100 Тогда
		ФормаТаблицыОстатков.Форма.Закрыть(0);
	КонецЕсли;			
	СохранитьЗначение("Список_флСкрыватьИзВосьмерки",   флСкрыватьИзВосьмерки);
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Процедура ОбработкаВнешнегоСобытия(Источник,Событие,Данные)
	
	Перем ВремТовар, ВремЕдиница, ВремКоличество;
	
	Если Событие = "BarCodeValue" Тогда
		Если глПолучитьТоварПоШтрихкоду(Данные, ВремТовар, ВремЕдиница, ВремКоличество) <> 0 Тогда
			АктивизироватьОбъект(ВремТовар);
			ОткрытьФорму(ВремТовар);	
		КонецЕсли;
		
		// Обработка закончена. Готовы к получению нового штрихкода.
		глСканерПосылкаДанных(1);
		
	Иначе
		глОбработкаВнешнегоСобытия(Источник, Событие, Данные);	
	КонецЕсли;

КонецПроцедуры // ОбработкаВнешнегоСобытия()

Функция ПокажиОстатки()
	Перем стр;

	Если НС=ТекущийЭлемент() Тогда
	    возврат 0;
	КонецЕсли;
	//Если НазваниеНабораПрав()="Сервис" Тогда
	//	возврат 0;
	//КонецЕсли;
        
	стр="";
	ТЗ_Остаток.УдалитьСтроки();
	Если ЭтоГруппа()=1 тогда
		стрк="";
		возврат стрк;
	КонецЕсли;
	Склад_Темп.ВыбратьЭлементы();
	Пока Склад_темп.ПолучитьЭлемент()=1 Цикл
		Рег1.УстановитьФильтр(,ТекущийЭлемент(),Склад_темп);
		Рег1.ВыгрузитьИтоги(ТЗ,1,1);	//В ТЗ мы запомнили все остатки товаров на складе
		
		рег2.УстановитьФильтр(ТекущийЭлемент(),Склад_темп);
		//	рег2.УстановитьЗначениеФильтра("Номенклатура",спТоваров,2);
		рег2.ВыгрузитьИтоги(ТЗ2,1,1);
		
		ТЗ2.Свернуть("Номенклатура","Количество");
		х					= ПолучитьПустоеЗначение(х);
		у					= 1;

		а=0;
		
		Если ТЗ2.НайтиЗначение(ТекущийЭлемент(),х,у)=1 Тогда	//Сначала общий резерв
			а=1;
			ТЗ_Остаток.НоваяСтрока();
			ТЗ_Остаток.Склад=Склад_Темп.ТекущийЭлемент();
			ТЗ_Остаток.Резерв	= ТЗ2.ПолучитьЗначение(х,"Количество");
			х					= ПолучитьПустоеЗначение(х);
		КонецЕсли;
		//Теперь из остатков ТМЦ
		х			= ПолучитьПустоеЗначение(х);
		у			= 2;
		Если ТЗ.НайтиЗначение(ТекущийЭлемент(),х,у)=1 Тогда
			Если а=0 Тогда
	    		ТЗ_Остаток.НоваяСтрока();
				ТЗ_Остаток.Склад=Склад_Темп.ТекущийЭлемент();
			КонецЕсли;
			ТЗ_Остаток.Остаток	= ТЗ.ПолучитьЗначение(х,"Количество");
		КонецЕсли;
	КонецЦикла;
	стрк="";
	НС=ТекущийЭлемент();
	возврат стрк;
КонецФункции


Процедура ПокажиДетализациюРезерва()
	Если ТЗ_Остаток.КоличествоСтрок()=0 Тогда
	    возврат;
	КонецЕсли;
	х	= ТЗ_Остаток.ТекущаяСтрока();
	скл	= ТЗ_Остаток.ПолучитьЗначение(х,"Склад");
	ном	= ТекущийЭлемент();
	ТекД= "";
	КонтРезерв	= СоздатьОбъект("СписокЗначений");	//Передача параметров в обработку "РезервыТМЦ" 
	КонтРезерв.ДобавитьЗначение(скл,"Склад");
	КонтРезерв.ДобавитьЗначение(ном,"Номенклатура");
	КонтРезерв.ДобавитьЗначение(ТекД,"ТекД");
	
	ОткрытьФормуМодально("Обработка.ПокажиРезервыТМЦ", контРезерв);
	
	
КонецПроцедуры  

//++Ерошенко 08.05.2007
Процедура ОткрытьФайл()
	          
	ИмяФ = "";
	ИмяК = "\\192.168.192.129\Q\Рабочая\Магазины\ТехИнфо\";
	Если ФС.ВыбратьФайл(0,ИмяФ,ИмяК,"Выберите файл для просмотра","Файлы(*.pdf)|*.pdf","pdf") = 1 Тогда
		Ф = ИмяК + ИмяФ;
		ЗапуститьПриложение(Ф);  
	КонецЕсли;
	
КонецПроцедуры
//--Ерошенко

//======================================================================
Процедура ПриИзменениифлСкрыватьИзВосьмерки()
	Если флСкрыватьИзВосьмерки = 1 Тогда
		УстановитьОтбор("ИзВосьмерки",0);
	Иначе
		УстановитьОтбор(,);
	КонецЕсли;
КонецПроцедуры // ПриИзменениифлСкрыватьИзВосьмерки

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 
ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");

ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название	= "Этикетка";
ТаблицаПечФорм.Кнопка	= "Этикетка";

ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название	= "Ценник";
ТаблицаПечФорм.Кнопка	= "Ценник";

ТЗ			= СоздатьОбъект("ТаблицаЗначений");
ТЗ2			= СоздатьОбъект("ТаблицаЗначений");
Рег1		= СоздатьОбъект("Регистр.ОстаткиТМЦ");
Рег2		= СоздатьОбъект("Регистр.РезервыТМЦ2");
Склад_темп	= СоздатьОбъект("Справочник.Склады");
Склад		= глПользователь.ОсновнойСклад;