////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// ПриИзмененииКонтроляКредита()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ПриИзмененииКонтроляКредита()
	
	Форма.ГлубинаКредита.Доступность(1-НеКонтролироватьКредит);
	Форма.СуммаКредита.Доступность(1-НеКонтролироватьКредит);
	
КонецПроцедуры // ПриИзмененииКонтроляКредита()

//******************************************************************************
// ПоКнопкеОткрыть(Объект)
//
// Параметры:
//  Объект - объект, который необходимо открыть.
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопки "Открыть".
//
// Описание:
//  Открывает форму объекта.
//
Процедура ПоКнопкеОткрыть(Объект, ЭлементДиалога="")
	
	Если Объект.Выбран() = 1 Тогда
		ОткрытьФормуМодально(Объект, , -1);
	Иначе
		Активизировать(ЭлементДиалога, 1);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеОткрыть()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ВводНового(ФлагКопирования)
	
	Если ФлагКопирования = 0 Тогда
		ВалютаВзаиморасчетов   = Родитель.ВалютаВзаиморасчетов;
		ТипЦен	               = Родитель.ТипЦен;
		Скидка                 = Родитель.Скидка;
		ГлубинаКредита         = Родитель.ГлубинаКредита;
		СуммаКредита           = Родитель.СуммаКредита;
	КонецЕсли;
	    
	Если ПустоеЗначение(ВалютаВзаиморасчетов) = 1 Тогда
		ВалютаВзаиморасчетов = глЗначениеПоУмолчанию("ОсновнаяВалютаВзаиморасчетов");
	КонецЕсли;
	
	Если ПустоеЗначение(ВалютаВзаиморасчетов) = 1 Тогда
		ВалютаВзаиморасчетов = глРубли;
	КонецЕсли;
КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	Если НазваниеНабораПрав()="Продавец_розница" Тогда
		сообщить("Вы не можете открывать данный элемент!");
		СтатусВозврата(0);
		форма.Закрыть(0);
		Возврат;         
	КонецЕсли;

	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнОчиститьСкидку.Доступность(0);
		
		Форма.кнЗаписать.Доступность(0);
		Форма.кнОК.Доступность(0);
		
		Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
	    Форма.КнопкаПоУмолчанию("кнОк");
	КонецЕсли;
	
	ПриИзмененииКонтроляКредита();
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога)
	
	Если ЭлементДиалога = "ВалютаВзаиморасчетов" Тогда
		Если Выбран() = 1 Тогда
			Объект = СоздатьОбъект("Документ");
			Объект.ВыбратьПоЗначению(,,"Контрагент", ТекущийЭлемент().Владелец);
			
			Пока Объект.ПолучитьДокумент() = 1 Цикл
				Если Объект.Договор = ТекущийЭлемент() Тогда
					Предупреждение("Договор """+Наименование+""" используется в документах.
					               |Изменять валюту взаиморасчетов нельзя!", 60);
					СтатусВозврата(0); Возврат;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗаписи()
	
	// Проверка заполненности обязательных реквизитов.
	
	// Наименование договора.
	Если ПустаяСтрока(Наименование) = 1 Тогда
		Предупреждение("Не указано наименование договора!", 60);
		Активизировать("Наименование");
		СтатусВозврата(0); Возврат;
		
	// Валюта взаиморасчетов по договору.
	ИначеЕсли ВалютаВзаиморасчетов.Выбран() = 0 Тогда
		Предупреждение("Не выбрана валюта взаиморасчетов по договору!", 60);
		Активизировать("ВалютаВзаиморасчетов");
		СтатусВозврата(0); Возврат;
	КонецЕсли;
	
	глОбновитьСписокДоговоров = 1;
	
КонецПроцедуры // ПриЗаписи()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 
