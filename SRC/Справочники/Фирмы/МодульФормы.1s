////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем ЮрАдрес, ФактАдрес, АдресИМНС; // Адреса юр. лица и ИМНС.
Перем ВиртРеквизитыПлательщикаИлиПолучателя;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// СформироватьРеквизит(РеквизитФормы, ЭлементДиалога)
//
// Параметры:
//  РеквизитФормы     - строка – значение реквизита в диалоге формы.
//  ЭлементДиалога    - строка – идентификатор редактируемго элемента диалога.
//
// Вызывается из формул элементов диалога:
//
// Описание:
// Процедура устанавливает доступность редактирования реквизита 
// "Получатель". Если реквизит недоступен, данные  
// поля заполняются стандартным представлением.
//
Процедура СформироватьРеквизит(РеквизитФормы, ЭлементДиалога)
	
	Если ПустаяСтрока(ВиртРеквизитыПлательщикаИлиПолучателя) = 1 Тогда

		//заполняем стандартным представлением		
		РеквизитФормы = "";

		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(СокрЛП(ИНН),"ИНН");
		Параметры.ДобавитьЗначение(?(ПустаяСтрока(ПолнНаименование)=1,НаименованиеЮрЛица,ПолнНаименование),"НаименованиеКонтрагента");
		Параметры.ДобавитьЗначение(СокрЛП(НомерСчета),"НомерСчета");
		Параметры.ДобавитьЗначение(Банк,"Банк");
		Параметры.ДобавитьЗначение(БанкДляРасчетов,"БанкДляРасчетов");

		глРеквизитыПлательщикаПолучателя(Параметры);

		РеквизитФормы = Параметры.Получить("Результат");
		
		Форма.ПолучитьАтрибут(ЭлементДиалога).Доступность(0);

	Иначе
		
		РеквизитФормы = ВиртРеквизитыПлательщикаИлиПолучателя;
		Форма.ПолучитьАтрибут(ЭлементДиалога).Доступность(1);
		
	КонецЕсли;
	
КонецПроцедуры // СформироватьРеквизит()

//******************************************************************************
// ПриИзмененииВалюты()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  ВалютаСчета.
//
// Описание:
//  Обновляет реквизиты диалога при изменении валюты счета.
//
Процедура ПриИзмененииВалюты()
	
	Если (ВалютаСчета = глРубли) ИЛИ (ВалютаСчета.Выбран() = 0) Тогда
		Форма.ТекстВидСчета.Заголовок("Вид счета (расчетный и т. д.):");
		ВидСчетаНомерРазрешения = ОсновнойСчет.ВидСчета;
	Иначе
		Форма.ТекстВидСчета.Заголовок("Номер и дата разрешения ЦБ:");
		ВидСчетаНомерРазрешения = ОсновнойСчет.НомерИДатаРазрешения;
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииВалюты()

//******************************************************************************
// ПриИзмененииОснСчета()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Обновляет реквизиты диалога при изменении основного счета.
//
Процедура ПриИзмененииОснСчета()
	
	// Проверка соответсвия основного счета юр. лицу.
	Если ОсновнойСчет.ЮрФизЛицо <> ЮрЛицо Тогда
		ОсновнойСчет = ПолучитьПустоеЗначение("Справочник.БанковскиеСчета");
	КонецЕсли;

	// Заполнение реквизитов слоя "ОсновнойСчет".
	НаименованиеОснСчета = ОсновнойСчет.Наименование;
	Банк                 = ОсновнойСчет.Банк;
	БанкДляРасчетов      = ОсновнойСчет.БанкДляРасчетов;
	НомерСчета           = ОсновнойСчет.НомерСчета;
	ВалютаСчета          = ОсновнойСчет.ВалютаСчета;
	ДатаОткрытияСчета    = ОсновнойСчет.ДатаОткрытияСчета;
	ДатаЗакрытияСчета    = ОсновнойСчет.ДатаЗакрытияСчета;
	СуммаБез00Копеек     = ОсновнойСчет.СуммаБез00Копеек;
	МесяцПрописью        = ОсновнойСчет.МесяцПрописью;
	ВиртРеквизитыПлательщикаИлиПолучателя = ОсновнойСчет.РеквизитыПлательщикаИлиПолучателя;
 	
	СформироватьРеквизит(ПлательщикПолучатель, "ПлательщикПолучатель");
 
	ПриИзмененииВалюты();
	
КонецПроцедуры // ПриИзмененииОснСчета()

//******************************************************************************
// ПриИзмененииТекущейДаты()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Обновляет периодические реквизиты диалога.
//
Процедура ПриИзмененииТекущейДаты()
	
	ТекДата = ИспользоватьДату();
	
	Форма.Заголовок("("+ТекДата+")", 1);
	
	// Заполнение реквизитов слоя "ДолжностныеЛица".
	Руководитель                = ЮрЛицо.Руководитель.Получить(ТекДата);
	ГлБухгалтер                 = ЮрЛицо.ГлБухгалтер.Получить(ТекДата);
	Кассир                      = ЮрЛицо.Кассир.Получить(ТекДата);
	
	// Заполнение реквизитов слоя "УчетнаяПолитика".
	МетодыОпределенияВыручки1   = ЮрЛицо.МетодОпределенияВыручки.Получить(ТекДата).ПорядковыйНомер();
	МетодыРасчетаСебестоимости1 = ЮрЛицо.МетодРасчетаСебестоимости.Получить(ТекДата).ПорядковыйНомер();
	МетодыУчетаРозницы1 		= ЮрЛицо.МетодУчетаРозницы.ПорядковыйНомер();
	СтавкаНПсАванса             = ЮрЛицо.СтавкаНПсАванса.Получить(ТекДата);
	
КонецПроцедуры // ПриИзмененииТекущейДаты()

//******************************************************************************
// ПриИзмененииЮрЛица()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Обновляет реквизиты диалога при изменении юридического лица.
//
Процедура ПриИзмененииЮрЛица()
	
	// Строки неограниченной длины надо очищать тщательнее!
	
	// Заполнение реквизитов слоя "ЮрЛицо".
	НаименованиеЮрЛица       = ЮрЛицо.Наименование;
	ПрефиксНомеровДокументов = ЮрЛицо.ПрефиксНомеровДокументов;
	ПолнНаименование         = ЮрЛицо.ТекущийЭлемент().ПолнНаименование;
	ИНН                      = ЮрЛицо.ИНН;
	Телефоны                 = ЮрЛицо.ТекущийЭлемент().Телефоны;
	ЮрАдрес                  = ЮрЛицо.ТекущийЭлемент().ЮрАдрес;
	ФактАдрес                = ЮрЛицо.ТекущийЭлемент().ФактАдрес;
	
	// Заполнение реквизитов слоя "ЮрЛицоДоп".
	ФормаСобственности       = ЮрЛицо.ТекущийЭлемент().ФормаСобственности;
	ДатаРегистрации          = ЮрЛицо.ДатаРегистрации;
	ОргПравФорма             = ЮрЛицо.ТекущийЭлемент().ОргПравФорма;
	ОфициальноеНаименование  = ЮрЛицо.ТекущийЭлемент().ОфициальноеНаименование;
	ОКПО                     = ЮрЛицо.ОКПО;
	ОКДП                     = ЮрЛицо.ОКДП;
	ОКОНХ                    = ЮрЛицо.ОКОНХ;
	ОКОПФ                    = ЮрЛицо.ОКОПФ;
	ОКФС                     = ЮрЛицо.ОКФС;
	ОсновнойВидДеятельности  = ЮрЛицо.ТекущийЭлемент().ОсновнойВидДеятельности;
	НомерЛистаКассовойКниги  = ЮрЛицо.НомерЛистаКассовойКниги.Получить(ИспользоватьДату());
	
	// Заполнение реквизитов слоя "ДолжностныеЛица".
	ДолжностьРуководителя    = ЮрЛицо.ТекущийЭлемент().ДолжностьРуководителя;
	НаименованиеИМНС         = ЮрЛицо.ТекущийЭлемент().НаименованиеИМНС;
	КодИМНС                  = ЮрЛицо.КодИМНС;
	АдресИМНС                = ЮрЛицо.ТекущийЭлемент().АдресИМНС;
	
	// Заполнение слоя "УчетнаяПолитика"
	УчитыватьНДС             = ЮрЛицо.УчитыватьНДС;
	УчитыватьНП              = ЮрЛицо.УчитыватьНП;
	
	ПриИзмененииТекущейДаты();
	
	// Проверка соответсвия основного счета юр. лицу.
	Если ОсновнойСчет.ЮрФизЛицо <> ЮрЛицо Тогда
		ПриИзмененииОснСчета();
	КонецЕсли;

КонецПроцедуры // ПриИзмененииЮрЛица()

//******************************************************************************
// ПриИзмененииУпрАналитики()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Обновляет реквизиты диалога при изменении упр. аналитики.
//
Процедура ПриИзмененииУпрАналитики()
	
	// Заполнение реквизитов слоя "УпрАналитика".
	НаименованиеУпрАналитики = УпрАналитика.Наименование;

КонецПроцедуры // ПриИзмененииУпрАналитики()

//******************************************************************************
// НужноЗаписатьЭлемент(Объект)
//
// Параметры:
//  Объект - объект, созданный методом "СоздатьОбъект()".
//
// Возвращаемое значение:
//  1 - объект нужно записывать.
//  0 - объект не нужно записывать.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Проверяет необходимость записи объекта.
//
Функция НужноЗаписатьЭлемент(Объект)
	
	// Объект может записываться впервые.
	Если ПустоеЗначение(Объект.ТекущийЭлемент()) = 1 Тогда
		Возврат 1;
	КонецЕсли;
	
	// Текущее значение атрибута может отличаеттся от записанного.
	Если Объект.Наименование <> Объект.ТекущийЭлемент().Наименование Тогда
		Возврат 1;
	КонецЕсли;
	
	// Проверка значений реквизитов объекта.
	мдСправочник = Метаданные.Справочник(Объект.Вид());
	Для Номер = 1 По мдСправочник.Реквизит() Цикл
		
		// Периодические реквизиты не сравниваются.
		Если мдСправочник.Реквизит(Номер).Периодический = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		Ид = мдСправочник.Реквизит(Номер).Идентификатор;
		// Текущее значение атрибута может отличаеттся от записанного.
		Если Объект.ПолучитьАтрибут(Ид) <> Объект.ТекущийЭлемент().ПолучитьАтрибут(Ид) Тогда
			Возврат 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции // НужноЗаписатьЭлемент()

//******************************************************************************
// МожноЗаписатьЭлемент(Объект)
//
// Параметры:
//  Объект - объект, созданный методом "СоздатьОбъект()".
//
// Возвращаемое значение:
//  1 - объект можно записывать.
//  0 - объект нельзя записывать.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Проверяет возможность записи объекта.
//
Функция МожноЗаписатьЭлемент(Объект)
	
	Если      Объект.Выбран() = 0 Тогда
		Возврат 1;

	ИначеЕсли Объект.Блокировка(1) = 1 Тогда
		Возврат 1;
	
	Иначе
		Элемент = Объект.ТекущийЭлемент();

		Стр = 
		"Элемент справочника """+Элемент.ПредставлениеВида()+""": ("+
		Элемент.Код+") """+Элемент.Наименование+""" заблокирован!";
		Предупреждение(Стр, 60);
		
		Возврат 0;
	КонецЕсли;
	
КонецФункции // МожноЗаписатьЭлемент()

//******************************************************************************
// ЗаписьЮрЛица()
//
// Параметры:
//
// Возвращаемое значение:
//  1 - запись произведена.
//  0 - запись не произведена.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Осуществляет запись юр. лица.
//
Функция ЗаписьЮрЛица()
	
	// Объект для записи юр. лица.
	Объект = СоздатьОбъект("Справочник.СвоиЮрЛица");
	Если Объект.НайтиЭлемент(ЮрЛицо) = 0 Тогда
		Объект.Новый();
	КонецЕсли;
	
	// Заполнение реквизитов слоя "ЮрЛицо".
	Объект.Наименование             = НаименованиеЮрЛица;
	Объект.ПрефиксНомеровДокументов = ПрефиксНомеровДокументов;
	Объект.ПолнНаименование         = ПолнНаименование;
	Объект.ИНН                      = ИНН;
	Объект.Телефоны                 = Телефоны;
	Объект.ЮрАдрес                  = ЮрАдрес;
	Объект.ФактАдрес                = ФактАдрес;
	
	// Заполнение реквизитов слоя "ЮрЛицоДоп".
	Объект.ФормаСобственности       = ФормаСобственности;
	Объект.ДатаРегистрации          = ДатаРегистрации;
	Объект.ОргПравФорма             = ОргПравФорма;
	Объект.ОфициальноеНаименование  = ОфициальноеНаименование;
	Объект.ОКПО                     = ОКПО;
	Объект.ОКДП                     = ОКДП;
	Объект.ОКОНХ                    = ОКОНХ;
	Объект.ОКОПФ                    = ОКОПФ;
	Объект.ОКФС                     = ОКФС;
	Объект.ОсновнойВидДеятельности  = ОсновнойВидДеятельности;
	
	// Заполнение реквизитов слоя "ДолжностныеЛица".
	Объект.ДолжностьРуководителя    = ДолжностьРуководителя;
	Объект.НаименованиеИМНС         = НаименованиеИМНС;
	Объект.КодИМНС                  = КодИМНС;
	Объект.АдресИМНС                = АдресИМНС;
	
	// Заполнение реквизитов слоя "УчетнаяПолитика".
	Объект.УчитыватьНДС             = УчитыватьНДС;
	Объект.УчитыватьНП              = УчитыватьНП;
	Объект.МетодУчетаРозницы = Перечисление.МетодыУчетаРозницы.ЗначениеПоНомеру(МетодыУчетаРозницы1);
	
	// Проверка возможности записи.
	// Имеются периодические реквизиты, 
	// поэтому элемент записывается всегда.
	Если МожноЗаписатьЭлемент(Объект) = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	// Обработке ЗаписьПериодическихРеквизитов необходимо передать
	// значения периодических реквизитов, введенные в форме диалога.
	Список = СоздатьОбъект("СписокЗначений");
	
	// Заполнение реквизитов слоя "ДолжностныеЛица".
	Список.Установить("Руководитель", Руководитель);
	Список.Установить("ГлБухгалтер",  ГлБухгалтер);
	Список.Установить("Кассир",       Кассир);
	
	// Заполнение реквизитов слоя "УчетнаяПолитика".
	Список.Установить("МетодОпределенияВыручки"		, Перечисление.МетодыОпределенияВыручки.ЗначениеПоНомеру(МетодыОпределенияВыручки1));
	Список.Установить("МетодРасчетаСебестоимости"	, Перечисление.МетодыРасчетаСебестоимости.ЗначениеПоНомеру(МетодыРасчетаСебестоимости1));
	Список.Установить("СтавкаНПсАванса"				, СтавкаНПсАванса);
	
	Список.Установить("НомерЛистаКассовойКниги"		, НомерЛистаКассовойКниги);
	
	// При записи вызывается обработка ЗаписьПериодическихРеквизитов 
	// для управления записью значений периодических реквизитов.
	Если глЗаписьПериодическихРеквизитов(Объект, Список, ИспользоватьДату(), '01.01.1980') = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	// Установка юридического лица фирмы.
	ЮрЛицо = Объект.ТекущийЭлемент();
	
	Возврат 1;
	
КонецФункции // ЗаписьЮрЛица()

//******************************************************************************
// ЗаписьОснСчета()
//
// Параметры:
//
// Возвращаемое значение:
//  1 - запись произведена.
//  0 - запись не произведена.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Осуществляет запись основного счета.
//
Функция ЗаписьОснСчета()
	
	// Счет записывается, если указан номер.
	Если (ОсновнойСчет.Выбран() = 0) И (ПустаяСтрока(НомерСчета) = 1) Тогда
		Возврат 1;
	КонецЕсли;
	
	// Объект для записи основного счета.
	Объект = СоздатьОбъект("Справочник.БанковскиеСчета");
	
	Если Объект.НайтиЭлемент(ОсновнойСчет) = 0 Тогда
		Объект.Новый();
	КонецЕсли;
		
	// Заполнение реквизитов слоя "ОсновнойСчет".
	Объект.Наименование      = НаименованиеОснСчета;
	Объект.ЮрФизЛицо         = ЮрЛицо;
	Объект.Банк              = Банк;
	Объект.БанкДляРасчетов   = БанкДляРасчетов;
	Объект.НомерСчета        = НомерСчета;
	Объект.ВалютаСчета       = ВалютаСчета;
	Объект.ДатаОткрытияСчета = ДатаОткрытияСчета;
	Объект.ДатаЗакрытияСчета = ДатаЗакрытияСчета;
	Объект.СуммаБез00Копеек  = СуммаБез00Копеек;
	Объект.МесяцПрописью     = МесяцПрописью;
	Объект.РеквизитыПлательщикаИлиПолучателя = ВиртРеквизитыПлательщикаИлиПолучателя;
	
	Если ВалютаСчета = глРубли Тогда
		Объект.ВидСчета             = ВидСчетаНомерРазрешения;
		Объект.НомерИДатаРазрешения = "";
	Иначе
		Объект.ВидСчета             = "";
		Объект.НомерИДатаРазрешения = ВидСчетаНомерРазрешения;
	КонецЕсли;
	
	// Проверка необходимости и возможности записи.
	Если      НужноЗаписатьЭлемент(Объект) = 0 Тогда
		Возврат 1;
	ИначеЕсли МожноЗаписатьЭлемент(Объект) = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Объект.Записать();
	
	// Установка основного счета.
	ОсновнойСчет = Объект.ТекущийЭлемент();
	
	Возврат 1;
	
КонецФункции // ЗаписьОснСчета()

//******************************************************************************
// ЗаписьУпрАналитики()
//
// Параметры:
//
// Возвращаемое значение:
//  1 - запись произведена.
//  0 - запись не произведена.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Осуществляет запись упр. аналитики.
//
Функция ЗаписьУпрАналитики()
	
	// Объект для записи упр. аналитики.
	Объект = СоздатьОбъект("Справочник.УпрАналитика");
	Если Объект.НайтиЭлемент(УпрАналитика) = 0 Тогда
		Объект.Новый();
	КонецЕсли;
	
	// Заполнение реквизитов слоя "УпрАнлитика".
	Объект.Наименование = НаименованиеУпрАналитики;
	
	// Проверка необходимости и возможности записи.
	Если      НужноЗаписатьЭлемент(Объект) = 0 Тогда
		Возврат 1;
	ИначеЕсли МожноЗаписатьЭлемент(Объект) = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	// Запись объекта.
	Объект.Записать();
	
	// Установка упр. аналитики.
	УпрАналитика = Объект.ТекущийЭлемент();
	
	Возврат 1;
	
КонецФункции // ЗаписьУпрАналитики()

//******************************************************************************
// Автозаполнение()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  НаименованиеЮрЛица, ОргПравФорма, НаименованиеУпрАналитики.
//
// Описание:
//  Автозаполнение наименований.
//
Процедура Автозаполнение()
	
	Если Форма.АктивныйЭлемент() = "НаименованиеЮрЛица" Тогда
		Если ПустаяСтрока(ПолнНаименование) = 1 Тогда
			ПолнНаименование = " """+СокрЛП(НаименованиеЮрЛица)+"""";
		КонецЕсли;
		
	ИначеЕсли Форма.АктивныйЭлемент() = "ОргПравФорма" Тогда
		Если ПустаяСтрока(ОфициальноеНаименование) = 1 Тогда
			ОфициальноеНаименование = СокрЛП(ОргПравФорма)+" """+СокрЛП(НаименованиеЮрЛица)+"""";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // Автозаполнение()

//******************************************************************************
// ПриИзмененииИНН()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  ИНН.
//
// Описание:
//  Проверяет уникальность ИНН в справочнике юр. лиц.
//
Процедура ПриИзмененииИНН()
	
	// ИНН может быть не указан.	
	Длина = Метаданные.Справочник("СвоиЮрЛица").Реквизит("ИНН").Длина;	
	Если СтрДлина(СокрЛП(ИНН)) < Длина Тогда
		Возврат;
	КонецЕсли;
	
	// Список найденных элементов с таким же ИНН.
	Список = СоздатьОбъект("СписокЗначений");
	
	Объект = СоздатьОбъект("Справочник.СвоиЮрЛица");
	Объект.ВыбратьЭлементыПоРеквизиту("ИНН", ИНН, 0, 1);
	
	// Поиск элементов с таким же ИНН.
	Пока Объект.ПолучитьЭлемент(1) = 1 Цикл
		Если Объект.ТекущийЭлемент() = ЮрЛицо Тогда
			Возврат; // элемент уже был записан
		Иначе
			Список.ДобавитьЗначение(Объект.ТекущийЭлемент());
		КонецЕсли;
	КонецЦикла;
	
	// Элементы с таким же ИНН не найдены.
	Если Список.РазмерСписка() = 0 Тогда
		Возврат;
	
	// Найден один элемент с таким же ИНН.
	ИначеЕсли Список.РазмерСписка() = 1 Тогда
		Стр = "В справочнике """+Объект.ПредставлениеВида()+""" уже есть элемент с таким же ИНН!
		      |Выбрать существующий элемент справочника?";
		
		Если Вопрос(Стр, "Да+Нет", 60) = "Да" Тогда
			ЮрЛицо = Список.ПолучитьЗначение(1);
			ПриИзмененииЮрЛица();
		КонецЕсли;
		
	// Найдено несколько элементов с таким же ИНН.
	Иначе
		Стр = "В справочнике """+Объект.ПредставлениеВида()+""" уже есть элементы с таким же ИНН!
		      |Выбрать один из существующих элементов справочника?";
		
		Если Вопрос(Стр, "Да+Нет", 60) = "Да" Тогда
			Если Список.ВыбратьЗначение(ЮрЛицо, "Элементы с ИНН "+ИНН, 0, 60, 0) = 1 Тогда
				ПриИзмененииЮрЛица();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииИНН()

//******************************************************************************
// ПриИзмененииНомераСчета()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  НомерСчета.
//
// Описание:
//  Проверяет уникальность номера банковского счета.
//
Процедура ПриИзмененииНомераСчета()
	
	СформироватьРеквизит(ПлательщикПолучатель, "ПлательщикПолучатель");
	
	// Номер счета может быть не указан.	
	Если СтрДлина(СокрЛП(НомерСчета)) < 20 Тогда
		Возврат;
	КонецЕсли;
	
	// Список найденных элементов с таким же номером счета.
	Список = СоздатьОбъект("СписокЗначений");
	
	Объект = СоздатьОбъект("Справочник.БанковскиеСчета");
	Объект.ВыбратьЭлементыПоРеквизиту("ЮрФизЛицо", ЮрЛицо, 0, 1);
	
	// Поиск элементов с таким же номером счета.
	Пока Объект.ПолучитьЭлемент(1) = 1 Цикл
		Если Объект.ТекущийЭлемент() = ОсновнойСчет Тогда
			Возврат; // элемент уже был записан
		ИначеЕсли Объект.НомерСчета = НомерСчета Тогда
			Список.ДобавитьЗначение(Объект.ТекущийЭлемент());
		КонецЕсли;
	КонецЦикла;
	
	// Элементы с таким же номером счета не найдены.
	Если Список.РазмерСписка() = 0 Тогда
		Возврат;
	
	// Найден один элемент с таким же номером счета.
	ИначеЕсли Список.РазмерСписка() = 1 Тогда
		Стр = "В справочнике """+Объект.ПредставлениеВида()+
		      """ уже есть элемент с таким же номером счета!
		      |Выбрать существующий элемент справочника?";
		
		Если Вопрос(Стр, "Да+Нет", 60) = "Да" Тогда
			ОсновнойСчет = Список.ПолучитьЗначение(1);
			ПриИзмененииОснСчета();
		КонецЕсли;
		
	// Найдено несколько элементов с таким же номером счета.
	Иначе
		Стр = "В справочнике """+Объект.ПредставлениеВида()+
		      """ уже есть элементы с таким же номером счета!
		      |Выбрать один из существующих элементов справочника?";
		
		Если Вопрос(Стр, "Да+Нет", 60) = "Да" Тогда
			Если Список.ВыбратьЗначение(ОсновнойСчет, "Элементы с номером счета "+НомерСчета, 0, 60, 0) = 1 Тогда
				ПриИзмененииОснСчета();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииНомераСчета()

//******************************************************************************
// ПоКнопкеНаименованиеФирмы()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  кнНаименованиеФирмы.
//
// Описание:
//  Формирует наименование фирмы.
//
Процедура ПоКнопкеНаименованиеФирмы()
	
	Если ПустаяСтрока(НаименованиеЮрЛица) = 0 Тогда
		Наименование = СокрЛП(НаименованиеЮрЛица);
	Иначе
		Наименование = "Не указано юр. лицо";
	КонецЕсли;
	
	Если ПустаяСтрока(НаименованиеУпрАналитики) = 0 Тогда
		Наименование = Наименование+" ("+СокрЛП(НаименованиеУпрАналитики)+")";
	Иначе
		Наименование = Наименование+" (не указана упр. аналитика)";
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеНаименованиеФирмы()

//******************************************************************************
// ПоКнопкеЗаполнитьОсновнойСчет()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка кнЗаполнитьОсновнойСчет.
//
// Описание:
//
Процедура ПоКнопкеЗаполнитьОсновнойСчет()

	Параметры = СоздатьОбъект("СписокЗначений");
	Параметры.ДобавитьЗначение(СокрЛП(ИНН),"ИНН");
	Параметры.ДобавитьЗначение(?(ПустаяСтрока(ПолнНаименование)=1,НаименованиеЮрЛица,ПолнНаименование),"НаименованиеКонтрагента");
	Параметры.ДобавитьЗначение(СокрЛП(НомерСчета),"НомерСчета");
	Параметры.ДобавитьЗначение(Банк,"Банк");
	Параметры.ДобавитьЗначение(БанкДляРасчетов,"БанкДляРасчетов");
	// 1-заполнение плательщика или получателя, 2-заполнение назначения платежа
	Параметры.ДобавитьЗначение(1,"Режим");
	ОткрытьФормуМодально("Обработка.ВводПлатежныхРеквизитов", Параметры);
	ВиртРеквизитыПлательщикаИлиПолучателя = ?(Параметры.Получить("Результат")="Авто","",Параметры.Получить("Результат"));
	
	СформироватьРеквизит(ПлательщикПолучатель, "ПлательщикПолучатель");

КонецПроцедуры // ПоКнопкеЗаполнитьОсновнойСчет()

//******************************************************************************
// ПоКнопкеОчистить()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  кнОчиститьЮрЛицо, кнОчиститьОснСчет, кнОчиститьУпрАналитику.
//
// Описание:
//  Очищает реквизиты фирмы.
//
Процедура ПоКнопкеОчистить()
	
	Если Форма.АктивныйЭлемент() = "кнОчиститьЮрЛицо" Тогда
		ЮрЛицо = ПолучитьПустоеЗначение("Справочник.СвоиЮрЛица");
		ПриИзмененииЮрЛица();
		
		Активизировать("НаименованиеЮрЛица");
		
	ИначеЕсли Форма.АктивныйЭлемент() = "кнОчиститьОснСчет" Тогда
		ОсновнойСчет = ПолучитьПустоеЗначение("Справочник.БанковскиеСчета");
		ПриИзмененииОснСчета();
		
		Активизировать("НаименованиеОснСчета"); 
		
	ИначеЕсли Форма.АктивныйЭлемент() = "кнОчиститьУпрАналитику" Тогда
		УпрАналитика = ПолучитьПустоеЗначение("Справочник.УпрАналитика");
		ПриИзмененииУпрАналитики();
		
		Активизировать("НаименованиеУпрАналитики"); 
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеОчистить()

//******************************************************************************
// ПоКнопкеВводаАдреса(Адрес1, Адрес2+"")
//
// Параметры:
//  Адрес1 - реквизит, в который будет введен адрес.
//  Адрес2 - реквизит, в который будет скопирован адрес.
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопки ввода адреса.
//
// Описание:
//  Вызывает обработку для ввода адреса.
//
Процедура ПоКнопкеВводаАдреса(Адрес1, Адрес2="")

	глВводАдреса(Адрес1);
	
	Если ПустаяСтрока(глПредставлениеАдреса(Адрес2)) = 1 Тогда
		Адрес2 = Адрес1;
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеВводаАдреса()

//******************************************************************************
// ПоКнопкеОткрыть(Объект)
//
// Параметры:
//  Объект - объект, который необходимо открыть.
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопки "Открыть".
//
// Описание:
//  Открывает форму объекта.
//
Процедура ПоКнопкеОткрыть(Объект, ЭлементДиалога="")
	
	Если Объект.Выбран() = 1 Тогда
		ОткрытьФормуМодально(Объект, , -1);
	Иначе
		Активизировать(ЭлементДиалога, 1);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеОткрыть()

//******************************************************************************
// ПоКнопкеВыбораДаты()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка выбора даты.
//
// Описание:
//  Выбирается дата просмотра и редактирования значений периодических реквизитов 
// выбирается по кнопке, чтобы не менялся признак модифицированности формы.
//
Процедура ПоКнопкеВыбораДаты()
	
	Если глВвестиДатуПериодическихРеквизитов(Контекст, 0) = 1 Тогда
		ПриИзмененииТекущейДаты();
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеВыбораДаты()

//******************************************************************************
// ПоКнопкеИстория()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "История".
//
// Описание:
//  Вызывается обработка "ЗначенияПериодическихРеквизитов" для просмотра истории 
// значений периодических реквизитов.
//
Процедура ПоКнопкеИстория()
	
	Если      Форма.Закладки.ТекущаяСтрока() = 3 Тогда // слой "ДолжностныеЛица"
		ВидимыеРеквизиты = "Руководитель, ГлБухгалтер, Кассир";
		
	ИначеЕсли Форма.Закладки.ТекущаяСтрока() = 4 Тогда // слой "УчетнаяПолитика"
		ВидимыеРеквизиты = "МетодОпределенияВыручки, МетодРасчетаСебестоимости, СтавкаНПсАванса";

	Иначе
		ВидимыеРеквизиты = "";
	КонецЕсли;
	
	Если ПустоеЗначение(ЮрЛицо.ТекущийЭлемент()) = 0 Тогда
		глЗначенияПериодическихРеквизитов(ЮрЛицо, , , ВидимыеРеквизиты);
	Иначе
		СтрокаДействийФормы = "# Записать?";
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеИстория()

//******************************************************************************
// ПоКнопкеОткрытьСправочник(Справочник, Знач Параметр, Знач Объект=0)
//
// Параметры:
//  Справочник - "строка" - вид справочника.
//  Пераметр   - параметр для передачи в форму.
//  Объект     - объект, который следует активизировать в форме.
//
// Возвращаемое значение:
//  В немодальном режиме - контекст открытой формы.
//
// Вызывается из формул элементов диалога:
//  Кнопки открытия подчиненных справочников.
//
// Описание:
//  Открывает форму списка указанного справочника.
//
Функция ПоКнопкеОткрытьСправочник(Справочник, Знач Параметр, Знач Объект=0)
	
	Если ПустоеЗначение(Параметр) = 1 Тогда
		Предупреждение("Элемент еще не записан. Справочник не может быть открыт!", 60);
		Возврат ПолучитьПустоеЗначение();
		
	Иначе
		ОткрытьФорму("Справочник."+Справочник, Параметр);
	КонецЕсли;
		
	Если (ТипЗначенияСтр(Параметр) = "ГрупповойКонтекст") И (ПустоеЗначение(Объект) = 0) Тогда
		Параметр.АктивизироватьОбъект(Объект);
	КонецЕсли;
	
	Возврат Параметр;
	
КонецФункции // ПоКнопкеОткрытьСправочник()

//******************************************************************************
// УстановитьЗаголовкиПеречисления(Идентификатор)
//
// Параметры:
//  Идентификатор - "МетодыОпределенияВыручки" 
//                  "МетодыРасчетаСебестоимости"
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Устанавливает заголовки элементов диалога.
//
Процедура УстановитьЗаголовкиПеречисления(Идентификатор)
	
	Для Номер = 1 По Метаданные.Перечисление(Идентификатор).Значение() Цикл
		мдЗначение  = Метаданные.Перечисление(Идентификатор).Значение(Номер);
		Заголовок   = ?(ПустаяСтрока(мдЗначение.Комментарий) = 0, " ("+Нрег(мдЗначение.Комментарий)+")", "");
		
		Форма.ПолучитьАтрибут(Идентификатор+Номер).Заголовок(мдЗначение.Представление+Заголовок); 
	КонецЦикла;
	
КонецПроцедуры // УстановитьЗаголовкиПеречисления()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ВводНового(ФлагКопирования)
	
	МетодыОпределенияВыручки1   = Перечисление.МетодыОпределенияВыручки.ПоОтгрузке.ПорядковыйНомер();
	МетодыРасчетаСебестоимости1 = Перечисление.МетодыРасчетаСебестоимости.FIFO.ПорядковыйНомер();
	МетодыУчетаРозницы1 		= Перечисление.МетодыУчетаРозницы.ПоПродажнымЦенам.ПорядковыйНомер();
	
	УчитыватьНДС                = 1;
	УчитыватьНП                 = 0;
	
	ВалютаСчета = глРубли;
	
КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнНаименованиеФирмы.Доступность(0);
		Форма.кнОчиститьЮрЛицо.Доступность(0);
		Форма.кнВводаЮрАдреса.Доступность(0);
		Форма.кнВводаФактАдреса.Доступность(0);
		Форма.кнВводаАдресаИМНС.Доступность(0);
		
		Форма.кнОчиститьОснСчет.Доступность(0);
		Форма.кнОчиститьБанкДляРасчетов.Доступность(0);
		
		Форма.кнОчиститьУпрАналитику.Доступность(0);
		
		Форма.кнЗаписать.Доступность(0);
		Форма.кнОК.Доступность(0);
		
		Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
	    Форма.КнопкаПоУмолчанию("кнОк");
	КонецЕсли;
	
	// Заполнение реквизитов диалога.
	// Если реквизиты справочника не заполнены - в диалоге 
	// сохранятся значения, присвоенные в процедуре ВводНового().
	
	Если ЮрЛицо.Выбран() = 1 Тогда
		ПриИзмененииЮрЛица();
	КонецЕсли;
	
	Если УпрАналитика.Выбран() = 1 Тогда
		ПриИзмененииУпрАналитики();
	КонецЕсли;
	
	Если ОсновнойСчет.Выбран() = 1 Тогда
		ПриИзмененииОснСчета();
	КонецЕсли;
	
	Закладка = 0;
	Если ПустоеЗначение(Форма.Параметр) = 0  Тогда // могли передать нужную закладку
		Закладка = Форма.Закладки.Получить(Форма.Параметр);
	КонецЕсли; 
	Закладка = Макс(1, Форма.Закладки.НайтиЗначение(Закладка));
	Форма.Закладки.ТекущаяСтрока(Закладка);
	Форма.ИспользоватьСлой(Форма.Закладки.ПолучитьЗначение(Закладка), 2);
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриВыбореЗакладки(НомерЗакладки, ЗначениеЗакладки)
	
	Форма.ИспользоватьСлой(ЗначениеЗакладки, 2);
	Если Найти(ЗначениеЗакладки,"ОсновнойСчет")<>0 Тогда
		СформироватьРеквизит(ПлательщикПолучатель, "ПлательщикПолучатель");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореЗакладки()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога)
	
	Если ЭлементДиалога = "НаименованиеЮрЛица" Тогда
		Параметр = ПоКнопкеОткрытьСправочник("СвоиЮрЛица", 1, ЮрЛицо);
		Если ТипЗначенияСтр(Параметр) = "Справочник" Тогда // модальный режим
			ЮрЛицо = Параметр;
			ПриИзмененииЮрЛица();
		КонецЕсли;
		
	ИначеЕсли ЭлементДиалога = "НаименованиеОснСчета" Тогда
		Параметр = ПоКнопкеОткрытьСправочник("БанковскиеСчета", ЮрЛицо, ОсновнойСчет);
		Если ТипЗначенияСтр(Параметр) = "Справочник" Тогда
			ОсновнойСчет = Параметр;
			ПриИзмененииОснСчета();
		КонецЕсли;
			
	ИначеЕсли ЭлементДиалога = "ВалютаСчета" Тогда
		Если ОсновнойСчет.Выбран() = 1 Тогда
			Предупреждение("Нельзя изменять валюту банковского счета.
			               |Введите новый счет или скопируйте существующий!", 60);
			СтатусВозврата(0);
		КонецЕсли;
		
	ИначеЕсли ЭлементДиалога = "НаименованиеУпрАналитики" Тогда
		Параметр = ПоКнопкеОткрытьСправочник("УпрАналитика", 1, УпрАналитика);
		Если ТипЗначенияСтр(Параметр) = "Справочник" Тогда // модальный режим
			УпрАналитика = Параметр;
			ПриИзмененииУпрАналитики();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаВыбораЗначения(НовоеЗначение, ЭлементДиалога)
	
	Если ЭлементДиалога = "НаименованиеЮрЛица" Тогда
		ЮрЛицо = НовоеЗначение;
		ПриИзмененииЮрЛица();
		
	ИначеЕсли ЭлементДиалога = "НаименованиеОснСчета" Тогда
		ОсновнойСчет = НовоеЗначение;
		ПриИзмененииОснСчета();
		
	ИначеЕсли ЭлементДиалога = "НаименованиеУпрАналитики" Тогда
		УпрАналитика = НовоеЗначение;
		ПриИзмененииУпрАналитики();
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаВыбораЗначения()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗаписи()
	
	// Обновление текстовых полей
	СформироватьРеквизит(ПлательщикПолучатель, "ПлательщикПолучатель");

	// Проверка заполненности обязательных реквизитов.
	
	// Наименование фирмы.
	Если ПустаяСтрока(Наименование) = 1 Тогда
		Предупреждение("Не указано наименование фирмы!", 60);
		Активизировать("Наименование");
		СтатусВозврата(0); Возврат;
	КонецЕсли;
		
	// Наименование юр. лица.
	Если ПустаяСтрока(НаименованиеЮрЛица) = 1 Тогда
		Предупреждение("Не указано наименование юридического лица фирмы!", 60);
		Форма.Закладки.ТекущаяСтрока(1);
		Форма.ИспользоватьСлой(Форма.Закладки.ПолучитьЗначение(1));
		Активизировать("НаименованиеЮрЛица");
		СтатусВозврата(0); Возврат;
	КонецЕсли;
	
	// Метод определения выручки юр. лица.
	Если МетодыОпределенияВыручки1 = 0 Тогда
		Предупреждение("Не выбран метод определения выручки юридического лица!", 60);
		Форма.Закладки.ТекущаяСтрока(4);
		Форма.ИспользоватьСлой(Форма.Закладки.ПолучитьЗначение(4));
		Активизировать("МетодыОпределенияВыручки1");
		СтатусВозврата(0); Возврат;
	КонецЕсли;
	
	// Метод расчета себестоимости юр. лица.
	Если МетодыРасчетаСебестоимости1 = 0 Тогда
		Предупреждение("Не выбран метод расчета себестоимости юридического лица!", 60);
		Форма.Закладки.ТекущаяСтрока(4);
		Форма.ИспользоватьСлой(Форма.Закладки.ПолучитьЗначение(4));
		Активизировать("МетодыРасчетаСебестоимости1");
		СтатусВозврата(0); Возврат;
	КонецЕсли;     
	
	// Метод учета розницы юр. лица.
	Если МетодыРасчетаСебестоимости1 = 0 Тогда
		Предупреждение("Не выбран метод учета розницы юридического лица!", 60);
		Форма.Закладки.ТекущаяСтрока(4);
		Форма.ИспользоватьСлой(Форма.Закладки.ПолучитьЗначение(4));
		Активизировать("МетодыУчетаРозницы1");
		СтатусВозврата(0); Возврат;
	КонецЕсли;
	
	// Наименование упр. аналитики.
	Если ПустаяСтрока(НаименованиеУпрАналитики) = 1 Тогда
		Предупреждение("Не указано наименование объекта упр. аналитики!", 60);
		Форма.Закладки.ТекущаяСтрока(5);
		Форма.ИспользоватьСлой(Форма.Закладки.ПолучитьЗначение(5));
		Активизировать("НаименованиеУпрАналитики");
		СтатусВозврата(0); Возврат;
	КонецЕсли;
	
	Если (ОсновнойСчет.Выбран() = 1) ИЛИ 
	     (ПустаяСтрока(НомерСчета) = 0) ИЛИ 
		 (ПустаяСтрока(НаименованиеОснСчета) = 0) Тогда
		 	
		// Наименование основного банковского счета.
		Если ПустаяСтрока(НаименованиеОснСчета) = 1 Тогда
			Предупреждение("Не указано наименование основного банковского счета!", 60);
			Форма.Закладки.ТекущаяСтрока(6);
			Форма.ИспользоватьСлой(Форма.Закладки.ПолучитьЗначение(6));
			Активизировать("НаименованиеОснСчета");
			СтатусВозврата(0); Возврат;
		КонецЕсли;

		// Валюта основного банковского счета.
		Если ВалютаСчета.Выбран() = 0 Тогда
			Предупреждение("Не выбрана валюта основного банковского счета!", 60);
			Форма.Закладки.ТекущаяСтрока(6);
			Форма.ИспользоватьСлой(Форма.Закладки.ПолучитьЗначение(6));
			Активизировать("ВалютаСчета");
			СтатусВозврата(0); Возврат;
		КонецЕсли;

		// Банк основного банковского счета.
		Если Банк.Выбран() = 0 Тогда
			Предупреждение("Не выбран банк основного банковского счета!", 60);
			Форма.Закладки.ТекущаяСтрока(6);
			Форма.ИспользоватьСлой(Форма.Закладки.ПолучитьЗначение(6));
			Активизировать("Банк");
			СтатусВозврата(0); Возврат;
		КонецЕсли;

		// Номер банковского счета.
		Если глПроверкаНомераСчета(НомерСчета, Банк.Код, Банк.КоррСчет) = 0 Тогда
			Если Вопрос("Возможно, номер основного счета указан неверно. Продолжить?", "Да+Нет", 60) <> "Да" Тогда
				Форма.Закладки.ТекущаяСтрока(6);
				Форма.ИспользоватьСлой(Форма.Закладки.ПолучитьЗначение(6));
				Активизировать("НомерСчета");
				СтатусВозврата(0); Возврат;
			КонецЕсли;
		КонецЕсли;     
		
	КонецЕсли;
		
	// Запись всех изменений производится в одной транзакции.
	
	Попытка
		НачатьТранзакцию();
		СтатусВозврата(0);
		
		Записать();
		Если ЗаписьЮрЛица() = 1 Тогда
			Если ЗаписьОснСчета() = 1 Тогда
				Если ЗаписьУпрАналитики() = 1 Тогда
					
					ЗафиксироватьТранзакцию();
					СтатусВозврата(1);
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

	Исключение
		Сообщить(ОписаниеОшибки(), "!");
	КонецПопытки;
	
КонецПроцедуры // ПриЗаписи()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 
// Номер закладки используется в процедурах и функциях модуля!
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение("Фирма, Кнопки, ЮрЛицо",          "Основные");
Форма.Закладки.ДобавитьЗначение("Фирма, Кнопки, ЮрЛицоДоп",       "Дополнительные");
Форма.Закладки.ДобавитьЗначение("Фирма, Кнопки, ДолжностныеЛица", "Должн. лица, МНС");
Форма.Закладки.ДобавитьЗначение("Фирма, Кнопки, УчетнаяПолитика", "Учетная политика");
Форма.Закладки.ДобавитьЗначение("Фирма, Кнопки, УпрАналитика",    "Упр. аналитика");
Форма.Закладки.ДобавитьЗначение("Фирма, Кнопки, ОсновнойСчет",    "Банковский счет");
Форма.Закладки.ДобавитьЗначение("Фирма, Кнопки, Прочее",          "Прочее");

УстановитьЗаголовкиПеречисления("МетодыОпределенияВыручки");
УстановитьЗаголовкиПеречисления("МетодыРасчетаСебестоимости");
УстановитьЗаголовкиПеречисления("МетодыУчетаРозницы");
