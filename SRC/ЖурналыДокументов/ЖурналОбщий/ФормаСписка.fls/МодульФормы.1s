////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем СписокДействий;  // Список действий по документу
Перем СписокВидовДокументов;
Перем КонтрагентДляОтбора, ФирмаДляОтбора, ЮрЛицоДляОтбора, 
      СкладДляОтбора, ВидДокументаДляОтбора, АвторДляОтбора, ПроектДляОтбора; // Переменные для отбора
Перем ЗначениеВВидеСтроки;

Перем ДокументОснованиеЖурнала; // если пустое, то открыта обычная форма журнала,
                                // иначе текущий документ, которому подчинен журнал
           
								
								
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// ПолучитьВремяДокументаЧислом(Док)
//
// Параметры: 
//  Док - документ, чьё время надо получить
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Преобразует время документа в число.
//
Функция ПолучитьВремяДокументаЧислом(Док)
    Перем Ч,М,С;  
	Док.ПолучитьВремя(Ч,М,С);
	Возврат 3600*Ч+60*М+С;
КонецФункции // ПолучитьВремяДокументаЧислом()
               

Функция ПолучитьПиктограмму() 
	Если (ТекущийДокумент.Вид() = "ПеремещениеТМЦ") или (ТекущийДокумент.Вид() = "Реализация") Тогда
		Возврат ТекущийДокумент.ЕстьДокументы;
	Иначе
		Возврат 0;
	КонецЕсли;	
КонецФункции	
//******************************************************************************
// УстановитьВремяДокументаЧислом(Док, Сек)
//
// Параметры: 
//  Док - документ, чьё время надо установить
//  Сек - число, новое время документа в секундах (с начала дня)
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Устанавливает новое время документа.
//
Процедура УстановитьВремяДокументаЧислом(Док, Сек)
    Перем Ч,М,С;
	Ч=Цел(Сек/3600);
	Сек=Сек-Ч*3600;
	М=Цел(Сек/60);
	С=Сек-М*60;
	Док.УстановитьВремя(Ч,М,С);
КонецПроцедуры // УстановитьВремяДокументаЧислом()


//******************************************************************************
// ПоКнопкеВремя()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
//  Пок кнопке "Время".
// 
// Описание:
//  Отрабатывает нажатие кнопки "Время".
//  Служит для изменения времени документа в пределах дня.
//
Процедура ПоКнопкеВремя()
	
	ТекДок=ТекущийДокумент;       
	Если ТекДок.Выбран()=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекДок.ДатаДок <= Константа.ДатаЗапретаРедактирования Тогда
	    
		Предупреждение("Нельзя изменять документы с датой, более ранней чем дата запрета редактирования документов!",60);
		Возврат;
	КонецЕсли;

	
	Меню=СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("Назад",  "Переместить назад");
	Меню.ДобавитьЗначение("Вперед", "Переместить вперед");
	Меню.ДобавитьЗначение("ВНачало","Переместить в начало дня");
	Меню.ДобавитьЗначение("ВКонец", "Переместить в конец дня");
	
	Вариант="";
	Если Меню.ВыбратьЗначение(Вариант,,,,1)=0 Тогда
		Возврат;
	КонецЕсли;	
	
	Док=СоздатьОбъект("Документ");
	
	Попытка;
		НачатьТранзакцию();
		
		Если Вариант="Назад" Тогда
			Док.ОбратныйПорядок(1);
			Док.ВыбратьДокументы(ТекДок.ДатаДок,СформироватьПозициюДокумента(ТекДок.ПолучитьПозицию(),-1));               
			Если Док.ПолучитьДокумент()=0 Тогда
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			Время2=ПолучитьВремяДокументаЧислом(Док);
			Если Док.ПолучитьДокумент()=1 Тогда
				Время1=ПолучитьВремяДокументаЧислом(Док);
			Иначе
				Время1=0;
			КонецЕсли;
			Время=Время2-60;
			Если Время<=Время1 Тогда
				Время=Окр((Время2+Время1)/2);
			КонецЕсли;
			Если (Время<=Время1) или (Время>=Время2) Тогда
				Предупреждение("Нет возможности переставить документ!", 60);
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
		ИначеЕсли Вариант="Вперед" Тогда
			Док.ОбратныйПорядок(0);
			Док.ВыбратьДокументы(СформироватьПозициюДокумента(ТекДок.ПолучитьПозицию(),1),ТекДок.ДатаДок);               
			Если Док.ПолучитьДокумент()=0 Тогда
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			Время1=ПолучитьВремяДокументаЧислом(Док);
			Если Док.ПолучитьДокумент()=1 Тогда
				Время2=ПолучитьВремяДокументаЧислом(Док);
			Иначе
				Время2=3600*23+60*59+59+1;
			КонецЕсли;
			Время=Время1+60;
			Если Время>=Время2 Тогда
				Время=Окр((Время2+Время1)/2);
			КонецЕсли;
			Если (Время<=Время1) или (Время>=Время2) Тогда
				Предупреждение("Нет возможности переставить документ!", 60);
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
		ИначеЕсли Вариант="ВНачало" Тогда
			Док.ОбратныйПорядок(0);
			Док.ВыбратьДокументы(ТекДок.ДатаДок,СформироватьПозициюДокумента(ТекДок.ПолучитьПозицию(),-1));               
			Если Док.ПолучитьДокумент()=0 Тогда
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;                 
			Время2=ПолучитьВремяДокументаЧислом(Док);
			Время1=0;
			Время=Время2-60;
			Если Время<=Время1 Тогда
				Время=Окр((Время2+Время1)/2);
			КонецЕсли;
			Если (Время<=Время1) или (Время>=Время2) Тогда
				Предупреждение("Нет возможности переставить документ!", 60);
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
		ИначеЕсли Вариант="ВКонец" Тогда
			Док.ОбратныйПорядок(1);
			Док.ВыбратьДокументы(СформироватьПозициюДокумента(ТекДок.ПолучитьПозицию(),1),ТекДок.ДатаДок);               
			Если Док.ПолучитьДокумент()=0 Тогда
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;                 
			Время1=ПолучитьВремяДокументаЧислом(Док);
			Время2=3600*23+60*59+59+1;
			Время=Время1+60;
			Если Время>=Время2 Тогда
				Время=Окр((Время2+Время1)/2);
			КонецЕсли;
			Если (Время<=Время1) или (Время>=Время2) Тогда
				Предупреждение("Нет возможности переставить документ!", 60);
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Провести=0;	
		Док.НайтиДокумент(ТекДок);
		Если Док.Проведен()=1 Тогда
			Док.СделатьНеПроведенным();
			Провести=1;	
		КонецЕсли;
		
		УстановитьВремяДокументаЧислом(Док,Время);	
		Док.Записать();
		
		Если Провести=1 Тогда
			Док.Провести();
		КонецЕсли;         
		
		ЗафиксироватьТранзакцию();
	Исключение
		Сообщить(ОписаниеОшибки(),"!");  
	КонецПопытки;
	
КонецПроцедуры // ПоКнопкеВремя()


//******************************************************************************
// РедактироватьРеквизиты()
//
// Параметры:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "Редактировать реквизиты документа".
//
// Описание:
//  Окрывает на редактирование документ.
//
Процедура РедактироватьРеквизиты()
	
	ОткрытьФорму("Отчет.РедактированиеРеквизитовДокумента#" + ЗначениеВСтрокуВнутр(ТекущийДокумент), ТекущийДокумент);
	
КонецПроцедуры // РедактироватьРеквизиты()

//******************************************************************************
// ПриВыбореКонтрагента()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореКонтрагента()   
	
	Если КонтрагентДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Контрагент", КонтрагентДляОтбора);
		КонтрагентДляОтбора = КонтрагентДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореКонтрагента()

//******************************************************************************
// ПриВыбореВидаДокумента()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореВидаДокумента()
	
	Если ПустоеЗначение(ВидДокументаДляОтбора) = 1 Тогда
		ВидДокументаДляОтбора=1;
	КонецЕсли;
	УстановитьОтбор(СписокВидовДокументов.ПолучитьЗначение(ВидДокументаДляОтбора), );
	
КонецПроцедуры // ПриВыбореВидаДокумента() 

//******************************************************************************
// ПриВыбореАвтора()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореАвтора()  
	
	Если АвторДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Автор", АвторДляОтбора);
		АвторДляОтбора = АвторДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореАвтора()

//******************************************************************************
// ПриВыбореФирмы()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореФирмы()  
	
	Если ФирмаДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Фирма", ФирмаДляОтбора);
		ФирмаДляОтбора = ФирмаДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореФирмы()
              
//******************************************************************************
// ПриВыбореЮрЛица()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореЮрЛица()  
	
	Если ЮрЛицоДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("ЮрЛицо", ЮрЛицоДляОтбора);
		ЮрЛицоДляОтбора = ЮрЛицоДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореЮрЛица()
              
//******************************************************************************
// ПриВыбореСклада()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореСклада()  
	
	Если СкладДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Склад", СкладДляОтбора);
		СкладДляОтбора = СкладДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореСклада()
              
//******************************************************************************
// ПриВыбореПроекта()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореПроекта()  
	
	Если ПроектДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Проект", ПроектДляОтбора);
		ПроектДляОтбора = ПроектДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореПроекта()

//******************************************************************************
// ПриУстановкеБыстрогоОтбора()
//
// Параметры:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Поле со списком "Вид отбора"
//
// Описание:
//  Устанавливает отбор в журнале по выбранному виду 
//
Процедура ПриУстановкеБыстрогоОтбора()
	Перем ТекущДок;                           
	
    //В форме журнала подчиненных документов отбора нет
	Если ПустоеЗначение(ДокументОснованиеЖурнала) = 0  Тогда
		Возврат;
	КонецЕсли;
	
	ТекущДок = ТекущийДокумент;
	ОтборПо  = ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока());
	
	Если ВидОтбора.ТекущаяСтрока() = 1 Тогда	// нет быстрого отбора
		    
		ЗначениеВВидеСтроки = "";
		Форма.кнЗначение.Доступность(0);
		
		УстановитьОтбор("");
		ВидыОтбора("*");

	Иначе	// есть быстрый отбор
        
		Форма.кнЗначение.Доступность(1);
		
		Если ОтборПо = "по контрагенту" Тогда   
			ПриВыбореКонтрагента();
			ЗначениеВВидеСтроки=Строка(КонтрагентДляОтбора);
			
		ИначеЕсли ОтборПо = "по виду документов" Тогда
			ПриВыбореВидаДокумента();                   
			СписокВидовДокументов.ПолучитьЗначение(ВидДокументаДляОтбора,ЗначениеВВидеСтроки);
			
		ИначеЕсли ОтборПо = "по автору" Тогда
			ПриВыбореАвтора();                          
			ЗначениеВВидеСтроки=Строка(АвторДляОтбора);
			
		ИначеЕсли ОтборПо = "по фирме" Тогда
			ПриВыбореФирмы();
			ЗначениеВВидеСтроки=Строка(ФирмаДляОтбора);
			
		ИначеЕсли ОтборПо = "по юр. лицу" Тогда
			ПриВыбореЮрЛица();
			ЗначениеВВидеСтроки=Строка(ЮрЛицоДляОтбора);
			
		ИначеЕсли ОтборПо = "по складу" Тогда
			ПриВыбореСклада();
			ЗначениеВВидеСтроки=Строка(СкладДляОтбора);
			
		ИначеЕсли ОтборПо = "по проекту" Тогда
			ПриВыбореПроекта();
			ЗначениеВВидеСтроки=Строка(ПроектДляОтбора);
			
		КонецЕсли;
		
		ВидыОтбора("");
		
	КонецЕсли;
	
	Попытка
		АктивизироватьОбъект(ТекущДок);
	Исключение	
		Сообщить(ОписаниеОшибки(),"!");
	КонецПопытки;	
		
КонецПроцедуры // ПриУстановкеБыстрогоОтбора()


//******************************************************************************
// ПоКнопкеЗначение()
//
// Параметры:
//  Нет
//
// Вызывается из формул элементов диалога:
//  По кнопке "Значение..."
//
// Описание:
//  Обрабатывает выбор значения отбора в журнале 
//
Процедура ПоКнопкеЗначение()  
	Перем ЗначениеИзменилось,СтароеЗначение,ВыбЗначение;                     
	
	ЗначениеИзменилось=0;     
	ОтборПо           = ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока());

	
	Если ОтборПо = "по контрагенту" Тогда
		СтароеЗначение=КонтрагентДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.Контрагенты");
		Если ВыбЗначение.Выбрать("Выбор контрагента","Форма списка")=1 Тогда
			КонтрагентДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;	
		
	ИначеЕсли ОтборПо = "по виду документов" Тогда
		СтароеЗначение=ВидДокументаДляОтбора;
		Если ПустоеЗначение(ВидДокументаДляОтбора)>0 Тогда
			ВыбЗначение = "";
		Иначе	
			ВыбЗначение = СписокВидовДокументов.ПолучитьЗначение(ВидДокументаДляОтбора);
		КонецЕсли;	
		СписокВидовДокументов.ВыбратьЗначение(ВыбЗначение,"Выбор вида документов",,60,0);
		ВидДокументаДляОтбора = СписокВидовДокументов.НайтиЗначение(ВыбЗначение);
		Если ВидДокументаДляОтбора<>СтароеЗначение Тогда 
			ЗначениеИзменилось=1;    
		КонецЕсли;	              
		
	ИначеЕсли ОтборПо = "по автору" Тогда
		СтароеЗначение=АвторДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.Пользователи");
		Если ВыбЗначение.Выбрать("Выбор автора","Форма списка")=1 Тогда
			АвторДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;	
		
	ИначеЕсли ОтборПо = "по фирме" Тогда
		СтароеЗначение=ФирмаДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.Фирмы");
		Если ВыбЗначение.Выбрать("Выбор фирмы",)=1 Тогда
			ФирмаДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;	
		
	ИначеЕсли ОтборПо = "по юр. лицу" Тогда
		СтароеЗначение=ЮрЛицоДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.СвоиЮрЛица");
		Если ВыбЗначение.Выбрать("Выбор юр.лица",)=1 Тогда
			ЮрЛицоДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;	
		
	ИначеЕсли ОтборПо = "по складу" Тогда
		СтароеЗначение=СкладДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.Склады");
		Если ВыбЗначение.Выбрать("Выбор склада",)=1 Тогда
			СкладДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;	
		
	ИначеЕсли ОтборПо = "по проекту" Тогда
		СтароеЗначение=ПроектДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.Проекты");
		Если ВыбЗначение.Выбрать("Выбор проекта",)=1 Тогда
			ПроектДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	        
	Если ЗначениеИзменилось=1 Тогда
		ПриУстановкеБыстрогоОтбора();
	КонецЕсли;	

КонецПроцедуры // ПоКнопкеЗначение()

//******************************************************************************
// ПриОбычномОткрытииЖурнала()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Выполняет необходимые действия при обычном открытии журнала
//
Процедура ПриОбычномОткрытииЖурнала()
	
	// определение контрагента для отбора
	КонтрагентДляОтбора =ВосстановитьЗначение("КонтрагентОтбораЖурнала");
	КонтрагентДляОтбора= ?(ПустоеЗначение(КонтрагентДляОтбора)=1, ПолучитьПустоеЗначение("Справочник.Контрагенты"), КонтрагентДляОтбора);
	
	// определение фирмы для отбора
	ФирмаДляОтбора =ВосстановитьЗначение("ФирмаОтбораЖурнала");
	ФирмаДляОтбора = ?(ПустоеЗначение(ФирмаДляОтбора)=1, ПолучитьПустоеЗначение("Справочник.Фирмы"), ФирмаДляОтбора);
	
	// определение юр. лица для отбора
	ЮрЛицоДляОтбора =ВосстановитьЗначение("ЮрЛицоОтбораЖурнала");
	ЮрЛицоДляОтбора = ?(ПустоеЗначение(ЮрЛицоДляОтбора)=1, ПолучитьПустоеЗначение("Справочник.СвоиЮрЛица"), ЮрЛицоДляОтбора);
	
	// определение склада для отбора
	СкладДляОтбора =ВосстановитьЗначение("СкладОтбораЖурнала");
	СкладДляОтбора= ?(ПустоеЗначение(СкладДляОтбора)=1, ПолучитьПустоеЗначение("Справочник.Склады"), СкладДляОтбора);
	
	// определение вида документа для отбора
	ВидДокументаДляОтбора = ВосстановитьЗначение("ВидДокументаДляОтбораЖурнала"); 
	ВидДокументаДляОтбора= ?(ПустоеЗначение(ВидДокументаДляОтбора)=1, 1, ВидДокументаДляОтбора);
	
	// определение автора документа для отбора
	АвторДляОтбора = ВосстановитьЗначение("АвторДляОтбораЖурнала");
	АвторДляОтбора= ?(ПустоеЗначение(АвторДляОтбора)=1, ПолучитьПустоеЗначение("Справочник.Пользователи"), АвторДляОтбора);
	
	// определение проекта документа для отбора
	ПроектДляОтбора = ВосстановитьЗначение("ПроектДляОтбораЖурнала");
	ПроектДляОтбора= ?(ПустоеЗначение(ПроектДляОтбора)=1, ПолучитьПустоеЗначение("Справочник.Проекты"), ПроектДляОтбора);
	
КонецПроцедуры // ПриОбычномОткрытииЖурнала()


//******************************************************************************
// ПриОткрытииИЛИПереоткрытииЖурнала(Режим)
//
// Параметры: 
//  Режим - Строка режима открытия ("Открытие" или "Переоткрытие") формы журнала
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Выполняет необходимые действия при открытии журнала
//
Процедура ПриОткрытииИЛИПереоткрытииЖурнала(Режим)
	
	// запомним документ основание для формы журнала подчиненных документов
	ДокументОснованиеЖурнала = ПодчинениеДокументу();
	
	Если Режим="Открытие" Тогда
		// Позиционируемся на последнем документе
		ТекДок = ВосстановитьЗначение("ТекДок");
		ТекДок = ?(ПустоеЗначение(ТекДок)=1, ПолучитьПустоеЗначение("Документ"), ТекДок);
			
		//Заполним список действий по кнопке "Действия"
		СписокДействий.УдалитьВсе();
		СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
		СписокДействий.ДобавитьЗначение("Отчет о бухгалтерских проводках");
		СписокДействий.ДобавитьЗначение("Структура подчиненности");
		СписокДействий.ДобавитьЗначение("Ввести на основании");

		Если ТипЗначенияСтр(ДокументОснованиеЖурнала) = "Документ" Тогда // форма журнала подчиненных документов
			Форма.Заголовок("Журнал подчиненных документов к документу " + глПредставлениеДокумента(ДокументОснованиеЖурнала));
			
			// Погасим реквизиты отбора
			Форма.ТекстБыстрыйОтбор.  Видимость(0);
			Форма.ВидОтбора.          Видимость(0);
			Форма.кнЗначение.         Видимость(0);
			Форма.ТекстЗначенияОтбора.Видимость(0);    
			
			// менять время документов и распечатывать реестр в журнале подчиненных считаем не нужным
			Форма.кнВремя. Доступность(0);
			Форма.кнРеестр.Доступность(0);
			
		Иначе	// Обычная форма журнала                        
			
			СписокДействий.ДобавитьЗначение("Скопировать в...");
			Форма.Заголовок("",1);
			// Определение был ли быстрый отбор
			ТекСтр = ВосстановитьЗначение("ВидОтбораЖурнала");
			ТекСтр = ?(ПустоеЗначение(ТекСтр)=1, 1, ТекСтр);
			ВидОтбора.ТекущаяСтрока(ТекСтр);
			
			ПриОбычномОткрытииЖурнала();
		КонецЕсли;
		
		
	ИначеЕсли Режим="Переоткрытие" Тогда	
		ТекДок = ТекущийДокумент;
	КонецЕсли;	
			
	Если ПустоеЗначение(Форма.Параметр)=0 Тогда
		Если ВРег(ТипЗначенияСтр(Форма.Параметр))="ДОКУМЕНТ" Тогда
			// НАДО ПОЗИЦИОНИРОВАТЬСЯ НА ЭТОМ ДОКУМЕНТЕ
			Док = Форма.Параметр.ТекущийДокумент();
			Если Док.Выбран()>0 Тогда 
				ТекДок = Док;
	
				// Если документ не попадает в текущий интервал журнала, то изменим интервал 
				ДатаНач = Мин(НачалоИнтервала(), Док.ДатаДок);
				ДатаКон = Макс(КонецИнтервала(), Док.ДатаДок);
				УстановитьИнтервал(ДатаНач,ДатаКон);
				
				// Надо сделать так, чтобы условия отбора удовлетворяли данному документу
				ОтборПо  = ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока());
	
				Если ВидОтбора.ТекущаяСтрока() > 1 Тогда	// есть быстрый отбор
					
					Если ОтборПо = "по контрагенту" Тогда   
						Если глЕстьРеквизитШапки("Контрагент", ТекДок.Вид()) = 1 Тогда
							КонтрагентДляОтбора = ТекДок.Контрагент;
						Иначе                 
							ВидОтбора.ТекущаяСтрока(1); // снимаем быстрый отбор
						КонецЕсли;
					ИначеЕсли ОтборПо  = "по виду документов" Тогда      
						ВидДокументаДляОтбора = СписокВидовДокументов.НайтиЗначение(ТекДок.Вид());
						СписокВидовДокументов.ТекущаяСтрока(ВидДокументаДляОтбора);
					ИначеЕсли ОтборПо  = "по автору" Тогда
						АвторДляОтбора = ТекДок.Автор;  // общий реквизит документов
					ИначеЕсли ОтборПо  = "по фирме" Тогда
						ФирмаДляОтбора = ТекДок.Фирма;  // общий реквизит документов
					ИначеЕсли ОтборПо  = "по юр. лицу" Тогда
						ЮрЛицоДляОтбора= ТекДок.ЮрЛицо; // общий реквизит документов
					ИначеЕсли ОтборПо = "по складу" Тогда
						Если глЕстьРеквизитШапки("Склад", ТекДок.Вид()) = 1 Тогда
							СкладДляОтбора = ТекДок.Склад;
						Иначе                 
							ВидОтбора.ТекущаяСтрока(1); // снимаем быстрый отбор
						КонецЕсли;
					ИначеЕсли ОтборПо = "по проекту" Тогда
						ПроектДляОтбора = ТекДок.Проект;// общий реквизит документов
						
					КонецЕсли; // ОтборПо
				КонецЕсли; // ВидОтбора.ТекущаяСтрока() > 1
			КонецЕсли; // Док.Выбран()>0	
		КонецЕсли; // ВРег(ТипЗначенияСтр(Форма.Параметр))="ДОКУМЕНТ"      
	КонецЕсли; // ПустоеЗначение(Форма.Параметр)=0
	
	ПриУстановкеБыстрогоОтбора();
		                                             
	Попытка
		АктивизироватьОбъект(ТекДок);
	Исключение	
		Сообщить(ОписаниеОшибки(),"!");
	КонецПопытки;	
		
КонецПроцедуры // ПриОткрытииИЛИПереоткрытииЖурнала()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриПовторномОткрытии()
	
	ПриОткрытииИЛИПереоткрытииЖурнала("Переоткрытие");
	
КонецПроцедуры // ПриПовторномОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()  
	
	ПриОткрытииИЛИПереоткрытииЖурнала("Открытие");
	
	// будем отслеживать только реальные изменения этих реквизитов
	Форма.ВидОтбора.ВыполнятьФормулуТолькоПриИзменении(1); 
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗакрытии()
	
	СохранитьЗначение("ТекДок",ТекущийДокумент);
		
    //В форме журнала подчиненных документов отбора нет
	Если ПустоеЗначение(ДокументОснованиеЖурнала) = 1  Тогда
	    СохранитьЗначение("КонтрагентОтбораЖурнала",КонтрагентДляОтбора); 
		СохранитьЗначение("ФирмаОтбораЖурнала",ФирмаДляОтбора);
		СохранитьЗначение("ЮрЛицоОтбораЖурнала",ЮрЛицоДляОтбора);
		СохранитьЗначение("СкладОтбораЖурнала",СкладДляОтбора);
		СохранитьЗначение("ВидДокументаДляОтбораЖурнала", ВидДокументаДляОтбора);
	    СохранитьЗначение("АвторДляОтбораЖурнала",АвторДляОтбора);
		СохранитьЗначение("ПроектДляОтбораЖурнала",ПроектДляОтбора);
	    
		СохранитьЗначение("ВидОтбораЖурнала",ВидОтбора.ТекущаяСтрока());
	КонецЕсли;
	
КонецПроцедуры // ПриЗакрытии()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 
//Инициализирум список действий по кнопке "Действия"
СписокДействий = СоздатьОбъект("СписокЗначений");
// Заполним список видов документов для быстрого отбора
СписокВидовДокументов = СоздатьОбъект("СписокЗначений");
Для Счетчик = 1 по Метаданные.Документ() Цикл
	СписокВидовДокументов.ДобавитьЗначение(Метаданные.Документ(Счетчик).Идентификатор, Метаданные.Документ(Счетчик).Представление());
КонецЦикла;
СписокВидовДокументов.СортироватьПоПредставлению();
	                  
// Заполним список возможных видов быстрого отбора
ВидОтбора.ДобавитьЗначение(" < отсутствует > "); 
ВидОтбора.ДобавитьЗначение("по контрагенту"); 
ВидОтбора.ДобавитьЗначение("по виду документов");
ВидОтбора.ДобавитьЗначение("по автору");
ВидОтбора.ДобавитьЗначение("по фирме"); 
ВидОтбора.ДобавитьЗначение("по юр. лицу"); 
ВидОтбора.ДобавитьЗначение("по складу"); 
ВидОтбора.ДобавитьЗначение("по проекту"); 

КонтрагентДляОтбора = СоздатьОбъект("Справочник.Контрагенты");  
ФирмаДляОтбора 		= СоздатьОбъект("Справочник.Фирмы"); 
ЮрЛицоДляОтбора 	= СоздатьОбъект("Справочник.СвоиЮрЛица"); 
СкладДляОтбора 		= СоздатьОбъект("Справочник.Склады"); 
АвторДляОтбора 		= СоздатьОбъект("Справочник.Пользователи");
ПроектДляОтбора 	= СоздатьОбъект("Справочник.Проекты");
ВидДокументаДляОтбора = 1;

ЗначениеВВидеСтроки = "";