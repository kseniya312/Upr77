////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем СписокДействий;  // Список действий по документу
Перем КонтекстПодбора;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// НомерПиктограммы()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Номер пиктограммы для текущего платежного документа.
//
// Вызывается из формул элементов диалога:
//  Текстовое поле с пиктограммами в табличной части журнала.
//
// Описание:
//  Для занесенных в выписку платежек показываем спец. значок для удобства поиска.
//
Функция НомерПиктограммы()
	
	Перем НомПикт;

	Если ТекущийДокумент.Выбран() = 1 Тогда
		НомПикт = 2;
		ДатаВыписки = "";
	    
		ПодчДок = СоздатьОбъект("Документ");
		Если ПодчДок.ВыбратьПодчиненныеДокументы(,,ТекущийДокумент) = 1 Тогда
			Пока ПодчДок.ПолучитьДокумент() = 1 Цикл
				Если (ПодчДок.Вид() = "СтрокаВыпискиРасход")
				и    (ПодчДок.Проведен() = 1)
				Тогда
		    		НомПикт = 1;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		НомПикт = 0;
	КонецЕсли;

	Возврат НомПикт;     
	
КонецФункции //НомерПиктограммы

//******************************************************************************
// ПоКнопкеОК()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "ОК".
//
// Описание:
//  Производим выбор.
//
Процедура ПоКнопкеОК()
	
	Форма.ВыполнитьВыбор(ВыбрДок);
	Форма.Закрыть(0);
	
КонецПроцедуры // ПоКнопкеОК()

//******************************************************************************
// Удалить()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "Удалить одно значение".
//
// Описание:
//  Удаляем одну строку в списке отобранных позиций.
//
Процедура Удалить()
	
	Если ВыбрДок.ТекущаяСтрока() > 0 Тогда
	    ВыбрДок.УдалитьСтроку(ВыбрДок.ТекущаяСтрока());
	КонецЕсли;
	
КонецПроцедуры // Удалить()

//******************************************************************************
// Очистить()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "Очистить список".
//
// Описание:
//  Очистка списка отобранных платежек.
//
Процедура Очистить()

	Если Вопрос("Очистить список выбранных платежных документов?", "Да+Нет", 60) = "Да" Тогда
		ВыбрДок.УдалитьСтроки();
	КонецЕсли;
	
КонецПроцедуры // Очистить()

//******************************************************************************
// СуммаУчтеннаяРанееПоДокументу()
//
// Параметры:
//  Док - документ (платежка)
//
// Возвращаемое значение:
//  Сумма.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Расчет суммы, на которую платежка уже учтена.
//
Функция СуммаУчтеннаяРанееПоДокументу(Док)
	
	Перем Сумма;
	
	Сумма = 0;
	ПодчДок = СоздатьОбъект("Документ");
	Если ПодчДок.ВыбратьПодчиненныеДокументы(, , Док) = 1 Тогда
		Пока ПодчДок.ПолучитьДокумент() = 1 Цикл
			Если (ПодчДок.Вид() = "СтрокаВыпискиРасход") 
			и    (ПодчДок.Проведен()=1)
			Тогда
				Сумма = Сумма + ПодчДок.Сумма;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
    
	Возврат Сумма;
	
КонецФункции //СуммаУчтеннаяРанееПоДокументу()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	КонтекстПодбора = Форма.Параметр;
	Если ПустоеЗначение(КонтекстПодбора.БанковскийСчет) = 1 Тогда
		Предупреждение("Сначала следует выбрать банковский счет!", 60);
		СтатусВозврата(0);Возврат;
	КонецЕсли;
	
	ВыбрДок.НоваяКолонка("ПлатДок",,,,"Выбранный документ",25);
	ВыбрДок.НоваяКолонка("Контрагент",,,,"Контрагент",25);
	ВыбрДок.НоваяКолонка("СуммаДок",,,,"Сумма",10,"Ч19.2.,",2);
	Форма.ОбработкаВыбораСтроки(1);
	
КонецПроцедуры //ПриОткрытии

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореСтроки()
	
	СтатусВозврата(0);
    
	Док = ТекущийДокумент.ТекущийДокумент();
	Если Док.БанковскийСчет <> КонтекстПодбора.БанковскийСчет Тогда
		Предупреждение("Банковский счет выбранного документа "+глПредставлениеДокумента(Док)+"
		|не соответствует указанному в выписке.",60);
		Возврат;
	КонецЕсли;
	
	Если (Док.Контрагент.Выбран() = 0)
	или  (Док.Договор.Выбран()    = 0)
	Тогда
		Предупреждение("В выбранном документе "+глПредставлениеДокумента(Док)+"
		|не указан контрагент или договор.", 60);
		Возврат;
	КонецЕсли;
	
	НомСтр = 0;
	ВыбрДок.НайтиЗначение(Док, НомСтр, "ПлатДок");
	Если НомСтр = 0 Тогда
		СуммаОстатка = Док.Сумма - СуммаУчтеннаяРанееПоДокументу(Док);
		Если СуммаОстатка > 0 Тогда
			ВыбрДок.НоваяСтрока();
			ВыбрДок.ПлатДок    = Док;
			ВыбрДок.Контрагент = Док.Контрагент;
			ВыбрДок.СуммаДок   = СуммаОстатка;
		Иначе     
			Предупреждение("Выбранный документ "+глПредставлениеДокумента(Док)+" уже был
			|занесен в выписку!", 60);
		КонецЕсли;
	Иначе                    
		Предупреждение("Документ "+глПредставлениеДокумента(Док)+" уже выбран!", 60);
		ВыбрДок.ПолучитьСтрокуПоНомеру(НомСтр);
	КонецЕсли;                  
	
КонецПроцедуры //ПриВыбореСтроки()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
//Инициализирум список действий по кнопке "Действия"
СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Структура подчиненности");