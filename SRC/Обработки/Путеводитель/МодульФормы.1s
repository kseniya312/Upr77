////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем ИсторияПереходов;  	// история переходов по старницам путеводителя
Перем ИндИстории;			// текущее положение в истории

Перем ПоказПриЗапуске;		// флаг показа при запуске
Перем ПоказПоследней;		// 

Перем ТекущаяСтраница;

Перем ЧислоРазделов;  		// число разделов в меню "содержание"

Перем МенюСодержание;		// меню содержание
Перем Главная;              // стартовая страница                  	

Перем СекцияЗаголовок,СекцияГлавная,СекцияШапка,СекцияВпервые;	// "служебные" секции путеводителя
Перем Показ,ГлавнаяСтр;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//        
//******************************************************************************
// ПоказатьСтраницу(ИмяСтраницы)
//
// Параметры: 
//  ИмяСтраницы - название страницы
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Переход на новую страницу в путеводителе
//
Процедура ПоказатьСтраницу(ИмяСтраницы)
	Таблица.Очистить();
	Таблица.ВывестиСекцию(СекцияЗаголовок);
	Если (ИмяСтраницы="ПервыйЗапуск")ИЛИ(ИмяСтраницы="Путеводитель") Тогда
		Таблица.ВывестиСекцию(СекцияШапка);
		Если Форма.Параметр = "ПервыйЗапуск" Тогда
			Таблица.ВывестиСекцию(СекцияВпервые);
		КонецЕсли;
	КонецЕсли;
	Если ПустаяСтрока(ИмяСтраницы)=0 Тогда
		Таблица.ИсходнаяТаблица(ИмяСтраницы);
		ТекущаяСтраница=ИмяСтраницы;		
	Иначе
		Таблица.ИсходнаяТаблица(ТекущаяСтраница);
	КонецЕсли;
	Таблица.ВывестиСекцию("Страница");
	Если ((ПустоеЗначение(ПоказПоследней)=1) ИЛИ (ПоказПоследней=-1)) И (ИмяСтраницы<>"ПервыйЗапуск") Тогда
		СекцияГлавная.Область("ГлавнаяСтр").Текст = ?(Главная=ИсторияПереходов.ПолучитьЗначение(ИндИстории),"ю","Ё");
		Таблица.ВывестиСекцию(СекцияГлавная);
	КонецЕсли;
	Таблица.Опции(0,0,1,0,,"Старт");
	Таблица.Защита(1);
	Таблица.ТолькоПросмотр();
	Таблица.Показать();
	
КонецПроцедуры // ПоказатьСтраницу()

//******************************************************************************
// ОбработкаЯчТаблицы(Зн)
//
// Параметры: 
//  Зн - расшифровка поля таблицы, на которой произведен двойной клик
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  обработка двойного клика на ячейке таблицы путеводителя
//
Процедура ОбработкаЯчТаблицы(Зн)
	Команда=Лев(Зн,3);
	Параметр=Сред(Зн,5,СтрДлина(Зн)-4);
	Если Команда="Таб" Тогда
		Если Параметр<>?(ИсторияПереходов.РазмерСписка()=ИндИстории,"",ИсторияПереходов.ПолучитьЗначение(ИндИстории+1)) Тогда
			Если ИсторияПереходов.РазмерСписка() > ИндИстории Тогда
				ИсторияПереходов.УдалитьЗначение(ИндИстории+1, ИсторияПереходов.РазмерСписка()-ИндИстории);
			КонецЕсли;
			ИсторияПереходов.ДобавитьЗначение(Параметр);
		КонецЕсли;
		ИндИстории = ИндИстории+1;
		ПоказатьСтраницу(Параметр);
	ИначеЕсли Команда="Фрм" Тогда
		Если Параметр="Операция" Тогда
			ОткрытьФорму(Параметр,,"яицарепо_яавопиТ");
		Иначе    
			ПозицРазделит = Найти(Параметр,"=");    
			Если ПозицРазделит>0 Тогда
				Параметр2 = Сред(Параметр,ПозицРазделит+1); 
				Параметр = Лев(Параметр,ПозицРазделит-1);
				ОткрытьФорму(Параметр,Параметр2);
			Иначе
				ОткрытьФорму(Параметр);
			КонецЕсли;	
		КонецЕсли;
	ИначеЕсли Команда="Элм" Тогда                  
		Если ПустаяСтрока(Параметр)=0 Тогда
			ПозицРазделит = Найти(Параметр,"=");    
			Параметр2 = "";
			Если ПозицРазделит>0 Тогда
				Параметр2 = Сред(Параметр,ПозицРазделит+1); 
				Параметр = Лев(Параметр,ПозицРазделит-1);
			КонецЕсли;	
			ЭлементСправочника = СоздатьОбъект(Параметр);
			ЭлементСправочника.ВыборГруппы(0);
			Если ЭлементСправочника.Выбрать("Выберите элемент справочника",)>0 Тогда
				Если Параметр2 = "ПоказатьДоговорыКонтрагентов" Тогда
					ОткрытьФорму("Справочник.Договоры",ЭлементСправочника.ТекущийЭлемент());
				ИначеЕсли Параметр2 = "ПоказатьСвойстваКонтрагентов" Тогда
					ОткрытьФорму("Справочник.СвойстваКонтрагентов",ЭлементСправочника.ТекущийЭлемент());
				ИначеЕсли Параметр2 = "ПоказатьЦены" Тогда
					ОткрытьФорму("Справочник.Цены",ЭлементСправочника.ТекущийЭлемент());
				ИначеЕсли Параметр2 = "ПоказатьСвойстваНоменклатуры" Тогда
					ОткрытьФорму("Справочник.СвойстваНоменклатуры",ЭлементСправочника.ТекущийЭлемент());
				ИначеЕсли Параметр2 = "ПоказатьСоставКомплекта" Тогда
					Фрм ="";
					ОткрытьФорму("Справочник.Комплектация",Фрм);
					Фрм.ИспользоватьВладельца(ЭлементСправочника.ТекущийЭлемент());
				ИначеЕсли ПустаяСтрока(Параметр2)=0 Тогда
					ОткрытьФорму(ЭлементСправочника.ТекущийЭлемент(),Параметр2);
					
				Иначе	
					ОткрытьФорму(ЭлементСправочника.ТекущийЭлемент());
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;
	ИначеЕсли Команда="ТКС" Тогда
		Предупреждение(Параметр, 60);
	ИначеЕсли Команда="Наз" Тогда
		Если ИндИстории > 1 Тогда
			ИндИстории=ИндИстории-1;
			ПоказатьСтраницу(ИсторияПереходов.ПолучитьЗначение(ИндИстории));
		КонецЕсли;
	ИначеЕсли Команда="Дал" Тогда
		Если ИндИстории<>0 Тогда
			Если ИндИстории<ИсторияПереходов.РазмерСписка() Тогда
				ИндИстории=ИндИстории+1;
				ПоказатьСтраницу(ИсторияПереходов.ПолучитьЗначение(ИндИстории));
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Команда="Реж" Тогда
		ПоказПриЗапуске=?(ПоказПриЗапуске=1,-1,1);
		СекцияЗаголовок.Область("Показ").Текст = ?(ПоказПриЗапуске=1,"ю","Ё");
		Имя = ?(ИндИстории=0,"ПервыйЗапуск",ИсторияПереходов.ПолучитьЗначение(ИндИстории));
		ПоказатьСтраницу(Имя);
	ИначеЕсли Команда="Стр" Тогда
		Если Параметр="ЗапускПоследней" Тогда
			ПоказПоследней=?(ПоказПоследней=1,-1,1);
		Иначе    
			Главная = ИсторияПереходов.ПолучитьЗначение(ИндИстории);
		КонецЕсли;
		ПоказатьСтраницу(ИсторияПереходов.ПолучитьЗначение(ИндИстории));

	ИначеЕсли Команда = "ОСН" Тогда
		// Ввод одного документа на основании другого
		ПозицияРазделителя = Найти(Параметр, "_");
		ВидДокумента = Лев(Параметр, ПозицияРазделителя-1);
		ВидОснования = Сред(Параметр, ПозицияРазделителя+1);
		Док = СоздатьОбъект(ВидОснования);
		Если Док.Выбрать("Выберите документ-основание") = 1 Тогда
		    ОткрытьФорму(ВидДокумента,, Док);
		КонецЕсли;                 
	
	ИначеЕсли Команда = "ДАТ" Тогда
		РабДата = РабочаяДата();
		ВвестиЗначение(РабДата,"Выберите значение рабочей даты","Дата");
		Если ПустоеЗначение(РабДата)=0 Тогда
			РабочаяДата(РабДата);
			ПоказатьСтраницу(ИсторияПереходов.ПолучитьЗначение(ИндИстории));
		КонецЕсли;                 
		
	ИначеЕсли Команда = "ПРН" Тогда	
		Док = СоздатьОбъект("Документ.ПриходнаяНакладная");
		Если Док.Выбрать("Выберите приходную накладную",)>0 Тогда
			ОткрытьФорму(Док.ТекущийДокумент());
		КонецЕсли;	
		
	КонецЕсли;
КонецПроцедуры	// ОбработкаЯчТаблицы()
                                                                     

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаЯчейкиТаблицы(Зн,Фл)
	Перем СтрокаЗначения;   
	СтрокаЗначения = "";
	Если Лев(Зн,2)="Сп" Тогда
		// Вызов списка 
		ПунктМеню="";
		Меню = СоздатьОбъект("СписокЗначений");
		Если Лев(Зн,3)="СпС" Тогда
			// Вызов списка "Содержание"
			МенюСодержание.Выгрузить(Меню);
		ИначеЕсли Лев(Зн,3)="СпН" Тогда               
			Для Н=1 По ИндИстории-1 Цикл
				Значение = "Таб_"+ИсторияПереходов.ПолучитьЗначение(ИндИстории-Н);  
				МенюСодержание.ПолучитьЗначение(МенюСодержание.НайтиЗначение(Значение),СтрокаЗначения);
				Меню.ДобавитьЗначение(Значение,СтрокаЗначения);
			КонецЦикла;
		ИначеЕсли Лев(Зн,3)="СпД" Тогда
			Для Н=ИндИстории+1 По ИсторияПереходов.РазмерСписка() Цикл
				Значение = "Таб_"+ИсторияПереходов.ПолучитьЗначение(Н);  
				МенюСодержание.ПолучитьЗначение(МенюСодержание.НайтиЗначение(Значение),СтрокаЗначения);
				Меню.ДобавитьЗначение(Значение,СтрокаЗначения);
			КонецЦикла;
			
		КонецЕсли;
		Если Меню.ВыбратьЗначение(ПунктМеню,,,,2) = 1 Тогда
			Если (Лев(Зн,3)="СпН") ИЛИ (Лев(Зн,3)="СпД" ) Тогда   
				ИндИстории = ИсторияПереходов.НайтиЗначение(Сред(ПунктМеню,5,СтрДлина(ПунктМеню)))-1;
			КонецЕсли;
			Если Лев(Зн,3)="СпС" Тогда
				Если ИндИстории<>0 Тогда
					Если Сред(ПунктМеню,5,СтрДлина(ПунктМеню)) <> ИсторияПереходов.ПолучитьЗначение(ИндИстории) Тогда
						ОбработкаЯчТаблицы(ПунктМеню);
					КонецЕсли;
				Иначе
					ОбработкаЯчТаблицы(ПунктМеню);
				КонецЕсли;
			Иначе
				ОбработкаЯчТаблицы(ПунктМеню);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ОбработкаЯчТаблицы(Зн);
	КонецЕсли;
	Фл=0;
КонецПроцедуры // ОбработкаЯчейкиТаблицы()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриПовторномОткрытии()
	Перем Фл;
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда 
		Если МенюСодержание.НайтиЗначение("Таб_"+Форма.Параметр) <> 0 Тогда
			Если  Форма.Параметр<> ИсторияПереходов.ПолучитьЗначение(ИндИстории) Тогда
				ОбработкаЯчейкиТаблицы("Таб_"+Форма.Параметр,Фл);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПриПовторномОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	Перем Страница;
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Содержание");
	Секц = Таб.ПолучитьСекцию("Страница");
	МенюСодержание.ДобавитьЗначение("Таб_ПервыйЗапуск","Первый Запуск");
	МенюСодержание.ДобавитьЗначение("Таб_Содержание","Содержание");
	Для Н=1 По ЧислоРазделов Цикл
		Обл = Секц.Область("П"+Формат(Н-1, "Ч(0)3"));
		МенюСодержание.ДобавитьЗначение(Обл.Расшифровка(),Обл.Текст);
	КонецЦикла;
	
	Таблица.ИсходнаяТаблица("Основная");
	СекцияЗаголовок=Таблица.ПолучитьСекцию("Заголовок");
	СекцияГлавная=Таблица.ПолучитьСекцию("ГлавныйРаздел");
	СекцияШапка=Таблица.ПолучитьСекцию("Шапка");
	СекцияВпервые=Таблица.ПолучитьСекцию("Впервые");

	Главная=ВосстановитьЗначение( "Главная страница" );
	Старт = Главная;
	ПоказПриЗапуске=ВосстановитьЗначение("ПоказПутеводителяПриЗапуске");
	СекцияЗаголовок.Показ = ?(ПоказПриЗапуске=1,"ю","Ё");
	ПоказПоследней=ВосстановитьЗначение("ПоказПоследней");
	ПоказПоследней = ?(ПустоеЗначение(ПоказПоследней)=1,-1,ПоказПоследней);
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда
		Если МенюСодержание.НайтиЗначение("Таб_"+Форма.Параметр) <> 0 Тогда
			Старт = Форма.Параметр;
		Иначе 
			Старт = "Содержание";
		КонецЕсли;
	Иначе
		Старт = ?((ПустоеЗначение(Главная)=1)ИЛИ(Главная="ПервыйЗапуск"),"Содержание",Главная);
	КонецЕсли;
	ИсторияПереходов.ДобавитьЗначение(Старт);  
	ИндИстории = 1;
	ПоказатьСтраницу(Старт);
КонецПроцедуры  // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗакрытии()
	СохранитьЗначение( "ПоказПутеводителяПриЗапуске",ПоказПриЗапуске);
	СохранитьЗначение( "ПоказПоследней",ПоказПоследней);
	Если ПоказПоследней=1 Тогда
		СохранитьЗначение( "Главная страница",ИсторияПереходов.ПолучитьЗначение(ИндИстории));
	Иначе    
		СохранитьЗначение( "Главная страница",Главная);
	КонецЕсли; 
КонецПроцедуры 		// ПриЗакрытии()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 
ИндИстории       = 0;
ИсторияПереходов = СоздатьОбъект("СписокЗначений");
МенюСодержание   = СоздатьОбъект("СписокЗначений");
ЧислоРазделов    = 45;