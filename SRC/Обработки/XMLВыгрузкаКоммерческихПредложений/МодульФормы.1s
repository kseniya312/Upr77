////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем	ИмяРеквизитаИдентификации;
Перем	ИДСхемыДляПроверки;
Перем	ТекущаяЗакладка;
Перем	СписокТоваров;
Перем	Документ;
Перем	СхемыДляПроверки;
Перем	ЭлементКаталога;

Перем	ТекущаяКатегорияЦен;

Перем	ЭлКоммерческаяИнформация;

Перем	РежимВыбораЗначенийСвойства;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 

//******************************************************************************
// СоздатьПодчиненныйЭлемент(Владелец, ИмяТега, Значение)
//
// Параметры:
// 	Владелец	-	xml-элемент, у которого создаем подчиненный
//	ИмяТега		-	имя тега создаваемого элемента
//	Значение	-	значение создаваемого xml-элемента
//
// Возвращаемое значение:
// 	Созданный xml-элемент
//
// Описание
// 	Создает подчиненный элемент для переданного владельца. Устанавливает значение, если указано.
//
Функция СоздатьПодчиненныйЭлемент(Владелец, ИмяТега, Значение="")
	
	ЭлВозврата = Владелец.СоздатьПодчиненныйЭлемент(ИмяТега, ,ИДСхемыДляПроверки);
	Если Значение <> "" Тогда
	    ЭлВозврата.Значение = Значение;
	КонецЕсли;
	
	Возврат ЭлВозврата;
	
КонецФункции		//	СоздатьПодчиненныйЭлемент()

//******************************************************************************
// ПроверитьИУстановитьАтрибут(Эл, ИдАтрибута, ЗнАтрибута);
//
// Параметры:
// 	Эл			-	элемент, у которого устанавливаем атрибут
//	ИдАтрибута	-	Идентификатор атрибута
//	ЗнАтрибута	-	значение атрибута
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Если передано не пустое значение, то устанавливаем атрибут xml-элемента
//
Процедура ПроверитьИУстановитьАтрибут(Эл, ИдАтрибута, ЗнАтрибута);
	
	Если ПустоеЗначение(ЗнАтрибута) = 0 Тогда
		Эл.УстановитьАтрибут(ИдАтрибута, ЗнАтрибута);
	КонецЕсли;
	
КонецПроцедуры		//	ПроверитьИУстановитьАтрибут()

//******************************************************************************
// Ид(Эл, Зн)
//
// Параметры:
// 	Эл - xml-элемент
//	Зн - значение, преобразуемое к корректному идентификатору
//
// Возвращаемое значение:
// 	Строка - корректный идентификатор
//
// Описание
// 	Переданное значение преобразует к корректному идентификатору (заменяет пробелы, и т.д.)
//
Функция Ид(Эл, Зн)
	
	Возврат Эл.ПреобразоватьВ_ИД(	СокрЛП(Зн.Код)	);
	
КонецФункции		//	Ид()

//******************************************************************************
// ОстатокТовара(Тов)
//
// Параметры:
// 	Тов - элемент справочника "Номенклатура"
//
// Возвращаемое значение:
// 	Число - Остаток товара на всех складах
//
// Описание
// 	Возвращает остаток товара на всех складах
//
Функция ОстатокТовара(Тов)
	
	Возврат Регистр.ОстаткиТМЦ.СводныйОстаток( , Тов.ТекущийЭлемент(), , ,"Количество");
	
КонецФункции		//	ОстатокТовара()

//******************************************************************************
// ДобавитьЗначенияВСписокПоЗапросу(Список, ТекстЗапроса, ИмяАтрибута)
//
// Параметры:
// 	Список			-	СписокЗначений
//	ТекстЗапроса	-	Строка - текст запроса
//	ИмяАтрибута		-	Имя атрибута, значение которго добавляется в список
//
// Возвращаемое значение:
// 	СписокЗначений - атрибутов элементов отобранных по произвольному запросу
//
// Описание
// 	Выбирает элементы каталога по запросу и заполняет СписокЗначений значениями указанного атрибута
//
Функция ДобавитьЗначенияВСписокПоЗапросу(Список, ТекстЗапроса, ИмяАтрибута)
	
	Выборка = ЭлементКаталога.ВыбратьУзлы(ТекстЗапроса);
	Для Сч = 0 По Выборка.КоличествоУзлов() - 1 Цикл
		Эл			= Выборка.ПолучитьУзел(Сч);
		ЗнАтрибута	= Эл.ПолучитьАтрибут(ИмяАтрибута);		
		Если Список.Принадлежит(ЗнАтрибута) = 0 Тогда
			Список.ДобавитьЗначение(ЗнАтрибута);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Список;    
	
КонецФункции		//	ДобавитьЗначенияВСписокПоЗапросу()
    

//******************************************************************************
// СписокОбязательныхСвойствТовара(Каталог, ИдентификаторВКаталоге)
//
// Параметры:
// 	Каталог					-	каталог товара
//	ИдентификаторВКаталоге	-	идентификатор товара в каталоге
//
// Возвращаемое значение:
// 	СписокЗначений - Список обязательных свойств товара
//
// Описание
// 	Возвращает список обязательных свойств товара
//
Функция СписокОбязательныхСвойствТовара(Каталог, ИдентификаторВКаталоге)
	
	Список = СоздатьОбъект("СписокЗначений");
	                                     
	зКаталог			= "//Каталог[@Идентификатор=""" + СокрЛП(Каталог.Идентификатор) + """]";
	зТовар				= "/Товар[@ИдентификаторВКаталоге=""" + СокрЛП(ИдентификаторВКаталоге) + """]";
	зСсылкаНаСвойство	= "/СсылкаНаСвойство[@Обязательно=""1""]";
	                                                                       
	ДобавитьЗначенияВСписокПоЗапросу(Список, зКаталог + зСсылкаНаСвойство, 			"ИдентификаторСвойства");
	ДобавитьЗначенияВСписокПоЗапросу(Список, зКаталог + зТовар + зСсылкаНаСвойство, "ИдентификаторСвойства");
                                                                                        
	ЭлТовар 	= ЭлементКаталога.ВыбратьУзел(зКаталог + зТовар);
	
	Попытка
		ИдРодителя	= ЭлТовар.ПолучитьАтрибут("Родитель");
	Исключение
		ИдРодителя	= "";
	КонецПопытки;
	
	Пока ПустоеЗначение(ИдРодителя) = 0 Цикл
	    ЭлРодитель	= ЭлементКаталога.ВыбратьУзел(зКаталог + "/Группа[@Идентификатор=""" + ИдРодителя  + """]");
		зГруппа 	= "/Группа[@Идентификатор=""" + ИдРодителя + """]";
		ИдРодителя	= ЭлРодитель.ПолучитьАтрибут("Родитель");
		
		ДобавитьЗначенияВСписокПоЗапросу(Список, зКаталог + зГруппа + зСсылкаНаСвойство, "ИдентификаторСвойства");		
	КонецЦикла;
	
    Возврат Список;
	
КонецФункции		//	СписокОбязательныхСвойствТовара()

//******************************************************************************
// УстановитьАтрибутыКаталога(Эл)
//
// Параметры:
// 	Эл - xml-элемент каталога
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Устанавливает атрибуты элемента "Каталог"
//
Процедура УстановитьАтрибутыКаталога(Эл)
	
	ПроверитьИУстановитьАтрибут(Эл, "Идентификатор",	СокрЛП(ФормКаталог.Идентификатор));
	ПроверитьИУстановитьАтрибут(Эл, "Наименование",		СокрЛП(ФормКаталог.Наименование));
	ПроверитьИУстановитьАтрибут(Эл, "Владелец",			СокрЛП(ФормКаталог.ВладелецКаталога.Идентификатор)	);
	ПроверитьИУстановитьАтрибут(Эл, "Описание", 		СокрЛП(ФормКаталог.Комментарий));
	ПроверитьИУстановитьАтрибут(Эл, "Единица", 			СокрЛП(ФормКаталог.ЕдиницаПоУмолчанию.Наименование));
	
КонецПроцедуры		//	УстановитьАтрибутыКаталога()

//******************************************************************************
// УстановитьАтрибутыПакетаПредложений(Эл)
//
// Параметры:
// 	Эл	-	xml-элемент "ПакетПредложений"
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Устанавливает атрибуты элемента "ПакетПредложений"
//
Процедура УстановитьАтрибутыПакетаПредложений(Эл)
	
	ПроверитьИУстановитьАтрибут(Эл, "ИдентификаторКаталога",	СокрЛП(ФормКаталог.Идентификатор));
	ПроверитьИУстановитьАтрибут(Эл, "Владелец",					СокрЛП(ФормКаталог.ВладелецКаталога.Идентификатор)	);
	ПроверитьИУстановитьАтрибут(Эл, "ДействительноС", 			ФормДатаНач);
	ПроверитьИУстановитьАтрибут(Эл, "ДействительноДо", 			ФормДатаКон);
	ПроверитьИУстановитьАтрибут(Эл, "Описание", 				СокрЛП(ФормКомментарий));
	
КонецПроцедуры		//	УстановитьАтрибутыПакетаПредложений()

//******************************************************************************
// УстановитьАтрибутыПредложения(Эл, Товар, ИдТовара, ОстатокТовара, Цена)
//
// Параметры:
// 	Эл				-	xml-элемент "Предложение"
//	Товар			-	элемент справочника "Номенклатура"
//	ИдТовара		-	идентификатор товара
//	ОстатокТовара	-	остаток товара на склад
//	Цена			-	элемент справочника "Цены"
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Устанавливает атрибуты элемента "Предложение"
//
Процедура УстановитьАтрибутыПредложения(Эл, Товар, ИдТовара="", ОстатокТовара=0, Цена, Единица)

	
	Валюта	=	ТекущаяКатегорияЦен.Валюта;
	
	//Единица	=	Цена.Единица.Получить(ФормДатаКон);
	//Цена	=	Цена.Цена.Получить(ФормДатаКон);
	
	
	ПроверитьИУстановитьАтрибут(Эл, "ИдентификаторТовара",	ИдТовара);
	ПроверитьИУстановитьАтрибут(Эл, "Количество",			ОстатокТовара);
	ПроверитьИУстановитьАтрибут(Эл, "Цена",					Цена);
	Если ПустоеЗначение(Единица) = 0 Тогда
		ПроверитьИУстановитьАтрибут(Эл, "НормаУпаковки",	Единица.Коэффициент);
		ПроверитьИУстановитьАтрибут(Эл, "Единица",			СокрЛП(Единица.ОКЕИ.Наименование));
	КонецЕсли;
	ПроверитьИУстановитьАтрибут(Эл, "Валюта",				Валюта.Наименование);
	
КонецПроцедуры		//	УстановитьАтрибутыПредложения()

//******************************************************************************
// ИдТовараВСобственномКаталоге(Товар, ИдАтрибутаИдентификации)
//
// Параметры:
// 	Товар					-	элемент справочника "Номенклатура"
//	ИдАтрибутаИдентификации	-	имя атрибута идентификации
//
// Возвращаемое значение:
// 	Значение идентифицирующего атрибута
//
// Описание
// 	Возвращает значение идентифицирующего атрибута
//
Функция ИдТовараВСобственномКаталоге(Товар, ИдАтрибутаИдентификации)
	                                                                
	Если ИдАтрибутаИдентификации <> "ШтрихКод" Тогда
		Возврат( СокрЛП(Товар.ПолучитьАтрибут(ИдАтрибутаИдентификации)) );
	Иначе
		Возврат( СокрЛП(Товар.ОсновнаяЕдиница.ШтрихКод) );
	КонецЕсли;

КонецФункции		//	ИдТовараВСобственномКаталоге()

//******************************************************************************
// ДобавитьСсылкуНаТовар(Эл, Каталог, Тов, ИмяАтрибутаИдентификации)
//
// Параметры:
// 	Эл							-	xml-элемент - владелец
//	Каталог						-	элемент справочника "Каталоги"
//	Тов							-	элемент справочника "Номенклатура"
//	ИмяАтрибутаИдентификации	-	имя атрибута идентификации
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Создает элемент "СсылкаНаТовар" и устанавливает его атрибуты
//
Процедура ДобавитьСсылкуНаТовар(Эл, Каталог, Тов, ИмяАтрибутаИдентификации)
	
	ЭлСсылкаНаТовар = СоздатьПодчиненныйЭлемент(Эл, "СсылкаНаТовар");
	ПроверитьИУстановитьАтрибут(ЭлСсылкаНаТовар, "ИдентификаторКаталога",	СокрЛП(Каталог.Идентификатор));
	ПроверитьИУстановитьАтрибут(ЭлСсылкаНаТовар, "ИдентификаторВКаталоге",	ИдТовараВСобственномКаталоге(Тов, ИмяАтрибутаИдентификации) );
	
КонецПроцедуры		//	ДобавитьСсылкуНаТовар()

//******************************************************************************
// ДобавитьСвойство(Каталог, ВидСвойства, ДобавлятьСсылку)
//
// Параметры:
// 	Каталог             элемент справочника "Каталоги"
//	ВидСвойства		-	элемент справочника "ВидыСвойств"
//	ДобавлятьСсылку	-	если 1, то добавляется также элемент "СсылкаНаСвойство"
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Если свойство еще не добавлено, то добаляет его в каталог. Устанавливает элемент "СсылкаНаСвойство"
//
Процедура ДобавитьСвойство(Каталог, ВидСвойства, ДобавлятьСсылку=0)
	
	зКаталог	= "//Каталог[@Идентификатор=""" + СокрЛП(Каталог.Идентификатор) + """]";
	УжеДобавлено = ЭлКоммерческаяИнформация.ВыбратьУзел(зКаталог + "/Свойство[(@Идентификатор=""" + СокрЛП(ВидСвойства.ИдентификаторВКаталоге) +""")]/@Наименование");
	            
	Если ПустоеЗначение(УжеДобавлено) = 0 Тогда Возврат КонецЕсли;
	
	ЭлКаталог	= ЭлКоммерческаяИнформация.ВыбратьУзел(зКаталог);
	
	ЭлСвойствоКаталога = СоздатьПодчиненныйЭлемент(ЭлКаталог, "Свойство");
	
	ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Идентификатор",	СокрЛП(ВидСвойства.ИдентификаторВКаталоге));
	ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Наименование",		СокрЛП(ВидСвойства.Наименование));
	ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Множественность",	ВидСвойства.Множественность);
                                                                  
	Если ДобавлятьСсылку = 1 Тогда
		ЭлСвойствоКаталога = СоздатьПодчиненныйЭлемент(ЭлКаталог, "СсылкаНаСвойство");
		
		ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "ИдентификаторКаталога",	СокрЛП(Каталог.Идентификатор));
		ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "ИдентификаторСвойства",	СокрЛП(ВидСвойства.ИдентификаторВКаталоге));
		//ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Обязательно",				0);
	КонецЕсли;
	
КонецПроцедуры		//	ДобавитьСвойство()

//******************************************************************************
// ДобавитьЗначенияСвойств(Эл, Каталог, Тов, СписокОбязательныхСвойств)
//
// Параметры:
// 	Эл, Каталог, Тов, СписокОбязательныхСвойств
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Добавляет значения свойств товара
//
Процедура ДобавитьЗначенияСвойств(Эл, Каталог, Тов, СписокОбязательныхСвойств="")
	
	СписокЗаполненныхСвойств = СоздатьОбъект("СписокЗначений");
	
    СпрЗнСвойств = СоздатьОбъект("Справочник.СвойстваНоменклатуры");
	СпрЗнСвойств.ИспользоватьВладельца(Тов);
	СпрЗнСвойств.ВыбратьЭлементы(1);
	Пока СпрЗнСвойств.ПолучитьЭлемент() = 1 Цикл
		Если СпрЗнСвойств.ПометкаУдаления() = 1 Тогда Продолжить КонецЕсли;
		Если СпрЗнСвойств.ВидСвойства.Каталог = Каталог Тогда
			Если ФлВыгружатьКаталог	= 1 Тогда
				ДобавитьСвойство(Каталог, СпрЗнСвойств.ВидСвойства);
			КонецЕсли;
			ИдСвойства			= СокрЛП(СпрЗнСвойств.ВидСвойства.ИдентификаторВКаталоге);
			ИдКаталога			= СокрЛП(Каталог.Идентификатор);
			
			ЭлЗначениеСвойства	= СоздатьПодчиненныйЭлемент(Эл, "ЗначениеСвойства");
			ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторКаталога",	ИдКаталога);
			ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторСвойства",	ИдСвойства);
			ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "Значение",					СокрЛП(СпрЗнСвойств.ЗначениеСвойства.Наименование));
			                                 
			Если ФлВыгружатьКаталог	= 1 Тогда
				Запрос = "СсылкаНаСвойство[(@ИдентификаторКаталога=""%%%"")and(@ИдентификаторСвойства=""***"")]";
				Запрос = СтрЗаменить(Запрос, "%%%", ИдКаталога);
				Запрос = СтрЗаменить(Запрос, "***", ИдСвойства);
				
				Если ПустоеЗначение(Эл.ВыбратьУзел(Запрос)) = 1 Тогда
					ЭлСсылкаНаСвойство	= СоздатьПодчиненныйЭлемент(Эл, "СсылкаНаСвойство");
					ПроверитьИУстановитьАтрибут(ЭлСсылкаНаСвойство, "ИдентификаторКаталога",	ИдКаталога);
					ПроверитьИУстановитьАтрибут(ЭлСсылкаНаСвойство, "ИдентификаторСвойства",	ИдСвойства);
				КонецЕсли;
			КонецЕсли;
			
			Если СписокЗаполненныхСвойств.Принадлежит(ИдСвойства) = 0 Тогда
				СписокЗаполненныхСвойств.ДобавитьЗначение(ИдСвойства);
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
	
	
	//	Дополнительную информацию и наименование для печати добавим в виде свойств
	
	Комментарий 		=	СокрЛП(Тов.Комментарий);
	ПолнНаименование	=	СокрЛП(Тов.ПолнНаименование);
	
	Если ПустоеЗначение(Комментарий) = 0 Тогда
		ЭлЗначениеСвойства	= СоздатьПодчиненныйЭлемент(Эл, "ЗначениеСвойства");
		ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторКаталога",	СокрЛП(Каталог.Идентификатор));
		ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторСвойства",	"Комментарий");
		ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "Значение",					Комментарий);
	КонецЕсли;
	Если ПустоеЗначение(ПолнНаименование) = 0 Тогда
		ЭлЗначениеСвойства	= СоздатьПодчиненныйЭлемент(Эл, "ЗначениеСвойства");
		ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторКаталога",	СокрЛП(Каталог.Идентификатор));
		ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторСвойства",	"ПолноеНаименование");
		ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "Значение",					ПолнНаименование);
	КонецЕсли;
	
	
	// Проверка заполнения обязательных свойств
	
	Если ТипЗначенияСтр(СписокОбязательныхСвойств) = "СписокЗначений" Тогда
		Для Сч = 1 По СписокОбязательныхСвойств.РазмерСписка() Цикл
		    ИдОбязательногоСвойства = СписокОбязательныхСвойств.ПолучитьЗначение(Сч);
			Если СписокЗаполненныхСвойств.Принадлежит(ИдОбязательногоСвойства) = 0 Тогда
				зКаталог	= "//Каталог[@Идентификатор=""" + СокрЛП(Каталог.Идентификатор) + """]";
				ИмяСвойства = ЭлементКаталога.ВыбратьУзел(зКаталог + "/Свойство[(@Идентификатор=""" + ИдОбязательногоСвойства +""")]/@Наименование").Значение;
				Сообщить("Для товара """ + Тов + """ не задано значение обязательного свойства """ + ИмяСвойства + """");
			КонецЕсли;
		КонецЦикла;					
	КонецЕсли;	
	
КонецПроцедуры		//	ДобавитьЗначенияСвойств()
            
//******************************************************************************
// ДобавитьСвойстваКаталога(Эл, Каталог)
//
// Параметры:
// 	Эл, Каталог
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Добавляет виды свойств, используемые для указанного каталога
//
Процедура ДобавитьСвойстваКаталога(Эл, Каталог)
	
	СпрВидыСвойств = СоздатьОбъект("Справочник.ВидыСвойств");
	СпрВидыСвойств.ВыбратьЭлементыПоРеквизиту("Каталог", Каталог, 0, 0);
	Пока СпрВидыСвойств.ПолучитьЭлемент() = 1 Цикл		
		ЭлСвойствоКаталога = СоздатьПодчиненныйЭлемент(Эл, "Свойство");
		
		ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Идентификатор",	СпрВидыСвойств.ИдентификаторВКаталоге );
		ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Наименование",		СпрВидыСвойств.Наименование );
		ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Множественность",	СпрВидыСвойств.Множественность );
	КонецЦикла;	
	
КонецПроцедуры		//	ДобавитьСвойстваКаталога()

//******************************************************************************
// УстановитьАтрибутыТовара(Эл, Тов, БезРодителя)
//
// Параметры:
// 	Эл, Тов, БезРодителя
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Устанавливает атрибуты элемента "Товар"
//
Процедура УстановитьАтрибутыТовара(Эл, Тов, БезРодителя=0)
	
	Если Тов.ЭтоГруппа() = 1 Тогда
		ИдВКаталоге = Тов.Код;
	Иначе
		ИдВКаталоге = ИдТовараВСобственномКаталоге(Тов, ИмяРеквизитаИдентификации);
	КонецЕсли;
		
	ПроверитьИУстановитьАтрибут(Эл, "Идентификатор",			Ид(Эл, Тов.ТекущийЭлемент()));
	ПроверитьИУстановитьАтрибут(Эл, "ИдентификаторВКаталоге",	ИдВКаталоге);
	ПроверитьИУстановитьАтрибут(Эл, "Наименование",				Тов.Наименование);
	ПроверитьИУстановитьАтрибут(Эл, "Родитель",					?(((ПустоеЗначение(Тов.Родитель)=1) Или (БезРодителя=1)), "", Ид(Эл, Тов.Родитель.ТекущийЭлемент())));
	ПроверитьИУстановитьАтрибут(Эл, "Единица",					СокрЛП(Тов.ОсновнаяЕдиница.ОКЕИ.Наименование));
	
КонецПроцедуры		//	УстановитьАтрибутыТовара()
    
//******************************************************************************
// УстановитьАтрибутыФирмы(Эл, Фирма)
//
// Параметры:
// 	Эл, Фирма
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Устанавливает атрибуты элемента "Контрагент"
//
Процедура УстановитьАтрибутыФирмы(Эл, Фирма)
	                                  
	Если Фирма.Вид() = "Фирмы" Тогда
		ЮрЛицо	=	Фирма.ЮрЛицо;
	Иначе
		ЮрЛицо	=	Фирма.ЮрФизЛицо;
	КонецЕсли;
	
	Фирма.ИспользоватьДату(РабочаяДата());
	
	Идентификатор				=	СокрЛП(Фирма.Идентификатор);
	Наименование				=	СокрЛП(ЮрЛицо.Наименование);
	ОтображаемоеНаименование	=	?(ПустоеЗначение(ЮрЛицо.ПолнНаименование)=1, Наименование, ЮрЛицо.ПолнНаименование);
    Адрес						=	СокрЛП(ЮрЛицо.ФактАдрес);
	ЮридическийАдрес			=	СокрЛП(ЮрЛицо.ЮрАдрес);
	
	//Сайт						=	"";
	//Комментарий					=	"";
	
    ПроверитьИУстановитьАтрибут(Эл, "Идентификатор", 			Идентификатор				);
	ПроверитьИУстановитьАтрибут(Эл, "Наименование",				Наименование				);
	ПроверитьИУстановитьАтрибут(Эл, "ОтображаемоеНаименование",	ОтображаемоеНаименование	);
	ПроверитьИУстановитьАтрибут(Эл, "Адрес",					Адрес						);
	ПроверитьИУстановитьАтрибут(Эл, "ЮридическийАдрес",			ЮридическийАдрес			);	
	
	//ПроверитьИУстановитьАтрибут(Эл, "Сайт",						Сайт	);
	//ПроверитьИУстановитьАтрибут(Эл, "Комментарий",				Комментарий	);
	
КонецПроцедуры		//	УстановитьАтрибутыФирмы()

//******************************************************************************
// ДобавитьГруппу(Эл, Группа)
//
// Параметры:
// 	Эл, Группа
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Добавляет подчиненный элемент каталога "Группа"
//
Процедура ДобавитьГруппу(Эл, Группа)
	
	ИдГруппы	= Ид(Эл, Группа.ТекущийЭлемент());
	Узел 		= Эл.ВыбратьУзел("Группа[(@Идентификатор=""" + СокрЛП(ИдГруппы) + """)]");
	
	Если ПустоеЗначение(Узел) = 0 Тогда Возврат КонецЕсли;
	
	ЭлГруппа = СоздатьПодчиненныйЭлемент(Эл, "Группа");
	    
	БезРодителя = 0;
	//Если СписокТоваров.Принадлежит(Группа.Родитель.ТекущийЭлемент()) = 1 Тогда
	//	БезРодителя = 0;
	//КонецЕсли;
	УстановитьАтрибутыТовара(ЭлГруппа, Группа, БезРодителя);
	
КонецПроцедуры		//	ДобавитьГруппу()

//******************************************************************************
// ВыгрузкаВладельца(ЭлКорень, Фирма)
//
// Параметры:
// 	ЭлКорень, Фирма
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Создает и заполняет элемент "Контрагент" - владелец каталога
//
Процедура ВыгрузкаВладельца(ЭлКорень, Фирма)
	
	Фирма.ИспользоватьДату(РабочаяДата());
	
	ЭлФирма = СоздатьПодчиненныйЭлемент(ЭлКорень, "Контрагент");
	
	УстановитьАтрибутыФирмы(ЭлФирма, Фирма);
	
	ЭлКонтакт = СоздатьПодчиненныйЭлемент(ЭлФирма, "Контакт");
	
	ЭлКонтакт.УстановитьАтрибут("Идентификатор",	ЭлКонтакт.ПреобразоватьВ_ИД("Фирма_" + Фирма.Код) );
	ЭлКонтакт.УстановитьАтрибут("Наименование",		"Контакт фирмы '"+СокрЛП(Фирма.Наименование)+"'"); 

	//СоздатьПодчиненныйЭлемент(ЭлКонтакт, "КонтактноеЛицо",	СокрЛП(Фирма.Руководитель));
	//СоздатьПодчиненныйЭлемент(ЭлКонтакт, "КонтактноеЛицо",	СокрЛП(Фирма.Кассир));
	//СоздатьПодчиненныйЭлемент(ЭлКонтакт, "КонтактноеЛицо",	СокрЛП(Фирма.ГлБухгалтер));
	СоздатьПодчиненныйЭлемент(ЭлКонтакт, "Телефон",			СокрЛП(Фирма.ЮрЛицо.Телефоны));
	СоздатьПодчиненныйЭлемент(ЭлКонтакт, "Почта",			СокрЛП(Фирма.ЭлПочта));
	
КонецПроцедуры		//	ВыгрузкаВладельца()

//******************************************************************************
// РаскрутитьРодителейДоВерхнегоУровня(ЭлРодитель, Группа)
//
// Параметры:
//  ЭлРодитель	-	элемент-родитель - "Товар" или "Группа"
//	Группа		-	группа или элемент  справочника "Номенклатура", родителей которой добавляем
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Добавляет родителей товара или группы вплоть Документ самого верхнего уровня
//
Процедура РаскрутитьРодителейДоВерхнегоУровня(ЭлКаталог, Группа)
	
	Если ПустоеЗначение(Группа) = 0 Тогда
		ДобавитьГруппу(ЭлКаталог, Группа);
		РаскрутитьРодителейДоВерхнегоУровня(ЭлКаталог, Группа.Родитель);
	КонецЕсли;
	
КонецПроцедуры		//	РаскрутитьРодителейДоВерхнегоУровня()

//******************************************************************************
// ДобавитьДопСвойствоТоваров(ЭлКаталог, ИдСвойства, ИмяСвойства)
//
// Параметры:
// 	ЭлКаталог, ИдСвойства, ИмяСвойства
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Служит для добавления произвольных свойств
//
Процедура ДобавитьДопСвойствоТоваров(ЭлКаталог, ИдСвойства, ИмяСвойства)
	
	ЭлСвойствоКаталога = СоздатьПодчиненныйЭлемент(ЭлКаталог, "Свойство");
	
	ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Идентификатор",	ИдСвойства );
	ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Наименование",		ИмяСвойства );

КонецПроцедуры		//	ДобавитьДопСвойствоТоваров()

//******************************************************************************
// ВыгрузкаКаталога(ЭлКорень)
//
// Параметры:
// 	ЭлКорень - корневой xml-элемент
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Выполняет выгрузку товарных позиций каталога
//
Процедура ВыгрузкаКаталога(ЭлКорень)	
	    
	Сч			=	0;
	ЭлКаталог	=	СоздатьПодчиненныйЭлемент(ЭлКорень, "Каталог");
	
	УстановитьАтрибутыКаталога(ЭлКаталог);
          
	//ДобавитьСвойстваКаталога(ЭлКаталог, ФормКаталог);
	
	ДобавитьДопСвойствоТоваров(ЭлКаталог, "Комментарий",		"Дополнительная информация");
	ДобавитьДопСвойствоТоваров(ЭлКаталог, "ПолноеНаименование", "Полное наименование");
	
	
	Если ТипЗначенияСтр(СписокТоваров) = "СписокЗначений" Тогда
		Для СчТоваров = 1 По СписокТоваров.РазмерСписка() Цикл
			Тов = СписокТоваров.ПолучитьЗначение(СчТоваров);
			
			Сч = Сч + 1;
			Если Сч % 100 = 0 Тогда
				Состояние("Выгрузка каталога. Выгружено товаров:  " + Сч);
			КонецЕсли;
			
			Если Тов.ЭтоГруппа() = 1 Тогда
				ДобавитьГруппу(ЭлКаталог, Тов);
			Иначе
				Если ПустоеЗначение(ИдТовараВСобственномКаталоге(Тов, ИмяРеквизитаИдентификации)) = 1 Тогда Продолжить КонецЕсли;
				Эл = СоздатьПодчиненныйЭлемент(ЭлКаталог, "Товар");
				ДобавитьЗначенияСвойств(Эл, ФормКаталог, Тов);
				УстановитьАтрибутыТовара(Эл, Тов.ТекущийЭлемент());
				РаскрутитьРодителейДоВерхнегоУровня(ЭлКаталог, Тов.Родитель);
			КонецЕсли;			
		КонецЦикла;
	Иначе
		
		СпрТоваров = СоздатьОбъект("Справочник.Номенклатура");
		СпрТоваров.ВыбратьЭлементы();
		Пока СпрТоваров.ПолучитьЭлемент() = 1 Цикл
			
			Сч = Сч + 1;
			Если Сч % 100 = 0 Тогда
				Состояние("Выгрузка каталога. Выгружено товаров:  " + Сч);
			КонецЕсли;
			
			Если СпрТоваров.ЭтоГруппа() = 1 Тогда
				Эл = СоздатьПодчиненныйЭлемент(ЭлКаталог, "Группа");
			Иначе
				Если ПустоеЗначение(ИдТовараВСобственномКаталоге(СпрТоваров, ИмяРеквизитаИдентификации)) = 1 Тогда Продолжить КонецЕсли;
				Эл = СоздатьПодчиненныйЭлемент(ЭлКаталог, "Товар");
				ДобавитьЗначенияСвойств(Эл, ФормКаталог, СпрТоваров);
			КонецЕсли;			
			УстановитьАтрибутыТовара(Эл, СпрТоваров.ТекущийЭлемент());
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры		//	ВыгрузкаКаталога()

//******************************************************************************
// СоздатьПредложение(ЭлПакет, Тов)
//
// Параметры:
// 	ЭлПакет, Тов
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Создает и заполняет элемент "Предложение"  
//
Процедура СоздатьПредложение(ЭлПакет, Тов)

	ОстатокТовара = 0;
	Если (ФлТолькоИмеющиеся = 1) Или (ФлВыгружатьКоличество = 1) Тогда
		ОстатокТовара = ОстатокТовара(Тов);
		Если (ОстатокТовара <= 0) И (ФлТолькоИмеющиеся = 1) Тогда Возврат КонецЕсли;
	КонецЕсли;
	
	ИдТовара	=	"";
	Цена		=	"";
	ЕдиницаЦены	=	"";
	
	Если ФормКаталог.ВладелецКаталога.Вид() = "Фирмы" Тогда
		
		//Если ПроверитьТовар(Тов) = 0 Тогда Возврат КонецЕсли;
		                                                  
		Если глВернутьЦену(Тов, ТекущаяКатегорияЦен, ФормДатаКон, Цена, ЕдиницаЦены) = 0 Тогда
			Сообщить("Не задана цена: """ + ТекущаяКатегорияЦен.Наименование + """ на товар: " + Тов.Наименование);
			Возврат;
		КонецЕсли;
		
		
		ЭлПредложение = СоздатьПодчиненныйЭлемент(ЭлПакет, "Предложение");
		
		Если ФлВыгружатьКаталог = 1 Тогда
			ИдТовара = Ид(ЭлПредложение, Тов.ТекущийЭлемент());
		Иначе
			ДобавитьСсылкуНаТовар(ЭлПредложение, ФормКаталог, Тов, ИмяРеквизитаИдентификации);
		КонецЕсли;
		                                                            
		УстановитьАтрибутыПредложения(ЭлПредложение, Тов, ИдТовара, ОстатокТовара, Цена, ЕдиницаЦены);
		    
		Если ФлВыгружатьКаталог	= 0 Тогда
			ДобавитьЗначенияСвойств(ЭлПредложение, ФормКаталог, Тов);
		КонецЕсли;
		
	Иначе
		
		СпрАналогов	= СоздатьОбъект("Справочник.Аналоги");
		СпрАналогов.ИспользоватьВладельца(Тов);
		
		Если СпрАналогов.ВыбратьЭлементыПоРеквизиту("Каталог", ФормКаталог, 1, 0) = 1 Тогда
			Если глВернутьЦену(Тов, ТекущаяКатегорияЦен, ФормДатаКон, Цена, ЕдиницаЦены) = 0 Тогда
				Сообщить("Не задана цена: """ + ТекущаяКатегорияЦен.Наименование + """ на товар: " + Тов.Наименование);
				Возврат;
			КонецЕсли;
		Иначе
			Возврат;
		КонецЕсли;
		
		Пока СпрАналогов.ПолучитьЭлемент() = 1 Цикл
			ЭлПредложение = СоздатьПодчиненныйЭлемент(ЭлПакет, "Предложение");
			
			ДобавитьСсылкуНаТовар(ЭлПредложение, ФормКаталог, СпрАналогов, "ИдентификаторВКаталоге");
			    
			СписокОбязательныхСвойств = "";
			Если ПустоеЗначение(ЭлементКаталога) = 0 Тогда
				СписокОбязательныхСвойств = СписокОбязательныхСвойствТовара(ФормКаталог, СпрАналогов.ИдентификаторВКаталоге);
			КонецЕсли;
			                             
			Если ФлВыгружатьКаталог	= 0 Тогда
				ДобавитьЗначенияСвойств(ЭлПредложение, ФормКаталог, Тов, СписокОбязательныхСвойств);
			КонецЕсли;
			
			УстановитьАтрибутыПредложения(ЭлПредложение, Тов, , ОстатокТовара, Цена, ЕдиницаЦены);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры		//	СоздатьПредложение()

//******************************************************************************
// ДобавитьЗначСвойствПоУмолчанию(Эл)
//
// Параметры:
// 	Эл - элемент каталога
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Добавляет в каталог указанные в диалоге знаения свойств по умолчанию
//
Процедура ДобавитьЗначСвойствПоУмолчанию(Эл)
	
    Для Сч = 1 По ФормСписЗначенийСвойств.РазмерСписка() Цикл
		
		ЗнСвойства	= ФормСписЗначенийСвойств.ПолучитьЗначение(Сч);
        ВидСвойства	= ЗнСвойства.Владелец;
		
		Если ФлВыгружатьКаталог	= 1 Тогда
			ДобавитьСвойство(ФормКаталог, ВидСвойства, 1);
		КонецЕсли;
		
		ЭлЗначениеСвойства	= СоздатьПодчиненныйЭлемент(Эл, "ЗначениеСвойства");
		
		ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторКаталога",	СокрЛП(ВидСвойства.Каталог.Идентификатор) );
		ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторСвойства",	СокрЛП(ВидСвойства.ИдентификаторВКаталоге) );
		ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "Значение",					СокрЛП(ЗнСвойства.Наименование) );
		
	КонецЦикла;
	
КонецПроцедуры		//	ДобавитьЗначСвойствПоУмолчанию()

//******************************************************************************
// ДобавитьСвойство_ТипЦены(Эл)
//
// Параметры:
// 	Эл - элемент каталога
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Добаляет предопределенное свойство каталога "ТипЦены"
//
Процедура ДобавитьСвойство_ТипЦены(Эл)
	
	ВидСвойства	= СоздатьОбъект("Справочник.ВидыСвойств");
	ВидСвойства.Новый();
	ВидСвойства.Наименование			= "Тип цены";
	ВидСвойства.Множественность			= 0;
	ВидСвойства.ИдентификаторВКаталоге	= ВидСвойства.Код;
	ВидСвойства.Каталог					= ФормКаталог.ТекущийЭлемент();
	
	Если ФлВыгружатьКаталог	= 1 Тогда
		ДобавитьСвойство(ФормКаталог, ВидСвойства, 1);
	КонецЕсли;
	
	ИдСвойства			= СокрЛП(ВидСвойства.ИдентификаторВКаталоге);
	ЭлЗначениеСвойства	= СоздатьПодчиненныйЭлемент(Эл, "ЗначениеСвойства");
	ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторКаталога",	СокрЛП(ФормКаталог.Идентификатор));
	ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторСвойства",	ИдСвойства);
	ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "Значение",					СокрЛП(ТекущаяКатегорияЦен.Наименование));
	
КонецПроцедуры		//	ДобавитьСвойство_ТипЦены()

//******************************************************************************
// ВыгрузкаПредложений(ЭлКорень)
//
// Параметры:
// 	ЭлКорень - корневой xml-узел
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Выполняет выгрузку предложений по установленному текущему типу цен
//
Процедура ВыгрузкаПредложений(ЭлКорень)
	              
	Сч		=	0;
	
	ЭлПакет	=	СоздатьПодчиненныйЭлемент(ЭлКорень, "ПакетПредложений");	
	УстановитьАтрибутыПакетаПредложений(ЭлПакет);
	                                
	ДобавитьСвойство_ТипЦены(ЭлПакет);
	ДобавитьЗначСвойствПоУмолчанию(ЭлПакет);
	
	Если ПустоеЗначение(СписокТоваров) = 0 Тогда
		Для СчТоваров = 1 По СписокТоваров.РазмерСписка() Цикл
			Тов = СписокТоваров.ПолучитьЗначение(СчТоваров);
			              
			Если ПустоеЗначение(ИдТовараВСобственномКаталоге(Тов, ИмяРеквизитаИдентификации)) = 1 Тогда
				Сообщить("У товара: `" + Тов + "` не установлен реквизит идентификации: " + ИмяРеквизитаИдентификации);
				Продолжить;
			КонецЕсли;
			
			Сч = Сч + 1;
			Если Сч % 100 = 0 Тогда
				Состояние("Выгрузка предложений по цене: " + ТекущаяКатегорияЦен + "  Обработано товаров:  " + Сч);
			КонецЕсли;
			
			СоздатьПредложение(ЭлПакет, Тов);
		КонецЦикла;
	Иначе
		СпрТоваров = СоздатьОбъект("Справочник.Номенклатура");
		СпрТоваров.ВыбратьЭлементы();
		Пока СпрТоваров.ПолучитьЭлемент() = 1 Цикл
			Если СпрТоваров.ЭтоГруппа() = 1 Тогда Продолжить КонецЕсли;
			
			Если ПустоеЗначение(ИдТовараВСобственномКаталоге(СпрТоваров, ИмяРеквизитаИдентификации)) = 1 Тогда
				Сообщить("У товара: `" + СпрТоваров + "` не установлен реквизит идентификации: " + ИмяРеквизитаИдентификации);
				Продолжить;
			КонецЕсли;
			
			Сч = Сч + 1;
			Если Сч % 100 = 0 Тогда
				Состояние("Выгрузка предложений по цене: " + ТекущаяКатегорияЦен + "  Обработано товаров:  " + Сч);
			КонецЕсли;
			
			СоздатьПредложение(ЭлПакет, СпрТоваров);
		КонецЦикла;		
	КонецЕсли;    
	
КонецПроцедуры		//	ВыгрузкаПредложений()

//******************************************************************************
// ВыбратьЗначения(Способ, Реж)
//
// Параметры:
// 	Способ, Реж
//
// Возвращаемое значение:
// 	Нет.
//
// Вызывается из формул элементов диалога:
//	См. кнопки подбора значений свойсвт по умолчанию
//
// Описание
// 	Открывает подбор значений видов свойств
//
Процедура ВыбратьЗначения(Способ, Реж)
	
	РежимВыбораЗначенийСвойства = Реж;
	
	Тек = "";
	Если ФормСписЗначенийСвойств.ТекущаяСтрока() > 0 Тогда
		Тек = ФормСписЗначенийСвойств.ПолучитьЗначение(ФормСписЗначенийСвойств.ТекущаяСтрока()).Владелец;
	КонецЕсли;
	
	Фрм	= ФормКаталог;
	ОткрытьПодбор("Справочник.ВидыСвойств", "ДляПодбора", Фрм, 1, Тек);
	
КонецПроцедуры		//	ВыбратьЗначения()

//******************************************************************************
// УдалитьЗначение(Спис, Реж)
//
// Параметры:
// 	Спис, Реж
//
// Возвращаемое значение:
// 	Нет.
//
// Вызывается из формул элементов диалога:
//	См. кнопки подбора значений свойсвт по умолчанию
//
// Описание
// 	Удаляет текущую строку из списка значений видов свойств по умолчанию
//
Процедура УдалитьЗначение(Спис, Реж="")
	
	Если Реж = "Все" Тогда
		Спис.УдалитьВсе();
	Иначе
		Поз	= Спис.ТекущаяСтрока();	
		Если Поз = 0 Тогда Возврат КонецЕсли;
		
		УдПоз = Спис.ТекущаяСтрока();
		Спис.УдалитьЗначение(Поз);
		Если Спис.РазмерСписка() < УдПоз Тогда 
			Спис.ТекущаяСтрока(УдПоз - 1);
		Иначе 
			Спис.ТекущаяСтрока(УдПоз);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры		//	УдалитьЗначение()

//******************************************************************************
// Сформировать()
//
// Параметры:
// 	Нет
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Базовая процедура - последовательно вызывает процедуры выгрузки каталога, пакетов предложений и т.д.
//
Процедура Сформировать()
	                  
	Если ФлОтправить = 0 Тогда	// Выгружаем в файл
		
		Если ПустаяСтрока(ФормИмяФайла)>0 Тогда
			Предупреждение("Не задано имя файла!");
		    Возврат;
		КонецЕсли;	
		    
		Попытка
			Если ФС.СуществуетФайл(СокрЛП(ФормИмяФайла)) > 0 Тогда
				Если Вопрос("Файл '"+СокрЛП(ФормИмяФайла)+"' существует. Переписать?","Да+Нет", 60)="Нет" Тогда
					Возврат;
				КонецЕсли;	
			КонецЕсли;	
		Исключение
			Предупреждение("Неверное имя файла!");
			Возврат;
		КонецПопытки;	
		    
		ИмяФайлаВыгрузки	=	СокрЛП(ФормИмяФайла);
		
	Иначе						// Выгружаем во временный файл и отправляем...
		
		Если ФормСайт.Выбран() = 0 Тогда
			Предупреждение("Не задан сайт!");
			Возврат;
		КонецЕсли;
		
		Если ПустаяСтрока(ФормИмяФайла) = 0 Тогда
			ИмяФайлаВыгрузки	=	СокрЛП(ФормИмяФайла);
		Иначе
			ИмяФайлаВыгрузки	=	КаталогИБ() + "Price.xml";
		КонецЕсли;
		
	КонецЕсли;
	
	
	Если ФормКаталог.Выбран()=0 Тогда
		Предупреждение("Не выбран каталог товаров!");
		Возврат;
	КонецЕсли;
	

	Если ПустоеЗначение( ФормКаталог.ВладелецКаталога.Идентификатор ) = 1 Тогда
		Предупреждение("Не указан идентификатор владельца каталога!");
		Возврат;
	КонецЕсли;	
    
	
	ФлТипЦенЗадан = 0;
	Для Сч = 1 По ФормСписКатегорийЦен.РазмерСписка() Цикл
	    Если ФормСписКатегорийЦен.Пометка(Сч) = 1 Тогда
			ФлТипЦенЗадан = 1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ФлТипЦенЗадан = 0 Тогда
		Предупреждение("Не отмечена ни одна из категорий цен!");
		Возврат;
	КонецЕсли;
	           
	
	Если ЗагрузитьВнешнююКомпоненту("V7Plus.dll") <> 1 Тогда
        Сообщить("Ошибка загрузки компоненты V7+");
		Возврат ;
	КонецЕсли;
	
	
	Если ПустоеЗначение(ФормКаталог.СпособИдентификацииНоменклатуры) = 1 Тогда
		ИмяРеквизитаИдентификации	=	"Код";
	Иначе
		ИмяРеквизитаИдентификации	=	ФормКаталог.СпособИдентификацииНоменклатуры.Идентификатор();
	КонецЕсли;
	
	
	Анализатор	= СоздатьОбъект("AddIn.XMLParser");
	Документ	= Анализатор.СоздатьДокумент();
	
	
	Если ПустоеЗначение(Документ) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	
	Документ.ЗагрузитьИзСтроки("<?xml version =""1.0""?><BizTalk xmlns = ""urn:schemas-biztalk-org:biztalk/biztalk-0.81.xml""><Route><From locationID = """" locationType = """" process = """" path = """" handle = """"/><To locationID = """" locationType = """" process = """" path = """" handle = """"/></Route><Body/></BizTalk>");
	
	
	Попытка     
		СхемыДляПроверки = Анализатор.СоздатьКоллекциюСхем();
		//СхемыДляПроверки.ДобавитьСхему("urn:schemas-biztalk-org:biztalk/biztalk-0.81.xml",КаталогИБ()+"ExtForms\EmptyBizTalkSchema.xdr");
		СхемыДляПроверки.ДобавитьСхему(ИДСхемыДляПроверки, КаталогИБ() + "ExtForms\CommerML.biz");
		Документ.Схемы = СхемыДляПроверки;		
	Исключение   
		Предупреждение("Неправильная схема XML-файла: " + ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
        
	ЭлементКаталога = ПолучитьПустоеЗначение();
	
	Если (ФормКаталог.ВладелецКаталога.Вид() = "Контрагенты") И (ПустоеЗначение(ФормКаталог.XMLФайл) = 0) Тогда
		Если Фс.СуществуетФайл(ФормКаталог.XMLФайл) = 1 Тогда
			ЭлементКаталога		= Анализатор.СоздатьДокумент();
			ЭлементКаталога.Схемы	= СхемыДляПроверки;
			ЭлементКаталога.Загрузить(ФормКаталог.XMLФайл);
		КонецЕсли;
	КонецЕсли;	
	
	
	
	ЭлКоммерческаяИнформация = СоздатьПодчиненныйЭлемент(Документ.ЭлементДокумента.ВыбратьУзел("Body"), "КоммерческаяИнформация");
	
	
	Если ФлВыгружатьВладельца = 1	Тогда
		ВыгрузкаВладельца(ЭлКоммерческаяИнформация, ФормКаталог.ВладелецКаталога);
	//Иначе
	//	ВыгрузкаВладельца(ЭлКоммерческаяИнформация, Константа.ОсновнаяФирма);
	КонецЕсли;
	        
	
	Если ФлВыгружатьКаталог	= 1		Тогда ВыгрузкаКаталога(ЭлКоммерческаяИнформация)	КонецЕсли;
	
	                   
	Для Сч = 1 По ФормСписКатегорийЦен.РазмерСписка() Цикл
	    Если ФормСписКатегорийЦен.Пометка(Сч) = 0 Тогда Продолжить КонецЕсли;
		ТекущаяКатегорияЦен	= ФормСписКатегорийЦен.ПолучитьЗначение(Сч);
		ВыгрузкаПредложений(ЭлКоммерческаяИнформация);
	КонецЦикла;
	
	
	Попытка
		//Документ.Проверить();	    
	Исключение
		Предупреждение(ОписаниеОшибки());
		Возврат;
	КонецПопытки;		
	
	
	Попытка     
		Документ.Записать(СокрЛП(ИмяФайлаВыгрузки));
	Исключение   
		Предупреждение("Не удалось записать файл " + СокрЛП(ФормИмяФайла) + ": " + ОписаниеОшибки());
		Возврат;
	КонецПопытки;	                                                                      
	
	
	Если ФлОтправить = 1 Тогда
		СписПараметров = СоздатьОбъект("СписокЗначений");
		СписПараметров.ДобавитьЗначение(ФормСайт.ТекущийЭлемент(),	"Сайт");
		СписПараметров.ДобавитьЗначение(СокрЛП(ИмяФайлаВыгрузки),		"ПолноеИмяФайла");
		
		ОткрытьФормуМодально("Обработка.XMLОтправкаФайлаПоHTTP", СписПараметров);
		
		Если СокрЛП(ИмяФайлаВыгрузки) <> СокрЛП(ФормИмяФайла) Тогда
			Если ФС.СуществуетФайл(СокрЛП(ИмяФайлаВыгрузки)) > 0 Тогда
				ФС.УдалитьФайл(СокрЛП(ИмяФайлаВыгрузки));
			КонецЕсли;
		КонецЕсли;
	Иначе
		Предупреждение("Выгрузка завершена!");
	КонецЕсли;
	                                      
КонецПроцедуры		//	Сформировать()

//******************************************************************************
// ВыборФайла()
//
// Параметры:
// 	Нет
//
// Возвращаемое значение:
// 	Нет.
//
// Вызывается из формул элементов диалога:
//	Кнопка выбора файла выгрузки
//
// Описание
// 	выбор файла в диалоге
//
Процедура ВыборФайла()
	
	Каталог = "";
	Если ФС.ВыбратьФайл(0, ФормИмяФайла, Каталог, "Выберите файл", "*.xml|*.xml", ".xml", ) = 1 Тогда
	    ФормИмяФайла = Каталог+ФормИмяФайла;
	КонецЕсли;
	
КонецПроцедуры		//	ВыборФайла()

//******************************************************************************
// ОткрытьФайл()
//
// Параметры:
// 	Нет
//
// Возвращаемое значение:
// 	Нет.
//
// Описание
// 	Открывает файл для просмотра
//
Процедура ОткрытьФайл()
	
	Если ФС.СуществуетФайл(ФормИмяФайла)=1 Тогда
		ЗапуститьПриложение(ФормИмяФайла);
	Иначе
		Предупреждение("Указанный файл не существует!", 5);
	КонецЕсли;
	
КонецПроцедуры		//	ОткрытьФайл()

//******************************************************************************
// ПриВыбореКаталога(ОчиститьСписок)
//
// Параметры:
// 	Нет
//
// Возвращаемое значение:
// 	Нет.
//
// Вызывается из формул элементов диалога:
//	Педаль выбора каталога. Реквизит диалога "ФормКаталог"
//
// Описание
// 	Устанавливает видимость элементов, в зависиости от выбранного каталога
//
Процедура ПриВыбореКаталога(ОчиститьСписок=0)
	
	Если ФормКаталог.Выбран() = 1 Тогда
		Если ФормКаталог.ВладелецКаталога.Вид() = "Фирмы" Тогда
			Форма.ФлВыгружатьКаталог.Видимость(1);
			Форма.ФлВыгружатьВладельца.Видимость(1);
		Иначе
			ФлВыгружатьКаталог		= 0;
			ФлВыгружатьВладельца	= 0;			
			Форма.ФлВыгружатьКаталог.Видимость(0);
			Форма.ФлВыгружатьВладельца.Видимость(0);
		КонецЕсли;
	Иначе
		ФлВыгружатьКаталог		= 0;
		ФлВыгружатьВладельца	= 0;			
		Форма.ФлВыгружатьКаталог.Видимость(0);
		Форма.ФлВыгружатьВладельца.Видимость(0);		
	КонецЕсли;
	
	Если ОчиститьСписок = 1 Тогда
		ФормСписЗначенийСвойств.УдалитьВсе();
	КонецЕсли;
	
КонецПроцедуры		//	ПриВыбореКаталога()

//******************************************************************************
// ОтобратьТовары()
//
// Параметры:
// 	Нет
//
// Возвращаемое значение:
// 	Нет.
//
// Вызывается из формул элементов диалога:
//	Кнопка "Подбор номенклатуры"
//
// Описание
// 	Вызов универсальной обработкуи подбора объектов
//
Процедура ОтобратьТовары()
	
	Если ТипЗначенияСтр(СписокТоваров) <> "СписокЗначений" Тогда
		СписокТоваров	= СоздатьОбъект("СписокЗначений");
	КонецЕсли;
	
	СписокПараметровВызова = СоздатьОбъект("СписокЗначений");
	СписокПараметровВызова.ДобавитьЗначение("Справочник",									"Тип");
	СписокПараметровВызова.ДобавитьЗначение("Номенклатура",									"Вид");
	СписокПараметровВызова.ДобавитьЗначение(СписокТоваров,									"Объекты");
	                  
	Путь	=	"";
	Имя		=	"";
	РасположениеФайла(Путь, Имя);
	Если ПустоеЗначение(Путь + Имя) = 0 Тогда
		ИмяВызвавшейФормы = Путь + Имя;
	Иначе
		ИмяВызвавшейФормы = "Обработка.XMLВыгрузкаКоммерческихПредложений";
	КонецЕсли;
	СписокПараметровВызова.ДобавитьЗначение(ИмяВызвавшейФормы,	"ИмяВызвавшейФормы");

	ОткрытьФорму("Обработка.ПодборОбъектов#", СписокПараметровВызова);
	
КонецПроцедуры		//	ОтобратьТовары()

//******************************************************************************
// ПриВыбореФлОтправить()
//
// Параметры:
// 	Нет
//
// Возвращаемое значение:
// 	Нет.
//
// Вызывается из формул элементов диалога:
//	Флажок - "Отправить на сайт"
//
// Описание
// 	Управляет доступностью элемента диалога  "ФормСайт" - выбор веб-сайта
//
Процедура ПриВыбореФлОтправить()
	
    Если ФлОтправить = 1 Тогда
    	Форма.ФормСайт.Доступность(1);
    Иначе
    	Форма.ФормСайт.Доступность(0);
	КонецЕсли;
	
КонецПроцедуры		//	ПриВыбореФлОтправить()
          


////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	Форма.Закладки.УдалитьВсе();
	Форма.Закладки.ДобавитьЗначение("Основная",			"Основная");
	Форма.Закладки.ДобавитьЗначение("Дополнительная",	"Дополнительная");
	
	Форма.ИспользоватьСлой("Основной, Кнопки", 2);
	ТекущаяЗакладка = "Основная";
	
	
	Если Форма.Параметр = "TorgCenter" Тогда
		Сайты = СоздатьОбъект("Справочник.Сайты");
		Сайты.ВыбратьЭлементы();
		Пока Сайты.ПолучитьЭлемент() = 1 Цикл
			Если Найти(НРег(Сайты.АдресСайта), "torgcenter.ru") > 0 Тогда
				ФормСайт	=	Сайты.ТекущийЭлемент();
				ФлОтправить	=	1;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	
	ПриВыбореКаталога();
	ПриВыбореФлОтправить();
	    
КонецПроцедуры		//	ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриВыбореЗакладки(Ном, Значен)
	
	Если Значен = "Основная" Тогда
		ТекущаяЗакладка = "Основная";
		Форма.ИспользоватьСлой("Основной, Кнопки", 2);
		ПриВыбореКаталога();
	ИначеЕсли Значен = "Дополнительная" Тогда
		ТекущаяЗакладка = "Дополнительная";
		Форма.ИспользоватьСлой("Дополнительный, Кнопки", 2);
	КонецЕсли;
	
КонецПроцедуры		//	ПриВыбореЗакладки()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПодбора(Зн, КонтФормы)
	
	Перем Фрм;      
	
	Если Зн.Вид() = "ВидыСвойств" Тогда
		ОткрытьПодбор("Справочник.ЗначенияСвойств", ,Фрм, РежимВыбораЗначенийСвойства);
		
		Попытка Фрм.ИспользоватьВладельца(Зн)	Исключение КонецПопытки;
		Попытка Фрм.ВыборГруппы(0)				Исключение КонецПопытки;
			
        КонтФормы.Форма.Закрыть();
	Иначе
	    Если ФормСписЗначенийСвойств.Принадлежит(Зн) = 0 Тогда
	    	ФормСписЗначенийСвойств.ДобавитьЗначение(Зн, Зн.Наименование + " (" + Зн.Владелец.Наименование + ")");
	    Иначе
	    	Предупреждение("Это значение уже выбрано!");
	    КонецЕсли;
	КонецЕсли;

КонецПроцедуры		//	ОбработкаПодбора()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриПовторномОткрытии()
	
	Если ТипЗначенияСтр(СписокТоваров) = "СписокЗначений" Тогда
		Если СписокТоваров.РазмерСписка() = 0 Тогда
			СписокТоваров = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры		//	ПриПовторномОткрытии()


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ФормСписКатегорийЦен.УдалитьВсе();
СпрКатегорийЦен = СоздатьОбъект("Справочник.ТипыЦен");
СпрКатегорийЦен.ВыбратьЭлементы();
Пока СпрКатегорийЦен.ПолучитьЭлемент() = 1 Цикл
	Если СпрКатегорийЦен.ПометкаУдаления() = 1 Тогда Продолжить КонецЕсли;
	ФормСписКатегорийЦен.ДобавитьЗначение(СпрКатегорийЦен.ТекущийЭлемент());
КонецЦикла;

Форма.ИспользоватьЗакладки(1);

ФлВыгружатьКаталог		= 0;
ФлВыгружатьВладельца	= 0;
ФлВыгружатьКоличество	= 0;
ФлТолькоИмеющиеся		= 0;
ФлОтправить				= 0;

Форма.ФормСайт.Доступность(0);
Форма.ФлВыгружатьКаталог.Видимость(0);
Форма.ФлВыгружатьВладельца.Видимость(0);

ИДСхемыДляПроверки = "urn:CommerceML";

ФормДатаНач = РабочаяДата();
ФормДатаКон = РабочаяДата();

ФормКаталог.ВыборГруппы(0);

//******************************************************************************




