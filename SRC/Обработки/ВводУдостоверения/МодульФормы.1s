////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//*****************************************************************************
// ПриРедактировании(Реквизит)
//
// Параметры:
//  Реквизит - реквизит диалога.
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Поля ввода.
//
// Описание:
//  Проверяет значение реквизита на наличие запятых и удаляет их.
//
Процедура ПриРедактировании(Реквизит)
	
	Если 0 < Найти(Реквизит,",") Тогда
		Предупреждение("Значения реквизитов не должны содержать запятых!", 60);
		Реквизит = СтрЗаменить(Реквизит,",","")
	КонецЕсли;
	
КонецПроцедуры // ПриРедактировании()

//*****************************************************************************
// Выполнить()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "ОК"
//
// Описание:
//	Присваивает параметру формы строку, сформированную из реквизитов документа.
//
Процедура Выполнить()
	
	Перем Документ;
	
	Документ = СокрП(КемВыдан);
	Документ = ?(ПустоеЗначение(ДатаВыдачи)=1,"",Формат(ДатаВыдачи,"ДДДММГГГГ"))+?(ПустоеЗначение(Документ)=0,","+Документ,"");
	Документ = СокрП(Номер)+?(ПустоеЗначение(Документ)=0,","+Документ,"");
	Документ = СокрП(Серия)+?(ПустоеЗначение(Документ)=0,","+Документ,"");
	Форма.Параметр = СокрЛП(Вид)+?(ПустоеЗначение(Документ)=0,","+Документ,"")
	
КонецПроцедуры // Выполнить()

//******************************************************************************
//	Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	Список = глРазложитьДокУдостоверяющийЛичность(Форма.Параметр);
	Вид        = ?(0 < Список.РазмерСписка(), Список.ПолучитьЗначение(1), "");
	Серия      = ?(1 < Список.РазмерСписка(), Список.ПолучитьЗначение(2), "");
	Номер      = ?(2 < Список.РазмерСписка(), Список.ПолучитьЗначение(3), "");
	ДатаВыдачи = ?(3 < Список.РазмерСписка(), Список.ПолучитьЗначение(4), "");
	КемВыдан   = ?(4 < Список.РазмерСписка(), Список.ПолучитьЗначение(5), "");
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр)
	
	Если ИдентЭлемДиалога = "Вид" Тогда
		СтатусВозврата(0);
		
		СписВидов = СоздатьОбъект("СписокЗначений");
		
		СписВидов.ДобавитьЗначение("Паспорт гражданина России");
		СписВидов.ДобавитьЗначение("Загранпаспорт гражданина РФ");
		СписВидов.ДобавитьЗначение("Паспорт гражданина СССР");
		СписВидов.ДобавитьЗначение("Загранпаспорт гр. СССР");
		СписВидов.ДобавитьЗначение("Удостоверение личности офицера");
		СписВидов.ДобавитьЗначение("Военный билет солдата (матроса, сержанта, старшины)");
		СписВидов.ДобавитьЗначение("Военный билет офицера запаса");
		СписВидов.ДобавитьЗначение("Паспорт Минморфлота");
 		СписВидов.ДобавитьЗначение("Паспорт моряка");
		СписВидов.ДобавитьЗначение("Свидетельство о рождении");
		СписВидов.ДобавитьЗначение("Дипломатический паспорт гражданина РФ");
		СписВидов.ДобавитьЗначение("Временное удостоверение личности гражданина РФ");
		СписВидов.ДобавитьЗначение("Удостоверение беженца в РФ");
		СписВидов.ДобавитьЗначение("Вид на жительство");
		СписВидов.ДобавитьЗначение("Свидетельство о регистрации ходатайства о признании иммигранта беженцем");
		СписВидов.ДобавитьЗначение("Иностранный паспорт");
		СписВидов.ДобавитьЗначение("Справка об освобождении из места лишения свободы ");
		
		ВыбЗнач = Вид;
		Если СписВидов.ВыбратьЗначение(ВыбЗнач,,,,2)  = 1 Тогда
			Вид = ВыбЗнач;
		КонецЕсли;
		
	КонецЕсли
	
	
КонецПроцедуры // ПриНачалеВыбораЗначения()




