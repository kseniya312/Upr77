////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 

Перем	ПолноеИмяФайла;	//	полное имя пересылаемого файла
Перем	Сайт;			//	адрес веб-сайта

Перем	НТТРСервис;		//	объект http-сервиса
	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************

Процедура ПослатьФайл()                  
	
	Ответ = "";
	                                  
	Параметры	=	"?PostPrices"		+ 
					"&user=" 			+ СокрЛП(Сайт.Пользователь) + 
					"&pass="			+ СокрЛП(Сайт.Пароль);
	
	Попытка
		Адрес = СтрЗаменить(Сайт.АдресСайта, "torgcenter.ru", "torgcenter.ru/admin/cml1c.jsp") + Параметры;
		
		НТТРСервис.УдалитьЗаголовкиЗапроса();
		НТТРСервис.УстановитьЗаголовокЗапроса("Content-Type", "text/xml-SOAP");
		НТТРСервис.ОтправитьДляОбработки(Адрес, ПолноеИмяФайла, 1, Ответ, 2) ;
		               
		Предупреждение("Посылка файла завершена!");
	Исключение
		
		Для Сч = 1 По НТТРСервис.КоличествоЗаголовковОтвета() Цикл
		    Заголовок	=	НТТРСервис.ПолучитьЗаголовокОтвета(Сч);
			Содержание	=	НТТРСервис.ПолучитьСодержаниеЗаголовкаОтвета(Сч);
			                       
			Если Заголовок = "Location" Тогда
				Сообщить("Данный ресурс возможно перенесен на новый адрес:
						 |" + СтрЗаменить(Содержание, Параметры, ""));
			КонецЕсли;

		КонецЦикла;

        //	Это можно использовать при отладке обмена
		//Сообщить(ОписаниеОшибки());
		//Сообщить(НТТРСервис.СтрокаСостоянияОтклика);
		
	КонецПопытки;
	                   
КонецПроцедуры

//****************************************************************************** 

Процедура АктивизироватьСайт()
	
	Ответ		=	"";
	Источник	=	"123";
	
	Параметры	=	"/admin/auth1c.jsp?"	+ 
					"login="				+	СокрЛП(Сайт.Пользователь) + 
					"&pwd="					+	СокрЛП(Сайт.Пароль);
	             
	Адрес		=	Сайт.АдресСайта + Параметры;
					
	Попытка
		НТТРСервис.УдалитьЗаголовкиЗапроса();
		НТТРСервис.Пользователь(СокрЛП(Сайт.Пользователь), СокрЛП(Сайт.Пароль));
		НТТРСервис.ОтправитьДляОбработки(Адрес, Источник, 2, Ответ, 2) ;
		
		ЗапуститьПриложение(Ответ);
		
		
	Исключение
		
		
		Для Сч = 1 По НТТРСервис.КоличествоЗаголовковОтвета() Цикл
		    Заголовок	=	НТТРСервис.ПолучитьЗаголовокОтвета(Сч);
			Содержание	=	НТТРСервис.ПолучитьСодержаниеЗаголовкаОтвета(Сч);
			                       
			Если Заголовок = "Location" Тогда
				Сообщить("Данный ресурс возможно перенесен на новый адрес:
						 |" + СтрЗаменить(Содержание, Параметры, ""));
			КонецЕсли;

		КонецЦикла;

		Сообщить(ОписаниеОшибки());
		Сообщить(НТТРСервис.СтрокаСостоянияОтклика);
		
	КонецПопытки;
	                   
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()                  
	    
	СтатусВозврата(0);

	
	Если ЗагрузитьВнешнююКомпоненту(КаталогИБ() + "v7plus.dll") <> 1 Тогда
		Если ЗагрузитьВнешнююКомпоненту(КаталогИБ() + "ExtForms\" + "v7plus.dll") <> 1 Тогда		
			Если ЗагрузитьВнешнююКомпоненту(КаталогПрограммы() + "v7plus.dll") <> 1 Тогда
				Предупреждение("Компонента v7plus.dll не найдена!");
				Возврат;
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	            
	
	НТТРСервис = СоздатьОбъект("AddIn.V7HTTPReader");
	
	
	Если		Форма.Параметр = "TorgCenter" Тогда
		
		Сайты = СоздатьОбъект("Справочник.Сайты");
		Сайты.ВыбратьЭлементы();
		Пока Сайты.ПолучитьЭлемент() = 1 Цикл
			Если Найти(НРег(Сайты.АдресСайта), "torgcenter.ru") > 0 Тогда
				Сайт = Сайты.ТекущийЭлемент();
				АктивизироватьСайт();
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		Сообщить("В справочнике 'Сайты' не найдена настройка TorgCenter.Ru!
				 |Смотрите информацию о подключении.");
		
	ИначеЕсли	ПустоеЗначение(Форма.Параметр) = 0 Тогда
		
		Сайт			= Форма.Параметр.Получить("Сайт");
		ПолноеИмяФайла	= Форма.Параметр.Получить("ПолноеИмяФайла");

		Если ПустаяСтрока(ПолноеИмяФайла) = 1		Тогда Предупреждение("Не задано имя файла!"); 	Возврат;	КонецЕсли;
		Если ФС.СуществуетФайл(ПолноеИмяФайла) = 0	Тогда Предупреждение("Файл не существует!"); 	Возврат;	КонецЕсли;
		Если ПустоеЗначение(Сайт) = 1 				Тогда Предупреждение("Не задан сайт!"); 		Возврат;	КонецЕсли;
		
		ПослатьФайл();
		
	КонецЕсли;	
	
КонецПроцедуры

//******************************************************************************


