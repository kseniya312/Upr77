////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем ТекСтрокаВТаблице;                         // Текущая строка в таблице значений  МФ
Перем МеткаЕсть, МеткиНет;                       // Номера иконок пометки строки таблицы
Перем ТекРеквизит;                               // Текущее значение в списке реквизитов
Перем ТекРеквизит1;                               // Текущее значение в списке реквизитов

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//                  
//******************************************************************************
// ВыбратьПоФильтру()
//
// Параметры:
//  Нет
//
// Описание:
//  Открывает обработку отбора элементов по произвольному фильтру
//
Процедура ВыбратьПоФильтру()
	
	Перем  ВидЗначенияПодбора;
	
	ВидЗначенияПодбора=СокрЛП(ТаблицаМФ.Вид);
	
	Если ПустоеЗначение(ВидЗначенияПодбора)=1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("",                "ИмяВызвавшейФормы");
	СписокПараметров.ДобавитьЗначение(ТаблицаМФ.Тип,     "Тип");
	СписокПараметров.ДобавитьЗначение(ВидЗначенияПодбора,"Вид");
	СписокПараметров.ДобавитьЗначение(СписокЭлементовМФ, "Объекты");
	ТаблицаМФ.ФлВкл=2;
	ОткрытьФорму("Обработка.ПодборОбъектов#",СписокПараметров);
	
КонецПроцедуры	// ВыбратьПоФильтру

//******************************************************************************
// ПриСменеВидаКонтрагента()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  ВидКонтрагента.
//
// Описание:
//  Отрабатывает действия, необходимые при смене вида контрагента.
//
Процедура ПриСменеВидаКонтрагента()
	
	ВидЮрФизЛица = ВидКонтрагента.ПолучитьЗначение(ВидКонтрагента.ТекущаяСтрока());
	
	// Проверка вида юр. / физ. лица.
	Если ВидЮрФизЛица <> ВыбЮрФизЛицо.Вид() Тогда
		ВыбЮрФизЛицо = ПолучитьПустоеЗначение("Справочник."+ВидЮрФизЛица); 
	КонецЕсли;
	
	Если ВидЮрФизЛица = "ФизЛица" Тогда
		Форма.ТекстЮрФизЛицо.Заголовок("Физическое лицо:");
	Иначе
		Форма.ТекстЮрФизЛицо.Заголовок("Юридическое лицо:");
	КонецЕсли;
	
	
КонецПроцедуры // ПриСменеВидаКонтрагента()

//******************************************************************************
// ПерерисовкаНазванийЗакладок
//
// Параметры:
// 	Нет
//
// Описание:
//  Добавляет "(!)" в заголовок закладки с множественным фильтром, когда этот фильтр задан
//
Функция ПерерисовкаНазванийЗакладок()      
	
	Форма.Закладки.УстановитьЗначение(2,?(глМножественныйФильтрЗадан(ТаблицаМФ)=1,"(!) ","")+"Множественный фильтр");
	
КонецФункции // ПерерисовкаНазванийЗакладок	

//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение: 
//	Нет
//
// Вызывается из формул реквизитов диалога:
// флажок ВклВид.
//
// Описание: 
//	Управление доступностью элементов диалога.
//
Процедура УправлениеДиалогом()
	
	Если Форма.ИспользоватьЗакладки() = 1 Тогда
		Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда           
			Форма.ИспользоватьСлой("Подбор, КнопкиПодбора");
			
			Если ВклВид =  1 Тогда
			    Форма.ВидКонтрагента. Доступность(1);
			    Форма.ВыбЮрФизЛицо.   Доступность(1);
			    Форма.кнХВыбЮрФизЛицо.Доступность(1);
			Иначе
			    Форма.ВидКонтрагента. Доступность(0);
			    Форма.ВыбЮрФизЛицо.   Доступность(0);
			    Форма.кнХВыбЮрФизЛицо.Доступность(0);
			КонецЕсли;                 
			
			
			Если ВклТипЦен =  1 Тогда
			    Форма.ТипЦен.Доступность(1);
			Иначе
			    Форма.ТипЦен.Доступность(0);
			КонецЕсли;                 
			
			
			Если ВклСкидка =  1 Тогда
			    Форма.Скидка.Доступность(1);
			Иначе
			    Форма.Скидка.Доступность(0);
			КонецЕсли;                 
			
			
			Если ВклФлагКредита =  1 Тогда
			    Форма.ЗначениеФлагаКредита.Доступность(1);
			Иначе
			    Форма.ЗначениеФлагаКредита.Доступность(0);
			КонецЕсли;                 
			
		Иначе     
			Форма.ИспользоватьСлой("МФ, КнопкиПодбора");
		КонецЕсли;
	Иначе
		Если ВариантДействия = 1 Тогда
			Форма.ПометкаУдаления.Доступность(1);
		Иначе
			Форма.ПометкаУдаления.Доступность(0);
		КонецЕсли;
		
		Если ВариантДействия = 2 Тогда
			Форма.ЗначениеСвойства.Доступность(1);
		Иначе
			Форма.ЗначениеСвойства.Доступность(0);
		КонецЕсли;
		
		Если ВариантДействия = 3 Тогда
			Форма.ГруппаКонтрагентов.Доступность(1);
		Иначе
			Форма.ГруппаКонтрагентов.Доступность(0);
		КонецЕсли;
		
		Если ВариантДействия = 4 Тогда
			Форма.УдаляемоеСвойство.Доступность(1);
		Иначе
			Форма.УдаляемоеСвойство.Доступность(0);
		КонецЕсли;
		
		Если ВариантДействия = 5 Тогда
			Форма.СписокРеквизитов.Доступность(1);
			Форма.ЗначениеРеквизита.Доступность(1);
		Иначе
			Форма.СписокРеквизитов.Доступность(0);
			Форма.ЗначениеРеквизита.Доступность(0);
		КонецЕсли;

		Если ВариантДействия = 6 Тогда
			Форма.СписокРеквизитов1.Доступность(1);
			Форма.ЗначениеРеквизита1.Доступность(1);
		Иначе
			Форма.СписокРеквизитов1.Доступность(0);
			Форма.ЗначениеРеквизита1.Доступность(0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры //УправлениеДиалогом()   

//******************************************************************************
// ПриСменеРеквизита()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
//  При изменении текущей строки в списке реквизитов
//
// Описание:
//  Устанавливает соответствующий тип значения реквизита, 
//  присваивает ему значение, заданное в списке реквизитов
//
Процедура ПриСменеРеквизита()
	
	НовыйРеквизит = СписокРеквизитов.ПолучитьЗначение(СписокРеквизитов.ТекущаяСтрока(),"");
	
	// Менять значение реквизита следует только при изменении текущей строки в списке реквизитов
	Если ТекРеквизит <> НовыйРеквизит Тогда
		ТекРеквизит = НовыйРеквизит;
		Если ТекРеквизит = "Контроль кредита" Тогда
			Форма.ЗначениеРеквизита.Видимость(0);
			Форма.ФлагКредита.Видимость(1);
		Иначе    
			Форма.ЗначениеРеквизита.Видимость(1);
			Форма.ФлагКредита.Видимость(0);
			Форма.ЗначениеРеквизита.УстановитьТип(ТекРеквизит);
			ЗначениеРеквизита = ТекРеквизит;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриСменеРеквизита()

//******************************************************************************
// ПриСменеРеквизита1()	- доп. функциональность
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
//  При изменении текущей строки в списке реквизитов
//
// Описание:
//  Устанавливает соответствующий тип значения реквизита, 
//  присваивает ему значение, заданное в списке реквизитов
//
Процедура ПриСменеРеквизита1()
	
	НовыйРеквизит1 = СписокРеквизитов1.ПолучитьЗначение(СписокРеквизитов1.ТекущаяСтрока(),"");
	
	// Менять значение реквизита следует только при изменении текущей строки в списке реквизитов
	Если ТекРеквизит1 <> НовыйРеквизит1 Тогда
		ТекРеквизит1 = НовыйРеквизит1;
		Форма.ЗначениеРеквизита1.Видимость(1);
		Форма.ЗначениеРеквизита1.УстановитьТип(ТекРеквизит1);
		ЗначениеРеквизита1 = ТекРеквизит1;
	КонецЕсли;
	
КонецПроцедуры // ПриСменеРеквизита1()

//******************************************************************************
// ГрупповаяПометка(Режим)
//
// Параметры: 
//  Режим:
//   1 - пометить все
//   2 - снять пометку
//   3 - инвертировать пометку
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
// кнопки групповой пометки
//
// Описание:
//  Производит групповые действия с пометкой строк таблицы формы
//
Процедура ГрупповаяПометка(Режим)
	
	ТаблицаКонтрагентов.ВыбратьСтроки();
	
	Пока ТаблицаКонтрагентов.ПолучитьСтроку() = 1 Цикл
		
		Если Режим=1 Тогда              
			
			ТаблицаКонтрагентов.Пометка = МеткаЕсть;
			
		ИначеЕсли Режим=2 Тогда
			
			ТаблицаКонтрагентов.Пометка = МеткиНет;
			
		ИначеЕсли Режим=3 Тогда
			
			Если ТаблицаКонтрагентов.Пометка=МеткиНет Тогда
				ТаблицаКонтрагентов.Пометка = МеткаЕсть;
			Иначе
				ТаблицаКонтрагентов.Пометка = МеткиНет;
			КонецЕсли;                           
			
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры // ГрупповаяПометка()         

//******************************************************************************
// ПерезаполнитьСтрокуТаблицы(НомерСтроки)
//
// Параметры: 
//  НомерСтроки - номер строки таблица, чьи колонки надо перезаполнить
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Заполняет значения ячеек для строки с переданным номером
//
Процедура ПерезаполнитьСтрокуТаблицы(НомерСтроки)
	
	Контрагент = ТаблицаКонтрагентов.ПолучитьЗначение(НомерСтроки, "Контрагент");
	ОснДоговор = Контрагент.ОсновнойДоговор;

	ТаблицаКонтрагентов.УстановитьЗначение(НомерСтроки, "ВидЮрФизЛица",     СокрЛП(Контрагент.ЮрФизЛицо.ПредставлениеВида()));
	ТаблицаКонтрагентов.УстановитьЗначение(НомерСтроки, "Группа",           СокрЛП(Контрагент.Родитель));
	ТаблицаКонтрагентов.УстановитьЗначение(НомерСтроки, "Статус",           ?(Контрагент.ПометкаУдаления() = 1, "Удален", ""));
	ТаблицаКонтрагентов.УстановитьЗначение(НомерСтроки, "ОсновноеСвойство", СокрЛП(Контрагент.ОсновноеСвойство.ЗначениеСвойства));
	ТаблицаКонтрагентов.УстановитьЗначение(НомерСтроки, "ТипЦен",           СокрЛП(ОснДоговор.ТипЦен));
	ТаблицаКонтрагентов.УстановитьЗначение(НомерСтроки, "Скидка",           Строка(ОснДоговор.Скидка));
	ТаблицаКонтрагентов.УстановитьЗначение(НомерСтроки, "КонтрольКредита", ?(ПустоеЗначение(ОснДоговор.НеКонтролироватьКредит) = 1, "", "Нет"));
	
КонецПроцедуры // ПерезаполнитьСтрокуТаблицы()

//******************************************************************************
// ПриДвойномЩелчкеНаТаблице()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
//  Таблица значений ТаблицаКонтрагентов
//
// Описание:
//  Обрабатывает двойной щелчок (нажатие клавиши "Enter") на таблице формы
//
Процедура ПриДвойномЩелчкеНаТаблице()
	Перем ТекКол, ТекСтр;
	
	ТаблицаКонтрагентов.ТекущаяКолонка(,ТекКол);
	ТекСтр    = ТаблицаКонтрагентов.ТекущаяСтрока();
	КодТекКол = ТаблицаКонтрагентов.ПолучитьПараметрыКолонки(ТекКол);
	
	Если ТекСтр > 0 Тогда
		
		Если КодТекКол = "Пометка" Тогда
			
			Если ТаблицаКонтрагентов.Пометка = МеткаЕсть Тогда
				ТаблицаКонтрагентов.Пометка = МеткиНет;
			Иначе
				ТаблицаКонтрагентов.Пометка = МеткаЕсть;
			КонецЕсли;
			
		ИначеЕсли КодТекКол = "Контрагент" Тогда
			
			ОткрытьФормуМодально(ТаблицаКонтрагентов.Контрагент);
			ПерезаполнитьСтрокуТаблицы(ТекСтр);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриДвойномЩелчкеНаТаблице()

//******************************************************************************
// ПоКнопкеВыполнить()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
//  По кнопке выполнить
//
// Описание:
//  Производит выбранное действия с выбранными контрагентами
//
Процедура ПоКнопкеВыполнить()      
	Перем Спросили;
	
	// Проверим, все ли задано
	Если      ВариантДействия = 1 Тогда  // Пометка удаления
		
		Если ПометкаУдаления.ТекущаяСтрока() = 0 Тогда
			Предупреждение("Не выбрано действие с пометкой на удаление.",60);
			Активизировать("ПометкаУдаления");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ВариантДействия = 2 Тогда  // Установить Осн. свойство
		
		Если ПустоеЗначение(ЗначениеСвойства) = 1 Тогда
			Предупреждение("Не выбрано значение основного свойства.",60);
			Активизировать("ЗначениеСвойства");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ВариантДействия = 3 Тогда  // Перенести в группу
		Если ГруппаКонтрагентов.Выбран() = 0 Тогда
			Предупреждение("Не выбрана группа, в которую надо перенестие элементы.",60);
			Активизировать("ГруппаКонтрагентов");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ВариантДействия = 4 Тогда  // Удалить свойство
		
		Если ПустоеЗначение(УдаляемоеСвойство) = 1 Тогда
			Предупреждение("Не выбрано свойство, которое надо удалить.",60);
			Активизировать("УдаляемоеСвойство");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ВариантДействия = 5 Тогда  // Установить реквизит осн. договора
		
		Если СписокРеквизитов.ТекущаяСтрока() = 0 Тогда
			Предупреждение("Не выбран устанавливаемый реквизит.",60);
			Активизировать("СписокРеквизитов");
			Возврат;
		КонецЕсли;

	ИначеЕсли ВариантДействия = 6 Тогда  // Установить реквизит отбора
		
		Если СписокРеквизитов1.ТекущаяСтрока() = 0 Тогда
			Предупреждение("Не выбран устанавливаемый реквизит.",60);
			Активизировать("СписокРеквизитов1");
			Возврат;
		КонецЕсли;
		
	КонецЕсли;	
	
	Попытка
		НачатьТранзакцию();
		Клиент       = СоздатьОбъект("Справочник.Контрагенты");
		СпрСвойств   = СоздатьОбъект("Справочник.СвойстваКонтрагентов");
		СпрДоговоров = СоздатьОбъект("Справочник.Договоры");
		ТаблицаКонтрагентов.ВыбратьСтроки();
		Пока ТаблицаКонтрагентов.ПолучитьСтроку() = 1 Цикл
			
			ТекСтрока = ТаблицаКонтрагентов.НомерСтроки; 
			
			// Не помечен к обработке
			Если ТаблицаКонтрагентов.Пометка = МеткиНет Тогда
				Продолжить;
			КонецЕсли;	
			
			// не оказалось клиента
			Если Клиент.НайтиЭлемент(ТаблицаКонтрагентов.Контрагент) = 0 Тогда
				Сообщить("Не найден """ + СокрЛП(ТаблицаКонтрагентов.Контрагент) + """! Действие не выполнено!","!");
				Продолжить;
			КонецЕсли;	
			
			Сообщить("Выполняется обработка позиции "+СокрЛП(Клиент),".");
			                 
			Если      ВариантДействия = 1 Тогда  // Пометка удаления
				
				Если ПометкаУдаления.ТекущаяСтрока() = 1 Тогда
					Если Клиент.ПометкаУдаления() = 0 Тогда
						Если глПриУдаленииЭлемента(Клиент, Спросили, 0, 0) = 1 Тогда
							Клиент.Удалить(0);
						Иначе
							Возврат;
						КонецЕсли;
					КонецЕсли;
				Иначе
					Если Клиент.ПометкаУдаления() = 1 Тогда
						Клиент.СнятьПометкуУдаления();
						Если глПриУдаленииЭлемента(Клиент, Спросили, 0, 1) = 0 Тогда
							Клиент.Удалить(0);
							Возврат;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли ВариантДействия = 2 Тогда  // Установить Осн. свойство
		
				СпрСвойств.ИспользоватьВладельца(Клиент);
				Если СпрСвойств.НайтиПоРеквизиту("ЗначениеСвойства", ЗначениеСвойства, 0) = 1 Тогда
						
					// Уже есть такое, проверим не удалено ли
					Если СпрСвойств.ПометкаУдаления() = 1 Тогда
					    СпрСвойств.СнятьПометкуУдаления();
					КонецЕсли;
				Иначе
					СпрСвойств.Новый();
					СпрСвойств.ЗначениеСвойства = ЗначениеСвойства;
					СпрСвойств.ВидСвойства      = ЗначениеСвойства.Владелец;  
					СпрСвойств.Записать();
					
				КонецЕсли;
				
				Если СпрСвойств.ВидСвойства = Константа.ВидСвойстваКонтрагентов Тогда
					Клиент.ОсновноеСвойство = СпрСвойств.ТекущийЭлемент();
				КонецЕсли;
					
			ИначеЕсли ВариантДействия = 3 Тогда  // Перенести в группу
					
				Клиент.Родитель = ГруппаКонтрагентов;	
					
			ИначеЕсли ВариантДействия = 4 Тогда  // Удалить свойство

				СпрСвойств.ИспользоватьВладельца(Клиент);
				Если СпрСвойств.НайтиПоРеквизиту("ЗначениеСвойства", УдаляемоеСвойство, 0) = 1 Тогда
					
					// Свойство м.б. основным
					Если СпрСвойств.ВидСвойства = Константа.ВидСвойстваКонтрагентов Тогда
						Клиент.ОсновноеСвойство = "";
					КонецЕсли;
					СпрСвойств.Удалить(1);
				КонецЕсли;
				
			ИначеЕсли ВариантДействия = 5 Тогда  // Установить реквизит осн. договора
				
				ТекРеквизит = "";
				Атрибут     = "";                        
				Если СпрДоговоров.НайтиЭлемент(Клиент.ОсновнойДоговор) = 1 Тогда
				
					СписокРеквизитов.ПолучитьЗначение(СписокРеквизитов.ТекущаяСтрока(), ТекРеквизит);
					Если      ТекРеквизит = "Тип цен" Тогда
						СпрДоговоров.ТипЦен = ЗначениеРеквизита;
					ИначеЕсли ТекРеквизит = "Скидка" Тогда 	
						СпрДоговоров.Скидка = ЗначениеРеквизита;
					ИначеЕсли ТекРеквизит = "Контроль кредита" Тогда 	
						СпрДоговоров.НеКонтролироватьКредит = ?(ФлагКредита.ТекущаяСтрока() = 1, 1, 0);
					КонецЕсли;
					СпрДоговоров.Записать();
				Иначе
					Сообщить("Не найден """ + СокрЛП(Клиент.ОсновнойДоговор) + """! Действие не выполнено!","!");
				КонецЕсли;
//******************************************************************************
			ИначеЕсли ВариантДействия = 6 Тогда  // Установить реквизит	- доп. функционал
				                                 
				ТекРеквизит1 = "";
				Атрибут  = "";
				СписокРеквизитов1.ПолучитьЗначение(СписокРеквизитов1.ТекущаяСтрока(), ТекРеквизит1);
				Если ТекРеквизит1 = "Виды Отбора" Тогда 	

					Атрибут = "НеПоказывать";    

				КонецЕсли;
				
				Если ПустоеЗначение(Атрибут) = 0 Тогда
					Клиент.УстановитьАтрибут(Атрибут,ЗначениеРеквизита1);
				КонецЕсли;
//******************************************************************************
				КонецЕсли;
			
			Клиент.Записать();  
			//ТаблицаКонтрагентов.УстановитьЗначение(ТекСтрока, "Контрагент", Клиент.ТекущийЭлемент());

			ПерезаполнитьСтрокуТаблицы(ТекСтрока);
		КонецЦикла;
		
		Сообщить("Обработка закончена",".");
		ЗафиксироватьТранзакцию();
		
	Исключение                          
		ОтменитьТранзакцию();
		Сообщить(ОписаниеОшибки(), "!");
	КонецПопытки;
	
КонецПроцедуры // ПоКнопкеВыполнить()

//******************************************************************************
// ПоКнопкеСформировать()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
//  По кнопке "Сформировать"
// 
// Описание:
//  Отбирает необходимых контрагентов и открывает слой обработки "Обработка"
//
Процедура ПоКнопкеСформировать()
	
	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
                         
	// Формируем таблицу контрагентов
	ТаблицаКонтрагентов.Очистить();
	ТаблицаКонтрагентов.НоваяКолонка("Пометка"         ,"Число" ,1,,                  , 4   );
	ТаблицаКонтрагентов.НоваяКолонка("Контрагент"      ,        , ,,                  ,30   );
	ТаблицаКонтрагентов.НоваяКолонка("ВидЮрФизЛица"    ,"Строка", ,,"Вид"             ,30   );                         
	ТаблицаКонтрагентов.НоваяКолонка("Группа"          ,"Строка", ,,"Группа"          ,25   ); 
	ТаблицаКонтрагентов.НоваяКолонка("Статус"          ,"Строка", ,,"Статус"          ,10,,2);
	ТаблицаКонтрагентов.НоваяКолонка("ОсновноеСвойство","Строка", ,,"Свойство"        ,15,,2);
	ТаблицаКонтрагентов.НоваяКолонка("ТипЦен"          ,"Строка", ,,"Тип цен"         ,15,,2);
	ТаблицаКонтрагентов.НоваяКолонка("Скидка"          ,"Строка", ,,"Скидка"          ,15,,2);
	ТаблицаКонтрагентов.НоваяКолонка("КонтрольКредита" ,"Строка", ,,"Контроль кредита",10,,2);
	
	ТаблицаКонтрагентов.ВыводитьПиктограммы("Пометка",1);
	
    // Запрос по справочнику
	ТекстЗапроса = "                    
		|Контрагент = Справочник.Контрагенты.ТекущийЭлемент;
		|ЮрФизЛицо  = Справочник.Контрагенты.ЮрФизЛицо;                  
		|Группировка Контрагент Без Групп;
		|";
		
	Загол="";
	НетОш = 1; // нет ошибок при наложении фильтров
 	НетОш = НетОш * глФильтрПоПеременнойЗапроса(ТаблицаМФ, "Контрагент",ВыбКонтрагент,"ВыбКонтрагент",ТекстЗапроса,Загол,"СвойстваКонтрагентов");
	
	// Если ошибка в запросе, то выход из процедуры
	Если НетОш = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если (ВклВид = 1)
	   И (ПустоеЗначение(ВыбЮрФизЛицо) = 0)  Тогда
		ТекстЗапроса = ТекстЗапроса + "Условие (ЮрФизЛицо в ВыбЮрФизЛицо);";
	КонецЕсли;                                                              
	
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;     
	
	Счетчик = 0;
	ТекСтр  = 0;
	Док     = СоздатьОбъект("Документ");
 	Пока Запрос.Группировка("Контрагент") = 1 Цикл
 		
		Состояние("Обработано контрагентов: " + Строка(Счетчик));
		Счетчик = Счетчик + 1;
		
		Контрагент = Запрос.Контрагент;
		
		// Проверим вхождение в документы
		Если ВклНетВдокументах = 1 Тогда
			Если Док.ВыбратьПоЗначению(,,"Контрагент", Контрагент) = 1 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Проверим фильтры по реквизитам основного договора
		Договор = Контрагент.ОсновнойДоговор;
		
		Если ВклТипЦен = 1 Тогда
		    Если Договор.ТипЦен <> ТипЦен Тогда
		        Продолжить;
		    КонецЕсли;
		КонецЕсли;
	
		Если ВклСкидка = 1 Тогда
		    Если Договор.Скидка <> Скидка Тогда
		        Продолжить;
		    КонецЕсли;
		КонецЕсли;
		
		Если ВклФлагКредита = 1 Тогда
			ВремНеКонтролироватьКредит = ?(ЗначениеФлагаКредита.ТекущаяСтрока() = 1, 1, 0);
		    Если Договор.НеКонтролироватьКредит <> ВремНеКонтролироватьКредит Тогда
		        Продолжить;
		    КонецЕсли;
		КонецЕсли;
		
		// Если досюда дошли, то контрагент удовлетворяет всем наложенным условиям
		ТаблицаКонтрагентов.НоваяСтрока();
		ТекСтр = ТекСтр + 1;
			
       	ТаблицаКонтрагентов.Контрагент  = Контрагент;
       	ТаблицаКонтрагентов.Пометка     = МеткаЕсть; //Все строки помечены по умолчанию
			
		ПерезаполнитьСтрокуТаблицы(ТекСтр);
	
	КонецЦикла;
	
	// Таблица сформирована, если есть что обрабатывать, то запускаем обработку формирования
	Если ТаблицаКонтрагентов.КоличествоСтрок() = 0 Тогда
		Предупреждение("Ни контрагент не удовлетворяет наложенным условиям.",60);
		Возврат;
	КонецЕсли;
	
	Форма.ИспользоватьЗакладки(0);
	Форма.ИспользоватьСлой("Обработка, КнопкиОбработки");
	
	ТаблицаКонтрагентов.Фиксировать(0);
	
	УправлениеДиалогом();
	ПриСменеРеквизита();
	Форма.КнопкаПоУмолчанию("кнВыполнить");
        	
КонецПроцедуры // ПоКнопкеСформировать()

//******************************************************************************
// ПоКнопкеНастройка()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул диалога
//  по кнопке "Настройка"
//
// Описание:
//  Переключает форму на закладку поддбора
//
Процедура ПоКнопкеНастройка()
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ТекущаяСтрока(1);
	ПерерисовкаНазванийЗакладок();
	УправлениеДиалогом();
	Форма.КнопкаПоУмолчанию("кнСформировать");
	
КонецПроцедуры // ПоКнопкеНастройка()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии(ФлагВосстановленияНастройки)
	
	// Инициализация переменных
	Если ФлагВосстановленияНастройки = 0 Тогда
		
		ВариантДействия = 1;
		
	КонецЕсли;
	
	ПерерисовкаНазванийЗакладок();
	
	ТаблицаМФ.ВидимостьКолонки("Тип",0);
	ТаблицаМФ.ВидимостьКолонки("Вид",0);
	ТаблицаМФ.ВидимостьКолонки("СписокЭлементов",0);          
	ТаблицаМФ.ВидимостьКолонки("ТипМФ",0);
	ТаблицаМФ.ВидимостьКолонки("ИмяПеременной",0);
	
	Форма.ВыбЮрФизЛицо.НеИзменятьВид(1);
	ПриСменеВидаКонтрагента();
	УправлениеДиалогом();
	Форма.КнопкаПоУмолчанию("кнСформировать");
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ВводНового()
	
	// эта предопределенная процедура выполняется при восстановлении настройки
	ПерерисовкаНазванийЗакладок();
	УправлениеДиалогом();       
	ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
	
КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореЗакладки(Номер,Значение)	
	
	// закладки
    Если Номер=1 Тогда
    	глПриСменеСтрокиТаблицыМФ(1,ТекСтрокаВТаблице,Контекст); // записываем изменения если они были
	КонецЕсли;      
	УправлениеДиалогом();
	ПерерисовкаНазванийЗакладок();
	
КонецПроцедуры	// ПриВыбореЗакладки()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога,ФлагСтандОбр)
	
	Если ИдентЭлемДиалога = "ЗначениеСвойства" Тогда
		Если Константа.ВидСвойстваКонтрагентов.Выбран() = 1 Тогда
			ЗначениеСвойства.ИспользоватьВладельца(Константа.ВидСвойстваКонтрагентов);
		Иначе
			Предупреждение("Перед выбором значения основного свойства выберите в настройках 
			               |параметров учета вид свойства для контрагентов", 60);
			ФлагСтандОбр = 0;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаВыбораЗначения(ВыбЗнач,ИдентЭлемДиалога,ФлагСтандОбр)
	
	Если ИдентЭлемДиалога = "ГруппаКонтрагентов" Тогда 
		
		Если ВыбЗнач.ЭтоГруппа() = 0 Тогда
		    ФлагСтандОбр = 0;   
			Предупреждение("Выберите группу!",60);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ОбработкаВыбораЗначения()

//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(Значение)  
	
	Если (СписокЭлементовМФ.НайтиЗначение(Значение)=0) Тогда
		Представление=""+Значение;
		Если ТипЗначенияСтр(Значение)="Справочник" Тогда
			Если СокрЛП(Метаданные.Справочник(Значение.Вид()).Владелец) <> "Метаданные" Тогда
				Представление=Представление+" ("+Значение.Владелец+")";
			КонецЕсли;
		КонецЕсли;	
		СписокЭлементовМФ.ДобавитьЗначение(Значение,Представление);
		ТаблицаМФ.ФлВкл=2;
	КонецЕсли;
	
КонецПроцедуры  // ОбработкаПодбора()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 
МеткаЕсть = 2;  МеткиНет = 1; // Номера иконок пометки

//Инициализация списков
ВидКонтрагента.УдалитьВсе();
ВидКонтрагента.ДобавитьЗначение("ЮрЛица",     "Стороннее юр. лицо");
ВидКонтрагента.ДобавитьЗначение("ФизЛица",    "Физическое лицо");
ВидКонтрагента.ДобавитьЗначение("СвоиЮрЛица", "Собственное юр. лицо");
ВидКонтрагента.ТекущаяСтрока(1);

ПометкаУдаления.УдалитьВсе();
ПометкаУдаления.ДобавитьЗначение("Поставить");						
ПометкаУдаления.ДобавитьЗначение("Снять");						
ПометкаУдаления.ТекущаяСтрока(1);

ЗначениеФлагаКредита.УдалитьВсе();
ЗначениеФлагаКредита.ДобавитьЗначение("Проставлен");
ЗначениеФлагаКредита.ДобавитьЗначение("Снят");
ЗначениеФлагаКредита.ТекущаяСтрока(1);     

ФлагКредита.УдалитьВсе();
ФлагКредита.ДобавитьЗначение("Не контролировать");
ФлагКредита.ДобавитьЗначение("Контролировать");
ФлагКредита.ТекущаяСтрока(1);     

СписокРеквизитов.УдалитьВсе();
СписокРеквизитов.ДобавитьЗначение(ПолучитьПустоеЗначение("Справочник.ТипыЦен"),"Тип цен");						
СписокРеквизитов.ДобавитьЗначение(ПолучитьПустоеЗначение("Справочник.Скидки"),"Скидка");
СписокРеквизитов.ДобавитьЗначение("Контроль кредита","Контроль кредита");
СписокРеквизитов.ТекущаяСтрока(1);

СписокРеквизитов1.УдалитьВсе();
СписокРеквизитов1.ДобавитьЗначение(Перечисление.ТипОтображенияВОтборе.Вариант1,"Виды Отбора");						
СписокРеквизитов1.ТекущаяСтрока(1);

// инициализация переменных множественного фильтра
ТипМФ.УдалитьВсе();
ТипМФ.ДобавитьЗначение("одно из");
ТипМФ.ДобавитьЗначение("все кроме");

ТаблицаМФ.УдалитьСтроки();
Пока ТаблицаМФ.КоличествоКолонок()>0 Цикл
    ТаблицаМФ.УдалитьКолонку(1);
КонецЦикла;  

ТаблицаМФ.НоваяКолонка("Тип");
ТаблицаМФ.НоваяКолонка("Вид");
ТаблицаМФ.НоваяКолонка("ИмяПеременной");
ТаблицаМФ.НоваяКолонка("СписокЭлементов");                 // список элементов, по которым производим фильтрацию
ТаблицаМФ.НоваяКолонка("ТипМФ");                           // текущая строка списка ТипМФ
ТаблицаМФ.НоваяКолонка("ФлВкл","Число",1,,"Вкл",5,,);      // фильтр включен ("1" или "0")
ТаблицаМФ.НоваяКолонка("Представление",,,,"Вид фильтра:");
ТаблицаМФ.ВыводитьПиктограммы("ФлВкл");
                                                                                
//                                 тип          вид                переменная          название
глДобавитьВТаблицуМФ(ТаблицаМФ, "Справочник", "Контрагенты",     "Контрагент",  "По контрагентам");
глДобавитьВТаблицуМФ(ТаблицаМФ, "Справочник", "ЗначенияСвойств", "Контрагент",  "По свойствам контрагентов");

ТекСтрокаВТаблице="";

// Инициализируем закладки
Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Основная");
Форма.Закладки.ДобавитьЗначение(2,"Множественный фильтр");
Форма.Закладки.ТекущаяСтрока(1);