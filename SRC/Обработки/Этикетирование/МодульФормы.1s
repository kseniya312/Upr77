Перем Единица;

//******************************************************************************
// ПоКнопкеПечать()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  выводит этикетку на принтер
//
Процедура ПоКнопкеПечать()
	
	Если ПустоеЗначение(Номенклатура) = 1 Тогда
	    Предупреждение("Не выбрана номенклатура");
		Возврат;
	КонецЕсли;
	
	// Проверим коррктность штрих-кода
	ВремШтрихкод = глСформироватьШтрихкод(глПрефиксВесовогоШтрихкода, Сред(Единица.ШтрихКод, 3, 5) + "00000");
	Если Единица.ШтрихКод <> ВремШтрихкод Тогда
	    Предупреждение("Для единицы измерений """+Единица.ОКЕИ+""" задан некорректный штрих-код.");
		Возврат;
	КонецЕсли;
	
	Штрихкод = глСформироватьШтрихкод(глПрефиксВесовогоШтрихкода,
		                              Сред(Единица.ШтрихКод, 3, 5) +
									  СокрЛП(Формат(Вес * 1000, "Ч(0)5")));
	Данные = СоздатьОбъект("ТаблицаЗначений");
	Данные.НоваяКолонка("Товар");
	Данные.НоваяКолонка("Штрихкод");
	
	Данные.НоваяСтрока();
	Данные.Товар	= СокрЛП(Номенклатура.Наименование);
	Данные.Штрихкод	= Штрихкод;
	
	Парам = СоздатьОбъект("СписокЗначений");
	Парам.ДобавитьЗначение(Данные, "ТаблицаТоваров");
	Парам.ДобавитьЗначение(УстройствоВывода.ПолучитьЗначение(УстройствоВывода.ТекущаяСтрока()), "Устройство");
	
	ОткрытьФормуМодально("Обработка.ПечатьЭтикетки", Парам);
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПриВыбореНоменклатуры()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Проверяет, какая номенклатура выбрана. Если не весовая, то очищается 
//  реквизит формы номенклатура и выдается предупреждение.
//
Процедура ПриВыбореНоменклатуры()
	Если Номенклатура.Весовой = 0 Тогда
	    Номенклатура	= "";
		Единица			= "";
		Предупреждение("Обработка предназначена только для формирования этикеток весовых товаров", 60);
	Иначе
		Единица = Номенклатура.БазоваяЕдиница;
	КонецЕсли;
КонецПроцедуры // ПриВыбореНоменклатуры()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	ВремВес = 0;
	глВесыПолучитьВес(ВремВес);
	Если ВремВес <> 0 Тогда
	    Вес = ВремВес;
	КонецЕсли;
	// установим асинхронный режим
	глВесыУстановитьРежим(1);
	Номенклатура.ВыборГруппы(0);
	
	// заполним список устройств
	УстройствоВывода.ДобавитьЗначение("принтер");
	УстройствоВывода.ДобавитьЗначение("экран");
	
	Парам = Форма.Параметр;
	Если ПустоеЗначение(Парам) = 0 Тогда
		Номенклатура	= Парам.Получить("Номенклатура");
	    Единица			= Парам.Получить("Единица");
		Если ПустоеЗначение(Единица) = 1 Тогда
		    Единица = Номенклатура.БазоваяЕдиница;
		КонецЕсли;
	КонецЕсли;
	
	Устройство = ВосстановитьЗначение("УстройствоВыводаЭтикетки");
	Если ПустоеЗначение(Устройство) = 1 Тогда
	    Устройство = "принтер";
	КонецЕсли;
	
	Поз = УстройствоВывода.НайтиЗначение(Устройство);
	Если Поз > 1 Тогда
		УстройствоВывода.ТекущаяСтрока(Поз);
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗакрытии()
	// отключим асинхронный режим
	глВесыУстановитьРежим(0);
	СохранитьЗначение("УстройствоВыводаЭтикетки", 
					  УстройствоВывода.ПолучитьЗначение(УстройствоВывода.ТекущаяСтрока()));
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаВнешнегоСобытия(Источник,Событие,Данные)
	ВремВес = 0;
	Если Событие <> "BarCodeValue" Тогда
		
		глВесыПолучитьВес(ВремВес);
		Если ВремВес <> 0 Тогда
		    Вес = ВремВес;
		КонецЕсли;
	КонецЕсли;
	
	глОбработкаВнешнегоСобытия(Источник, Событие, Данные);
	
КонецПроцедуры // ОбработкаВнешнегоСобития()