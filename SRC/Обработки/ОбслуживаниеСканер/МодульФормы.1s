Перем ОписаниеРезультата;

//******************************************************************************
// Подключить(Компонента, ПрогИд, Сканер)
//
// Параметры: 
//  Компонента - имя файла внешней компоненты
//  ПрогИд     - программный идентификатор
//  Сканер     - выходящий параметр - ссылка на созданный объект
//
// Возвращаемое значение:
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание:
//  Загружает внешнюю компоненту из файла, имя которго задано в параметре Компонента,
//  создает объект, программный идентификатор которо заданы в параметре ПрогИд
//
Функция Подключить(Компонента, ПрогИд, Сканер)
	Рез = 0;
	
	Если ЗагрузитьВнешнююКомпоненту(Компонента) = 0 Тогда
		ОписаниеРезультата = "Не удалось загрузить внешнюю компоненту """ + Компонента + """";
	Иначе
		Попытка
			Сканер = СоздатьОбъект("AddIn." + ПрогИд);
		Исключение
		КонецПопытки;
		
		Если ПустоеЗначение(Сканер) = 1 Тогда
			ОписаниеРезультата = "Не удалось создать объект внешней компоненты с программым идентификатром AddIn." + ПрогИд;
		Иначе
			
			Если Сканер.УстройствоВключено = 1 Тогда
				Рез = 1;
			Иначе
				Если Сканер.Подсоединить("Scanner") <> 0 Тогда
					ОписаниеРезультата = "Не удалось подсоединить устройство";
				Иначе
					Если Сканер.Занять(1) <> 0 Тогда
						ОписаниеРезультата = "Не удалось получить монопольный доступ у устройству";
					Иначе
						Сканер.УстройствоВключено = 1;
						Если Сканер.Результат <> 0 Тогда
							ОписаниеРезультата = "Не удалось включить устройство";
						Иначе
							Рез	= 1;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Рез = 1 Тогда
		// очищаем буфер компоненты
		Сканер.ОчиститьВход();
		Сканер.ОчиститьВыход();
		// включаем режим немедленной посылки данных
		Сканер.ПосылкаДанных = 1;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции  // Подключить(()

//******************************************************************************
// Отключить(Сканер)
//
// Параметры: 
//  Сканер     - ссылка на объект внешней компоненты.
//
// Возвращаемое значение:
//  1
//
// Описание:
//  осбождает порт, занятый сканером
//
Функция Отключить(Сканер)
	Сканер.УстройствоВключено = 0;
	Сканер.Отсоединить();
	Возврат 1;
КонецФункции // Отключить()
			
//******************************************************************************
// Отключить(Сканер)
//
// Параметры: 
//  Сканер     - ссылка на объект внешней компоненты.
//  Флаг 
//
// Возвращаемое значение:
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание:
//  Если параметр Флаг = 1, компонента при получении штрихкода срузу будет
//  посылать в Предприятие. Если Флаг = 0, компонента будет сохранять
//  полученные штрихкод в очереди.
//
Функция ПосылкаДанных(Сканер, Флаг)
	Сканер.ПосылкаДанных = Флаг;
	
	Если Сканер.Результат = 0 Тогда
		Рез = 1;
	Иначе
		Рез = 0;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции // ПосылкаДанных

//******************************************************************************
// Отключить(Сканер)
//
// Параметры: 
//  Сканер     - ссылка на объект внешней компоненты.
//
// Возвращаемое значение:
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание:
//  очищает входную очередь сканера
//
Функция ОчиститьВход(Сканер)
	
	Сканер.ОчиститьВход();
	Сканер.ОчиститьВыход();
	
	Если Сканер.Результат = 0 Тогда
		Рез = 1;
	Иначе
		Рез = 0;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции // ОчиститьВход

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
Процедура ПриОткрытии()
	Перем Сканер, Вкл, Парам;
	
	СтатусВозврата(0);
	Форма.Параметр.Выгрузить(Парам);
	Форма.Параметр.УдалитьВсе();
	
	Если Парам.РазмерСписка() = 0 Тогда
		Рез = 0;
	Иначе
		
		Процесс = НРег(Парам.Получить("Процесс"));
		
		Если Процесс = "подключить" Тогда
			Компонента = Парам.Получить("Компонента");
			ПрогИд     = Парам.Получить("ПрогИд");
			Рез        = Подключить(Компонента, ПрогИд, Сканер);
			
			Форма.Параметр.Установить("Объект"    , Сканер);
			
		ИначеЕсли Процесс = "отключить" Тогда
			Рез = Отключить(Парам.Получить("Объект"));
		
		ИначеЕсли Процесс = "посылка_данных" Тогда
			Сканер = Парам.Получить("Объект");
			Флаг   = Парам.Получить("Флаг");
			
			Рез = ПосылкаДанных(Сканер, Флаг);
			
		ИначеЕсли Процесс = "очистить_вход" Тогда
			Сканер = Парам.Получить("Объект");
			Рез    = ОчиститьВход(Сканер);
		
		Иначе
			Рез = 0;
		КонецЕсли;
	КонецЕсли;

	Форма.Параметр.Установить("ОписаниеРезультата", ОписаниеРезультата);
	Форма.Параметр.Установить("Результат"        , Рез);
			
	
КонецПроцедуры // ПриОткрытии