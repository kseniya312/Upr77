////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем Номенклатура, Коэффициент, Цена;
Перем ВиденФлагКомплектующие, ОтработалаПриИзмененииКоличество;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ОК()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  кнопка "Ок"
//
// Описание:
//  Передаем в вызывавший обработку модуль результаты
//
Процедура ОК()
	
	Форма.Параметр.Установить("Количество"	, Количество);
	Форма.Параметр.Установить("Единица"		, Единица);
	Форма.Параметр.Установить("Цена"		, Цена);
	Форма.Параметр.Установить("ДобавлятьКомплектующие"		, ДобавлятьКомплектующие);
	Форма.Параметр.Установить("СтатусВозврата"	, 1);
	
КонецПроцедуры

//******************************************************************************
// ПриИзмененииЕдиницы()
//
// Параметры: 
//  Нет
//
// Вызывается из формул элементов диалога
// Поле ввода единица
// 
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Пересчитывает цену при изменении единицы
Процедура ПриИзмененииЕдиницы()
	
	// сохраним старый коэффициент
	ВремКоэфф = Коэффициент;
	
	// заполним коэффициент из единицы
	Коэффициент = Единица.Коэффициент;
	Если ВремКоэфф <> 0 Тогда
		
		// цена была за другой коэффициент. Пересчитываем
		Цена = Цена * Коэффициент / ВремКоэфф;         
	КонецЕсли;
		
КонецПроцедуры // ПриИзмененииЕдиницы()

//******************************************************************************
// ПриИзмененииКоличество()
//
// Параметры: 
//  Нет
//
// Вызывается из формул элементов диалога
// Поле ввода количества
// 
// Возвращаемое значение:
//  Нет
//
// Описание:
//  При нажатии на кнопку Enter в поле Количество выполняет действия,
//  как по кнопке ОК и закрывает форму
//
Процедура ПриИзмененииКоличество()
	
	Если (Количество <> 0) И (ВиденФлагКомплектующие = 0) Тогда
		
		// Количество в параметр прописываем при закрытии
		ОтработалаПриИзмененииКоличество = 1;
		Форма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииКоличество()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
// *****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если ТипЗначенияСтр(Форма.Параметр) <> "СписокЗначений" Тогда
		Предупреждение("Обработка вызывается только из формы подбора!", 60);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Номенклатура = Форма.Параметр.Получить("Номенклатура");
	Количество   = Форма.Параметр.Получить("Количество");
	Единица 	 = Форма.Параметр.Получить("Единица");
	ПарамЦена	 = Форма.Параметр.Получить("Цена");
	Коэффициент  = Единица.Коэффициент; // заполним коэффициент из единицы
	
	Если ТипЗначенияСтр(ПарамЦена) = "СписокЗначений" Тогда
		Если ПарамЦена.РазмерСписка() > 0 Тогда
			Цена = ПарамЦена.ПолучитьЗначение(1);
		КонецЕсли;
	Иначе
		Цена = ПарамЦена;
	КонецЕсли;
	Цена = Число(Цена);
	
	Если ПустоеЗначение(глКомплектуется(Номенклатура)) = 0 Тогда
		ВиденФлагКомплектующие	= 1;
		// Работает с комплектом
		ДобавлятьКомплектующие =  ВосстановитьЗначение("ДобавлятьКомплектующие");
		Если ПустоеЗначение(ДобавлятьКомплектующие) = 1  Тогда
		    ДобавлятьКомплектующие = 0;
		Иначе
			ДобавлятьКомплектующие = 1;
		КонецЕсли;
		
	Иначе // Обычный товар или услуга
		Форма.ДобавлятьКомплектующие.Видимость(0);
		ВиденФлагКомплектующие	= 0;
	КонецЕсли;
	
	Форма.Заголовок("Количество """ + Номенклатура+ """");
	
	Форма.Параметр.Установить("СтатусВозврата"	, 0);
	
	Если ТипЗначенияСтр(ПарамЦена) = "СписокЗначений" Тогда
		Форма.Единица.Доступность(0);
	КонецЕсли;
	                                 
	Активизировать("Количество");
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр)
	
	Перем Параметр;
	
	Если ИдентЭлемДиалога = "Единица" Тогда
		ФлагСтандОбр = 0;
		Параметр = Номенклатура;
		ОткрытьФорму("Справочник.Единицы", Параметр);
		
		Если Форма.МодальныйРежим() = 1 Тогда
			Единица = Параметр;
			ПриИзмененииЕдиницы();
		КонецЕсли;
	КонецЕсли;                             
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗакрытии()
	
	Если Форма.АктивныйЭлемент() = "кнОтмена" Тогда  // Нажата кнопка "Отмена"
		Форма.Параметр.Установить("СтатусВозврата"	, 0);
	ИначеЕсли ОтработалаПриИзмененииКоличество = 1 Тогда // Прописываем возвращаемое количество
		ОК();
	КонецЕсли;
	Если ПустоеЗначение(глКомплектуется(Номенклатура)) = 0 Тогда
		// Работаем с комплектом
		СохранитьЗначение("ДобавлятьКомплектующие", ДобавлятьКомплектующие);
	КонецЕсли;
	
КонецПроцедуры // ПриЗакрытии()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 
ОтработалаПриИзмененииКоличество = 0;