////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем РеквизитДокумента;
Перем Парам;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ПолучитьИНН(ИННКПП)
//
// Параметры:
//  ИННКПП  - строка, написанные вместе ИНН и КПП юр лица.
//
// Возвращаемое значение:
//  значение ИНН юр лица.
//  
// Описание:
//  Функция возращает ИНН из переданной строки 
//
Функция ПолучитьИНН(ИННКПП)
	
	Если Найти(ИННКПП, "\") <> 0 Тогда
		// найден разделитель
		Возврат Лев(ИННКПП, Найти(ИННКПП, "\") - 1);

	Иначе
		Возврат СокрЛП(ИННКПП);

	КонецЕсли;

КонецФункции // ПолучитьИНН()

//******************************************************************************
// ПолучитьКПП(ИННКПП)
//
// Параметры:
//  ИННКПП  - строка, написанные вместе ИНН и КПП юр лица.
//
// Возвращаемое значение:
//  значение КПП юр лица.
//  
// Описание:
//  Функция возращает КПП из переданной строки 
//
Функция ПолучитьКПП(ИННКПП)

	Если Найти(ИННКПП, "\") <> 0 Тогда
		// найден разделитель
		Возврат Сред(ИННКПП, Найти(ИННКПП, "\") + 1);

	Иначе
		Возврат "";

	КонецЕсли;

КонецФункции // ПолучитьКПП()

//******************************************************************************
// СдвинутьСтроку(Направление)
//
// Параметры: 
//  Режим - число, режим работы: 
//          1  - сдвинуть текущую строку списка вниз на одну позицию
//          -1 - сдвинуть текущую строку списка вверх на одну позицию
//
// Вызывается из формул элементов диалога:
//  Кнопка "Стрелка вверх" и Кнопка "Стрелка вниз" 
//
// Описание: 
//  Сдвигает текущую строку списка вверх, вниз на одну позицию.
// Если текущая строка является последней и движение вниз, устанавливает ее первой в списке.
// Если текущая строка является первой и движение вверх, устанавливает ее последней в списке.
//
Процедура СдвинутьСтроку(Направление)
	
	ТекСтрок    = Список.ТекущаяСтрока();
	РазСписка   = Список.РазмерСписка();
	Направление = ?(ТекСтрок + Направление > РазСписка, (-1) * (РазСписка - 1), Направление);
	Направление = ?(ТекСтрок + Направление < 1, РазСписка - 1, Направление);
	
	Список.СдвинутьЗначение(Направление, ТекСтрок);
    Список.ТекущаяСтрока(ТекСтрок + Направление);
	
КонецПроцедуры // СдвинутьСтроку()

//******************************************************************************
// Установить(Режим="") 
//
// Параметры: 
//  Режим - строка, режим работы: 
//          "Все" - отметить все строки
//          "Сброс" - снять отметку всех строк
//          "" - инвертировать отметку всех строк
//
// Вызывается из формул элементов диалога:
//  Кнопка "Выделить все", Кнопка "Отменить выделение" и Кнопка "Инвентировать" 
//
// Описание: 
//  Устанавливает, снимает или инвертирует отметки выбора строк.
//
Процедура Установить(Режим = "") 
	
	Для Номер = 1 По Список.РазмерСписка() Цикл
		Если Режим = "Все" Тогда
			Список.Пометка(Номер, 1);
		ИначеЕсли Режим = "Сброс" Тогда
			Список.Пометка(Номер, 0); 
		ИначеЕсли Режим = "Инвертировать" Тогда
			Список.Пометка(Номер, ?(Список.Пометка(Номер) = 1, 0, 1)); 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // Установить()

//******************************************************************************
// Сформировать() 
//
// Параметры: 
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнОК" и Кнопка "кнАвто" 
//
// Описание: 
//  Формирует, очищает значение редактируемого элемента.
//
Процедура Сформировать() 
	
	Значение = "";
	
	Если Форма.АктивныйЭлемент() = "кнОК" Тогда
		Для Номер = 1 По Список.РазмерСписка() Цикл
			Если Список.Пометка(Номер) = 1 Тогда
				Значение = Значение + Список.ПолучитьЗначение(Номер) + " ";
			КонецЕсли;
		КонецЦикла;
		
		Значение = СтрЗаменить(Значение, "<с новой строки> ", РазделительСтрок);

	ИначеЕсли Форма.АктивныйЭлемент() = "кнАвто" Тогда
		Значение = "Авто";
	КонецЕсли;

	Если (ПустоеЗначение(Парам.Получить("РеквизитРезультата"))=0) И
		 (ТипЗначенияСтр(Парам.Получить("Контекст"))="ГрупповойКонтекст") Тогда
		// прямая запись в вызывающую форму

		Кнт = Парам.Получить("Контекст");
		Кнт.УстановитьАтрибут(Парам.Получить("РеквизитРезультата"),?(Значение="Авто","",Значение));
	КонецЕсли;

	Парам.ДобавитьЗначение(Значение,"Результат");		

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
Перем ВидДокСпр,РеквРез;
	
	Парам = Форма.Параметр;
	
	Если ТипЗначенияСтр(Парам) <> "СписокЗначений" Тогда
		СтатусВозврата(0); Возврат;
	КонецЕсли;
	
	БанковскийСчет = Парам.Получить("БанковскийСчет");
	Если ТипЗначенияСтр(Парам.Получить("Контекст")) = "ГрупповойКонтекст" Тогда
		ВидДокСпр  = Парам.Получить("Контекст").Вид();
		РеквРез    = Парам.Получить("РеквизитРезультата");
	Иначе
		ВидДокСпр  = "";
		РеквРез    = "Плательщик или Получатель";
	КонецЕсли;
	Источник       = ?(ТипЗначенияСтр(БанковскийСчет) = "Справочник","ИБ","Диалог");

	Если Парам.Получить("Режим") = 1 Тогда
		// Если нужно сформировать строку с реквизитами плательщика или получателя
		
		ИНН = "";
		Если Источник = "ИБ" Тогда
			Если БанковскийСчет.Выбран()=1  Тогда
				ИНН = СокрЛП(БанковскийСчет.ЮрФизЛицо.ИНН);
			КонецЕсли;
		Иначе
			ИНН = Парам.Получить("ИНН");
		КонецЕсли;
			
		Если ПустоеЗначение(ИНН)=0 Тогда
			Список.ДобавитьЗначение("ИНН " + ИНН);
			Список.Пометка(Список.РазмерСписка(), 0);
				
			Если 0 < Найти(ИНН, "\") Тогда
				Список.ДобавитьЗначение("ИНН " + ПолучитьИНН(ИНН));
				Список.Пометка(Список.РазмерСписка(), 0);
					
				Список.ДобавитьЗначение("КПП " + ПолучитьКПП(ИНН));
				Список.Пометка(Список.РазмерСписка(), 0);
			КонецЕсли;
		КонецЕсли;
			
		Если Источник = "ИБ" Тогда
			Если ПустаяСтрока(БанковскийСчет.ЮрФизЛицо.ПолнНаименование) = 0 Тогда
				Список.ДобавитьЗначение(СокрЛП(БанковскийСчет.ЮрФизЛицо.ПолнНаименование));
				Список.Пометка(Список.РазмерСписка(), 1);
				
			ИначеЕсли ПустаяСтрока(БанковскийСчет.ЮрФизЛицо.Наименование) = 0 Тогда
				Список.ДобавитьЗначение(СокрЛП(БанковскийСчет.ЮрФизЛицо.Наименование));
				Список.Пометка(Список.РазмерСписка(), 1);
			КонецЕсли;
			Если БанковскийСчет.БанкДляРасчетов.Выбран() = 1 Тогда
				Список.ДобавитьЗначение("р/с " + СокрЛП(БанковскийСчет.НомерСчета));
				Список.Пометка(Список.РазмерСписка(), 1);
				
				Список.ДобавитьЗначение("в " + СокрЛП(БанковскийСчет.Банк.Наименование) + " " + СокрЛП(БанковскийСчет.Банк.Местонахождение));
				Список.Пометка(Список.РазмерСписка(), 1);
			КонецЕсли;
		Иначе
			Список.ДобавитьЗначение(СокрЛП(Парам.Получить("НаименованиеКонтрагента")));
			Список.Пометка(Список.РазмерСписка(), 1);

			Если ПустоеЗначение(Парам.Получить("БанкДляРасчетов"))=0 Тогда
				Список.ДобавитьЗначение("р/с " + СокрЛП(Парам.Получить("НомерСчета")));
				Список.Пометка(Список.РазмерСписка(), 1);
				
				Если ПустоеЗначение(Парам.Получить("Банк"))=0 Тогда
					Список.ДобавитьЗначение("в " + СокрЛП(Парам.Получить("Банк").Наименование) + " " + СокрЛП(Парам.Получить("Банк").Местонахождение));
					Список.Пометка(Список.РазмерСписка(), 1);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		Если Метаданные.Документ(ВидДокСпр).Выбран() = 1 Тогда
			Если Метаданные.Документ(ВидДокСпр).РеквизитШапки(РеквРез).Выбран()=1 Тогда
				Форма.Заголовок.Заголовок("Заполнение поля """+Метаданные.Документ(ВидДокСпр).РеквизитШапки(РеквРез).Синоним+"""");
        	КонецЕсли;
		ИначеЕсли Метаданные.Справочник(ВидДокСпр).Выбран() = 1 Тогда
			Если (Метаданные.Справочник(ВидДокСпр).Реквизит(РеквРез).Выбран()=1) Тогда
				Форма.Заголовок.Заголовок("Заполнение поля """+Метаданные.Справочник(ВидДокСпр).Реквизит(РеквРез).Синоним+"""");
			КонецЕсли;
		Иначе
			Форма.Заголовок.Заголовок("Заполнение поля """+РеквРез+"""");
		КонецЕсли;

	ИначеЕсли (Парам.Получить("Режим")=2) Тогда
		// Если нужно сформировать строку с назначением платежа

		Назначение = БанковскийСчет.Назначение;
		
		Для Номер = 1 По СтрКоличествоСтрок(Назначение) Цикл
			Текст = СокрЛП(СтрПолучитьСтроку(Назначение, Номер));
			
			Если ПустаяСтрока(Текст) = 0 Тогда
				Список.ДобавитьЗначение(Текст);
				Список.Пометка(Список.РазмерСписка(), 1);
			КонецЕсли;
		КонецЦикла;
		
		Сумма = 0;
		СуммаНДС = 0;
		
		Если глЕстьРеквизитШапки("Сумма", ВидДокСпр) = 1 Тогда
			Сумма   = Парам.Получить("Контекст").Сумма;
			Список.ДобавитьЗначение("сумма " + СокрЛ(Формат(Сумма,"Ч15.2-")));
			Список.Пометка(Список.РазмерСписка(), 1);
		КонецЕсли;
		
		Если глЕстьРеквизитШапки("СтавкаНДС", ВидДокСпр) = 1 Тогда
			ПроцНДС = Парам.Получить("Контекст").СтавкаНДС;
			Если глВыделяемыйНДС(ПроцНДС) <> 0 Тогда
				СуммаНДС = Число(Сумма)*глВыделяемыйНДС(ПроцНДС);
				Стр = "в т.ч. НДС ("+ПроцНДС+"): "+СокрЛ(Формат(СуммаНДС,"Ч15.2-"));
			Иначе
				Стр = "без налога (НДС)";
			КонецЕсли;
			
			Список.ДобавитьЗначение("<с новой строки> "+Стр);
			Список.Пометка(Список.РазмерСписка(), 1);
		КонецЕсли;
		
		Форма.Заголовок.Заголовок("Заполнение поля ""Назначение платежа""");
	КонецЕсли;

КонецПроцедуры // ПриОткрытии()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//