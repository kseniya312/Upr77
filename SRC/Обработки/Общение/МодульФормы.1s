Перем ВхТекст;   
Перем Меню;  
Перем Отправитель;     
Перем ТЗ;
 
//*******************************************  

Процедура ПроверитьСообщение()
    
	Если СокрЛП(глПользователь) <> "" Тогда
		Нов = 0;
		
		Спр = СоздатьОбъект("Справочник.Сообщения"); 
		Спр.ВыбратьЭлементы();
		Пока Спр.ПолучитьЭлемент() = 1 Цикл
			Если Спр.Прочтено = 1 Тогда
				Продолжить;
			КонецЕсли;
			Если (Спр.Адресат <> глПользователь) И (СокрЛП(Спр.Адресат) <> "") Тогда
				Продолжить;
			КонецЕсли; 
			Если СокрЛП(Спр.Адресат) = "" Тогда
				Т = " (всем)"; 
			Иначе
				Т = "";
			КонецЕсли;

			ТЗ.НоваяСтрока(1);   
			ТЗ.Время =     Спр.Наименование;
			ТЗ.Автор =     ""+Спр.Автор+Т;
			ТЗ.Сообщение = Спр.Сообщение;  
			Отправитель =  Спр.Автор;  
			
			Поз = Меню.НайтиЗначение(Отправитель);
			Если Поз <> 0 Тогда
				Меню.УдалитьЗначение(Поз);
				Меню.ВставитьЗначение(2,Отправитель);
			КонецЕсли;
			
			Нов = 1;
			
			Спр.Прочтено = 1;                      
			Спр.Записать();
		КонецЦикла;
		              
		Если Нов = 1 Тогда  
			Таблица.Очистить(); 
			ТЗ.ВыбратьСтроки();
			Пока ТЗ.ПолучитьСтроку() = 1 Цикл
				Сообщ = ТЗ.Сообщение; 
				Время = ТЗ.Время;
				Автор = ТЗ.Автор;
				Если ТЗ.Моё = 1 Тогда
					Таблица.ВывестиСекцию("МоёСообщение");
				Иначе
					Таблица.ВывестиСекцию("ВходящееСообщение");
				КонецЕсли;
			КонецЦикла;
			Таблица.Опции(0,0,0,0);
			Таблица.Показать();     
		КонецЕсли;
		
	КонецЕсли;  
	
КонецПроцедуры   

//*******************************************  

Процедура ПриОткрытии()
	
	Если СокрЛП(глПользователь) = "" Тогда 
		Сообщить("Пользователь не определён");
		СтатусВозврата(0);   
	Иначе
		Форма.Заголовок("Я : "+СокрЛП(глПользователь));  
		СпрПольз = СоздатьОбъект("Справочник.Пользователи");
		Если СпрПольз.НайтиЭлемент(глПользователь) = 1 Тогда
			СпрПольз.ЗапущеноОбщение = 1;
			СпрПольз.Записать();    
		КонецЕсли;
	КонецЕсли;
	
	Таблица.Опции(0,0,0,0);
	Таблица.Показать();    
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("Время");
	ТЗ.НоваяКолонка("Автор");
	ТЗ.НоваяКолонка("Сообщение","Строка");
	ТЗ.НоваяКолонка("Моё");
	
	СпрПольз = СоздатьОбъект("Справочник.Пользователи");
	Меню =     СоздатьОбъект("СписокЗначений"); 
	 
	СпрПольз.ВыбратьЭлементы();
	Пока СпрПольз.ПолучитьЭлемент() = 1 Цикл
		Если СпрПольз.ЭтоГруппа() = 1 Тогда
			Продолжить;
		КонецЕсли;
		Если СпрПольз.ПометкаУдаления() = 1 Тогда
			Продолжить;
		КонецЕсли;
		Меню.ДобавитьЗначение(СпрПольз.ТекущийЭлемент());  
	КонецЦикла; 
	Меню.СортироватьПоПредставлению();
	Меню.ВставитьЗначение(1,"ВСЕ");	
	                                         
	Сообщение = "";
	
	Форма.ОбработкаОжидания("ПроверитьСообщение",3);
	
КонецПроцедуры	// ПриОткрытии

//*******************************************

Процедура ДобавитьСообщение(Адресат)
    
	Если Адресат = "ВСЕ" Тогда
		Т = "(всем) : ";
	Иначе
		Т = "(для "+Адресат+") : ";
	КонецЕсли;
	
	ТЗ.НоваяСтрока(1); 
	ТЗ.Время =     ""+ТекущаяДата()+"/"+ТекущееВремя();   
	ТЗ.Автор =     "Я "+Т;
	ТЗ.Сообщение = СокрЛП(Сообщение);
	ТЗ.Моё =       1;
	
	Таблица.Очистить();     
	
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		Сообщ = ТЗ.Сообщение;   
		Время = ТЗ.Время;  
		Автор = ТЗ.Автор;
		Если ТЗ.Моё = 1 Тогда
			Таблица.ВывестиСекцию("МоёСообщение");
		Иначе
			Таблица.ВывестиСекцию("ВходящееСообщение");
		КонецЕсли;
	КонецЦикла;
	Таблица.Опции(0,0,0,0);
	Таблица.Показать();     
	
	Спр = СоздатьОбъект("Справочник.Сообщения"); 
	
	Если Адресат = "ВСЕ" Тогда
		СпрП = СоздатьОбъект("Справочник.Пользователи");  
		СпрП.ВыбратьЭлементы();
		Пока СпрП.ПолучитьЭлемент() = 1 Цикл
			Если СпрП.ЭтоГруппа() = 1 Тогда
				Продолжить;
			КонецЕсли;
			Если СпрП.ПометкаУдаления() = 1 Тогда
				Продолжить;
			КонецЕсли;
			Спр.Новый();   
			Спр.Наименование = ""+ТекущаяДата()+"/"+ТекущееВремя();
			Спр.Автор =        глПользователь;
			Спр.Адресат =      СпрП.ТекущийЭлемент();
			Спр.Сообщение =    СокрЛП(Сообщение);
			Спр.Прочтено =     0;
			Спр.Записать();
		КонецЦикла;
	Иначе
		Спр.Новый();   
		Спр.Наименование = ""+ТекущаяДата()+"/"+ТекущееВремя();
		Спр.Автор =        глПользователь;
		Спр.Адресат =      Адресат;
		Спр.Сообщение =    СокрЛП(Сообщение);
		Спр.Прочтено =     0;
		Спр.Записать();
	КонецЕсли;
	
КонецПроцедуры   

//*******************************************

Процедура Выполнить()
	
	Если СокрЛП(Сообщение) = "" Тогда
		Сообщить("Наберите текст сообщения");
	Иначе   
		Адресат = ""; 
		Поз =     "";
		Если Меню.ВыбратьЗначение(Адресат,"",Поз,,1) <> 1 Тогда
			Возврат;
		КонецЕсли;   
		Меню.УдалитьЗначение(Поз);
		Меню.ВставитьЗначение(2,Адресат);
		
		ДобавитьСообщение(Адресат);
	КонецЕсли;
	Сообщение = "";
	               
КонецПроцедуры     
 
//*******************************************

Процедура Ответить()

	Если СокрЛП(Сообщение) = "" Тогда
		Сообщить("Наберите текст сообщения");
	Иначе
		Если СокрЛП(Отправитель) = "" Тогда
			Сообщить("Выберите адресата"); 
		Иначе
			ДобавитьСообщение(Отправитель);   
			Сообщение = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//*******************************************

Процедура ПриЗакрытии()
	
	Ответ = Вопрос("Обработка должна быть запущена в течении всего рабочего дня. 
	|Хотите завершить работу?","Да+Нет");
	
	Если Ответ = "Да" Тогда   
		СпрПольз = СоздатьОбъект("Справочник.Пользователи");
		Если СпрПольз.НайтиЭлемент(глПользователь) = 1 Тогда  
			Попытка
				СпрПольз.ЗапущеноОбщение = 0;
				СпрПольз.Записать();     
			Исключение
			КонецПопытки;
		КонецЕсли;
		ЗавершитьРаботуСистемы(1);  
	Иначе
		СтатусВозврата(0);
	КонецЕсли;
	
КонецПроцедуры
