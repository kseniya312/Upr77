//******************************************************************************
// УправлениеДиалогом()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
//  При смене вида разделителя
//  (фирма, упр.аналитика, юр.лицо)
//
// Описание:
//  Переключает слои формы в зависимости от вида разделителя
//
Процедура УправлениеДиалогом()
	Форма.ИспользоватьСлой("Основной,Разделитель"+СокрЛП(ВидРазделителя));
КонецПроцедуры // УправлениеДиалогом()


//******************************************************************************
// ПриИзмененииГраницыРасчета()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ПриИзмененииГраницыРасчета()
	
	Если ВидГраницы <> 3 Тогда
		Форма.ДатаОстатков.Доступность(0);
	Иначе
		Форма.ДатаОстатков.Доступность(1);
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииГраницыРасчета()

//******************************************************************************
// ОК()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  кнопка "Ок"
//
// Описание:
//  Передаем в вызывавший обработку модуль результаты
//
Процедура ОК()
	
	Объект = "";    
	Если ВидРазделителя = 1 Тогда
		Если ПустоеЗначение(ВыбРазделитель1) = 0 Тогда
		    ФирмаДляОстатковТМЦ = ВыбРазделитель1;
		Иначе	
			СпрФирм             = СоздатьОбъект("Справочник.Фирмы");
			ФирмаДляОстатковТМЦ = СоздатьОбъект("СписокЗначений"); 
			СпрФирм.ВыбратьЭлементы();
			Пока СпрФирм.ПолучитьЭлемент() = 1 Цикл
	        	ФирмаДляОстатковТМЦ.ДобавитьЗначение(СпрФирм.ТекущийЭлемент());
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли ВидРазделителя = 2 Тогда
		Объект         = ВыбРазделитель2;
		ИмяОбъекта     = "ЮрЛицо"; 
	ИначеЕсли ВидРазделителя = 3 Тогда
		Объект         = ВыбРазделитель3;
		ИмяОбъекта     = "УпрАналитика"; 
	КонецЕсли;
	
	Если ПустоеЗначение(Объект) = 0 Тогда // Надо заполнить список фирм
		СпрФирм             = СоздатьОбъект("Справочник.Фирмы");
		ФирмаДляОстатковТМЦ = СоздатьОбъект("СписокЗначений"); 
		СпрФирм.ВыбратьЭлементы();
		Пока СпрФирм.ПолучитьЭлемент() = 1 Цикл
	    	Если СпрФирм.ПолучитьАтрибут(ИмяОбъекта)= Объект Тогда
	        	ФирмаДляОстатковТМЦ.ДобавитьЗначение(СпрФирм.ТекущийЭлемент());
	    	КонецЕсли;
		КонецЦикла;
		
		Если ФирмаДляОстатковТМЦ.РазмерСписка() = 0 Тогда
			// Если нет ни одной фирмы с таким лицом, то отбирать нечего
			Предупреждение("Нет ни одной фирмы со значением """ + Объект.ПредставлениеВида() + """ равным """ + 
			                СокрЛП(Объект) + """.",60);
			Активизировать("ВыбРазделитель"+ СокрЛП(ВидРазделителя));
			СтрокаДействийФормы = "";
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Форма.Параметр.Установить("СтатусВозврата",      1);
	Форма.Параметр.Установить("ФирмаДляОстатковТМЦ", ФирмаДляОстатковТМЦ);
	Форма.Параметр.Установить("ВидРазделителя",      ВидРазделителя);
	Форма.Параметр.Установить("Фирма",               ВыбРазделитель1);
	Форма.Параметр.Установить("ЮрЛицо",              ВыбРазделитель2);
	Форма.Параметр.Установить("УпрАналитика",        ВыбРазделитель3);
	Форма.Параметр.Установить("Склад",               Склад);
	Форма.Параметр.Установить("ВидГраницы",          ВидГраницы);
	Форма.Параметр.Установить("ВключатьВпрайс",      ВключатьВпрайс);
	
	Если ВидГраницы = 3 Тогда
    	// выбрана дата
		Если ПустоеЗначение(ДатаОстатков) = 1 Тогда
			Предупреждение("Не задана дата показа остатков", 60);
			АктивизироватьОбъект(ДатаОстатков);
			Форма.СтрокаДействийФормы = "";
			Возврат;
		КонецЕсли;
		Форма.Параметр.Установить("ДатаОстатков", ДатаОстатков);
	КонецЕсли;
	
КонецПроцедуры

//******************************************************************************
// Отмена()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "Отмена"
//
// Описание:
//  Передаем в вызывавший обработку модуль статус возврата 0 (отмена)
//         
Процедура Отмена()
	
КонецПроцедуры

// *****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если ТипЗначенияСтр(Форма.Параметр) <> "СписокЗначений" Тогда
		Предупреждение("Обработка вызывается только из формы подбора!", 60);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	ВидРазделителя  = Форма.Параметр.Получить("ВидРазделителя");
	ВыбРазделитель1 = Форма.Параметр.Получить("Фирма");
	ВыбРазделитель2 = Форма.Параметр.Получить("ЮрЛицо");
	ВыбРазделитель3 = Форма.Параметр.Получить("УпрАналитика");
	Склад		    = Форма.Параметр.Получить("Склад");
	ВидГраницы      = Форма.Параметр.Получить("ВидГраницы");
	ДатаОстатков    = Форма.Параметр.Получить("ДатаОстатков");
	ВключатьВпрайс  = Форма.Параметр.Получить("ВключатьВпрайс");
	
	Форма.Параметр.Установить("СтатусВозврата"	, 0);
	
	Если Форма.Параметр.Получить("ИспДокумент") <> 1 Тогда
		Форма.ВидГраницыДок.Доступность(0);
	КонецЕсли;
	ПриИзмененииГраницыРасчета();
	УправлениеДиалогом();
	
КонецПроцедуры // ПриОткрытии()