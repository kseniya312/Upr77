////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем	Док;					//	выгружаемый документ
Перем	СписокАтрибутов;		//	вспомогательный список
								//	При выгрузке документа список заполняется значениями реквизитов, которые затем 
								//	извлекаются, при добавлении XML-элементов

Перем	КаталогИдентификации;	//	каталог (элемент справочника "Каталоги") по которому идентифицируются товары
Перем	СпособИдентификации;	//	если каталог собственный, то по какому реквизиту идентифицируются товары (код, артикул, штрихкод)
Перем	ВместеСКаталогом;		//	если 1, то нужно добавить XML-элемент "Каталог" с описанием товарных позиций документа
Перем	ЭлКаталог;				//	XML-элемент "Каталог", если ВместеСКаталогом = 1

Перем	ФлагОшибки;				//	если 1, значит где-то произошел сбой...
  

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// СоздатьПодчиненныйЭлемент(ЭлВладелец, ИмяТега, Значение, ФлЕслиПустоеНеДобавлять)
//
// Параметры:
//	ЭлВладелец				-	элемент родитель
//	ИмяТега					-	тег подчиненного элемента
//	Значение				-	значение подчиненного элемента
//	ФлЕслиПустоеНеДобавлять	-	флаг, если переданное значение пустое, то элемент добавлять не следует
//
// Возвращаемое значение:
//	XML-элемент
//
// Описание
//	Создает и возвращает элемент, подчиненный  элементу-владельцу, если передано значение, то устанавливается
//
Функция СоздатьПодчиненныйЭлемент(ЭлВладелец, ИмяТега, Значение="", ФлЕслиПустоеНеДобавлять=0)
	
	Если (ФлЕслиПустоеНеДобавлять = 1) И (ПустоеЗначение(Значение) = 1) Тогда Возврат "" КонецЕсли;
	
	ЭлВозврата = ЭлВладелец.СоздатьПодчиненныйЭлемент(ИмяТега, , "urn:CommerceML");
	Если Значение <> "" Тогда
	    ЭлВозврата.Значение = СокрЛП(Значение);
	КонецЕсли;
	
	Возврат ЭлВозврата;
	
КонецФункции		//	СоздатьПодчиненныйЭлемент()

//******************************************************************************
// ПроверитьИУстановитьАтрибут(Эл, ИдАтрибута, ЗнАтрибута)
//
// Параметры:
// 	Эл			-	Элемент, атрибут которого устанавливаем
//	ИдАтрибута	-	Идентификатор атрибута
//	ЗнАтрибута	-	устанавливаемое значение
//
// Возвращаемое значение:
// 	нет
//
// Описание
//	Устанавливает значение атрибута элемента, если оно не пустое
//
Процедура ПроверитьИУстановитьАтрибут(Эл, ИдАтрибута, ЗнАтрибута);
	
	Если ПустоеЗначение(ЗнАтрибута) = 0 Тогда
		Если ТипЗначенияСтр(ЗнАтрибута) <> "Дата" Тогда
			Эл.УстановитьАтрибут(ИдАтрибута, СокрЛП(ЗнАтрибута));
		Иначе
        	Эл.УстановитьАтрибут(ИдАтрибута, ЗнАтрибута);
		КонецЕсли;
		
	КонецЕсли;    
	
КонецПроцедуры		//	ПроверитьИУстановитьАтрибут()

//******************************************************************************
// Ид(Эл, Зн)
//		
// Параметры:
//	Эл	-	любой XML-элемент
//	Зн	-	значение, которое преобразуется в id
//		
// Возвращаемое значение:
//	Генерирует корректный идентификатор типа - id
//		
Функция Ид(Эл, Зн)
	
	Если ПустоеЗначение(Зн) = 0 Тогда
		Возврат Эл.ПреобразоватьВ_ИД(ЗначениеВСтрокуВнутр(Зн));
	Иначе
		Возврат("");
	КонецЕсли;
	
КонецФункции		//	Ид()

//******************************************************************************
// УстановитьАтрибутыКаталога()
//
// Параметры:
//	Эл	-	XML-элемент 
//
// Возвращаемое значение:
// 	Устанавливает атрибуты каталога
//
// Описание
// 	Устанавливает атрибуты каталога в соответствии со схемой CommerceML
//
Процедура УстановитьАтрибутыКаталога(Эл)
	                                                    
	ПроверитьИУстановитьАтрибут(Эл, "Идентификатор",	КаталогИдентификации.Идентификатор);
	ПроверитьИУстановитьАтрибут(Эл, "Наименование",		КаталогИдентификации.Наименование);
	ПроверитьИУстановитьАтрибут(Эл, "Владелец",			КаталогИдентификации.ВладелецКаталога.Идентификатор);
	ПроверитьИУстановитьАтрибут(Эл, "Описание", 		КаталогИдентификации.Комментарий);
	ПроверитьИУстановитьАтрибут(Эл, "Единица", 			КаталогИдентификации.ЕдиницаПоУмолчанию.Наименование);
	
КонецПроцедуры		//	УстановитьАтрибутыКаталога()

//******************************************************************************
// УстановитьАтрибутыКонтрагента_ПоКонтрагенту(ЭлКонтрагент, Контрагент)
//		
// Параметры:
//	ЭлКонтрагент	-	XML-элемент "Контрагент"
//	Контрагент		-	элемент справочника "Контрагенты"
//		
// Возвращаемое значение:
// 	Нет.
//		
// Описание
//	Устанавливает атрибуты элемента "Контрагент" в соответствии со схемой
//		
Процедура УстановитьАтрибутыКонтрагента(ЭлКонтрагент, Контрагент)
	
	Если Контрагент.Вид() = "Фирмы" Тогда
		ЮрФизЛицо				=	Контрагент.ЮрЛицо;
	Иначе
		ЮрФизЛицо				=	Контрагент.ЮрФизЛицо;
	КонецЕсли;
	
    Идентификатор				=	СокрЛП(Контрагент.Идентификатор);
	ОтображаемоеНаименование	=	СокрЛП(Контрагент.Наименование);
	Наименование				=	?(ПустоеЗначение(ЮрФизЛицо.ПолнНаименование)=1, ОтображаемоеНаименование, СокрЛП(ЮрФизЛицо.ПолнНаименование));
	Адрес						=	СокрЛП(ЮрФизЛицо.ФактАдрес);
	ЮридическийАдрес			=	СокрЛП(ЮрФизЛицо.ЮрАдрес);
	//Сайт						=	СокрЛП(Контрагент.Сайт.АдресСайта);
	//Комментарий				=	СокрЛП(Контрагент.Комментарий);
	//ОсновнойКонтакт			=	"";

    ПроверитьИУстановитьАтрибут(ЭлКонтрагент, "Идентификатор", 				Идентификатор);
	ПроверитьИУстановитьАтрибут(ЭлКонтрагент, "Наименование",				Наименование);
	ПроверитьИУстановитьАтрибут(ЭлКонтрагент, "ОтображаемоеНаименование",	ОтображаемоеНаименование);
	ПроверитьИУстановитьАтрибут(ЭлКонтрагент, "Адрес",						Адрес);
	ПроверитьИУстановитьАтрибут(ЭлКонтрагент, "ЮридическийАдрес",			ЮридическийАдрес);
	//ПроверитьИУстановитьАтрибут(ЭлКонтрагент, "Сайт",						Сайт);
	//ПроверитьИУстановитьАтрибут(ЭлКонтрагент, "Комментарий",				Комментарий);
	//ПроверитьИУстановитьАтрибут(ЭлКонтрагент, "ОсновнойКонтакт",			ОсновнойКонтакт);
	
КонецПроцедуры		//	УстановитьАтрибутыКонтрагента()
    
//******************************************************************************
// ДобавитьЭлемент_ДополнительныйРеквизит()
//		
// Параметры:
//	ЭлРодитель		-	XML-элемент
//	ИмяРеквизита	-	Идентификатор дополнительного реквизита
//	ЗнРеквизита		-	значение дополнительного реквизита
//		
// Возвращаемое значение:
// 	Нет.
//		
// Описание
//	Устанавливает атрибуты элемента "ДополнительныйРеквизит" в соответствии со схемой
//		                                                                                                    
Процедура ДобавитьЭлемент_ДополнительныйРеквизит(ЭлРодитель, ИмяРеквизита, ЗнРеквизита)
	                                                    
	ЭлРеквизит = СоздатьПодчиненныйЭлемент(ЭлРодитель, "ДополнительныйРеквизит");
	
	ПроверитьИУстановитьАтрибут(ЭлРеквизит, "Наименование",	ИмяРеквизита);
	ПроверитьИУстановитьАтрибут(ЭлРеквизит, "Значение",		ЗнРеквизита);
              
КонецПроцедуры		//	ДобавитьЭлемент_ДополнительныйРеквизит()

//******************************************************************************
// ДобавитьЭлемент_Контакт()
//		
// Параметры:
//	ЭлРодитель	-	XML-Элемент
//	Контрагент	-	контактный контрагент
//		
// Возвращаемое значение:
// 	XML-элемент "Контакт"
//		
// Описание
//	Добавляет XML-элемент "Контакт" и устанавливает его атрибуты в соответствии со схемой
//		
Функция ДобавитьЭлемент_Контакт(ЭлРодитель, Контрагент)
	
	Если ПустоеЗначение(Контрагент) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
	
	ЭлКонтакт = СоздатьПодчиненныйЭлемент(ЭлРодитель, "Контакт");
	
	Если Контрагент.Вид() = "Фирмы" Тогда
		ЮрФизЛицо	=	Контрагент.ЮрЛицо;
	Иначе
		ЮрФизЛицо	=	Контрагент.ЮрФизЛицо;
	КонецЕсли;
	
	ПроверитьИУстановитьАтрибут(ЭлКонтакт, "Идентификатор",	Ид(ЭлКонтакт, Контрагент));
	ПроверитьИУстановитьАтрибут(ЭлКонтакт, "Наименование",	Контрагент.Наименование);
	ПроверитьИУстановитьАтрибут(ЭлКонтакт, "Комментарий",	"");
	
	СоздатьПодчиненныйЭлемент(ЭлКонтакт, "КонтактноеЛицо",	СписокАтрибутов.Получить("КонтактноеЛицо"),	1);
	СоздатьПодчиненныйЭлемент(ЭлКонтакт, "Телефон", 		ЮрФизЛицо.Телефоны,							1);
	СоздатьПодчиненныйЭлемент(ЭлКонтакт, "Почта", 			Контрагент.ЭлПочта,							1);
	//СоздатьПодчиненныйЭлемент(ЭлКонтакт, "Факс", 			"",											1);
	//СоздатьПодчиненныйЭлемент(ЭлКонтакт, "ICQ", 			"",											1);
	         
	Возврат ЭлКонтакт;
	
КонецФункции		//	ДобавитьЭлемент_Контакт()
                        
//******************************************************************************
// ДобавитьЭлемент_Банк(ЭлРодитель, Банк)
//		
// Параметры:
//	ЭлРодитель	-	XML-Элемент
//	Банк		-	элемент справочника "Банки"
//		
// Возвращаемое значение:
// 	XML-элемент "Банк"
//		
// Описание
//	Добавляет XML-элемент "Банк" и устанавливает его атрибуты в соответствии со схемой
//		
Функция ДобавитьЭлемент_Банк(ЭлРодитель, Банк)
    
	Если ПустоеЗначение(Банк) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
	
	ЭлБанк = СоздатьПодчиненныйЭлемент(ЭлРодитель, "Банк");
	
	ПроверитьИУстановитьАтрибут(ЭлБанк, "Идентификатор",	Ид(ЭлБанк, Банк));
	ПроверитьИУстановитьАтрибут(ЭлБанк, "Наименование",		Банк.Наименование);
	ПроверитьИУстановитьАтрибут(ЭлБанк, "Местонахождение",	Банк.Местонахождение);
	ПроверитьИУстановитьАтрибут(ЭлБанк, "БИК",				Банк.Код);
	ПроверитьИУстановитьАтрибут(ЭлБанк, "НомерСчета",		Банк.КоррСчет);
	ПроверитьИУстановитьАтрибут(ЭлБанк, "Адрес",			Банк.Адрес);
	ПроверитьИУстановитьАтрибут(ЭлБанк, "Телефон",			Банк.Телефоны);
	//ПроверитьИУстановитьАтрибут(ЭлБанк, "Комментарий",		"");	
         
	Возврат ЭлБанк;
	
КонецФункции		//	ДобавитьЭлемент_Банк()

//******************************************************************************
// ДобавитьЭлемент_РасчетныйСчет(ЭлРодитель, РасчетныйСчет)
//		
// Параметры:
//	ЭлРодитель		-	XML-Элемент
//	РасчетныйСчет	-	элемент справочника "БанковскиеСчета"
//		
// Возвращаемое значение:
// 	XML-элемент "РасчетныйСчет"
//		
// Описание
//	Добавляет XML-элемент "РасчетныйСчет" и устанавливает его атрибуты в соответствии со схемой
//		
Функция ДобавитьЭлемент_РасчетныйСчет(ЭлРодитель, РасчетныйСчет)
	
	Если ПустоеЗначение(РасчетныйСчет) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
    
	ЭлРасчетныйСчет = СоздатьПодчиненныйЭлемент(ЭлРодитель, "РасчетныйСчет");
	                                          
	ДобавитьЭлемент_Банк(ЭлРодитель.Родитель, РасчетныйСчет.Банк);
	ДобавитьЭлемент_Банк(ЭлРодитель.Родитель, РасчетныйСчет.БанкДляРасчетов);
	
	ПроверитьИУстановитьАтрибут(ЭлРасчетныйСчет , "НомерСчета",			РасчетныйСчет.НомерСчета);
	ПроверитьИУстановитьАтрибут(ЭлРасчетныйСчет , "Банк",				Ид(ЭлРасчетныйСчет, РасчетныйСчет.Банк));
	ПроверитьИУстановитьАтрибут(ЭлРасчетныйСчет , "БанкКорреспондент",	Ид(ЭлРасчетныйСчет, РасчетныйСчет.БанкДляРасчетов));
	//ПроверитьИУстановитьАтрибут(ЭлРасчетныйСчет , "Комментарий",		"");
	          
	Возврат ЭлРасчетныйСчет;
	
КонецФункции		//	ДобавитьЭлемент_РасчетныйСчет()
    
//******************************************************************************
// ДобавитьЭлемент_Контрагент(ЭлРодитель, Контрагент)
//		
// Параметры:
//	ЭлРодитель		-	XML-Элемент
//	Контрагент	-	элемент справочника "Контрагенты"
//		
// Возвращаемое значение:
// 	XML-элемент "Контрагент"
//		
// Описание
//	Добавляет XML-элемент "Контрагент" и устанавливает его атрибуты в соответствии со схемой
//		
Функция ДобавитьЭлемент_Контрагент(ЭлРодитель, Контрагент)
	
	Если ПустоеЗначение(Контрагент) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
	
	ЭлКонтрагент = СоздатьПодчиненныйЭлемент(ЭлРодитель, "Контрагент");
	
	УстановитьАтрибутыКонтрагента(ЭлКонтрагент, Контрагент);
	
	Если 		Контрагент.Вид() = "Фирмы" Тогда		
		
		ДобавитьЭлемент_РасчетныйСчет(ЭлКонтрагент,	СписокАтрибутов.Получить("РасчетныйСчет"));
		ДобавитьЭлемент_Контакт(ЭлКонтрагент,		СписокАтрибутов.Получить("Контакт"));
		
	КонецЕсли;

	Возврат ЭлКонтрагент;
	
КонецФункции		//	ДобавитьЭлемент_Контрагент()

//******************************************************************************
// ДобавитьЭлемент_Склад(ЭлРодитель, Склад)
//		
// Параметры:
//	ЭлРодитель	-	XML-Элемент
//	Склад		-	элемент справочника "МестаХранения"
//		
// Возвращаемое значение:
// 	XML-элемент "Склад"
//		
// Описание
//	Добавляет XML-элемент "Склад" и устанавливает его атрибуты в соответствии со схемой
//		
Функция ДобавитьЭлемент_Склад(ЭлРодитель, Склад)
                                               
	Если ПустоеЗначение(Склад) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
	
	ЭлСклад = СоздатьПодчиненныйЭлемент(ЭлРодитель, "Склад");
	
	ПроверитьИУстановитьАтрибут(ЭлСклад, "Идентификатор",	Ид(ЭлСклад, Склад));
	ПроверитьИУстановитьАтрибут(ЭлСклад, "Наименование",	Склад.Наименование);
	//ПроверитьИУстановитьАтрибут(ЭлСклад, "Адрес",			"");
	//ПроверитьИУстановитьАтрибут(ЭлСклад, "ОсновнойКонтакт",	"");	
	//ПроверитьИУстановитьАтрибут(ЭлСклад, "Комментарий",		Склад.Комментарий);
	       
	Возврат ЭлСклад;
	
КонецФункции		//	ДобавитьЭлемент_Склад()

//******************************************************************************
// ДобавитьЭлемент_ПредприятиеВДокументе(ЭлРодитель, Контрагент)
//		
// Параметры:
//	ЭлРодитель	-	XML-Элемент
//	Контрагент	-	элемент справочника "Контрагенты"
//		
// Возвращаемое значение:
// 	XML-элемент "ПредприятиеВДокументе"
//		
// Описание
//	Добавляет XML-элемент "ПредприятиеВДокументе" и устанавливает его атрибуты в соответствии со схемой
//		
Функция ДобавитьЭлемент_ПредприятиеВДокументе(ЭлРодитель, Контрагент)
	
	Если ПустоеЗначение(Контрагент) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
	
	ДобавитьЭлемент_Контрагент(ЭлРодитель.Родитель, Контрагент);
	                                            
	//ДобавитьЭлемент_Склад(ЭлРодитель.Родитель, СписокАтрибутов.Получить("Склад"));
	
	ЭлПредприятие = СоздатьПодчиненныйЭлемент(ЭлРодитель, "ПредприятиеВДокументе");
		
	ПроверитьИУстановитьАтрибут(ЭлПредприятие, "Контрагент",	Контрагент.Идентификатор);
	ПроверитьИУстановитьАтрибут(ЭлПредприятие, "РасчетныйСчет",	?(ПустоеЗначение(СписокАтрибутов.Получить("РасчетныйСчет")) = 1, "", СписокАтрибутов.Получить("РасчетныйСчет").НомерСчета));
	ПроверитьИУстановитьАтрибут(ЭлПредприятие, "Контакт",		Ид(ЭлПредприятие, СписокАтрибутов.Получить("Контакт")));
	//ПроверитьИУстановитьАтрибут(ЭлПредприятие, "Склад",			Ид(ЭлПредприятие, СписокАтрибутов.Получить("Склад")));
	
	ПроверитьИУстановитьАтрибут(ЭлПредприятие, "Роль",			СписокАтрибутов.Получить("Роль"));
	                                                                
	Возврат ЭлПредприятие;
	
КонецФункции		//	ДобавитьЭлемент_ПредприятиеВДокументе()

//******************************************************************************
// ДобавитьЭлемент_ДополнительныйРасход(ЭлРодитель, Сумма, Комментарий)
//		
// Параметры:
//	ЭлРодитель	-	XML-Элемент
//	Сумма		-	Сумма дополниетельного расхода
//	Комментарий	-	строка - произвольный комментарий
//		
// Возвращаемое значение:
// 	XML-элемент "ДополнительныйРасход"
//		
// Описание
//	Добавляет XML-элемент "ДополнительныйРасход" и устанавливает его атрибуты в соответствии со схемой
//		
Функция ДобавитьЭлемент_ДополнительныйРасход(ЭлРодитель, Сумма=0, Комментарий="")

	Если ПустоеЗначение(Сумма) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
	
	ЭлДополнительныйРасход = СоздатьПодчиненныйЭлемент(ЭлРодитель, "ДополнительныйРасход");
		
	ПроверитьИУстановитьАтрибут(ЭлДополнительныйРасход, "Сумма",		Сумма);
	ПроверитьИУстановитьАтрибут(ЭлДополнительныйРасход, "Комментарий",	Комментарий);
	                                                                
	Возврат ЭлДополнительныйРасход;
	    
КонецФункции		//	ДобавитьЭлемент_ДополнительныйРасход()

//******************************************************************************
// ДобавитьЭлемент_СуммаНалога(ЭлРодитель, ИгнорироватьПустуюСумму)
//		
// Параметры:
//	ЭлРодитель				-	XML-Элемент
//	ИгнорироватьПустуюСумму	-	число - (если 1) и (Сумма = 1), то 
//		
// Возвращаемое значение:
// 	XML-элемент "СуммаНалога"
//		
// Описание
//	Добавляет XML-элемент "СуммаНалога" и устанавливает его атрибуты в соответствии со схемой
//	если (сумма = 0) И (ИгнорироватьПустуюСумму = 0), то элемент не добавляется
//		
Функция ДобавитьЭлемент_СуммаНалога(ЭлРодитель, ИгнорироватьПустуюСумму=0)

	Если ПустоеЗначение(СписокАтрибутов.Получить("Налог")) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
	Если ИгнорироватьПустуюСумму = 0 Тогда
		Если ПустоеЗначение(СписокАтрибутов.Получить("Сумма")) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
	КонецЕсли;
	
	ЭлНалог = СоздатьПодчиненныйЭлемент(ЭлРодитель, "СуммаНалога");
		
	ПроверитьИУстановитьАтрибут(ЭлНалог, "Налог",			СписокАтрибутов.Получить("Налог"));
	ПроверитьИУстановитьАтрибут(ЭлНалог, "Ставка",			СписокАтрибутов.Получить("Ставка"));
	ПроверитьИУстановитьАтрибут(ЭлНалог, "Сумма",			СписокАтрибутов.Получить("Сумма"));
	ПроверитьИУстановитьАтрибут(ЭлНалог, "ВключенВСумму",	СписокАтрибутов.Получить("ВключенВСумму"));
	                                                                
	Возврат ЭлНалог;	
	    
КонецФункции		//	ДобавитьЭлемент_СуммаНалога()

//******************************************************************************
// ДобавитьЭлемент_ПредприятиеВДокументе(ЭлРодитель, Контрагент)
//		
// Параметры:
//	ЭлРодитель	-	XML-Элемент
//	Контрагент	-	элемент справочника "Контрагенты"
//		
// Возвращаемое значение:
// 	XML-элемент "ПредприятиеВДокументе"
//		
// Описание
//	Добавляет XML-элемент "ПредприятиеВДокументе" и устанавливает его атрибуты в соответствии со схемой
//		
Функция ИдТовараВСобственномКаталоге(Товар, ИдАтрибутаИдентификации)
	                                                                
	Если ИдАтрибутаИдентификации <> "ШтрихКод" Тогда
		Возврат( СокрЛП(Товар.ПолучитьАтрибут(ИдАтрибутаИдентификации)) );
	Иначе
		Возврат( СокрЛП(Товар.ОсновнаяЕдиница.ШтрихКод) );
	КонецЕсли;

КонецФункции		//	ИдТовараВСобственномКаталоге()

//******************************************************************************
// ПолучитьИдТовараПоКаталогу(Товар, Каталог)
//		
// Параметры:
//	Товар	-	товар (Справочник.Номенклатура)
//	Каталог	-	Каталог	(Справочник.Каталоги)
//		
// Возвращаемое значение:
// 	Идентификатор товара в каталоге
//		
// Описание
//	Если каталог наш, 		то	возвращается значение реквизита идентификации
//	Если каталог внешний,	то	возвращается значение реквизита "ИдентификаторВКаталоге" соответствующего аналога
//
Функция ПолучитьИдТовараПоКаталогу(Товар, Каталог="")
	                                                
	Если ПустоеЗначение(Каталог) = 1 Тогда Каталог = КаталогИдентификации КонецЕсли;
	
	Если Каталог.ВладелецКаталога.Вид() = "Фирмы" Тогда
		Возврат ИдТовараВСобственномКаталоге(Товар, СпособИдентификации);
	Иначе
		СпрАналоги = СоздатьОбъект("Справочник.Аналоги");
		СпрАналоги.ИспользоватьВладельца(Товар);
		Если СпрАналоги.НайтиПоРеквизиту("Каталог", Каталог, 0) = 0 Тогда
			Предупреждение("Для товара " + Товар + " не задан аналог!");
			Возврат "";
		Иначе
			Возврат СокрЛП(СпрАналоги.ИдентификаторВКаталоге);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции		//	ПолучитьИдТовараПоКаталогу()

//******************************************************************************
// ДобавитьСвойство(Каталог, ВидСвойства, ДобавлятьСсылку)
//
// Параметры:
//  Каталог			-	элемент справочника "Каталоги"
//	ВидСвойства		-	элемент справочника "Виды свойств"
//	ДобавлятьСсылку	-	число - если 1, то кроме элемента "Свойство" будет добавлен также элемент "СсылкаНаСвойство"
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Добавляет XML-элемент "Свойство" и устанавливает его атрибуты в соответствии со схемой - описание вида свойсства в каталоге
//
Процедура ДобавитьСвойство(Каталог, ВидСвойства, ДобавлятьСсылку=0)
	
	УжеДобавлено = ЭлКаталог.ВыбратьУзел("Свойство[(@Идентификатор=""" + СокрЛП(ВидСвойства.ИдентификаторВКаталоге) +""")]/@Наименование");
	            
	Если ПустоеЗначение(УжеДобавлено) = 0 Тогда Возврат КонецЕсли;
	
	ЭлСвойствоКаталога = СоздатьПодчиненныйЭлемент(ЭлКаталог, "Свойство");
	
	ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Идентификатор",	ВидСвойства.ИдентификаторВКаталоге);
	ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Наименование",		ВидСвойства.Наименование);
	ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "Множественность",	ВидСвойства.Множественность);
                                                                  
	Если ДобавлятьСсылку = 1 Тогда
		ЭлСвойствоКаталога = СоздатьПодчиненныйЭлемент(ЭлКаталог, "СсылкаНаСвойство");
		
		ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "ИдентификаторКаталога",	Каталог.Идентификатор);
		ПроверитьИУстановитьАтрибут(ЭлСвойствоКаталога, "ИдентификаторСвойства",	ВидСвойства.ИдентификаторВКаталоге);
	КонецЕсли;
	
КонецПроцедуры		//	ДобавитьСвойство()

//******************************************************************************
// ДобавитьЗначенияСвойств(Эл, Каталог, Тов, СписокОбязательныхСвойств)
//
// Параметры:
//  Эл							-	элемент-родитель, для которого добавляем значения свойств
//	Каталог						-	элемент справочника "Каталоги"
//	Тов							-	элемент справочника "Номенклатура", - товар, для которого добавляем значения свойств
//	СписокОбязательныхСвойств	-	список значений - обязательных для заполения свойств
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Добавляет XML-элемент "ЗначениеСвойства" и устанавливает его атрибуты в соответствии со схемой
//
Процедура ДобавитьЗначенияСвойств(Эл, Каталог, Тов, СписокОбязательныхСвойств="")
	
	СписокЗаполненныхСвойств	=	СоздатьОбъект("СписокЗначений");
	        
	ИдКаталога					=	СокрЛП(Каталог.Идентификатор);
    СпрЗнСвойств				=	СоздатьОбъект("Справочник.СвойстваНоменклатуры");
	СпрЗнСвойств.ИспользоватьВладельца(Тов);
	СпрЗнСвойств.ВыбратьЭлементы(1);
	Пока СпрЗнСвойств.ПолучитьЭлемент() = 1 Цикл
		Если СпрЗнСвойств.ПометкаУдаления() = 1 Тогда Продолжить КонецЕсли;
		Если СпрЗнСвойств.ВидСвойства.Каталог = Каталог Тогда
			
			//	Описание вида св-ва в каталоге
			ДобавитьСвойство(Каталог, СпрЗнСвойств.ВидСвойства);
			
			//	Значение св-ва
			ИдСвойства			= СокрЛП(СпрЗнСвойств.ВидСвойства.ИдентификаторВКаталоге);
			ЭлЗначениеСвойства	= СоздатьПодчиненныйЭлемент(Эл, "ЗначениеСвойства");
			ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторКаталога",	ИдКаталога);
			ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "ИдентификаторСвойства",	ИдСвойства);
			ПроверитьИУстановитьАтрибут(ЭлЗначениеСвойства, "Значение",					СпрЗнСвойств.ЗначениеСвойства.Наименование);
			
			//	Ссылка на св-во
			Запрос = "СсылкаНаСвойство[(@ИдентификаторКаталога=""%%%"")and(@ИдентификаторСвойства=""***"")]";
			Запрос = СтрЗаменить(Запрос, "%%%", ИдКаталога);
			Запрос = СтрЗаменить(Запрос, "***", ИдСвойства);
			
			Если ПустоеЗначение(Эл.ВыбратьУзел(Запрос)) = 1 Тогда
				ЭлСсылкаНаСвойство	= СоздатьПодчиненныйЭлемент(Эл, "СсылкаНаСвойство");
				ПроверитьИУстановитьАтрибут(ЭлСсылкаНаСвойство, "ИдентификаторКаталога",	ИдКаталога);
				ПроверитьИУстановитьАтрибут(ЭлСсылкаНаСвойство, "ИдентификаторСвойства",	ИдСвойства);
			КонецЕсли;
			
			Если СписокЗаполненныхСвойств.Принадлежит(ИдСвойства) = 0 Тогда
				СписокЗаполненныхСвойств.ДобавитьЗначение(ИдСвойства);
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;

КонецПроцедуры		//	ДобавитьЗначенияСвойств()
            
//******************************************************************************
// УстановитьАтрибутыТовара(Эл, Тов, БезРодителя)
//
// Параметры:
//  Эл			-	элемент-родитель, у которого устанавливаем атрибуты
//	Тов			-	элемент справочника "Номенклатура"
//	БезРодителя	-	число - если 1, то атрибут "Родитель" не устанавливаем
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Устанавливает атрибуты у переданного элемента "Товар"
//
Процедура УстановитьАтрибутыТовара(Эл, Тов, БезРодителя=0)
	
	Если Тов.ЭтоГруппа() = 1 Тогда
		ИдВКаталоге = Тов.Код;
	Иначе
		ИдВКаталоге = ПолучитьИдТовараПоКаталогу(Тов);
	КонецЕсли;
		
	ПроверитьИУстановитьАтрибут(Эл, "Идентификатор",			Ид(Эл, Тов.ТекущийЭлемент()));
	ПроверитьИУстановитьАтрибут(Эл, "ИдентификаторВКаталоге",	ИдВКаталоге);
	ПроверитьИУстановитьАтрибут(Эл, "Наименование",				Тов.Наименование);
	ПроверитьИУстановитьАтрибут(Эл, "Родитель",					?(((ПустоеЗначение(Тов.Родитель)=1) Или (БезРодителя=1)), "", Ид(Эл, Тов.Родитель.ТекущийЭлемент())));
	ПроверитьИУстановитьАтрибут(Эл, "Единица",					Тов.ОсновнаяЕдиница);
	
КонецПроцедуры		//	УстановитьАтрибутыТовара()
    
//******************************************************************************
// ДобавитьГруппу(Эл, Группа)
//
// Параметры:
//  Эл			-	элемент-родитель - "Каталог"
//	Группа		-	группа справочника "Номенклатура", которую добавляем
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Добавляет элемент "Группа"
//
Процедура ДобавитьГруппу(Эл, Группа)
	
	ИдГруппы	= Ид(Эл, Группа.ТекущийЭлемент());
	Узел 		= Эл.ВыбратьУзел("Группа[(@Идентификатор=""" + СокрЛП(ИдГруппы) + """)]");
	
	Если ПустоеЗначение(Узел) = 0 Тогда Возврат КонецЕсли;
	
	ЭлГруппа = СоздатьПодчиненныйЭлемент(Эл, "Группа");
	    
	УстановитьАтрибутыТовара(ЭлГруппа, Группа);
	
КонецПроцедуры		//	ДобавитьГруппу()

//******************************************************************************
// РаскрутитьРодителейДоВерхнегоУровня(ЭлРодитель, Группа)
//
// Параметры:
//  ЭлРодитель	-	элемент-родитель - "Товар" или "Группа"
//	Группа		-	группа или элемент  справочника "Номенклатура", родителей которой добавляем
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Добавляет родителей товара или группы вплоть Документ самого верхнего уровня
//
Процедура РаскрутитьРодителейДоВерхнегоУровня(ЭлРодитель, Группа)
	
	Если ПустоеЗначение(Группа) = 0 Тогда
		ДобавитьГруппу(ЭлРодитель, Группа);
		РаскрутитьРодителейДоВерхнегоУровня(ЭлРодитель, Группа.Родитель);
	КонецЕсли;
	
КонецПроцедуры		//	РаскрутитьРодителейДоВерхнегоУровня()

//******************************************************************************
// ДобавитьЭлемент_ТоварнаяПозиция(ЭлРодитель, Документ)
//		
// Параметры:
//	ЭлРодитель	-	XML-элемент
//	Документ	-	спозиционированная строка Документа
//		
// Возвращаемое значение:
// 	XML-элемент "ТоварнаяПозиция"
//		
// Описание
//	Добавляет XML-элемент "ТоварнаяПозиция" и устанавливает его атрибуты в соответствии со схемой
//		
Функция ДобавитьЭлемент_ТоварнаяПозиция(ЭлРодитель, Документ)
	
	Если ПустоеЗначение(Документ) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
        
	
	ГТД					=	"";
	СтранаПроисхождения	=	"";
	Комментарий			=	"";
	
	
	ЭлТовар = СоздатьПодчиненныйЭлемент(ЭлРодитель, "ТоварнаяПозиция");
	
	
	Если Документ.Вид() <> "СчетФактураВыданный" Тогда
		
		Товар		=	Документ.Номенклатура;
		ИдТовара	=	ПолучитьИдТовараПоКаталогу(Товар);
		Если ПустоеЗначение(ИдТовара) = 1 Тогда
			Сообщить("Товар:  '" + Товар + "' не идентифицирован по каталогу: " + КаталогИдентификации);
			Возврат "";
		КонецЕсли;
		
		Если глЕстьРеквизитМнЧ("Партия", Документ.Вид()) = 1 Тогда
			Партия	=	Документ.Партия;
			Если ПустоеЗначение(Партия) = 0 Тогда
				ГТД					=	Партия.ГТД;
				СтранаПроисхождения	=	Партия.СтранаПроисхождения;
			КонецЕсли;
		КонецЕсли;
		
		Единица				=	Документ.Единица.ОКЕИ.Наименование;
		Комментарий			=	Товар.Комментарий;
	Иначе
	    ГТД					=	Документ.ГТД;
		СтранаПроисхождения	=	Документ.СтранаПроисхождения;
		ИдТовара			=	Док.Наименование;
		Единица				=	Документ.Единица.Наименование;
	КонецЕсли;
	
	
	ПроверитьИУстановитьАтрибут(ЭлТовар, "Каталог",				КаталогИдентификации.Идентификатор);
	ПроверитьИУстановитьАтрибут(ЭлТовар, "Товар",				ИдТовара);
	ПроверитьИУстановитьАтрибут(ЭлТовар, "Единица",				Единица);
	ПроверитьИУстановитьАтрибут(ЭлТовар, "Количество",			Документ.Количество);
	Если ПустоеЗначение(Документ.Количество) = 0 Тогда
		ПроверитьИУстановитьАтрибут(ЭлТовар, "Цена",			Окр(Документ.Сумма / Документ.Количество, 2, 1));
	КонецЕсли;
	ПроверитьИУстановитьАтрибут(ЭлТовар, "Сумма",				Документ.Сумма);
	ПроверитьИУстановитьАтрибут(ЭлТовар, "Описание",			Комментарий);
	ПроверитьИУстановитьАтрибут(ЭлТовар, "СтранаПроисхождения",	СтранаПроисхождения);
	ПроверитьИУстановитьАтрибут(ЭлТовар, "ГТД",					ГТД);

	
	Если (ВместеСКаталогом = 1) И (Документ.Вид() <> "СчетФактураВыданный") Тогда
		РаскрутитьРодителейДоВерхнегоУровня(ЭлКаталог, Товар.Родитель);
		Если ПустоеЗначение(ЭлКаталог.ВыбратьУзел("Товар[@Идентификатор=""" + Ид(ЭлКаталог, Товар.ТекущийЭлемент()) + """]")) = 1 Тогда
					//	Такого товара еще не было
			ЭлТоварВКаталоге = СоздатьПодчиненныйЭлемент(ЭлКаталог, "Товар");
			УстановитьАтрибутыТовара(ЭлТоварВКаталоге, Товар);
			ДобавитьЗначенияСвойств(ЭлТоварВКаталоге, КаталогИдентификации, Товар);
		КонецЕсли;
	КонецЕсли;
   	
	
	Возврат ЭлТовар;
	
КонецФункции		//	ДобавитьЭлемент_ТоварнаяПозиция()

//******************************************************************************
// ДобавитьЭлемент_Документ(ЭлРодитель, Документ)
//		
// Параметры:
//	ЭлРодитель	-	XML-элемент
//	Документ	-	выгружаемый документ
//		
// Возвращаемое значение:
// 	XML-элемент "Документ"
//		
// Описание
//	Добавляет XML-элемент "Документ" и устанавливает его атрибуты в соответствии со схемой
//		
Функция ДобавитьЭлемент_Документ(ЭлРодитель, Документ)
	
	Если ПустоеЗначение(Документ) = 1 Тогда Возврат ПолучитьПустоеЗначение() КонецЕсли;
	
	ЭлДокумент = СоздатьПодчиненныйЭлемент(ЭлРодитель, "Документ");
		
	ПроверитьИУстановитьАтрибут(ЭлДокумент, "Дата", 		Док.ДатаДок			);
	ПроверитьИУстановитьАтрибут(ЭлДокумент, "Номер", 		Док.НомерДок		);
	ПроверитьИУстановитьАтрибут(ЭлДокумент, "Время", 		Док.ПолучитьВремя()	);
	ПроверитьИУстановитьАтрибут(ЭлДокумент, "Комментарий",	Док.Комментарий		);
	
	ПроверитьИУстановитьАтрибут(ЭлДокумент, "ХозОперация",	СписокАтрибутов.Получить("ХозОперация"));
	ПроверитьИУстановитьАтрибут(ЭлДокумент, "Сумма", 		СписокАтрибутов.Получить("Сумма"));	
	
	ПроверитьИУстановитьАтрибут(ЭлДокумент, "Валюта", 		Док.Валюта	);
	ПроверитьИУстановитьАтрибут(ЭлДокумент, "Курс",			Док.Курс	);
	ПроверитьИУстановитьАтрибут(ЭлДокумент, "Кратность", 	Док.Валюта.Кратность.Получить(Док.ДатаДок)	);
	
	Если глЕстьРеквизитШапки("ДатаОплаты", Док.Вид()) = 1 Тогда
		ПроверитьИУстановитьАтрибут(ЭлДокумент, "СрокПлатежа",	Док.ДатаОплаты);
	КонецЕсли;
	
	Возврат ЭлДокумент;
	
КонецФункции		//	ДобавитьЭлемент_Документ()
                                                      
//******************************************************************************
// ДобавитьТовары(ЭлДокумент)
//		
// Параметры:
//	ЭлДокумент	-	XML-элемент "Документ"
//		
// Возвращаемое значение:
// 	Нет.
//		
// Описание
//	Перебирает строки документа и добавляет элементы "ТоварнаяПозиция" и "СуммаНалога"
//		
Функция ДобавитьТовары(ЭлДокумент)
	
	СписокАтрибутов.УдалитьВсе();
	                   
	Док.ВыбратьСтроки();
	Пока Док.ПолучитьСтроку() = 1 Цикл
		ЭлТовар = ДобавитьЭлемент_ТоварнаяПозиция(ЭлДокумент, Док);
		
		Если ПустоеЗначение(ЭлТовар) = 1 Тогда
			ФлагОшибки = 1;
			Возврат("");
		КонецЕсли;
		
		СписокАтрибутов.УдалитьВсе();
		
		Если глЕстьРеквизитШапки("УчитыватьНДС", Док.Вид()) = 1 Тогда
			Если Док.УчитыватьНДС = 1 Тогда
				СписокАтрибутов.Установить("Налог",			"AVT");
				СписокАтрибутов.Установить("Ставка",		глНачисляемыйНДС(Док.СтавкаНДС)*100 );
				СписокАтрибутов.Установить("Сумма",			Док.СуммаНДС);
				СписокАтрибутов.Установить("ВключенВСумму",	Док.СуммаВклНДС);
				
				ДобавитьЭлемент_СуммаНалога(ЭлТовар);
			КонецЕсли;
		КонецЕсли;
		       
		
		Если глЕстьРеквизитШапки("УчитыватьНП", Док.Вид()) = 1 Тогда
			Если Док.УчитыватьНП = 1 Тогда
				СписокАтрибутов.Установить("Налог",			"ST");
				СписокАтрибутов.Установить("Ставка",		Док.СтавкаНП.Ставка );
				СписокАтрибутов.Установить("Сумма",			Док.СуммаНП);
				СписокАтрибутов.Установить("ВключенВСумму",	Док.СуммаВклНП);
				
				ДобавитьЭлемент_СуммаНалога(ЭлТовар);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат 1;
    
КонецФункции		//	ДобавитьТовары()

//******************************************************************************
// ДобавитьТовары(ЭлДокумент)
//		
// Параметры:
//	ЭлДокумент	-	XML-элемент "Документ"
//		
// Возвращаемое значение:
// 	Нет.
//		
// Описание
//	Добавляет элементы "СуммаНалога" с итоговыми значениями НДС и НП
//		
Процедура НалогиИтого(ЭлДокумент)
        
	СписокАтрибутов.УдалитьВсе();
	   
	Если глЕстьРеквизитШапки("УчитыватьНДС", Док.Вид()) = 1 Тогда
		Если Док.УчитыватьНДС = 1 Тогда
			СписокАтрибутов.Установить("Налог",			"AVT");
			СписокАтрибутов.Установить("Ставка",		"0");
			СписокАтрибутов.Установить("Сумма",			Док.Итог("СуммаНДС"));
			СписокАтрибутов.Установить("ВключенВСумму",	Док.СуммаВклНДС);
			
			ДобавитьЭлемент_СуммаНалога(ЭлДокумент);
		КонецЕсли;
	КонецЕсли;
           
	Если глЕстьРеквизитШапки("УчитыватьНП", Док.Вид()) = 1 Тогда
		Если Док.УчитыватьНП = 1 Тогда
			СписокАтрибутов.Установить("Налог",			"ST");
			СписокАтрибутов.Установить("Ставка",		"0");
			СписокАтрибутов.Установить("Сумма",			Док.Итог("СуммаНП"));
			СписокАтрибутов.Установить("ВключенВСумму",	Док.СуммаВклНП);
			
			ДобавитьЭлемент_СуммаНалога(ЭлДокумент);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры		//	НалогиИтого()

//******************************************************************************        
// Документ_ВозвратПоставщику(ЭлРодитель)
// 
// Параметры: 
//  ЭлРодитель - узел создаваемого XML-документа
//
// Возвращаемое значение: 
//  Нет (не используется)
//
// Описание:
// 	Выгружает соответствующий документ
//
Процедура Документ_ВозвратПоставщику(ЭлРодитель)

	СписокАтрибутов.Установить("ХозОперация", 	?(Док.КодОперации=глКО.ВозвратПоставщику, "BackSale", "BackComission"));
	СписокАтрибутов.Установить("Сумма", 		Док.Итог("Сумма"));
	
	ЭлДокумент = ДобавитьЭлемент_Документ(ЭлРодитель, Док);
	
	СписокАтрибутов.Установить("Роль",			"Saler");
	СписокАтрибутов.Установить("РасчетныйСчет",	Док.Фирма.ОсновнойСчет);
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		Док.Фирма);
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Фирма);
	
	СписокАтрибутов.Установить("Роль",			"Buyer");
	СписокАтрибутов.Установить("РасчетныйСчет",	"");
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		"");
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Контрагент);
	
	НалогиИтого(ЭлДокумент);
	                      
	ДобавитьТовары(ЭлДокумент);
	               
КонецПроцедуры		//	Документ_ВозвратПоставщику()

//******************************************************************************        
// Документ_Реализация(ЭлРодитель)
// 
// Параметры: 
//  ЭлРодитель - узел создаваемого XML-документа
//
// Возвращаемое значение: 
//  Нет (не используется)
//
// Описание:
// 	Выгружает соответствующий документ
//
Процедура Документ_Реализация(ЭлРодитель)
	
	СписокАтрибутов.Установить("ХозОперация", 	?(Док.КодОперации=глКО.Продажа, "Sale", "Comission"));
	СписокАтрибутов.Установить("Сумма", 			Док.Итог("Сумма"));
	
	ЭлДокумент = ДобавитьЭлемент_Документ(ЭлРодитель, Док);
	
	СписокАтрибутов.Установить("Роль",			"Saler");
	СписокАтрибутов.Установить("РасчетныйСчет",	Док.Фирма.ОсновнойСчет);
	//СписокАтрибутов.Установить("Склад",			Док.Склад);
	СписокАтрибутов.Установить("Контакт",		Док.Фирма);	
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Фирма);
	
	СписокАтрибутов.Установить("Роль",			"Buyer");
	СписокАтрибутов.Установить("РасчетныйСчет",	"");
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		"");
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Контрагент);
	
	НалогиИтого(ЭлДокумент);
	
	ДобавитьТовары(ЭлДокумент);
    
КонецПроцедуры		//	Документ_Реализация()

//******************************************************************************        
// Документ_ОтчетКомитенту(ЭлРодитель)
// 
// Параметры: 
//  ЭлРодитель - узел создаваемого XML-документа
//
// Возвращаемое значение: 
//  Нет (не используется)
//
// Описание:
// 	Выгружает соответствующий документ
//
Процедура Документ_ОтчетКомитенту(ЭлРодитель)
	    
	РегРеализованныйТовар	=	СоздатьОбъект("Регистр.РеализованныйТовар");
	
	СписокАтрибутов.Установить("ХозОперация", 		"ReportComission");
	            
	ИтогоНДС			=	0;
	ПродСтоимость		=	0;
	РегРеализованныйТовар.ВыбратьДвиженияДокумента(Док);
	Пока РегРеализованныйТовар.ПолучитьДвижение() = 1 Цикл
		ПродСтоимость	=	ПродСтоимость + РегРеализованныйТовар.ПродСтоимость - РегРеализованныйТовар.Вознаграждение;
	КонецЦикла;
	    
	СписокАтрибутов.Установить("Сумма",	глПересчет(ПродСтоимость, глРубли, 1, Док.Валюта, Док.Курс));
	
	
	ЭлДокумент = ДобавитьЭлемент_Документ(ЭлРодитель, Док);
	
	СписокАтрибутов.Установить("Роль",				"Payer");			//	плательщик
	СписокАтрибутов.Установить("РасчетныйСчет",		Док.Фирма.ОсновнойСчет);
	СписокАтрибутов.Установить("Контакт",			Док.Фирма);	
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Фирма);
	
	СписокАтрибутов.Установить("Роль",				"Recipient");		//	получатель
	СписокАтрибутов.Установить("РасчетныйСчет",		"");
	СписокАтрибутов.Установить("Склад",				"");
	СписокАтрибутов.Установить("Контакт",			"");
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Контрагент);
	
	
	// Добавляем товары
	
		СписокАтрибутов.УдалитьВсе();
		РегРеализованныйТовар.ВыбратьДвиженияДокумента(Док);
		Пока РегРеализованныйТовар.ПолучитьДвижение() = 1 Цикл
			ЭлТовар = СоздатьПодчиненныйЭлемент(ЭлДокумент, "ТоварнаяПозиция");
			            
			ГТД					=	"";
			СтранаПроисхождения	=	"";
			Комментарий			=	"";
			Товар				=	РегРеализованныйТовар.Номенклатура;
			ИдТовара			=	ПолучитьИдТовараПоКаталогу(Товар);
			Если ПустоеЗначение(ИдТовара) = 1 Тогда
				Сообщить("Товар:  '" + Товар + "' не идентифицирован по каталогу: " + КаталогИдентификации);
				Возврат;
			КонецЕсли;
			
			Партия	=	РегРеализованныйТовар.Партия;
			Если ПустоеЗначение(Партия) = 0 Тогда
				ГТД					=	Партия.ГТД;
				СтранаПроисхождения	=	Партия.СтранаПроисхождения;
			КонецЕсли;
			
			Единица				=	Товар.БазоваяЕдиница.ОКЕИ.Наименование;
			Комментарий			=	Товар.Комментарий;
			Количество			=	РегРеализованныйТовар.Количество;
			Сумма				=	РегРеализованныйТовар.ПродСтоимость - РегРеализованныйТовар.Вознаграждение;
			Цена				=	?(Количество <> 0, Сумма / Количество, 0);
			
			ПроверитьИУстановитьАтрибут(ЭлТовар, "Каталог",				КаталогИдентификации.Идентификатор);
			ПроверитьИУстановитьАтрибут(ЭлТовар, "Товар",				ИдТовара);
			ПроверитьИУстановитьАтрибут(ЭлТовар, "Единица",				Единица);
			ПроверитьИУстановитьАтрибут(ЭлТовар, "Количество",			Количество);
			ПроверитьИУстановитьАтрибут(ЭлТовар, "Цена",				глПересчет(Цена,  глРубли, 1, Док.Валюта, Док.Курс));
			ПроверитьИУстановитьАтрибут(ЭлТовар, "Сумма",				глПересчет(Сумма, глРубли, 1, Док.Валюта, Док.Курс));
			ПроверитьИУстановитьАтрибут(ЭлТовар, "Описание",			Комментарий);
			ПроверитьИУстановитьАтрибут(ЭлТовар, "СтранаПроисхождения",	СтранаПроисхождения);
			ПроверитьИУстановитьАтрибут(ЭлТовар, "ГТД",					ГТД);
			
			Если ВместеСКаталогом = 1 Тогда
				РаскрутитьРодителейДоВерхнегоУровня(ЭлКаталог, Товар.Родитель);
				Если ПустоеЗначение(ЭлКаталог.ВыбратьУзел("Товар[@Идентификатор=""" + Ид(ЭлКаталог, Товар.ТекущийЭлемент()) + """]")) = 1 Тогда
							//	Такого товара еще не было
					ЭлТоварВКаталоге = СоздатьПодчиненныйЭлемент(ЭлКаталог, "Товар");
					УстановитьАтрибутыТовара(ЭлТоварВКаталоге, Товар);
					ДобавитьЗначенияСвойств(ЭлТоварВКаталоге, КаталогИдентификации, Товар);
				КонецЕсли;
			КонецЕсли;
			
			СписокАтрибутов.УдалитьВсе();
			
			Ставка		=	глНачисляемыйНДС(Товар.СтавкаНДС);
			СуммаНДС	=	Сумма - (Сумма / (1 + Ставка));
			ИтогоНДС	=	ИтогоНДС + СуммаНДС;
			
			СписокАтрибутов.Установить("Налог",			"AVT");
			СписокАтрибутов.Установить("Ставка",		Ставка * 100 );
			СписокАтрибутов.Установить("Сумма",			глПересчет(СуммаНДС, глРубли, 1, Док.Валюта, Док.Курс));
			СписокАтрибутов.Установить("ВключенВСумму",	"1");
			ДобавитьЭлемент_СуммаНалога(ЭлТовар);
					
		КонецЦикла;
		
	//	СуммаНДС Итого
	
		СписокАтрибутов.УдалитьВсе();
		СписокАтрибутов.Установить("Налог",			"AVT");
		СписокАтрибутов.Установить("Ставка",		"0");
		СписокАтрибутов.Установить("Сумма",			глПересчет(ИтогоНДС, глРубли, 1, Док.Валюта, Док.Курс));
		СписокАтрибутов.Установить("ВключенВСумму",	"1");
		ДобавитьЭлемент_СуммаНалога(ЭлДокумент);
    
КонецПроцедуры		//	Документ_ОтчетКомитенту()

//******************************************************************************        
// Документ_ЗаказПоставщику(ЭлРодитель)
// 
// Параметры: 
//  ЭлРодитель - узел создаваемого XML-документа
//
// Возвращаемое значение: 
//  Нет (не используется)
//
// Описание:
// 	Выгружает соответствующий документ
//
Процедура Документ_ЗаказПоставщику(ЭлРодитель)
	
	СписокАтрибутов.Установить("ХозОперация", 	"Order");
	СписокАтрибутов.Установить("Сумма", 			Док.Итог("Сумма"));
	
	ЭлДокумент = ДобавитьЭлемент_Документ(ЭлРодитель, Док);
	                              
	СписокАтрибутов.Установить("Роль",			"Buyer");
	СписокАтрибутов.Установить("РасчетныйСчет",	"");
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		Док.Фирма);
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Фирма);
	
	СписокАтрибутов.Установить("Роль",			"Saler");
	СписокАтрибутов.Установить("РасчетныйСчет",	"");
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		"");
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Контрагент);
	
	НалогиИтого(ЭлДокумент);
	
	ДобавитьТовары(ЭлДокумент);
	
КонецПроцедуры		//	Документ_ЗаказПоставщику()

//******************************************************************************        
// Документ_ЗаявкаПокупателя(ЭлРодитель)
// 
// Параметры: 
//  ЭлРодитель - узел создаваемого XML-документа
//
// Возвращаемое значение: 
//  Нет (не используется)
//
// Описание:
// 	Выгружает соответствующий документ
//
Процедура Документ_ЗаявкаПокупателя(ЭлРодитель)
	
	СписокАтрибутов.Установить("ХозОперация", 	"PayableBill");
	СписокАтрибутов.Установить("Сумма", 			Док.Итог("Сумма"));
	
	ЭлДокумент = ДобавитьЭлемент_Документ(ЭлРодитель, Док);
	
	СписокАтрибутов.Установить("Роль",			"Saler");
	СписокАтрибутов.Установить("РасчетныйСчет",	Док.БанковскийСчет);
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		Док.Фирма);
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Фирма);
	
	СписокАтрибутов.Установить("Роль",			"Buyer");
	СписокАтрибутов.Установить("РасчетныйСчет",	"");
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		"");
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Контрагент);

	НалогиИтого(ЭлДокумент);

	ДобавитьТовары(ЭлДокумент);
	
КонецПроцедуры		//	Документ_ЗаявкаПокупателя()

//******************************************************************************        
// Документ_СчетФактураВыданный(ЭлРодитель)
// 
// Параметры: 
//  ЭлРодитель - узел создаваемого XML-документа
//
// Возвращаемое значение: 
//  Нет (не используется)
//
// Описание:
// 	Выгружает соответствующий документ
//
Процедура Документ_СчетФактураВыданный(ЭлРодитель)
	
	СписокАтрибутов.Установить("ХозОперация", 	"Invoice");
	//СписокАтрибутов.Установить("Сумма", 		Док.Итог("Сумма"));
	СписокАтрибутов.Установить("Сумма", 		0);
	
	ЭлДокумент = ДобавитьЭлемент_Документ(ЭлРодитель, Док);
	
	СписокАтрибутов.Установить("Роль",			"Saler");
	СписокАтрибутов.Установить("РасчетныйСчет",	Док.Фирма.ОсновнойСчет);
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		Док.Фирма);
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Фирма);
	
	СписокАтрибутов.Установить("Роль",			"Buyer");
	СписокАтрибутов.Установить("РасчетныйСчет",	"");
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		"");
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Контрагент);
                    
	ДокОснование	=	Док.ДокОснование;
	                                     
	Если ПустоеЗначение(ДокОснование) = 0 Тогда
		Док = ДокОснование;
	КонецЕсли;
	
	НалогиИтого(ЭлДокумент);
	ДобавитьТовары(ЭлДокумент);
	ПроверитьИУстановитьАтрибут(ЭлДокумент, "Сумма", Док.Итог("Сумма"));
	
	
КонецПроцедуры		//	Документ_СчетФактураВыданный()

//******************************************************************************        
// Документ_РКО(ЭлРодитель)
// 
// Параметры: 
//  ЭлРодитель - узел создаваемого XML-документа
//
// Возвращаемое значение: 
//  Нет (не используется)
//
// Описание:
// 	Выгружает соответствующий документ
//
Процедура Документ_РКО(ЭлРодитель)
	
	СписокАтрибутов.Установить("ХозОперация", 	?(Док.КодОперации=глКО.ВозвратОплатыПокупателю, "BackCash", "Cash"));
	СписокАтрибутов.Установить("Сумма", 			Док.Сумма);
	
	ЭлДокумент = ДобавитьЭлемент_Документ(ЭлРодитель, Док);
	
	СписокАтрибутов.Установить("Роль",			"Payer");
	СписокАтрибутов.Установить("РасчетныйСчет",	"");
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		Док.Фирма);
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Фирма);
	
	СписокАтрибутов.Установить("Роль",			"Recipient");
	СписокАтрибутов.Установить("РасчетныйСчет",	"");
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		"");
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Контрагент);

	//СписокАтрибутов.Установить("Налог",			"AVT");
	//СписокАтрибутов.Установить("Ставка",			СтавкаНалога(, "НДС"));
	//СписокАтрибутов.Установить("Сумма",			"0");
	//СписокАтрибутов.Установить("ВключенВСумму",	"");
	//
	//ДобавитьЭлемент_СуммаНалога(ЭлДокумент);
	//
	//СписокАтрибутов.Установить("Налог",			"ST");
	//СписокАтрибутов.Установить("Ставка",			СтавкаНалога(, "НП"));
	//СписокАтрибутов.Установить("Сумма",			"0");
	//СписокАтрибутов.Установить("ВключенВСумму",	"");
	//
	//ДобавитьЭлемент_СуммаНалога(ЭлДокумент);
	
КонецПроцедуры		//	Документ_РКО()

//******************************************************************************        
// Документ_СтрокаВыпискиРасход(ЭлРодитель)
// 
// Параметры: 
//  ЭлРодитель - узел создаваемого XML-документа
//
// Возвращаемое значение: 
//  Нет (не используется)
//
// Описание:
// 	Выгружает соответствующий документ
//
Процедура Документ_СтрокаВыпискиРасход(ЭлРодитель)
	
	СписокАтрибутов.Установить("ХозОперация", 	?(Док.КодОперации=глКО.ВозвратОплатыПокупателю, "BackPayment", "Payment"));
	СписокАтрибутов.Установить("Сумма", 			Док.Сумма);
    
	ЭлДокумент = ДобавитьЭлемент_Документ(ЭлРодитель, Док);
	
	СписокАтрибутов.Установить("Роль",			"Payer");
	СписокАтрибутов.Установить("РасчетныйСчет",	Док.БанковскийСчет);
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		Док.Фирма);
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Фирма);
	
	СписокАтрибутов.Установить("Роль",			"Recipient");
	СписокАтрибутов.Установить("РасчетныйСчет",	"");
	СписокАтрибутов.Установить("Склад",			"");
	СписокАтрибутов.Установить("Контакт",		"");
	СписокАтрибутов.Установить("КонтактноеЛицо",	"");	
	
	ДобавитьЭлемент_ПредприятиеВДокументе(ЭлДокумент, Док.Контрагент);

КонецПроцедуры		//	Документ_СтрокаВыпискиРасход()

//******************************************************************************
// ВыгрузитьДокумент()
//		
// Параметры:
//	ЭлРодитель	-	XML-элемент (корневой)
//		
// Возвращаемое значение:
// 	Нет.
//		
// Описание
//	В зависимости от вида документа вызывает нужную процедуру
//		
Процедура ВыгрузитьДокумент(ЭлРодитель)
	
	СписокАтрибутов.УдалитьВсе();
	
	Вид	= Док.Вид();
	
	Если		Вид = "ЗаказПоставщику"			Тогда	Документ_ЗаказПоставщику(ЭлРодитель);
	ИначеЕсли	Вид = "ЗаявкаПокупателя"		Тогда	Документ_ЗаявкаПокупателя(ЭлРодитель);
	ИначеЕсли	Вид = "Реализация"				Тогда	Документ_Реализация(ЭлРодитель);
	ИначеЕсли	Вид = "ВозвратПоставщику"		Тогда	Документ_ВозвратПоставщику(ЭлРодитель);
	ИначеЕсли	Вид = "СчетФактураВыданный"		Тогда	Документ_СчетФактураВыданный(ЭлРодитель);
	ИначеЕсли	Вид = "РКО"						Тогда	Документ_РКО(ЭлРодитель);
	ИначеЕсли	Вид = "СтрокаВыпискиРасход"		Тогда	Документ_СтрокаВыпискиРасход(ЭлРодитель);
	ИначеЕсли	Вид = "ОтчетКомитенту"			Тогда	Документ_ОтчетКомитенту(ЭлРодитель);
	КонецЕсли;
	          
КонецПроцедуры		//	ВыгрузитьДокумент()


////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//****************************************************************************** 
//	Предопределенная процедура
Процедура ПриОткрытии()
	
	СтатусВозврата(0);
	         
	СписокАтрибутов		=	СоздатьОбъект("СписокЗначений");
	
	СписокПараметров	=	Форма.Параметр;
	     
	Если		ПустоеЗначение(СписокПараметров) = 1				Тогда	Возврат;
	ИначеЕсли	ТипЗначенияСтр(СписокПараметров) = "Документ"	Тогда	Возврат;
	Иначе
		ИмяФайла			= СписокПараметров.Получить("ПолноеИмяФайла");
		Док					= СписокПараметров.Получить("Документ");
		КаталогИдентификации	= СписокПараметров.Получить("Каталог");
		ВместеСКаталогом		= СписокПараметров.Получить("ФлВыгружатьКаталог");
	КонецЕсли;
	     
	Если		ПустоеЗначение(КаталогИдентификации) = 1 Тогда
		СпособИдентификации	=	"Код";
	ИначеЕсли	ПустоеЗначение(КаталогИдентификации.СпособИдентификацииНоменклатуры) = 1 Тогда
		СпособИдентификации	=	"Код";
	Иначе
		СпособИдентификации	=	КаталогИдентификации.СпособИдентификацииНоменклатуры.Идентификатор();
	КонецЕсли;

	Если ЗагрузитьВнешнююКомпоненту(КаталогИБ() + "v7plus.dll") <> 1 Тогда
		Если ЗагрузитьВнешнююКомпоненту(КаталогИБ() + "ExtForms\" + "v7plus.dll") <> 1 Тогда		
			Если ЗагрузитьВнешнююКомпоненту(КаталогПрограммы() + "v7plus.dll") <> 1 Тогда
				Предупреждение("Компонента v7plus.dll не найдена!");
				Возврат;
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Анализатор		= СоздатьОбъект("AddIn.XMLParser");
	
	XMLДокумент		= Анализатор.СоздатьДокумент();
	
	Если ПустоеЗначение(XMLДокумент) = 1 Тогда	Возврат КонецЕсли;
	
	
	XMLДокумент.ЗагрузитьИзСтроки("<?xml version =""1.0""?><BizTalk xmlns = ""urn:schemas-biztalk-org:biztalk/biztalk-0.81.xml""><Route><From locationID = """" locationType = """" process = """" path = """" handle = """"/><To locationID = """" locationType = """" process = """" path = """" handle = """"/></Route><Body/></BizTalk>");
	
	КоллекцияСхем		=	Анализатор.СоздатьКоллекциюСхем();
	КоллекцияСхем.ДобавитьСхему("urn:CommerceML", КаталогИБ() + "ExtForms\CommerML.biz");
	XMLДокумент.Схемы	=	КоллекцияСхем;
                         
	//	Создаем корневой элемент
	ЭлКоммИнфо			=	СоздатьПодчиненныйЭлемент(XMLДокумент.ЭлементДокумента.ВыбратьУзел("Body"), "КоммерческаяИнформация");
	
	Если ВместеСКаталогом = 1 Тогда
		ЭлКаталог		=	СоздатьПодчиненныйЭлемент(ЭлКоммИнфо, "Каталог");
		УстановитьАтрибутыКаталога(ЭлКаталог);
	КонецЕсли;
	
	ФлагОшибки = 0;
	
	ВыгрузитьДокумент(ЭлКоммИнфо);
	             
	Если ФлагОшибки = 1 Тогда Возврат КонецЕсли;

	//XMLДокумент.Проверить();		// проверка полученного докумета на соответствие схеме CommerML.biz
	                              
	Если ПустоеЗначение(ИмяФайла) = 1 Тогда
		ИмяФайла = КаталогИБ() + СокрЛП(Док.НомерДок) + ".xml";
		Если Фс.ВыбратьФайл(1, ИмяФайла, КаталогИБ(), "Выберите файл выгрузки", "*.xml|*.xml", , ) = 0 Тогда Возврат КонецЕсли;
	КонецЕсли;
	
	XMLДокумент.Записать(ИмяФайла);	
	
КонецПроцедуры		//	ПриОткрытии()

//******************************************************************************





