////////////////////////////////////////////////////////////////////////////////
// ОПИСАНИЕ МОДУЛЬНЫХ ПЕРЕМЕННЫХ
//

Перем КонтекстДокумента; 		// Переменная, в которой запоминаем контекст редактируемого документа
Перем ЕстьТипЦен;    			// Флаг наличия в документе реквизита "Тип цен"
Перем ЕстьЦена;    				// Флаг наличия в документе реквизита "Цена" мн. части
Перем ЕстьЕдиница;    			// Флаг наличия в документе реквизита "Единица" мн. части
Перем ЕстьСкидка;    			// Флаг наличия в документе реквизита "Скидка"
Перем ЕстьСуммаВклНДС;			// Флаг наличия в документе реквизита "СуммаВклНДС"
Перем ЕстьСуммаВклНП;			// Флаг наличия в документе реквизита "СуммаВклНП"
Перем ЕстьУчитыватьНДС;			// Флаг наличия в документе реквизита "УчитыватьНДС"
Перем ЕстьУчитыватьНП;          // Флаг наличия в документе реквизита "УчитыватьНП" 
Перем ЕстьОблагаетсяЕНВД;       // Флаг наличия в документе реквизита "ОблагаетсяЕНВД"

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ПриСменеТипаЦен()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  "Тип цен".
//
// Описание:
//	Установим флаги учета налогов в соответствии с реквизитами типа цен.
//
Процедура ПриСменеТипаЦен()
	Если НазваниеНабораПрав()="Сервис" Тогда
	    возврат;
	КонецЕсли;
	
	Если ТипЦен.Выбран()=1 Тогда
		Если КонтекстДокумента.КоличествоСтрок()=0 Тогда
			СуммаВклНП =ТипЦен.ЦенаВклНП;
			СуммаВклНДС=ТипЦен.ЦенаВклНДС;
		Иначе
			Если УчитыватьНП = 0 Тогда
				СуммаВклНП =ТипЦен.ЦенаВклНП;
			КонецЕсли;
			Если УчитыватьНДС = 0 Тогда
				СуммаВклНДС =ТипЦен.ЦенаВклНДС;
			КонецЕсли;
		КонецЕсли;                                      
	КонецЕсли;
	
КонецПроцедуры // ПриСменеТипаЦен()

//******************************************************************************
// ПриИзмененииУчетаНП()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Флаг "Учитывать НП".
//
// Описание:
//  Производит все необходимые проверки при изменении флага
//	"Учитывать НП".
//
Процедура ПриИзмененииУчетаНП()
	
	Если ЕстьСуммаВклНП = 1 Тогда
		Форма.СуммаВклНП.Видимость(УчитыватьНП);
	КонецЕсли;                               
	
	ВидКонт	= КонтекстДокумента.Вид();
	Если (ВидКонт = "РеализацияРозница") 
	 ИЛИ (ВидКонт = "ОтчетККМ") 
	 ИЛИ (ВидКонт = "ПеремещениеТМЦ")
	Тогда
		СуммаВклНП	= УчитыватьНП;
	Иначе
		Если ТипЦен.Выбран()=1 Тогда
			СуммаВклНП = ТипЦен.ЦенаВклНП;
		КонецЕсли;                        
	КонецЕсли;                        
	
КонецПроцедуры // ПриИзмененииУчетаНП()

//******************************************************************************
// ПриИзмененииУчетаНДС()
//
// Параметры:
//   Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Флаг "Учитывать НДС".
//
// Описание:
//  Производит все необходимые проверки при изменении флага
//	"Учитывать НДС".
//
Процедура ПриИзмененииУчетаНДС()
	
	Если ЕстьСуммаВклНДС = 1 Тогда
		Форма.СуммаВклНДС.Видимость(УчитыватьНДС);
	КонецЕсли;                               
	
	ВидКонт	= КонтекстДокумента.Вид();
	Если (ВидКонт = "РеализацияРозница") 
	 ИЛИ (ВидКонт = "ОтчетККМ") 
	 ИЛИ (ВидКонт = "ПеремещениеТМЦ") 
	Тогда
		СуммаВклНДС	= УчитыватьНДС;
	Иначе
		Если ТипЦен.Выбран()=1 Тогда
			СуммаВклНДС=ТипЦен.ЦенаВклНДС;
		КонецЕсли;                        
	КонецЕсли;                        

КонецПроцедуры // ПриИзмененииУчетаНДС()
                   
//******************************************************************************
// УправлениеДоступностью()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Управляет доступностью элементов диалога
//
Процедура УправлениеДоступностью()
	
	Если ОблагаетсяЕНВД = 1 Тогда
		Форма.УчитыватьНДС.Доступность(0);
		Форма.УчитыватьНП.Доступность(0);
	Иначе                   
		Форма.УчитыватьНДС.Доступность(1);
		Форма.УчитыватьНП.Доступность(1);
	КонецЕсли;                  

	Форма.Курс.Доступность(?(Валюта=глРубли,0,1));
	
КонецПроцедуры // УправлениеДоступностью()

//******************************************************************************
// ПриИзмененииОблагаетсяЕНВД()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Флаг "Облагается ЕНВД".
//
// Описание:
//  Производит все необходимые проверки при изменении флага
//	"Облагается ЕНВД".
//
Процедура ПриИзмененииОблагаетсяЕНВД()
	
	Если ОблагаетсяЕНВД = 1 Тогда
		УчитыватьНДС 	= 0;
		УчитыватьНП 	= 0;
	Иначе                   
		УчитыватьНДС 	= 1;
		УчитыватьНП 	= 1;
	КонецЕсли;                  
	
	УправлениеДоступностью();
	ПриИзмененииУчетаНДС();    
	ПриИзмененииУчетаНП();
	
КонецПроцедуры // ПриИзмененииОблагаетсяЕНВД()

//******************************************************************************
// ПриСменеВалюты()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Валюта.
//
// Описание:
//  При смене валюты производим все необходимые действия.
//
Процедура ПриСменеВалюты()
	
	Если ПустоеЗначение(Валюта) = 1 Тогда
		Возврат;
	КонецЕсли;
    
	УправлениеДоступностью();
	
	// При смене валюты зачитываем текущий курс на дату
	Курс = глКурсДляВалюты(Валюта, КонтекстДокумента.ДатаДок);
	
КонецПроцедуры // ПриСменеВалюты()

//*****************************************************************************
// ЗаписатьИзменения()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
// 	Процедура установки выбранных значений в документе.
//
Процедура ЗаписатьИзменения()

	Если (УчитыватьНДС=1) и (УчитыватьНП=1) и (СуммаВклНДС=0) и (СуммаВклНП=1) Тогда
		Предупреждение("Нельзя задавать вариант расчета налогов
		|с включением НП в сумму и не включением НДС в сумму!", 60);
		СтрокаДействийФормы=""; 
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	// Запомним старые значения 
	Валюта_Прежн = КонтекстДокумента.Валюта;
	Курс_Прежн   = КонтекстДокумента.Курс;
	
	// запомним новые значения
	Если ПустоеЗначение(Валюта) = 1 Тогда
		Предупреждение("Не задана валюта!",60);
		СтрокаДействийФормы=""; 
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	                                        
	Если ПустоеЗначение(Курс) = 1 Тогда
		Предупреждение("Не задан курс валюты!",60);
		СтрокаДействийФормы=""; 
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	КонтекстДокумента.Валюта       = Валюта;
	КонтекстДокумента.Курс         = Курс;
    
	// Если изменилась валюта или курс
	Если ((Валюта_Прежн <> Валюта)) 
		И (ЕстьЦена = 1) 
		И (КонтекстДокумента.КоличествоСтрок()>0) Тогда
		          
		Ответ =	Вопрос("Изменена валюта документа! Пересчитать суммы?","Да+Нет", 60);
		Если Ответ ="Да" Тогда
			глПересчитатьСтрокиДокумента(КонтекстДокумента, Валюта_Прежн, Курс_Прежн);
		ИначеЕсли  Ответ = "Таймаут" Тогда
			СтрокаДействийФормы="";
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	
	Если ЕстьТипЦен = 1 Тогда
		
		ТипЦенПрежн = КонтекстДокумента.ТипЦен;
		КонтекстДокумента.ТипЦен = ТипЦен;      
		
		Если (ТипЦенПрежн <> ТипЦен)
		   И (КонтекстДокумента.КоличествоСтрок() > 0) Тогда
			Если НазваниеНабораПрав()="Сервис" Тогда
			    возврат;
			КонецЕсли;
			
			Ответ =	Вопрос("Изменен тип цены документа! Пересчитать цены?","Да+Нет", 60);
			Если Ответ ="Да" Тогда                                      
				                                   
				ДатаДок = КонтекстДокумента.ДатаДок;
				
				// Надо привести в соответствие флаги учета налогов в типе цен и в документе
				// чтобы не пересчитывать налоги лишний раз
				// Общий подход: если в документе флаг "Учитывать налог" выключен, 
				// то цены должны браться напрямую из справочника.
				// Если же флаг включен, то цены должны пересчитываться при подстановке в документ: 
				// налог должен включаться или исключаться…
				Если УчитыватьНП = 0 Тогда
					Если ЕстьСуммаВклНП = 1 Тогда
						КонтекстДокумента.СуммаВклНП = СуммаВклНП ;
					КонецЕсли;
				КонецЕсли;
				Если УчитыватьНДС = 0 Тогда
					Если ЕстьСуммаВклНДС = 1 Тогда
						КонтекстДокумента.СуммаВклНДС = СуммаВклНДС ;
					КонецЕсли;
				КонецЕсли;
				
				// Цикл по всем товарам в документе
				КонтекстДокумента.ВыбратьСтроки();
				Пока КонтекстДокумента.ПолучитьСтроку() = 1 Цикл
					               
					ТекТМЦ = КонтекстДокумента.Номенклатура;
					
					// Получаем цену
					Цена         = 0;
					ЕдиницаКонт  = КонтекстДокумента.Единица;
					ЕдиницаЦены  = ЕдиницаКонт;                          
					ВалютаЦены   = ТипЦен.Валюта; 
					ЦенаВклНП    = ТипЦен.ЦенаВклНП; 
					ЦенаВклНДС   = ТипЦен.ЦенаВклНДС;
					
					Если глВернутьЦену(ТекТМЦ, ТипЦен, ДатаДок, Цена, ЕдиницаЦены, ВалютаЦены) = 1 Тогда
						
						// Приводим к одной единице (если в документе она есть)
						Если ЕстьЕдиница= 1 Тогда
							Если ЕдиницаЦены.Коэффициент  <>  КонтекстДокумента.Коэффициент Тогда
								Если ЕдиницаЦены.Коэффициент <> 0  Тогда
									Цена = (Цена * КонтекстДокумента.Коэффициент) / ЕдиницаЦены.Коэффициент;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
	
						глПересчитатьЦенуВДокументе(КонтекстДокумента, ЕстьУчитыватьНП, ЕстьУчитыватьНДС, Цена, ВалютаЦены, ЦенаВклНП, ЦенаВклНДС);
					Иначе
						КонтекстДокумента.Цена = 0;
					КонецЕсли;  
					
					глПересчетТаблЧасти(КонтекстДокумента,"Цена");
						
					Если КонтекстДокумента.Вид() = "ПоступлениеТМЦИмпорт" Тогда
						КонтекстДокумента.ТаможеннаяСтоимость = глПересчет(КонтекстДокумента.Сумма,КонтекстДокумента.Валюта,КонтекстДокумента.Курс,глРубли,КонтекстДокумента.ДатаДок);
						КонтекстДокумента.СуммаПошлиныРуб 	  = КонтекстДокумента.ТаможеннаяСтоимость * КонтекстДокумента.СтавкаПошлины/100;
						КонтекстДокумента.СуммаНДСРуб 		  = (КонтекстДокумента.ТаможеннаяСтоимость + КонтекстДокумента.СуммаПошлиныРуб) * глНачисляемыйНДС(КонтекстДокумента.СтавкаНДС);
					КонецЕсли;
					
				КонецЦикла;
				
				
			ИначеЕсли  Ответ = "Таймаут" Тогда
				СтрокаДействийФормы="";
				СтатусВозврата(0);
				Возврат;
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьСкидка = 1 Тогда
		Если КонтекстДокумента.Скидка <> Скидка Тогда
			Если (КонтекстДокумента.КоличествоСтрок()>0) Тогда
				Ответ = Вопрос("Изменена скидка! Пересчитать суммы?","Да+Нет",60); 
				Если Ответ ="Да" Тогда            
					КонтекстДокумента.Скидка = Скидка; // запишем скидку и пересчитаем
					глПересчитатьСкидки(КонтекстДокумента);
				ИначеЕсли  Ответ = "Нет" Тогда
					КонтекстДокумента.Скидка = Скидка;// запишем скидку (без пересчета)
				ИначеЕсли  Ответ = "Таймаут" Тогда
					СтрокаДействийФормы=""; // не закроем окно
					СтатусВозврата(0);
					Возврат;
				КонецЕсли;                                        
			Иначе
				КонтекстДокумента.Скидка = Скидка;	
			КонецЕсли;                                        
		КонецЕсли;
	КонецЕсли;
	                 
	ИзменилиНалоги = 0; // флаг, показывающий необходимость пересчета налогов
	ВключаяНП      = 0; // были заданы цены
	ВключаяНДС     = 0; // были заданы цены
	
	Если ЕстьУчитыватьНП = 1 Тогда
		ИзменилиНалоги=?(КонтекстДокумента.УчитыватьНП<>УчитыватьНП,1,ИзменилиНалоги);
		КонтекстДокумента.УчитыватьНП  = УчитыватьНП;                            
		
		Если ЕстьСуммаВклНП = 1 Тогда
			ИзменилиНалоги=?(КонтекстДокумента.СуммаВклНП<>СуммаВклНП,1,ИзменилиНалоги);
			ВключаяНП = КонтекстДокумента.СуммаВклНП  ;
			КонтекстДокумента.СуммаВклНП  = СуммаВклНП;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьУчитыватьНДС = 1 Тогда
		ИзменилиНалоги=?(КонтекстДокумента.УчитыватьНДС<>УчитыватьНДС,1,ИзменилиНалоги);
		КонтекстДокумента.УчитыватьНДС = УчитыватьНДС;
		
		Если ЕстьСуммаВклНДС = 1 Тогда                                                  
			ИзменилиНалоги=?(КонтекстДокумента.СуммаВклНДС<>СуммаВклНДС,1,ИзменилиНалоги);
			ВключаяНДС = КонтекстДокумента.СуммаВклНДС  ;
			КонтекстДокумента.СуммаВклНДС= СуммаВклНДС;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьОблагаетсяЕНВД <> 0 Тогда
		КонтекстДокумента.ОблагаетсяЕНВД = ОблагаетсяЕНВД;
	КонецЕсли;
	
	Если ИзменилиНалоги = 1 Тогда  
		глПересчитатьНалоги(КонтекстДокумента, ВключаяНП, ВключаяНДС);
	КонецЕсли;
	
КонецПроцедуры // ЗаписатьИзменения()

//*****************************************************************************
// Изменили()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  1  -  изменили 0 - нет.
//
// Вызывается из формул элементов диалога:
//
// Описание:
//	Функция проверки изменения параметров документа в форме обработки.
// 	Если изменились, спрашиваем установить ли новые значения, 
// 	если да, то возвращаем 1, иначе - 0.
//
Функция Изменили()
	
	Перем Результат;
	
	Результат = 0;
	
	Если ЕстьУчитыватьНДС = 1 Тогда
		Если КонтекстДокумента.УчитыватьНДС <> УчитыватьНДС Тогда;
			Результат = 1;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьСуммаВклНДС = 1 Тогда
		Если КонтекстДокумента.СуммаВклНДС  <> СуммаВклНДС Тогда;
			Результат = 1;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьУчитыватьНП = 1 Тогда
		Если КонтекстДокумента.УчитыватьНП  <> УчитыватьНП Тогда;
			Результат = 1;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьСуммаВклНП = 1 Тогда
		Если КонтекстДокумента.СуммаВклНП  <> СуммаВклНП Тогда;
			Результат = 1;
		КонецЕсли;
	КонецЕсли;

	Если КонтекстДокумента.Валюта  <> Валюта Тогда
		Результат = 1;
	КонецЕсли;
	
	Если КонтекстДокумента.Курс  <> Курс Тогда;
		Результат = 1;
	КонецЕсли;
	
	Если  ЕстьТипЦен = 1 Тогда
		Если КонтекстДокумента.ТипЦен  <> ТипЦен Тогда;
			Результат = 1;
		КонецЕсли;
	КонецЕсли;
	
	Если  ЕстьСкидка = 1 Тогда
		Если КонтекстДокумента.Скидка <> Скидка Тогда;
			Результат = 1;
		КонецЕсли;
	КонецЕсли;

	Если  ЕстьОблагаетсяЕНВД = 1 Тогда
		Если КонтекстДокумента.ОблагаетсяЕНВД <> ОблагаетсяЕНВД Тогда;
			Результат = 1;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции  //Изменили
	
////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Перем ВидДок;
	// Запоминаем параметр (Обработка всегда должна вызываться с параметром)
	КонтекстДокумента = Форма.Параметр;  

	Если ТипЗначенияСтр(КонтекстДокумента) <> "ГрупповойКонтекст" Тогда
		Предупреждение("Обработка вызывается только из формы документов!", 60);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	// Анализируем реквизиты документа
	ВидДок				= КонтекстДокумента.Вид();
	ЕстьЦена			= глЕстьРеквизитМнЧ("Цена", 			ВидДок);
	ЕстьЕдиница			= глЕстьРеквизитМнЧ("Единица", 			ВидДок);
	ЕстьСкидка			= глЕстьРеквизитШапки("Скидка", 		ВидДок);
	ЕстьУчитыватьНП 	= глЕстьРеквизитШапки("УчитыватьНП",	ВидДок);
	ЕстьУчитыватьНДС	= глЕстьРеквизитШапки("УчитыватьНДС", 	ВидДок);
	ЕстьСуммаВклНП		= глЕстьРеквизитШапки("СуммаВклНП", 	ВидДок);
	ЕстьСуммаВклНДС		= глЕстьРеквизитШапки("СуммаВклНДС",	ВидДок);
	ЕстьОблагаетсяЕНВД	= глЕстьРеквизитШапки("ОблагаетсяЕНВД",	ВидДок);
	
	// Инициализируем текущие значения
	Валюта       = КонтекстДокумента.Валюта;
	Курс         = КонтекстДокумента.Курс;
	
	// В розничных документах нельзя менять валюту
	Если (ВидДок = "РеализацияРозница") 
	 или (ВидДок = "ОтчетККМ")
	 или (ВидДок = "ЧекККМ") Тогда
	    Форма.Валюта.Доступность(0);
		Форма.Курс.Доступность(0);
		
	ИначеЕсли (ВидДок = "ПеремещениеТМЦ")
			или (ВидДок = "ИнвентаризацияТМЦ") 
			или (ВидДок = "ОприходованиеТМЦ") 
			или (ВидДок = "ВозвратОтПокупателя") 
			или (ВидДок = "СписаниеТМЦ") Тогда    
		Если КонтекстДокумента.Склад.РозничныйСклад = 1 Тогда
		    Форма.Валюта.Доступность(0);
			Форма.Курс.Доступность(0);   
		ИначеЕсли ВидДок = "ПеремещениеТМЦ" Тогда
			Если КонтекстДокумента.СкладПолучатель.РозничныйСклад = 1 Тогда
			    Форма.Валюта.Доступность(0);
				Форма.Курс.Доступность(0);   
			КонецЕсли;    
		ИначеЕсли ВидДок = "ИнвентаризацияТМЦ" Тогда
			Если КонтекстДокумента.ВидОперации = Перечисление.ВидыИнвентаризаций.ПоРознице Тогда
			    Форма.Валюта.Доступность(0);
				Форма.Курс.Доступность(0);   
			КонецЕсли;				
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьУчитыватьНП = 1 Тогда
		
		УчитыватьНП  = КонтекстДокумента.УчитыватьНП;
		Форма.УчитыватьНП.Видимость(1);
		
		Если ЕстьСуммаВклНП = 1 Тогда
			Если (ВидДок = "ОтчетККМ") 
			или	 (ВидДок = "ПеремещениеТМЦ") 
			или  (ВидДок = "РеализацияРозница")
			Тогда
				Форма.СуммаВклНП.Доступность(0);
			КонецЕсли;
		    СуммаВклНП	= КонтекстДокумента.СуммаВклНП;
			Форма.СуммаВклНП.Видимость(УчитыватьНП);
		Иначе
			Форма.СуммаВклНП.Видимость(0);
		КонецЕсли;
			
	Иначе
		Форма.УчитыватьНП.Видимость(0);
		Форма.СуммаВклНП.Видимость(0);
		Форма.РамкаНП.Видимость(0);
	КонецЕсли;
	
	
	Если ЕстьУчитыватьНДС = 1 Тогда
		УчитыватьНДС  = КонтекстДокумента.УчитыватьНДС;
		Форма.УчитыватьНДС.Видимость(1);
		
		Если ЕстьСуммаВклНДС = 1 Тогда
			Если (ВидДок = "ОтчетККМ") 
			или	 (ВидДок = "ПеремещениеТМЦ") 
			или	 (ВидДок = "РеализацияРозница")
			Тогда
				Форма.СуммаВклНДС.Доступность(0);	
			КонецЕсли;
		    СуммаВклНДС	= КонтекстДокумента.СуммаВклНДС;
			Форма.СуммаВклНДС.Видимость(УчитыватьНДС);
		Иначе
			Форма.СуммаВклНДС.Видимость(0);
		КонецЕсли;
			
	Иначе
		Форма.УчитыватьНДС.Видимость(0);
		Форма.СуммаВклНДС.Видимость(0);
		Форма.РамкаНДС.Видимость(0);
	КонецЕсли;
                      
	Если ЕстьОблагаетсяЕНВД <> 0 Тогда
		Форма.ОблагаетсяЕНВД.Видимость(1);
		ОблагаетсяЕНВД = КонтекстДокумента.ОблагаетсяЕНВД;
	Иначе
		Форма.ОблагаетсяЕНВД.Видимость(0);	
	КонецЕсли;
	
	Если (ВидДок = "РеализацияРозница") 
	 или (ВидДок = "ОтчетККМ")
	 или (ВидДок = "ЧекККМ")
	 или (ВидДок = "ПеремещениеТМЦ")
	Тогда
		Если КонтекстДокумента.Склад.РозничныйСклад = 1 Тогда
		    // для розничного склада тип цен показывать не нужно
			ЕстьТипЦен	= 0;
		Иначе
			ЕстьТипЦен	= 1;
		КонецЕсли;
	Иначе
		ЕстьТипЦен	= глЕстьРеквизитШапки("ТипЦен", ВидДок);
	КонецЕсли;
	
	Если ЕстьТипЦен = 1 Тогда
		ТипЦен = КонтекстДокумента.ТипЦен;
		Форма.ТипЦен.Видимость(1);
		Форма.ТипЦенТекст.Видимость(1);
	Иначе
		Форма.ТипЦен.Видимость(0);
		Форма.ТипЦенТекст.Видимость(0);
	КонецЕсли;
	
	Если ЕстьСкидка = 1 Тогда
		//Если ПустоеЗначение(КонтекстДокумента.Скидка) = 1 Тогда
		//    Скидка = КонтекстДокумента.Контрагент.ОсновнаяСкидка;	
		//иначе 
			Скидка = КонтекстДокумента.Скидка;	
		//КонецЕсли;
		
		Форма.Скидка.Видимость(1);
		Форма.СкидкаТекст.Видимость(1);
		Форма.кнХСкидка.Видимость(1);
	Иначе
		Форма.Скидка.Видимость(0);
		Форма.СкидкаТекст.Видимость(0);
		Форма.кнХСкидка.Видимость(0);
	КонецЕсли;

	УправлениеДоступностью(); // элементов диалога в зависомости от условий
	
	// Меняем заголовок формы
	Форма.Заголовок("Параметры документа");
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога,ФлагСтандОбр)
	Если ИдентЭлемДиалога = "Курс" Тогда                       
		ФлагСтандОбр = 0;
		ДатаКурса = КонтекстДокумента.ДатаДок;
		Если ВвестиДату(ДатаКурса, "Выберите дату установки курса.", 60) = 1 Тогда
			Курс = глКурсДляВалюты(Валюта, ДатаКурса);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПриНачалеВыбораЗначения()


//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии() 
	
	// Если нужно записать изменения: спрашиваем и записываем
	Если Изменили() = 1 Тогда
		Ответ = Вопрос("Сохранить изменения?", "Да+Нет",60);
		Если Ответ= "Да" Тогда
			ЗаписатьИзменения();
		ИначеЕсли Ответ = "Таймаут" Тогда
			СтатусВозврата(0);
		КонецЕсли;   
	КонецЕсли;  
	
КонецПроцедуры // ПриЗакрытии()