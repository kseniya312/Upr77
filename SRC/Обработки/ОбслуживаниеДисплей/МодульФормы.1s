Перем ОписаниеРезультата;

//******************************************************************************
// ПолучитьОписаниеРезультата(Объект)
//
// Параметры:
//  Объект - ссылка на объект внешней компоненты
//
// Возвращаемое значение: 
//  Текстовое описание последней операции
//
// Описание: 
//  Функция возвращает текстовое описание результата выполнения последней оперции
//
Функция ПолучитьОписаниеРезультата(Объект)
	Возврат Объект.ОписаниеРезультата;
КонецФункции // ПолучитьОписаниеРезультата()

//******************************************************************************
// УдалитьТекущееОкно(Объект)
//
// Параметры:
//  Объект - ссылка на объект внешней компоненты
//
// Возвращаемое значение: 
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание: 
//  Удаляет с экрана дисплея текущее окно
//
Функция УдалитьТекущееОкно(Объект)
	Рез = 1;
	
	Если Объект.ТекущееОкно <> 0 Тогда
		// у дисплея всегда есть окно с номер 0 и его удалить нельзя
		Объект.УдалитьОкно();
		Объект.ТекущееОкно = 0;
		Объект.ОчиститьТекст();
		
		Если Объект.Результат <> 0 Тогда
			Рез = 0;
			ОписаниеРезультата = ПолучитьОписаниеРезультата(Объект);
		КонецЕсли;
	КонецЕсли;
	
	Возврат	Рез;
КонецФункции // УдалитьТекущееОкно()

//******************************************************************************
// СоздатьОкно(Объект)
//
// Параметры:
//  Объект - ссылка на объект внешней компоненты
//
// Возвращаемое значение: 
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание: 
//  Создает на экране дисплея окно
//
Функция СоздатьОкно(Объект)
	
	Если УдалитьТекущееОкно(Объект) = 1 Тогда
		Объект.СоздатьОкно(0, 0, 2, 20, 2, 20);
		
		Если Объект.Результат <> 0 Тогда
			Рез = 0;
			ОписаниеРезультата = ПолучитьОписаниеРезультата(Объект);
		Иначе
			Рез = 1;
		КонецЕсли;
	Иначе
		Рез = 0;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции // СоздатьОкно()

//******************************************************************************
// ВывестиСтроку(Объект, Стр, НомСтр = 0)
//
// Параметры:
//  Объект - ссылка на объект внешней компоненты
//  Стр    - строка, которую нужно вывести
//  НомСтр - номер строки в окне. Может  принимать значения 0 или 1
//
// Возвращаемое значение: 
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание: 
//  Выводит текстовую строку на дисплей
//
Функция ВывестиСтроку(Объект, Стр, НомСтр = 0)
	Объект.ПоказатьТекстПоз(НомСтр, 0, Стр, 0);
		
	Если Объект.Результат <> 0 Тогда
		Рез = 0;
		ОписаниеРезультата = ПолучитьОписаниеРезультата(Объект);
	Иначе
		Рез = 1;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции // ВывестиСтроку()

//******************************************************************************
// БегущаяСтрока(Объект, БегСтр)
//
// Параметры:
//  Объект - ссылка на объект внешней компоненты
//  БегСтр - текст бегущей строки
//
// Возвращаемое значение: 
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание: 
//  Создает на экране окно и выводит в него бегущую строку
//
Функция БегущаяСтрока(Объект, БегСтр)
	
	Если УдалитьТекущееОкно(Объект) = 1 Тогда
		
		Объект.СоздатьОкно(0, 0, 1, 20, 1, 45);
		Объект.ТипБегСтроки             = 5; // инициализация строки
		Объект.ЗадержкаПовтораБегСтроки = 500;
		Объект.ЗадержкаПоказаБегСтроки  = 150;
		Объект.ОчиститьТекст();
		Объект.ПоказатьТекст(БегСтр, 0);
		Объект.ТипБегСтроки = 3; // бегущая строка
			
		Если Объект.Результат <> 0 Тогда
			Рез = 0;
			ОписаниеРезультата = ПолучитьОписаниеРезультата(Объект);
		Иначе
			Рез = 1;
		КонецЕсли;
	Иначе
        Рез = 0;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

//******************************************************************************
// Подключить(Компонента, ПрогИд, Объект)
//
// Параметры: 
//  Компонента - имя файла внешней компоненты
//  ПрогИд     - программный идентификатор
//  Объект     - выходящий параметр - ссылка на созданный объект
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Загружает внешнюю компоненту из файла, имя которго задано в параметре Компонента,
//  создает объект, программный идентификатор которо заданы в параметре ПрогИд
//
Функция Подключить(Компонента, ПрогИд, Объект)
	Рез = 0;
	
	Если ЗагрузитьВнешнююКомпоненту(Компонента) = 0 Тогда
		ОписаниеРезультата = "Не удалось загрузить внешнюю компоненту """ + Компонента + """";
	Иначе
		Попытка
			Объект = СоздатьОбъект("AddIn." + ПрогИд);
		Исключение
		КонецПопытки;
		
		Если ПустоеЗначение(Объект) = 1 Тогда
			ОписаниеРезультата = "Не удалось создать объект внешней компоненты с программым идентификатром AddIn." + ПрогИд;
		Иначе
			Если Объект.УстройствоВключено = 1 Тогда
				Рез = 1;
			Иначе
				Объект.УстройствоВключено = 1;
				Если Объект.Результат <> 0 Тогда
					ОписаниеРезультата = ПолучитьОписаниеРезультата(Объект);
				Иначе
					Рез	= 1;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

//******************************************************************************
// Отключить(Объект)
//
// Параметры: 
//  Объект     - ссылка на объект внешней компоненты.
//
// Возвращаемое значение:
//  1
//
// Описание:
//  осбождает порт, занятый дисплеем
//
Функция Отключить(Объект)
	УдалитьТекущееОкно(Объект);
	Объект.УстройствоВключено = 0;
	
	Возврат 1;
КонецФункции

//******************************************************************************
// ОткрытьЯщик(Объект)
//
// Параметры:
//  Объект - ссылка на объект внешней компоненты
//
// Возвращаемое значение: 
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание: 
//  Открывает денежный ящик
//
Функция ОткрытьЯщик(Объект)
	
	Если Объект.ЯщикОткрыт = 0 Тогда
		
		Объект.ОткрытьЯщик();
			
		Если Объект.Результат <> 0 Тогда
			Рез = 0;
			ОписаниеРезультата = ПолучитьОписаниеРезультата(Объект);
		Иначе
			Рез = 1;
		КонецЕсли;
	    
	КонецЕсли;
	
	Возврат Рез;
	
КонецФункции // ОткрытьЯщик()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	Перем Объект, Вкл, Парам;
	
	СтатусВозврата(0);
	Форма.Параметр.Выгрузить(Парам);
	Форма.Параметр.УдалитьВсе();
	
	Если Парам.РазмерСписка() = 0 Тогда
		Рез = 0;
		ОписаниеРезультата = "В обработку не переданы параметры";
	Иначе
		
		Процесс = НРег(Парам.Получить("Процесс"));
		
		Если Процесс = "подключить" Тогда
			Компонента = Парам.Получить("Компонента");
			ПрогИд     = Парам.Получить("ПрогИд");
			Рез        = Подключить(Компонента, ПрогИд, Объект);
			
			Форма.Параметр.Установить("Объект"    , Объект);
		
		ИначеЕсли Процесс = "отключить" Тогда
			Рез = Отключить(Парам.Получить("Объект"));
		
		ИначеЕсли Процесс = "изменить_режим" Тогда
			
			Режим  = Парам.Получить("Режим");
			Дисплей = Парам.Получить("Объект");
			
			Если Режим = 1 Тогда
				// бегущая строка
				БегСтр = Парам.Получить("БегСтрока");
				Рез    = БегущаяСтрока(Дисплей, БегСтр);
			Иначе
				Рез    = СоздатьОкно(Дисплей);
			КонецЕсли;
			
		ИначеЕсли Процесс = "вывести_строки" Тогда
			Дисплей = Парам.Получить("Объект");
			Дисплей.ОчиститьТекст();
			Рез     = ВывестиСтроку(Дисплей, Парам.Получить("Строка1"), 0);
			Рез     = Рез * ВывестиСтроку(Дисплей, Парам.Получить("Строка2"), 1);
			Форма.Параметр.Установить("Результат" , Рез);
			
		ИначеЕсли Процесс = "открыть_денежный_ящик" Тогда
			Ящик = Парам.Получить("Объект");
			Рез  = ОткрытьЯщик(Ящик);
			Форма.Параметр.Установить("Результат" , Рез);
		
		Иначе
			Рез = 0;
			ОписаниеРезультата = "Неизвестная команда";
		КонецЕсли;
		
	КонецЕсли;
			
	Форма.Параметр.Установить("Результат"         , Рез);
	Форма.Параметр.Установить("ОписаниеРезультата", ОписаниеРезультата);
КонецПроцедуры

ОписаниеРезультата = "";