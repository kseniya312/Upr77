////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
Перем РабочийКО;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ФильтрКассы(ВремКасса)
//
// Параметры:
//  ВремКасса
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Установка фильтра по регистру Касса.
//
Процедура ФильтрКассы(ВремКасса)
	
	ВремКасса.УстановитьЗначениеФильтра("Фирма",	Фирма,	1);
	ВремКасса.УстановитьЗначениеФильтра("Касса",	Касса,	1);
	ВремКасса.УстановитьЗначениеФильтра("Валюта",	Валюта,	1);
	Если ИтогиАктуальны() = 0 Тогда
		ВремКасса.ВременныйРасчет(1);
	КонецЕсли;
	
КонецПроцедуры // ФильтрКассы()

//******************************************************************************
// ОбработкаПроведенияПоКассе(ВремКасса)
//
// Параметры:
//  ВремКасса - рассчитанный регистр "Касса"
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Осуществляет движения по регистру "Касса".
//
Процедура ОбработкаПроведенияПоКассе(ВремКасса)
	
	Регистр.Касса.Фирма		= Фирма;
	Регистр.Касса.Касса 	= Касса;
	Регистр.Касса.Валюта 	= Валюта;      
	Регистр.Касса.ДвижениеДенежныхСредств = ДвижениеДенежныхСредств;
	
	ОстатокВал = ВремКасса.Остаток(Фирма,Касса,Валюта,"СуммаВал"); 
	ОстатокУпр = ВремКасса.Остаток(Фирма,Касса,Валюта,"СуммаУпр"); 
	ОстатокРуб = ВремКасса.Остаток(Фирма,Касса,Валюта,"СуммаРуб"); 
	
	СуммаУпр = глПересчет(Сумма,Валюта,Курс,глДоллары,ДатаДок);
	СуммаРуб = глПересчет(Сумма,Валюта,Курс,глРубли,ДатаДок);
	
	Если ОстатокВал>0 Тогда
		ПогаситьВал = Мин(ОстатокВал,Сумма);
		ПогаситьУпр = Окр(ОстатокУпр*ПогаситьВал/ОстатокВал,2,1);
		ПогаситьРуб = Окр(ОстатокРуб*ПогаситьВал/ОстатокВал,2,1);
		                               
		СписатьУпр = Окр(СуммаУпр * ПогаситьВал/Сумма,2,1);
		СписатьРуб = Окр(СуммаРуб * ПогаситьВал/Сумма,2,1);
		КурсРазницаУпр = СписатьУпр - ПогаситьУпр;
		КурсРазницаРуб = СписатьРуб - ПогаситьРуб;
		
		Если (КурсРазницаУпр<>0) или (КурсРазницаРуб<>0) Тогда
			
			Регистр.Касса.КодОперации = глКО.КурсоваяРазница;
			
			Если КурсРазницаРуб < 0 Тогда
				Регистр.Касса.СуммаВал 	= 0;
				Регистр.Касса.СуммаУпр 	= - КурсРазницаУпр;
				Регистр.Касса.СуммаРуб 	= - КурсРазницаРуб;
				
				Регистр.Касса.ДвижениеРасходВыполнить();
			Иначе
				Регистр.Касса.СуммаВал 	= 0;
				Регистр.Касса.СуммаУпр 	= КурсРазницаУпр;
				Регистр.Касса.СуммаРуб 	= КурсРазницаРуб;
				
				Регистр.Касса.ДвижениеПриходВыполнить();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;                                                  
	        
	Регистр.Касса.СуммаВал 	= Сумма;
	Регистр.Касса.СуммаУпр 	= СуммаУпр;
	Регистр.Касса.СуммаРуб 	= СуммаРуб;
		
	Регистр.Касса.КодОперации = РабочийКО;
	Регистр.Касса.СтатьяДР	= СтатьяДР;
	Регистр.Касса.ФизЛицо	= ФизЛицо;

	Регистр.Касса.ДвижениеРасходВыполнить();
	
КонецПроцедуры // ОбработкаПроведенияПоКассе()


Процедура ОбработкаПроведенияПоБанку()
	
	Регистр.Банк.Фирма			= Фирма;
	Регистр.Банк.БанковскийСчет	= БанковскийСчет;
	
	СуммаУпр = глПересчет(Сумма,Валюта,Курс,глДоллары,ДатаДок);
	СуммаРуб = глПересчет(Сумма,Валюта,Курс,глРубли,ДатаДок);

	Регистр.Банк.СуммаВал = Сумма;
	Регистр.Банк.СуммаУпр = СуммаУпр;
	Регистр.Банк.СуммаРуб = СуммаРуб;
		
	Регистр.Банк.КодОперации = РабочийКО;
	Регистр.Банк.ДвижениеДенежныхСредств = ДвижениеДенежныхСредств;

	Регистр.Банк.СтатьяДР	= СтатьяДР;
	Регистр.Банк.ФизЛицо	= ФизЛицо;
	
	Регистр.Банк.ДвижениеПриходВыполнить();
	                                       
	
КонецПроцедуры // ОбработкаПроведенияПоБанку()
//******************************************************************************
// ОбработкаПроведенияПрочее()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//
// Описание:
//  Обработка проведения по банку прочее.
//
Процедура ОбработкаПроведенияПрочее()
	
	
	ОбработкаПроведенияПоБанку();

	ВремРегистры = СоздатьОбъект("Регистры");
	ВремКасса = ВремРегистры.Касса;
	ФильтрКассы(ВремКасса);
	Если ИтогиАктуальны() = 0 Тогда
		ВремРегистры.Актуальность(1);
		ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
	ОбработкаПроведенияПоКассе(ВремКасса);
КонецПроцедуры // ОбработкаПроведенияПрочее()

//******************************************************************************
// ПроведениеПоРегистрам()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Проведение по регистрам оперативного учета.
//
Процедура ПроведениеПоРегистрам()
	
	// Удаление движений по регистрам.
	Для Номер = 1 По Метаданные.Регистр() Цикл
		ОчиститьДвижения("Регистр."+Метаданные.Регистр(Номер).Идентификатор);
	КонецЦикла;
   	
	ВалютаСчета = ?(ПустоеЗначение(БанковскийСчет.ВалютаСчета)=1,глРубли,БанковскийСчет.ВалютаСчета);
	Если ВалютаСчета <> Валюта Тогда
		глНеПроводить(Контекст,"Валюта документа не совпадает с валютой банковского счета!");
		Возврат;
	КонецЕсли;
	
	ОбработкаПроведенияПрочее();
КонецПроцедуры // ПроведениеПоРегистрам()
// ********************
//
Процедура ОбработкаПроведения(ВидыДвижений)

	РабочийКО = КодОперации;	
	
	СписокОбязРеквизитов = "Фирма,БанковскийСчет,Валюта,КодОперации,Сумма";
	// Проверка заполненности обязательных реквизитов.
	Если глВсеРеквизитыДокументаЗаполнены(Контекст,СписокОбязРеквизитов)=0 Тогда
		Возврат;
	КонецЕсли;
	// Проведение по регистрам оперативного учета.	
	Если (ПустоеЗначение(ВидыДвижений) = 1) ИЛИ (Найти(ВидыДвижений, "Регистр") <> 0) Тогда
		ПроведениеПоРегистрам();
		
		Если СтатусВозврата() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	глПриПроведении(Контекст, ВидыДвижений);
	

    //Здесь следует написать алгоритм проведения документа

КонецПроцедуры
