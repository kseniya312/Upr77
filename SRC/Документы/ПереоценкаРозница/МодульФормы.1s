////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем СписокДействий; // для механизма кнопки "Действия"
Перем НачальнаяДатаДокумента; // для механизма контроля уникальности номеров

// Для контроля необходимости пересчетов
Перем СтараяФирма, СтарыйСклад;

Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;

Перем ОбщРег, ОстаткиТМЦ, СписокПараметров; 
                              // для выбора розничных цен, по которым есть остатки

//******************************************************************************
// ОбновитьНадписи()
//
// Описание:
// 	Обновляет текст информационных надписей в форме документа
//
Процедура ОбновитьНадписи()    
	
	Форма.ТекстФирмы			.Заголовок(глСтрокаФирмы		(Контекст));
	Форма.ТекстСклада			.Заголовок(глСтрокаСклада		(Склад));
	Форма.ТекстОплаты			.Заголовок(глСтрокаОплаты		(Контекст));

КонецПроцедуры //ОбновитьНадписи()

//*****************************************************************************
// ЗаголовокФормы()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение: 
//  Название операции
//
// Описание:
// 	Формирует название операции и заголовок формы диалога
//
Функция ЗаголовокФормы() 
	Перем Заголовок, Название;
            
	Заголовок = глНазваниеДокументаВЖурнале(Контекст);
	
	Название = Заголовок + " №";
	
	Если Выбран() = 1 Тогда  
		Если Проведен() = 1 Тогда
			Заголовок = Заголовок + ". Проведен";
		Иначе
			Заголовок = Заголовок + ". Не проведен";
		КонецЕсли;
	Иначе
		Заголовок = Заголовок + ". Новый";
	КонецЕсли;
	Форма.Заголовок(Заголовок);               
	Возврат Название;
	
КонецФункции // ЗаголовокФормы
                                    
//******************************************************************************                                        
// ИнформацияОНоменклатуре()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение: 
//  Строка информации о Номенклатуре в документе
//
// Описание:
// 	Формирует строку, включающую название, артикул Номенклатуры, реквизиты партии
//
Функция ИнформацияОНоменклатуре()       
	
	ИнфоТекст="";
	
	Если Номенклатура.Выбран()=0 Тогда
		Возврат(ИнфоТекст);
	КонецЕсли;
	
	ИнфоТекст=ИнфоТекст+СокрЛП(Номенклатура.Наименование);
	
	Если ПустоеЗначение(Номенклатура.Артикул)=0 Тогда
		ИнфоТекст=?(ИнфоТекст="","",ИнфоТекст+",");
		ИнфоТекст=ИнфоТекст+" арт. "+СокрЛП(Номенклатура.Артикул);
	КонецЕсли;
	
	Возврат(ИнфоТекст);
	
КонецФункции //ИнформацияОНоменклатуре()

//******************************************************************************
// ЗаполнитьПоСкладу()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Текст строки ошибки либо пустая строка в случае успешного заполнения
//
// Описание:
//	Заполняет документ остатками по складу.
//
Функция ЗаполнитьПоСкладу()
	Перем Остатки;     
	
	// фирма и склад должны быть заполнены
	Если ПустоеЗначение(Фирма) = 1 Тогда
		Возврат "Не выбрана фирма!";
	КонецЕсли;
	
    ФирмаДляОстатковТМЦ = глФирмаДляОстатковТМЦ(Фирма);
	
	Если ПустоеЗначение(Склад) = 1 Тогда
		Возврат "Не выбран розничный склад!";
	КонецЕсли;
	
	ОбщРегистры = СоздатьОбъект("Регистры");
	РегОтданные = ОбщРегистры.ОстаткиТМЦ;
	
	// установим фильтры на измерения
	РегОтданные.УстановитьЗначениеФильтра("Фирма", ФирмаДляОстатковТМЦ, 2);
	РегОтданные.УстановитьЗначениеФильтра("Склад", Склад              , 1);
	
	// если нужно, то выполним временный расчет регистра
	Если Выбран() = 0 Тогда
		// Новый документ
		Если ДатаДок < ПолучитьДатуТА() Тогда
			// выписываемый задним числом
			РегОтданные.ВременныйРасчет();
			ОбщРегистры.РассчитатьРегистрыПо(ДатаДок);
		КонецЕсли;
	Иначе 
		// Сохраненный документ.                                     
		Если СравнитьТА() < 1 Тогда
			//Позиция не больше ТА.
			РегОтданные.ВременныйРасчет();
			ОбщРегистры.РассчитатьРегистрыНа(ТекущийДокумент());
		КонецЕсли;
	КонецЕсли;
	
	
	РегОтданные.ВыгрузитьИтоги(Остатки, 0, 1);
	
	// Надо свернуть по номенклатуре и уенам
	Остатки.Свернуть("Номенклатура,ЦенаПрод","Количество");
	Остатки.Сортировать("Номенклатура");
	
	Остатки.ВыбратьСтроки();

	Пока Остатки.ПолучитьСтроку() = 1 Цикл
		НоваяСтрока();
		
		Номенклатура	= Остатки.Номенклатура;
		Единица			= Номенклатура.ОсновнаяЕдиница;
		Коэффициент		= Единица.Коэффициент;
		Количество		= Остатки.Количество / ?(Коэффициент = 0, 1, Коэффициент);
		ЦенаСтарая		= Остатки.ЦенаПрод * Коэффициент;
		ЦенаНовая		= Остатки.ЦенаПрод * Коэффициент;
		
	КонецЦикла;
	
	Если КоличествоСтрок() = 0 Тогда
		Возврат "Товары отсутствуют на складе";
	КонецЕсли;
	
	Возврат "";
	
КонецФункции // ЗаполнитьПоСкладу()

//******************************************************************************
// ПриИзмененииФирмы()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение: 
//  Нет
//
// Описание: 
//  Вызывается из формулы реквизита диалога "Фирма"
//
Процедура ПриИзмененииФирмы()      
	
	Если СтараяФирма <> Фирма Тогда
		// только если изменили
		глПриИзмененииФирмы(Контекст);
		СтараяФирма = Фирма; 
	КонецЕсли;

КонецПроцедуры // ПриИзмененииФирмы()

//******************************************************************************
// ПриИзмененииСклада()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение: 
//  Нет
//
// Описание: 
//  Вызывается из формулы реквизита диалога "Склад"
//
Процедура ПриИзмененииСклада()      
	
	Если СтарыйСклад <> Склад Тогда
		Если Склад.РозничныйСклад = 0 Тогда
			Предупреждение("Выбранный склад не является розничным!
			|Переоценка на таком складе не производится!",60);
			Склад = "";
		КонецЕсли;
		
		СтарыйСклад = Склад;
	               
		Если Склад.Выбран()=1 Тогда
			ТекстВопроса = "Заполнить документ остатками на складе """+Склад+"""?";
			Если КоличествоСтрок() <> 0 Тогда
				ТекстВопроса = ТекстВопроса + РазделительСтрок +" (перед заполнением табличная часть будет очищена)";
			КонецЕсли;
			
			Если Вопрос(ТекстВопроса, "Да+Нет", 60) <> "Да" Тогда
				Возврат;
			КонецЕсли;
			
			УдалитьСтроки();
			Результат = ЗаполнитьПоСкладу();
			Если ПустоеЗначение(Результат) = 0 Тогда
				Предупреждение(Результат, 60);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры // ПриИзмененииСклада()        
    
//******************************************************************************
// ПересчетТаблЧасти(ИмяРеквизита)
//
// Параметры:                
//		ИмяРеквизита - идентификатор текущей колонки табличной части
//
// Описание:
//	Производится пересчет всего необходимого при редактировании табличной части.
//
Процедура ПересчетТаблЧасти(ИмяРеквизита)
	
	Перем НоваяНоменклатура;
	                                               
	Если ИмяРеквизита = "Номенклатура" Тогда
		Если ПустоеЗначение(Номенклатура) = 1 Тогда
			//Очистили Номенклатуру.
			Единица 	= 0;
			Коэффициент = 0;                  
			Количество 	= 0;
			Возврат;
		КонецЕсли;

		// Определим, поменяли ли Номенклатуру?
		НоваяНоменклатура = 0;
		Если ПустоеЗначение(Единица) = 1 Тогда
			НоваяНоменклатура = 1;
		ИначеЕсли Номенклатура <> Единица.Владелец Тогда
			НоваяНоменклатура = 1;
		КонецЕсли;
	
		Если НоваяНоменклатура = 0 Тогда
			Возврат;
		КонецЕсли;
	
		// заполняем единицу
		
		Единица 		= Номенклатура.ОсновнаяЕдиница;
		Коэффициент 	= Единица.Коэффициент;    
		
		Если глПересчетРегистров(Контекст, СписокПараметров) = 0 Тогда
			Возврат;
		КонецЕсли;
		ТовЦена    = "";
		ТовОстаток = "";
		глПолучитьРозничныйОстатокЦену(Номенклатура, Единица, ОстаткиТМЦ, ТовОстаток, ТовЦена);
		СписокЦен = ЗначениеИзСтроки(ТовЦена);
		Если СписокЦен.РазмерСписка() = 0 Тогда
			ЦенаСтарая = 0;
		Иначе
			ЦенаСтарая = СписокЦен.ПолучитьЗначение(1);
		КонецЕсли;
		
		ЦенаНовая = ?(ЦенаНовая = 0,  ЦенаСтарая, ЦенаНовая);
		
		СписокОстатков = ЗначениеИзСтроки(ТовОстаток);
		Если СписокОстатков.РазмерСписка() > 0 Тогда
			Количество = ?(Количество = 0,СписокОстатков.ПолучитьЗначение(1), Количество);
		КонецЕсли;
			                         
	ИначеЕсли (ИмяРеквизита = "ЦенаСтарая") Тогда	
		ЦенаНовая = ?(ЦенаНовая = 0, ЦенаСтарая, ЦенаНовая);
		
	ИначеЕсли ИмяРеквизита = "Единица" Тогда	  
		// сохраним старый коэффициент
		ВремКоэфф = Коэффициент;
		// заполним коэффициент в документе из единицы
		Коэффициент = Единица.Коэффициент;
		Если ВремКоэфф <> 0 Тогда
			// цена была за другой коэффициент. Пересчитываем
			ЦенаСтарая 	= ЦенаСтарая * Коэффициент / ВремКоэфф;
			ЦенаНовая 	= ЦенаНовая  * Коэффициент / ВремКоэфф;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПересчетТаблЧасти()

//******************************************************************************
// ПриИзмененииНоменклатуры()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Колонка "Номенклатура" табличной части документа.
//
// Описание:
//  Производит проверку правильности выбора номенклатуры (Не даем выбрать услугу)
//
Процедура ПриИзмененииНоменклатуры()
	
	Если Номенклатура.Выбран()=1 Тогда
		Если Номенклатура.ВидНоменклатуры=Перечисление.ВидыНоменклатуры.Услуга Тогда
			Предупреждение("В этом документе услугу выбирать нельзя!",60);
		    Номенклатура = "";
		КонецЕсли;	
	КонецЕсли;
	ПересчетТаблЧасти(Форма.ТекущаяКолонка());
	
КонецПроцедуры // ПриИзмененииНоменклатуры()        
    
//*****************************************************************************
// ПоКнопкеЦены()
// 
// Параметры: 
//  Нет
//
// Возвращаемое значение: 
//  Нет
//
// Описание:
// 	Вызывается по кнопке редактирования параметров оплаты в документе
//
Процедура ПоКнопкеЦены()
	
	Перем КонтекстДокумента;
	
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОценах", КонтекстДокумента);
	ОбновитьНадписи();
	
КонецПроцедуры	//ПоКнопкеЦены()
                                      
//*****************************************************************************
// ПоКнопкеЗаполнить()
// 
// Параметры: 
//  Нет
//
// Возвращаемое значение: 
//  Нет
//
// Описание:
// 	Вызывается по кнопке "Заполнить". 
// 	Производит выбор способа заполнения и непосредственно заполнение документа
//
Процедура ПоКнопкеЗаполнить()
	
	Если КоличествоСтрок() <> 0 Тогда
		Если Вопрос("Заполнить остатками на складе?" +
		            РазделительСтрок +
					"(перед заполнением табличная часть будет очищена)", "Да+Нет", 60) <> "Да" Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	УдалитьСтроки();
	Результат = ЗаполнитьПоСкладу();
	Если ПустоеЗначение(Результат) = 0 Тогда
		Предупреждение(Результат, 60);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеЗаполнить()

//******************************************************************************
// ПоКнопкеПодбор()
//
// Параметры:
//  нет.
//
// Возвращаемое значение:
//  нет.
//
// Описание:

Процедура ПоКнопкеПодбор()
	
	Параметры = СоздатьОбъект("СписокЗначений");
	Параметры.ДобавитьЗначение(Фирма,     "Фирма");
	Параметры.ДобавитьЗначение(Склад,     "Склад");
	Параметры.ДобавитьЗначение(0,         "ЕстьВидТМЦ");
	Параметры.ДобавитьЗначение("Розница", "ЦенаВподборе");
	Параметры.ДобавитьЗначение(Валюта,    "Валюта");
	Параметры.ДобавитьЗначение(Курс,      "Курс");
	
	Если Выбран() = 0 Тогда
		Параметры.ДобавитьЗначение("Дата",  "ТипГраницы");
		Параметры.ДобавитьЗначение(ДатаДок, "ЗначениеГраницы");
	Иначе
		Параметры.ДобавитьЗначение("Позиция",         "ТипГраницы");
		Параметры.ДобавитьЗначение(ПолучитьПозицию(), "ЗначениеГраницы");
	КонецЕсли;
	
	Параметры.ДобавитьЗначение("Подбор номенклатуры в документ " + ПредставлениеВида() + " № " + НомерДок, "Заголовок");
	глПодбор(Контекст, Параметры);
	
КонецПроцедуры // ПоКнопкеПодбор()

//******************************************************************************
// Печать(СразуНаПринтер, КолЭкз)
//
// Параметры:
//  СразуНаПринтер - (1) если печать на принтер, (0) - с предварительным просмотром
//  КолЭкз - количество экземпляров печати
//
// Возвращаемое значение:
//  нет.
//
// Описание:
//
Процедура Печать(СразуНаПринтер = 0,КолЭкз = 1)
	Перем НачПовт, КонПовт;
	
	ИсхТабл = СоздатьОбъект("Таблица");
	НачПовт = 0; КонПовт = 0;
	
	// секция Заголовок
	
	ИсхТабл.ВывестиСекцию("Заголовок");
	НачПовт = НачПовт + ИсхТабл.ВысотаСекции("Заголовок");
	
	// Фирма
	ИсхТабл.ВывестиСекцию("Фирма");
	НачПовт = НачПовт + ИсхТабл.ВысотаСекции("Фирма");
	
	// Секция Склад
	ИсхТабл.ВывестиСекцию("Склад");
	НачПовт = НачПовт + ИсхТабл.ВысотаСекции("Склад");
	
	// Секция Шапка таблицы
	ИсхТабл.ВывестиСекцию("ШапкаТаблицы");
	КонПовт = НачПовт + ИсхТабл.ВысотаСекции("ШапкаТаблицы");
	ИсхТабл.ПовторятьПриПечатиСтроки(НачПовт + 1, КонПовт);
	
	// выводим табличную часть
	НомСтроки = 0;
	ВыбратьСтроки();
	
	Пока ПолучитьСтроку() = 1 Цикл
		НомСтроки = НомСтроки + 1;
		ИсхТабл.ВывестиСекцию("Строка");
	КонецЦикла;
		
	// секция Итого
	ИсхТабл.ВывестиСекцию("Итого");
	
	// секция Подписи
	ИсхТабл.ВывестиСекцию("Подписи");

	ИсхТабл.Опции(0,0,0,0,ПарСтрДок,ПарСтрДок);
	ИсхТабл.ТолькоПросмотр(1);
	ИсхТабл.Показать("Печать счета","");
	
	Если СразуНаПринтер = 0 Тогда
		ИсхТабл.Опции(0,0,0,0,ПарСтрДок);

		Если глПолучитьПолномочие("РазрешитьРедактированиеТаблиц") = 0 Тогда
			ИсхТабл.Защита(1);
		Иначе
			ИсхТабл.Защита(0);
		КонецЕсли;
		ИсхТабл.ТолькоПросмотр(1);

		ИсхТабл.Показать(глНазваниеДокументаВжурнале(Контекст),"");  
	Иначе     
		ИсхТабл.ПараметрыСтраницы(,,,,,,,,,1,,);
		ИсхТабл.КоличествоЭкземпляров(КолЭкз);
		ИсхТабл.Напечатать(0);
	КонецЕсли; 
КонецПроцедуры

//******************************************************************************
// ПоКнопкеПечать()
// 
// Параметры: 
//	Нет
//
// Описание:
// 	Вызывается по кнопке "Печать"  
//
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	// проверим полномочия печати непроведенных документов
	Если Проведен() = 0 Тогда
		Если глПолучитьПолномочие("РазрешитьПечатьНепроведенныхДокументов") = 0 Тогда
			Предупреждение("Недостаточно полномочий для печати непроведенного документа!", 60);
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Печать(СразуНаПринтер, КолЭкз);
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(глВзятьКонтекст(Контекст), "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Параметры:
//  нет.    
//
// Возвращаемое значение:
//  нет.
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// предопределенная процедура
//
Процедура ВводНового(Скопирован)
	                  
	глЗаполнитьШапку(Контекст, Скопирован);
	
	Если Скопирован = 1 Тогда	//копирование документа 
		Возврат;
	КонецЕсли;                              
	
	Если (Склад.РозничныйСклад = 0) Тогда
		Склад = ""; // неправильный склад
	КонецЕсли;
	
КонецПроцедуры // ВводНового()

//******************************************************************************
// предопределенная процедура

Процедура ПриОткрытии() 
	                     
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
	Парам = Форма.Параметр;
	Если ТипЗначенияСтр(Парам)="СписокЗначений" Тогда
		Если ПустоеЗначение(Парам.Получить("Команда"))=1 Тогда
			Сообщить("В форму документа "+Вид()+" передан неверный параметр!","I");
			СтатусВозврата(0);Возврат;
		КонецЕсли;
		Если Парам.Получить("Команда") = "ПечатьНаПринтер" Тогда
			КолЭкз = Макс(Число(Парам.Получить("КолЭкз")),1);
			ПоКнопкеПечать(1,КолЭкз);
			СтатусВозврата(0);Возврат;
		ИначеЕсли Парам.Получить("Команда") = "ПечатьНаЭкран" Тогда
			ПоКнопкеПечать(0);                
			СтатусВозврата(0);Возврат;
		ИначеЕсли Парам.Получить("Команда") = "ПереоценкаОстатков" Тогда

			Валюта		= глРубли;
			Курс		= 1;
			Фирма		= Парам.Получить("Фирма");
			Склад		= Парам.Получить("Склад");
			Проект		= Парам.Получить("Проект");
			Комментарий	= Парам.Получить("Комментарий");
			ТЗПереоценка = Парам.Получить("ТЗ");
		
			ТЗПереоценка.ВыбратьСтроки();
			Пока ТЗПереоценка.ПолучитьСтроку() = 1 Цикл
				
				НоваяСтрока();
				Номенклатура	= ТЗПереоценка.Номенклатура;
				Количество		= ТЗПереоценка.Количество;
				Единица			= ТЗПереоценка.Номенклатура.БазоваяЕдиница;
				Коэффициент 	= 1;
				ЦенаСтарая		= ТЗПереоценка.ЦенаСтарая;
				ЦенаНовая 		= ТЗПереоценка.ЦенаНовая; 
				
			КонецЦикла;            
			        
		Иначе
			Сообщить("В форму документа "+Вид()+" передана неверная команда "+Парам.Получить("Команда")+"!","I");
			СтатусВозврата(0);Возврат;
		КонецЕсли;
	КонецЕсли;
	
	//Инициализирум список действий по кнопке "Действия"
	СписокДействий = СоздатьОбъект("СписокЗначений");
	СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
	СписокДействий.ДобавитьЗначение("Отчет о бухгалтерских проводках");
              
	// инициализация модульных переменных, контролирующих выполнение
	// пересчетов и обновление надписей в форме
	СтараяФирма				= Фирма;
	
	НачальнаяДатаДокумента	= ДатаДок;

	Форма.Номенклатура	.ВыполнятьФормулуТолькоПриИзменении(1);
	Форма.Единица		.ВыполнятьФормулуТолькоПриИзменении(1);
	Форма.Количество	.ВыполнятьФормулуТолькоПриИзменении(1);
	Форма.ЦенаСтарая	.ВыполнятьФормулуТолькоПриИзменении(1);
	Форма.ЦенаНовая		.ВыполнятьФормулуТолькоПриИзменении(1);

	глПроверкаРазрешенияРедактирования(Контекст);
	
	// если дата проведенного документа больше ТА, то открываем только на просмотр,
	// так как его все равно не удастся сохранить после редактирования.
	Если (Проведен() = 1) И (ДатаДок > ПолучитьДатуТА()) Тогда
		Форма.ТолькоПросмотр(1);
	КонецЕсли;
	
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
	    // СДЕЛАТЬ НЕДОСТУПНЫМИ КНОПКИ МОДИФИКАЦИИ ДОКУМЕНТА
		Форма.кнОК.Доступность(0);
		Форма.кнХПроект.Доступность(0);
		Форма.кнЗаписать.Доступность(0);
		Форма.кнПровести.Доступность(0);
		Форма.кнЗаполнить.Доступность(0);
		Форма.кнПодбор.Доступность(0);
	Иначе
		СписокДействий.ДобавитьЗначение("Изменить спецификацию");
	КонецЕсли;
	
	СписокДействий.ДобавитьЗначение("Ввести на основании");
	СписокДействий.ДобавитьЗначение("Обновление цен в справочнике");
	СписокДействий.ДобавитьЗначение("Перейти в журнал");
	
	// кнопка по умолчанию
	Если (Форма.ТолькоПросмотр() = 1) ИЛИ ((ДатаДок < Макс(РабочаяДата(), ПолучитьДатуТА()) ) И (Выбран() = 1)) Тогда
	    Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
	    Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Шапка");
	Форма.Закладки.ДобавитьЗначение("Табличная часть");
	          
	Если Выбран() = 0 Тогда   
		глАктивизироватьРеквизит(Контекст);

		Форма.Закладки.ТекущаяСтрока(1);
		Форма.ИспользоватьСлой("Шапка,Основной,Подвал");
	Иначе
		// введенный документ открываем на второй закладке
		Форма.Закладки.ТекущаяСтрока(2);
		Форма.ИспользоватьСлой("Шапка,ТабличнаяЧасть,Подвал");
	КонецЕсли;
	
	//Если документ еще не проведен, тогда 
	//проведение делаем только в потоке
	Если ( Проведен() = 0 ) Тогда
		ПроводитьПослеТА(1,1);
	КонецЕсли;  
	              
	ОбновитьНадписи();
	
КонецПроцедуры  // ПриОткрытии()                                                                 

//******************************************************************************
// предопределенная процедура

Процедура ПриВыбореЗакладки(НомерЗакладки, ЗначениеЗакладки)
	
	Если НомерЗакладки=1 Тогда
		Форма.ИспользоватьСлой("Шапка,Основной,Подвал");
	Иначе
		ОбновитьНадписи();
		Форма.ИспользоватьСлой("Шапка,ТабличнаяЧасть,Подвал");
	КонецЕсли;       
	
КонецПроцедуры // ПриВыбореЗакладки()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр)
	
	Если ИдентЭлемДиалога = "ЦенаСтарая" Тогда
		ФлагСтандОбр = 0;
		// регистры рассчитываем, только если хочется редактировать цены вручную
		Если глПересчетРегистров(Контекст, СписокПараметров) = 0 Тогда
			Возврат;
		КонецЕсли;
		ТовЦена    = "";
		ТовОстаток = "";                         
		Поз        = 0;
		глПолучитьРозничныйОстатокЦену(Номенклатура, Единица, ОстаткиТМЦ, ТовОстаток, ТовЦена);
		СписокЦен = ЗначениеИзСтроки(ТовЦена);
		СписокЦен.ВыбратьЗначение(ЦенаСтарая,,Поз, 60,2);
		ЦенаНовая  = ?(ЦенаНовая = 0, ЦенаСтарая, ЦенаНовая);
		СписокОстатков = ЗначениеИзСтроки(ТовОстаток);
		Если Поз > 0 Тогда
			Количество = ?(Количество = 0,СписокОстатков.ПолучитьЗначение(Поз), Количество);
		КонецЕсли;
			                         
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//******************************************************************************
//Предопределенная процедура

Процедура ПриЗаписи() 
	
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);                        
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
    ПриЗаписиДокумента(Контекст);
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(ВыбЗнач, КонтФормыПодбора)
	
	Перем ТабТоварыВсоставе, ТаблицаПодбора;
	
	ТипЗнач		= ТипЗначенияСтр(ВыбЗнач);
	Если ТипЗнач = "СписокЗначений" Тогда
		
		ТаблицаПодбора = ВыбЗнач.Получить("ТаблицаПодбора");
	Иначе
		
		// если не список ничего не делаем
		Возврат;
	КонецЕсли;
	
	ТипЗнач	= ТипЗначенияСтр(ТаблицаПодбора);
	Если ТипЗнач = "ТаблицаЗначений" Тогда
		ВыгрузитьТабличнуюЧасть(ТабТоварыВсоставе, "Единица, НомерСтроки, ЦенаСтарая");
		
		ТаблицаПодбора.ВыбратьСтроки();
		Пока ТаблицаПодбора.ПолучитьСтроку() = 1 Цикл
			ТекНоменклатура	= ТаблицаПодбора.Номенклатура;
			ТекЕдиница			= ТаблицаПодбора.Единица;
			
			Если ТекНоменклатура <> ТекЕдиница.Владелец Тогда
			    Сообщить("В подборе выбрана единица другого элемента справочника Номенклатура.
				         |Проверьте правильность указания базовой и основной единицы для элемента
						 |""" + СокрЛП(ТекНоменклатура) + """, а также правильность указания единиц измерения 
						 |в справочниках единиц и цен для данной позиции номенклатуры." );
				Продолжить;
			КонецЕсли;
			
			// ищем номенклатуру вместе с ценой среди подобрааных
			Поз	= 0; 
			ТабТоварыВсоставе.ВыбратьСтроки();
			Пока ТабТоварыВсоставе.ПолучитьСтроку() = 1 Цикл
			    Если (ТабТоварыВсоставе.ЦенаСтарая = ТаблицаПодбора.Цена) И (ТабТоварыВсоставе.Единица = ТекЕдиница) Тогда
				   	Поз = ТабТоварыВсоставе.НомерСтроки;
			        Прервать;  // Нашли
			    КонецЕсли;
			КонецЦикла;
			
			Если Поз > 0 Тогда
				
				// нашли, увеличиваем количество
				ПолучитьСтрокуПоНомеру(Поз);
				Количество	= Количество + ТаблицаПодбора.Количество;
			Иначе
				НоваяСтрока();
				Номенклатура  = ТаблицаПодбора.Номенклатура;
				Количество    = ТаблицаПодбора.Количество;
				Единица       = ТаблицаПодбора.Единица;
				Коэффициент   = Единица.Коэффициент;    
				          
				ВремЦена   = глПересчет(ТаблицаПодбора.Цена, глРубли, ДатаДок, Валюта, Курс);
				ЦенаСтарая = ВремЦена;
				ЦенаНовая  = ВремЦена;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Активизировать("Количество");
	АктивизироватьСтроку();
	
КонецПроцедуры // ОбработкаПодбора()

//******************************************************************************
Процедура ОбработкаВнешнегоСобытия(Источник,Событие,Данные)
	// Процедура разбирает штрих-код, считанный сканером
	// и заполняет строки накладной
	Перем Упаковка,ТекКоличество, Спецификация;
	Перем ВремТовар, ВремЕдиница, ВремКоличество, ВремЦена;
	
	Перем СтрокаВозврЦена;

	Если Событие = "BarCodeValue" Тогда
        Если Форма.ТолькоПросмотр() = 0 Тогда
			Если глПолучитьТоварПоШтрихкоду(Данные, ВремТовар, ВремЕдиница, ВремКоличество) <> 0 Тогда
				ТаблицаПодбора = СоздатьОбъект("ТаблицаЗначений");
				ТаблицаПодбора.НоваяКолонка("Номенклатура");
				ТаблицаПодбора.НоваяКолонка("Единица");
				ТаблицаПодбора.НоваяКолонка("Количество", "Число");
				ТаблицаПодбора.НоваяКолонка("Цена", "Число");
				
				ТаблицаПодбора.НоваяСтрока();
				ТаблицаПодбора.Номенклатура = ВремТовар;
				ТаблицаПодбора.Единица      = ВремЕдиница;
				ТаблицаПодбора.Количество   = ВремКоличество;
				
				ТовОстатки = СоздатьОбъект("Регистр.ОстаткиТМЦ");
				ТовОстатки.УстановитьЗначениеФильтра("Фирма", Фирма, 1);
				ТовОстатки.УстановитьЗначениеФильтра("Склад", Склад, 1);
				
				глПолучитьРозничныйОстатокЦену(ВремТовар, ВремЕдиница, ТовОстатки, , СтрокаВозврЦена);
				
				СписВозврЦен = ЗначениеИзстроки(СтрокаВозврЦена);
				Если СписВозврЦен.РазмерСписка() = 1 Тогда
					ТаблицаПодбора.Цена  = глПересчет(СписВозврЦен.ПолучитьЗначение(1), глРубли, ДатаДок, Валюта, Курс);
					
				ИначеЕсли СписВозврЦен.РазмерСписка() > 1 Тогда
					
					Если СписВозврЦен.ВыбратьЗначение(ВремЦена,"Выберите цену для товара " + ВремТовар.Наименование, , 60) = 1 Тогда
						ТаблицаПодбора.Цена  = глПересчет(ВремЦена, глРубли, ДатаДок, Валюта, Курс);
					Иначе	
						ТаблицаПодбора.Цена  = 0;
					КонецЕсли;
				Иначе
					ТаблицаПодбора.Цена  = 0;
				КонецЕсли;
				
				ПараметрыВыбора = СоздатьОбъект("СписокЗначений");
				ПараметрыВыбора.ДобавитьЗначение(ТаблицаПодбора, "ТаблицаПодбора");
			    
				ОбработкаПодбора(ПараметрыВыбора,);
			КонецЕсли;
	
		КонецЕсли;
		
		// Обработка закончена. Готовы к получению нового штрихкода.
		глСканерПосылкаДанных(1);
		
	Иначе
		глОбработкаВнешнегоСобытия(Источник, Событие, Данные);	
	КонецЕсли;

КонецПроцедуры // ОбработкаВнешнегоСобытия()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
// формируем таблицу печатных форм
НомерТекущейФормы	= 0;

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");

ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Печатная форма";
ТаблицаПечФорм.Кнопка       = "Печать";

// Для выбора цен
ОбщРег           = СоздатьОбъект("Регистры");
ОстаткиТМЦ       = ОбщРег.ОстаткиТМЦ;
СписокПараметров = СоздатьОбъект("СписокЗначений"); 
СписокПараметров.Установить("ОбщРег"              , ОбщРег);
СписокПараметров.Установить("ОстаткиТМЦ"          , ОстаткиТМЦ);
СписокПараметров.Установить("ФильтрПоСкладу"      , ПолучитьПустоеЗначение("Справочник.Склады"));
СписокПараметров.Установить("ФильтрПоФирме"       , ПолучитьПустоеЗначение("Справочник.Фирмы"));
СписокПараметров.Установить("ПозицияРегистра"     , 0);
	
