////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ДокПодч;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ПроверкаСФДокумента(ДокДляПроверки, НайденнаяСФ)
//
// Параметры:
//  ДокДляПроверки 	- документ              
//	НайденнаяСФ 	- переменная, в которую возвращается найденная СФ
//
// Возвращаемое значение:
//  1 - СФ есть и включено автоматическое формирование, можно вносить запись, 0 - иначе
//
// Описание:
//  У документа производится поиск счета фактуры. Если она есть и в ней включено 
// 	автоматическое формирование записей книги продаж, то возвращаем 1.
//
Функция ПроверкаСФДокумента(ДокДляПроверки,НайденнаяСФ)
	                    
	Если (ДокДляПроверки.Вид() = "ОтчетККМ") или (ДокДляПроверки.Вид() = "РеализацияРозница")Тогда
		НайденнаяСФ = ДокДляПроверки;
		Возврат 1;
	КонецЕсли;
	
	Если ДокДляПроверки.Вид() = "ПКО" Тогда
		Если ДокДляПроверки.КодОперации = глКО.РозничнаяВыручка Тогда
		    НайденнаяСФ = ДокДляПроверки; 
			Возврат 1;
		КонецЕсли;	    
	КонецЕсли;
	
	ДокПодч.ВыбратьПодчиненныеДокументы(,,ДокДляПроверки);
	Пока ДокПодч.ПолучитьДокумент()=1 Цикл
		Если (ДокПодч.Вид()="СчетФактураВыданный") и (ДокПодч.Проведен()=1) Тогда
			Если ДокПодч.АвтоКнигаПродаж = 1 Тогда
				НайденнаяСФ = ДокПодч.ТекущийДокумент();
				Возврат 1;
			Иначе     
				Возврат 0;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;   
	
	
	глСообщениеПроведения("На документ "+ДокДляПроверки+" не зарегистрирована счет - фактура!",ДокДляПроверки,, ТекущийДокумент(),1);
	Возврат 0;
	
КонецФункции // ПроверкаСФДокумента()

//******************************************************************************
// ПроведениеПоСторноАвансов(ВремКнигаПродаж)
//
// Параметры:
//  ВремКнигаПродаж
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПроведениеПоСторноАвансов(ВремКнигаПродаж)
	Перем Запрос, ТекстЗапроса;   
	Перем ДатаНачала, ПериодПо, ТекДок;
	Перем СписокНужныхКодов, СписокНужныхВидовДолга;
	
	// отбираем движения регистров, выбирая те из них, которые должны 
	// быть включены в книгу продаж.
	ДатаНачала = НачМесяца(ДатаДок);   
	
	// Сторнирование авансов в день вступления в силу Постановления Правительства РФ № 575 
	// через книгу покупок
	Дата575 = глДатаИзмененияПорядкаВычетаНДССАвансов - 1; 
	
	// Если весь отчетный период лежит после даты вступления в силу 
	// Постановления Правительства РФ № 575, то сторно авансов будет осуществляться
	// через книгу покупок
	Если ДатаНачала > Дата575 Тогда
		Возврат;
	КонецЕсли;

	// определение периода построения запросов
	Если ДатаДок > Дата575  Тогда
		ПериодПо = "по Дата575";
	Иначе
		Если ИтогиАктуальны() = 1 Тогда                                  
			ПериодПо = "";
		Иначе                  
			ТекДок = ТекущийДокумент();
			ПериодПо = "по ТекДок";
		КонецЕсли;         
	КонецЕсли;
    
	// движения по покупателям, включаемые в книгу продаж
	СписокНужныхКодов = СоздатьОбъект("СписокЗначений");
	СписокНужныхКодов.ДобавитьЗначение(глКО.СторнированАванс);  
	СписокНужныхКодов.ДобавитьЗначение(глКО.ВозвратОплатыПокупателю);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ВозвратОплатыПокупателюВал);
	СписокНужныхКодов.ДобавитьЗначение(глКО.Прочее);
	
	СписокНужныхВидовДолга = СоздатьОбъект("СписокЗначений");
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.Аванс);
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала "+ ПериодПо+";
	|ФирмаРег 		= Регистр.Покупатели.Фирма;
	|ВидДолга 		= Регистр.Покупатели.ВидДолга;
	|КредДокумент 	= Регистр.Покупатели.КредДокумент;
	|СуммаРуб 		= Регистр.Покупатели.СуммаРуб;
	|КодОперации 	= Регистр.Покупатели.КодОперации;
	|Функция СуммаРубПриход = Приход(СуммаРуб);
	|Группировка 	КредДокумент;
	|Группировка 	Документ;
	|Группировка 	ВидДолга;
	|Условие	(ФирмаРег 	= Фирма);
	|Условие	(КодОперации 	в СписокНужныхКодов);
	|Условие	(ВидДолга 		в СписокНужныхВидовДолга);
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;                                   
	
    НайденнаяСФ = "";            
	ТИКнигаПродаж = СоздатьОбъект("ТаблицаЗначений");
		
	Пока Запрос.Группировка("КредДокумент") = 1 Цикл
	    Пока Запрос.Группировка("Документ") = 1 Цикл
        	Пока Запрос.Группировка("ВидДолга") = 1 Цикл
				ВремКнигаПродаж.УстановитьЗначениеФильтра("КредДокумент",Запрос.КредДокумент,1);
				ВремКнигаПродаж.УстановитьЗначениеФильтра("ВидДолга",    Запрос.ВидДолга,    1);
				
				ВремКнигаПродаж.ВыгрузитьИтоги(ТИКнигаПродаж,1,1);      
				ОстПогСуммаРуб = - Запрос.СуммаРубПриход; // сумма сторнированного аванса
				                              
				Если ТИКнигаПродаж.КоличествоСтрок()<>0 Тогда
					Если ПроверкаСФДокумента(Запрос.КредДокумент,НайденнаяСФ) = 0 Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;    
				
				ТИКнигаПродаж.ВыбратьСтроки();
				Пока (ТИКнигаПродаж.ПолучитьСтроку() = 1)
				и    (ОстПогСуммаРуб <> 0)
				Цикл
					// в этом случае итоги в регистре "КнигаПродаж" отрицательные
					СписСуммаРуб = Мин(0,Макс(ТИКнигаПродаж.СуммаРуб,ОстПогСуммаРуб));
					Если СписСуммаРуб <> 0 Тогда
						КоэффСпис    = ?(ТИКнигаПродаж.СуммаРуб = 0,0,СписСуммаРуб/(ТИКнигаПродаж.СуммаРуб));
						СписСуммаНДС = Окр(ТИКнигаПродаж.СуммаНДС * КоэффСпис,2,1);
						СписСуммаНП  = Окр(ТИКнигаПродаж.СуммаНП  * КоэффСпис,2,1);
						
						Регистр.КнигаПродаж.КредДокумент    = ТИКнигаПродаж.КредДокумент;
						Регистр.КнигаПродаж.ВидДолга		= ТИКнигаПродаж.ВидДолга;
						Регистр.КнигаПродаж.СтавкаНДС		= ТИКнигаПродаж.СтавкаНДС;
						
						Регистр.КнигаПродаж.СуммаРуб		=  СписСуммаРуб;
						Регистр.КнигаПродаж.СуммаНДС		=  СписСуммаНДС;
						Регистр.КнигаПродаж.СуммаНП	        =  СписСуммаНП;
						
					                                                                 
						// Надо прописать ставку НП. Важно для розничной выручки
						Если ТИКнигаПродаж.КредДокумент.Вид() = "ПКО" Тогда
							СтавкаНП = ?(ТИКнигаПродаж.КредДокумент.ОблагаетсяНП = 0,
							             ПолучитьПустоеЗначение("Справочник.СтавкиНП"),
										 ТИКнигаПродаж.КредДокумент.СтавкаНП
										);
						    Регистр.КнигаПродаж.СтавкаНП 	= СтавкаНП;
						КонецЕсли;
						Регистр.КнигаПродаж.КодОперации     = глКО.Прочее;
						Регистр.КнигаПродаж.ДокументОплаты	= Запрос.Документ;
						                                             
						Регистр.КнигаПродаж.ДвижениеРасходВыполнить();
						
						ОстПогСуммаРуб = ОстПогСуммаРуб - СписСуммаРуб;
					КонецЕсли; // есть что списывать
				КонецЦикла; // по строкам таблицы итогов КП
			КонецЦикла; // по виду долга
		КонецЦикла; // по тек документу
	КонецЦикла;// по кред. документу
	
КонецПроцедуры // ПроведениеПоСторноАвансов()

//******************************************************************************
// ПроведениеПоОплате(ВремКнигаПродаж)
//
// Параметры:
//  ВремКнигаПродаж
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Здесь можно перечислить элементы диалога.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПроведениеПоОплате(ВремКнигаПродаж)
	Перем Запрос, ТекстЗапроса;   
	Перем ДатаНачала, ПериодПо, ТекДок;
	Перем СписокНужныхКодов, СписокНужныхВидовДолга;
	
	// отбираем движения регистров, выбирая те из них, которые должны 
	// быть включены в книгу продаж.
	ДатаНачала = НачМесяца(ДатаДок);
	
	// определение периода построения запросов
	Если ИтогиАктуальны() = 1 Тогда
		ПериодПо = "";
	Иначе       
		ТекДок = ТекущийДокумент();
		ПериодПо = "по ТекДок";
	КонецЕсли;         
    
	// движения по покупателям, включаемые в книгу продаж
	СписокНужныхКодов = СоздатьОбъект("СписокЗначений");
	СписокНужныхКодов.ДобавитьЗначение(глКО.ОплатаОтПокупателя);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ОплатаОтПокупателяВал);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ЗачтенАвансПокупателя);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ЗачтеноКомиссионноеВознаграждение); 
	СписокНужныхКодов.ДобавитьЗначение(глКО.РозничнаяПродажа); // оплата совпадает с отгрузкой
	СписокНужныхКодов.ДобавитьЗначение(глКО.Прочее);           // оплата прочими средствами
	СписокНужныхКодов.ДобавитьЗначение(глКО.ЗачтенВозвратПокупателя);                      
	СписокНужныхКодов.ДобавитьЗначение(глКО.РозничнаяВыручка);                      
	СписокНужныхКодов.ДобавитьЗначение(глКО.ВводОстатков);
	
	СписокНужныхВидовДолга = СоздатьОбъект("СписокЗначений");
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаТовары);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаПродукцию);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаУслуги);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаМатериалы);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаОС);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаНМА);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.Прочее);
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала "+ ПериодПо+";
	|ФирмаРег 		= Регистр.Покупатели.Фирма;
	|ВидДолга 		= Регистр.Покупатели.ВидДолга;
	|КредДокумент 	= Регистр.Покупатели.КредДокумент;
	|ДокументОплаты = Регистр.Покупатели.ДокументОплаты;
	|ТекДокумент 	= Регистр.Покупатели.ТекущийДокумент;
	|СуммаРуб 		= Регистр.Покупатели.СуммаРуб;
	|КодОперации 	= Регистр.Покупатели.КодОперации;
	|СтавкаНП	 	= Регистр.Покупатели.СтавкаНП;
	|Функция СуммаРубРасход = Расход(СуммаРуб);
	|Группировка 	КредДокумент;
	|Группировка 	ДокументОплаты;
	|Группировка 	ВидДолга;
	|Группировка 	СтавкаНП;
	|Условие	(ФирмаРег 	= Фирма);
	|Условие	(КодОперации 	в СписокНужныхКодов);
	|Условие	(ВидДолга 		в СписокНужныхВидовДолга);
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;                                   
	
    НайденнаяСФ = "";            
	ТИКнигаПродаж = СоздатьОбъект("ТаблицаЗначений");
		
	Пока Запрос.Группировка("КредДокумент") = 1 Цикл
		Если ПроверкаСФДокумента(Запрос.КредДокумент,НайденнаяСФ) = 0 Тогда
			Продолжить;
		КонецЕсли;
	    Пока Запрос.Группировка("ДокументОплаты") = 1 Цикл
        	Пока Запрос.Группировка("ВидДолга") = 1 Цикл
	        	Пока Запрос.Группировка("СтавкаНП") = 1 Цикл
					ВремКнигаПродаж.УстановитьЗначениеФильтра("СтавкаНП"	,Запрос.СтавкаНП,1);
					ВремКнигаПродаж.УстановитьЗначениеФильтра("КредДокумент",Запрос.КредДокумент,1);
					ВремКнигаПродаж.УстановитьЗначениеФильтра("ВидДолга",    Запрос.ВидДолга,    1);
					
					ВремКнигаПродаж.ВыгрузитьИтоги(ТИКнигаПродаж,1,1);      
					ОстПогСуммаРуб = Запрос.СуммаРубРасход; // сумма оплаты
				
					ТИКнигаПродаж.ВыбратьСтроки();
					Пока (ТИКнигаПродаж.ПолучитьСтроку() = 1)
					и    (ОстПогСуммаРуб <> 0)
					Цикл
						
						СписСуммаРуб = Макс(0,Мин(ТИКнигаПродаж.СуммаРуб,ОстПогСуммаРуб));
						Если СписСуммаРуб <> 0 Тогда
							КоэффСпис    = ?(ТИКнигаПродаж.СуммаРуб = 0,0,СписСуммаРуб/(ТИКнигаПродаж.СуммаРуб));
							СписСуммаНДС = Окр(ТИКнигаПродаж.СуммаНДС * КоэффСпис,2,1);
							СписСуммаНП  = Окр(ТИКнигаПродаж.СуммаНП  * КоэффСпис,2,1);
						
							Регистр.КнигаПродаж.КредДокумент    = ТИКнигаПродаж.КредДокумент;
							Регистр.КнигаПродаж.ВидДолга		= ТИКнигаПродаж.ВидДолга;
							Регистр.КнигаПродаж.СтавкаНДС		= ТИКнигаПродаж.СтавкаНДС;
						
							Регистр.КнигаПродаж.СуммаРуб		= СписСуммаРуб;
							Регистр.КнигаПродаж.СуммаНДС		= СписСуммаНДС;
							Регистр.КнигаПродаж.СуммаНП	        = СписСуммаНП;
						
							Регистр.КнигаПродаж.КодОперации     = глКО.Прочее;
							Регистр.КнигаПродаж.СтавкаНП 		= Запрос.СтавкаНП;
						
							// Для того чтобы док-нт Сторно попал в книгу, сделаем его документом оплаты
							Если Запрос.ТекДокумент.Вид() = "Сторно" Тогда
								Регистр.КнигаПродаж.ДокументОплаты	= Запрос.ТекДокумент;
							Иначе    
								Регистр.КнигаПродаж.ДокументОплаты	= Запрос.ДокументОплаты;
							КонецЕсли;
							
							Регистр.КнигаПродаж.ДвижениеРасходВыполнить();
							
							ОстПогСуммаРуб = ОстПогСуммаРуб - СписСуммаРуб;
						КонецЕсли; // есть что списывать                   
					КонецЦикла; // по строкам таблицы итогов КП
				КонецЦикла; // по ставкам НП
			КонецЦикла; // по виду долга
		КонецЦикла; // по тек документу
	КонецЦикла;// по кред. документу
	
КонецПроцедуры // ПроведениеПоОплате()
                                      
//******************************************************************************
// ПроведениеПоОтгрузке(ВремКнигаПродаж)
//
// Параметры:
//  ВремКнигаПродаж
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Здесь можно перечислить элементы диалога.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПроведениеПоОтгрузке(ВремКнигаПродаж)
	Перем Запрос, ТекстЗапроса;   
	Перем ДатаНачала, ПериодПо, ТекДок;
	Перем СписокНужныхКодов, СписокНужныхВидовДолга;
	
	// отбираем движения регистров, выбирая те из них, которые должны 
	// быть включены в книгу продаж.
	ДатаНачала = НачМесяца(ДатаДок);
	
	// определение периода построения запросов
	Если ИтогиАктуальны() = 1 Тогда
		ПериодПо = "";
	Иначе       
		ТекДок = ТекущийДокумент();
		ПериодПо = "по ТекДок";
	КонецЕсли;         
    
	// движения по покупателям, включаемые в книгу продаж
	СписокНужныхКодов = СоздатьОбъект("СписокЗначений");
	СписокНужныхКодов.ДобавитьЗначение(глКО.Продажа);
	СписокНужныхКодов.ДобавитьЗначение(глКО.НачисленоКомиссионноеВознаграждение);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ЗачтеноКомиссионноеВознаграждение);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ОтчетРеализатора); 
	СписокНужныхКодов.ДобавитьЗначение(глКО.СуммоваяРазница);  
	СписокНужныхКодов.ДобавитьЗначение(глКО.РозничнаяПродажа);
	СписокНужныхКодов.ДобавитьЗначение(глКО.КорректировкаОтгрузкиНачислениеНП);
	СписокНужныхКодов.ДобавитьЗначение(глКО.КорректировкаОтгрузкиСторнированиеНП);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ЗачтенАвансПокупателя);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ОплатаОтПокупателя);
	
	СписокНужныхВидовДолга = СоздатьОбъект("СписокЗначений");
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаТовары);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаПродукцию);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаУслуги);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаМатериалы);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаОС);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаНМА);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.Прочее);
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала "+ ПериодПо+";
	|ФирмаРег 		= Регистр.Покупатели.Фирма;
	|ВидДолга 		= Регистр.Покупатели.ВидДолга;
	|КредДокумент 	= Регистр.Покупатели.КредДокумент;
	|ДокументОплаты = Регистр.Покупатели.ДокументОплаты;
	|ТекДокумент 	= Регистр.Покупатели.ТекущийДокумент;
	|СуммаРуб 		= Регистр.Покупатели.СуммаРуб;
	|КодОперации 	= Регистр.Покупатели.КодОперации;
	|СтавкаНП	 	= Регистр.Покупатели.СтавкаНП;
	|Функция СуммаРубПриход = Приход(СуммаРуб);
	|Группировка 	КредДокумент;
	|Группировка 	ВидДолга;
	|Группировка 	СтавкаНП;
	|Условие	(ФирмаРег 	= Фирма);
	|Условие	(КодОперации 	в СписокНужныхКодов);
	|Условие	(ВидДолга 		в СписокНужныхВидовДолга);
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;                                   
	
    НайденнаяСФ = "";            
	ТИКнигаПродаж = СоздатьОбъект("ТаблицаЗначений");
		
	Пока Запрос.Группировка("КредДокумент") = 1 Цикл
		Если ПроверкаСФДокумента(Запрос.КредДокумент,НайденнаяСФ) = 0 Тогда
			Продолжить;
		КонецЕсли;
        	Пока Запрос.Группировка("ВидДолга") = 1 Цикл           
				Пока Запрос.Группировка("СтавкаНП") = 1 Цикл       
					
					ВремКнигаПродаж.УстановитьЗначениеФильтра("КредДокумент",Запрос.КредДокумент,1);
					ВремКнигаПродаж.УстановитьЗначениеФильтра("ВидДолга",    Запрос.ВидДолга,    1);
					
					ВремКнигаПродаж.ВыгрузитьИтоги(ТИКнигаПродаж,1,1);      
					ОстПогСуммаРуб = Запрос.СуммаРубПриход; // сумма отгрузки   
					СуммаПогашения = ОстПогСуммаРуб;
					
					ТИКнигаПродаж.ВыбратьСтроки();
					Пока (ТИКнигаПродаж.ПолучитьСтроку() = 1)
					и    (ОстПогСуммаРуб <> 0)
					Цикл
						
						Если ОстПогСуммаРуб < 0 Тогда
							СписСуммаРуб = Мин(0,Макс(ТИКнигаПродаж.СуммаРуб,ОстПогСуммаРуб));
						Иначе    
							СписСуммаРуб = Макс(0,Мин(ТИКнигаПродаж.СуммаРуб,ОстПогСуммаРуб));
						КонецЕсли;
						Если СписСуммаРуб <> 0 Тогда
							КоэффСпис    = ?(ТИКнигаПродаж.СуммаРуб = 0,0,СписСуммаРуб/(ТИКнигаПродаж.СуммаРуб));
							СписСуммаНДС = Окр(ТИКнигаПродаж.СуммаНДС * КоэффСпис,2,1);
							СписСуммаНП  = Окр(ТИКнигаПродаж.СуммаНП  * КоэффСпис,2,1);
							
							Регистр.КнигаПродаж.КредДокумент    = ТИКнигаПродаж.КредДокумент;
							Регистр.КнигаПродаж.ВидДолга		= ТИКнигаПродаж.ВидДолга;
							Регистр.КнигаПродаж.СтавкаНДС		= ТИКнигаПродаж.СтавкаНДС;
							
							Регистр.КнигаПродаж.СуммаРуб		= СписСуммаРуб;
							Регистр.КнигаПродаж.СуммаНДС		= СписСуммаНДС;
							Регистр.КнигаПродаж.СуммаНП	        = СписСуммаНП;
							Регистр.КнигаПродаж.СтавкаНП 		= Запрос.СтавкаНП;
							Регистр.КнигаПродаж.КодОперации     = глКО.Прочее;
							
							// Для того чтобы док-нт Сторно попал в книгу, сделаем его документом оплаты
							Если Запрос.ТекДокумент.Вид() = "Сторно" Тогда
								Регистр.КнигаПродаж.ДокументОплаты	= Запрос.ТекДокумент;
							Иначе    
								Регистр.КнигаПродаж.ДокументОплаты	= Запрос.ДокументОплаты;
							КонецЕсли;
							
							Регистр.КнигаПродаж.ДвижениеРасходВыполнить();
							
							ОстПогСуммаРуб = ОстПогСуммаРуб - СписСуммаРуб;
						КонецЕсли; // есть что списывать           
					КонецЦикла; // по строкам таблицы итогов КП
				КонецЦикла; // по ставке НП
			КонецЦикла; // по виду долга
	КонецЦикла;// по кред. документу
	
КонецПроцедуры // ПроведениеПоОтгрузке()

//******************************************************************************
// ПроведениеПоРегистрам()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Проведение по регистрам оперативного учета.
//
Процедура ПроведениеПоРегистрам()
	
	Перем ВремРегистры, ВремКнигаПродаж;
	
	// Удаление движений по регистрам.
	Для Номер = 1 По Метаданные.Регистр() Цикл
		ОчиститьДвижения("Регистр."+Метаданные.Регистр(Номер).Идентификатор);
	КонецЦикла;

	ВремРегистры = СоздатьОбъект("Регистры");
	ВремКнигаПродаж = ВремРегистры.КнигаПродаж;
    
	// при необходимости делаем временный расчет итогов
	Если ИтогиАктуальны() = 0 Тогда
		ВремКнигаПродаж.ВременныйРасчет();
		ВремРегистры.Актуальность(1);
		ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
                         
	Если Фирма.ЮрЛицо.МетодОпределенияВыручки.Получить(ДатаДок) = 
	     Перечисление.МетодыОпределенияВыручки.ПоОплате 
	Тогда
		 ПроведениеПоОплате(ВремКнигаПродаж);	
	Иначе     
		 ПроведениеПоОтгрузке(ВремКнигаПродаж);	
	КонецЕсли;
                                                    
	ПроведениеПоСторноАвансов(ВремКнигаПродаж); // а это надо в любом случае
	
КонецПроцедуры // ПроведениеПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПроведения(ВидыДвижений)   
	
	// Проверка заполненности обязательных реквизитов.
	Если глВсеРеквизитыДокументаЗаполнены(Контекст,"Фирма")=0 Тогда
		Возврат;
	КонецЕсли;        
	
	// проверим, не был ли уже проведен документ за этот месяц по этой же фирме
	ДокКП = СоздатьОбъект("Документ.КнигаПродаж");
	ДокКП.ВыбратьДокументы(НачМесяца(ДатаДок),КонМесяца(ДатаДок));
	Пока ДокКП.ПолучитьДокумент()=1 Цикл
	    Если (ДокКП.ТекущийДокумент()<>ТекущийДокумент()) и 
			 (ДокКП.Проведен()=1) и
			 (ДокКП.Фирма = Фирма) Тогда    
			 	
	        глНеПроводить(Контекст,"За " + Формат(ДатаДок, "Д ММММГГГГ")+
			"по фирме """+Фирма.Наименование+
			""" формирование книги продаж уже проведено ("+ДокКП.ТекущийДокумент()+")!");
			Возврат;
			
	    КонецЕсли;
	КонецЦикла;       
	                     
	// Проведение по регистрам оперативного учета.	
	Если (ПустоеЗначение(ВидыДвижений) = 1) ИЛИ (Найти(ВидыДвижений, "Регистр") <> 0) Тогда
		ПроведениеПоРегистрам();
		
		Если СтатусВозврата() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	глПриПроведении(Контекст, ВидыДвижений);
	
КонецПроцедуры //ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ДокПодч = СоздатьОбъект("Документ");