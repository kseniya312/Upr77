////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ДокПодч;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ПроверкаСФДокумента(ДокДляПроверки, НайденнаяСФ)
//
// Параметры:
//  ДокДляПроверки 	- документ              
//	НайденнаяСФ 	- переменная, в которую возвращается найденная СФ
//
// Возвращаемое значение:
//  1 - СФ есть и включено автоматическое формирование, можно вносить запись, 0 - иначе
//
// Описание:
//  У документа производится поиск счета фактуры. Если она есть и в ней включено 
// 	автоматическое формирование записей книги покупок, то возвращаем 1.
//
Функция ПроверкаСФДокумента(ДокДляПроверки,НайденнаяСФ,ВидСФ="СчетФактураПолученный")  
	
	Если ДокДляПроверки.Вид() = "СчетФактураВыданный" Тогда
		НайденнаяСФ = ДокДляПроверки;  
		Возврат 0;
	КонецЕсли;

	ДокПодч.ВыбратьПодчиненныеДокументы(,,ДокДляПроверки);
	Пока ДокПодч.ПолучитьДокумент()=1 Цикл
		Если (ДокПодч.Вид()=ВидСФ) и (ДокПодч.Проведен()=1) Тогда 
			Если ВидСФ = "СчетФактураПолученный" Тогда
				Если ДокПодч.АвтоКнигаПокупок = 1 Тогда
					НайденнаяСФ = ДокПодч.ТекущийДокумент();
					Возврат 1;
				Иначе     
					Возврат 0;
				КонецЕсли;
			Иначе
				Если ДокПодч.АвтоКнигаПродаж = 1 Тогда
					НайденнаяСФ = ДокПодч.ТекущийДокумент();
					Возврат 1;
				Иначе     
					Возврат 0;
				КонецЕсли
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;   
	                   
	Если глЕстьРеквизитШапки("УчитыватьНДС", ДокДляПроверки.Вид()) = 1 Тогда
		Если ДокДляПроверки.УчитыватьНДС = 0 Тогда
			Возврат 1;
		КонецЕсли;
	КонецЕсли;
	
	глСообщениеПроведения( "На документ "+ДокДляПроверки+" не зарегистрирована счет - фактура!", ДокДляПроверки,,ТекущийДокумент(),1);
	Возврат 0;
	
КонецФункции // ПроверкаСФДокумента()     


//******************************************************************************
// ПроведениеПоСторноАвансов(ВремКнигаПродаж)
//
// Параметры:
//  ВремКнигаПродаж
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПроведениеПоСторноАвансов(ВремКнигаПродаж)
	Перем Запрос, ТекстЗапроса;   
	Перем ДатаНачала, ПериодПо, ТекДок;
	Перем СписокНужныхКодов, СписокНужныхВидовДолга;
	
	// отбираем движения регистров, выбирая те из них, которые должны 
	// быть включены в книгу продаж.
	ДатаНачала = НачМесяца(ДатаДок);   
	
	Дата575 = глДатаИзмененияПорядкаВычетаНДССАвансов;
	
	// Если весь отчетный период лежит после даты вступления в силу 
	// Постановления Правительства РФ № 575, то сторно авансов будет осуществляться
	// через книгу покупок
	Если ДатаДок < Дата575 Тогда
		Возврат;
	КонецЕсли;
		
	
	// определение периода построения запросов
	Если ДатаНачала > Дата575 Тогда
		ПериодС = "Период с ДатаНачала ";
	Иначе
		ПериодС = "Период с Дата575 ";
	КонецЕсли;
	Если ИтогиАктуальны() = 1 Тогда                                  
		ПериодПо = "";
	Иначе                  
		ТекДок = ТекущийДокумент();
		ПериодПо = "по ТекДок";
	КонецЕсли;         
    
	// движения по покупателям, включаемые в книгу продаж
	СписокНужныхКодов = СоздатьОбъект("СписокЗначений");
	СписокНужныхКодов.ДобавитьЗначение(глКО.СторнированАванс);  
	СписокНужныхКодов.ДобавитьЗначение(глКО.ОплатаОтПокупателя);  
	СписокНужныхКодов.ДобавитьЗначение(глКО.ВозвратОплатыПокупателю);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ВозвратОплатыПокупателюВал);
	СписокНужныхКодов.ДобавитьЗначение(глКО.Прочее);
	
	СписокНужныхВидовДолга = СоздатьОбъект("СписокЗначений");
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.Аванс);
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|"+ ПериодС + ПериодПо+";
	|ФирмаРег 		= Регистр.Покупатели.Фирма;
	|ВидДолга 		= Регистр.Покупатели.ВидДолга;
	|КредДокумент 	= Регистр.Покупатели.КредДокумент;
	|СуммаРуб 		= Регистр.Покупатели.СуммаРуб;
	|КодОперации 	= Регистр.Покупатели.КодОперации;
	|Функция СуммаРубПриход = Приход(СуммаРуб);
	|Функция СуммаРубРасход = Расход(СуммаРуб);
	|Группировка 	КредДокумент;
	|Группировка 	Документ;
	|Группировка 	ВидДолга;
	|Условие	(ФирмаРег 	= Фирма);
	|Условие	(КодОперации 	в СписокНужныхКодов);
	|Условие	(ВидДолга 		в СписокНужныхВидовДолга);
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;                                   
	
    НайденнаяСФ = "";            
	ТИКнигаПродаж = СоздатьОбъект("ТаблицаЗначений");
		
	Пока Запрос.Группировка("КредДокумент") = 1 Цикл   
		ОбщаяСуммаАванса = Запрос.СуммаРубРасход;
	    Пока Запрос.Группировка("Документ") = 1 Цикл
        	Пока Запрос.Группировка("ВидДолга") = 1 Цикл
				ВремКнигаПродаж.УстановитьЗначениеФильтра("КредДокумент",Запрос.КредДокумент,1);
				ВремКнигаПродаж.УстановитьЗначениеФильтра("ВидДолга",    Запрос.ВидДолга,    1);
				
				ВремКнигаПродаж.ВыгрузитьИтоги(ТИКнигаПродаж,1,1);        
				ОстПогСуммаРуб = Запрос.СуммаРубПриход; // сумма сторнированного аванса
				                              
				Если ТИКнигаПродаж.КоличествоСтрок()<>0 Тогда
					Если ПроверкаСФДокумента(Запрос.КредДокумент,НайденнаяСФ,"СчетФактураВыданный") = 0 Тогда
						Продолжить; 
					ИначеЕсли (НайденнаяСФ.ДатаДок >= Запрос.Документ.ДатаДок) и (НайденнаяСФ.Итог("Сумма") < ОбщаяСуммаАванса) Тогда
						Продолжить;						
					КонецЕсли;
				КонецЕсли;

				ТИКнигаПродаж.ВыбратьСтроки();
				Пока (ТИКнигаПродаж.ПолучитьСтроку() = 1)
				и    (ОстПогСуммаРуб <> 0)
				Цикл          
					
					// в этом случае итоги в регистре "КнигаПродаж" отрицательные
					СписСуммаРуб = -Мин(0,Макс(ТИКнигаПродаж.СуммаРуб, -ОстПогСуммаРуб));
					Если СписСуммаРуб <> 0 Тогда
						КоэффСпис    = ?(ТИКнигаПродаж.СуммаРуб = 0,0,СписСуммаРуб/(ТИКнигаПродаж.СуммаРуб));
						СписСуммаНДС = Окр(ТИКнигаПродаж.СуммаНДС * КоэффСпис,2,1);
						СписСуммаНП  = Окр(ТИКнигаПродаж.СуммаНП  * КоэффСпис,2,1);
						
						Регистр.КнигаПокупок.КредДокумент   = ТИКнигаПродаж.КредДокумент;
						Регистр.КнигаПокупок.ВидДолга		= ТИКнигаПродаж.ВидДолга;
						Регистр.КнигаПокупок.СтавкаНДС		= ТИКнигаПродаж.СтавкаНДС;
						
						Регистр.КнигаПокупок.СуммаРуб		= СписСуммаРуб;
						Регистр.КнигаПокупок.СуммаНДС		= СписСуммаНДС;
						Регистр.КнигаПокупок.СуммаНП	    = СписСуммаНП;
						
						Регистр.КнигаПокупок.КодОперации    = глКО.Прочее;
						Регистр.КнигаПокупок.ДокументОплаты	= Запрос.Документ;
						
						Регистр.КнигаПокупок.ДвижениеРасходВыполнить();
						Регистр.КнигаПокупок.ДвижениеПриходВыполнить();
						
						// Уберем итоги из книги продаж
						Регистр.КнигаПродаж.КредДокумент    = ТИКнигаПродаж.КредДокумент;
						Регистр.КнигаПродаж.ВидДолга		= ТИКнигаПродаж.ВидДолга;
						Регистр.КнигаПродаж.СтавкаНДС		= ТИКнигаПродаж.СтавкаНДС;
						
						Регистр.КнигаПродаж.СуммаРуб		= СписСуммаРуб;
						Регистр.КнигаПродаж.СуммаНДС		= СписСуммаНДС;
						Регистр.КнигаПродаж.СуммаНП	        = СписСуммаНП;
						
					                                                                 
						// Надо прописать ставку НП. Важно для розничной выручки
						Если ТИКнигаПродаж.КредДокумент.Вид() = "ПКО" Тогда
							СтавкаНП = ?(ТИКнигаПродаж.КредДокумент.ОблагаетсяНП = 0,
							             ПолучитьПустоеЗначение("Справочник.СтавкиНП"),
										 ТИКнигаПродаж.КредДокумент.СтавкаНП
										);
						    Регистр.КнигаПродаж.СтавкаНП 	= СтавкаНП;
						КонецЕсли;
						Регистр.КнигаПродаж.КодОперации     = глКО.Прочее;
						Регистр.КнигаПродаж.ДокументОплаты	= Запрос.Документ;
						                                             
						Регистр.КнигаПродаж.ДвижениеПриходВыполнить();
						
						ОстПогСуммаРуб = ОстПогСуммаРуб - СписСуммаРуб;
					КонецЕсли; // есть что списывать
				КонецЦикла; // по строкам таблицы итогов КП
			КонецЦикла; // по виду долга
		КонецЦикла; // по тек документу
	КонецЦикла;// по кред. документу
	
КонецПроцедуры // ПроведениеПоСторноАвансов()


//******************************************************************************
// ПроведениеПоРегистрам()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Проведение по регистрам оперативного учета.
//
Процедура ПроведениеПоРегистрам()
	
	Перем Запрос, ТекстЗапроса;   
	Перем ДатаНачала, ПериодПо, ТекДок;
	Перем СписокНужныхКодов, СписокНужныхВидовДолга;
	Перем ВремРегистры, ВремКнигаПокупок;
	
	// Удаление движений по регистрам.
	Для Номер = 1 По Метаданные.Регистр() Цикл
		ОчиститьДвижения("Регистр."+Метаданные.Регистр(Номер).Идентификатор);
	КонецЦикла;
   	
	ВремРегистры = СоздатьОбъект("Регистры");
	ВремКнигаПокупок = ВремРегистры.КнигаПокупок;
	ВремКнигаПродаж = ВремРегистры.КнигаПродаж;
    
	// при необходимости делаем временный расчет итогов
	Если ИтогиАктуальны() = 0 Тогда
		ВремКнигаПокупок.ВременныйРасчет();
		ВремКнигаПродаж.ВременныйРасчет();
		ВремРегистры.Актуальность(1);
		ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
        
	// отбираем движения регистров (различных), выбирая те из них, которые должны 
	// быть включены в книгу покупок.
	// Регистры могут быть самые разные - Поставщики, Подотчетники, да и сама КнигаПокупок
	
	ДатаНачала = НачМесяца(ДатаДок);
	
	// определение периода построения запросов
	Если ИтогиАктуальны() = 1 Тогда
		ПериодПо = "";
	Иначе       
		ТекДок = ТекущийДокумент();
		ПериодПо = "по ТекДок";
	КонецЕсли;  
	
	ПроведениеПоСторноАвансов(ВремКнигаПродаж);
    
	// движения по покупателям, включаемые в книгу покупок
	СписокНужныхКодов = СоздатьОбъект("СписокЗначений");
	СписокНужныхКодов.ДобавитьЗначение(глКО.ОплатаПоставщику);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ОплатаПоставщикуВал);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ЗачтенАвансПоставщику);
	СписокНужныхКодов.ДобавитьЗначение(глКО.АвансовыйОтчет); // оплата поставщику подотчетником
	СписокНужныхКодов.ДобавитьЗначение(глКО.АвансовыйОтчетВал); // оплата поставщику подотчетником
	СписокНужныхКодов.ДобавитьЗначение(глКО.Прочее); // оплата прочими средствами
	СписокНужныхКодов.ДобавитьЗначение(глКО.ЗачтенВозвратПоставщику);
	СписокНужныхКодов.ДобавитьЗначение(глКО.ВводОстатков);
	
	СписокНужныхВидовДолга = СоздатьОбъект("СписокЗначений");
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаТовары);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаПродукцию);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаУслуги);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаМатериалы);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.Прочее);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаОС);
	СписокНужныхВидовДолга.ДобавитьЗначение(глВД.ДолгЗаНМА);
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала "+ ПериодПо+";
	|ФирмаРег 		= Регистр.Поставщики.Фирма;
	|ВидДолга 		= Регистр.Поставщики.ВидДолга;
	|КредДокумент 	= Регистр.Поставщики.КредДокумент;
	|ДокументОплаты = Регистр.Поставщики.ДокументОплаты;
	|ТекДокумент 	= Регистр.Поставщики.ТекущийДокумент;
	|СуммаРуб 		= Регистр.Поставщики.СуммаРуб;
	|КодОперации 	= Регистр.Поставщики.КодОперации;
	|Функция СуммаРубПриход = Приход(СуммаРуб);
	|Группировка 	КредДокумент;
	|Группировка 	ДокументОплаты;
	|Группировка 	ВидДолга;
	|Условие	(ФирмаРег 	= Фирма);
	|Условие	(КодОперации 	в СписокНужныхКодов);
	|Условие	(ВидДолга 		в СписокНужныхВидовДолга);
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;                                   
	
    НайденнаяСФ = "";            
	ТИКнигаПокупок = СоздатьОбъект("ТаблицаЗначений");
		
	Пока Запрос.Группировка("КредДокумент") = 1 Цикл
		Если ПроверкаСФДокумента(Запрос.КредДокумент,НайденнаяСФ) = 0 Тогда
			Продолжить;
		КонецЕсли;
	    Пока Запрос.Группировка("ДокументОплаты") = 1 Цикл
        	Пока Запрос.Группировка("ВидДолга") = 1 Цикл
				ВремКнигаПокупок.УстановитьЗначениеФильтра("КредДокумент",Запрос.КредДокумент,1);
				ВремКнигаПокупок.УстановитьЗначениеФильтра("ВидДолга",    Запрос.ВидДолга,    1);
				
				ВремКнигаПокупок.ВыгрузитьИтоги(ТИКнигаПокупок,1,1);      
				ОстПогСуммаРуб = Запрос.СуммаРубПриход; // сумма оплаты
				
				ТИКнигаПокупок.ВыбратьСтроки();
				Пока (ТИКнигаПокупок.ПолучитьСтроку() = 1)
				и    (ОстПогСуммаРуб <> 0)
				Цикл
					
					Если ОстПогСуммаРуб < 0 Тогда
						СписСуммаРуб = Мин(0,Макс(-ТИКнигаПокупок.СуммаРуб,ОстПогСуммаРуб));
					Иначе    
						СписСуммаРуб = Макс(0,Мин(-ТИКнигаПокупок.СуммаРуб,ОстПогСуммаРуб));
					КонецЕсли;
					Если СписСуммаРуб <> 0 Тогда
						КоэффСпис    = ?(ТИКнигаПокупок.СуммаРуб = 0,0,СписСуммаРуб/(-ТИКнигаПокупок.СуммаРуб));
						СписСуммаНДС = Окр(-ТИКнигаПокупок.СуммаНДС * КоэффСпис,2,1);
						СписСуммаНП  = Окр(-ТИКнигаПокупок.СуммаНП  * КоэффСпис,2,1);
						
						Регистр.КнигаПокупок.КредДокумент   = ТИКнигаПокупок.КредДокумент;
						Регистр.КнигаПокупок.ВидДолга		= ТИКнигаПокупок.ВидДолга;
						Регистр.КнигаПокупок.СтавкаНДС		= ТИКнигаПокупок.СтавкаНДС;
						
						Регистр.КнигаПокупок.СуммаРуб		= СписСуммаРуб;
						Регистр.КнигаПокупок.СуммаНДС		= СписСуммаНДС;
						Регистр.КнигаПокупок.СуммаНП	    = СписСуммаНП;
						
						Регистр.КнигаПокупок.КодОперации    = глКО.Прочее;
						
						// Для того чтобы док-нт Сторно попал в книгу, сделаем его документом оплаты
						Если Запрос.ТекДокумент.Вид() = "Сторно" Тогда
							Регистр.КнигаПокупок.ДокументОплаты	= Запрос.ТекДокумент;
						Иначе    
							Регистр.КнигаПокупок.ДокументОплаты	= Запрос.ДокументОплаты;
						КонецЕсли;
						
						Регистр.КнигаПокупок.ДвижениеПриходВыполнить();
						
						ОстПогСуммаРуб = ОстПогСуммаРуб - СписСуммаРуб;
					КонецЕсли; // есть что списывать
				КонецЦикла; // по строкам таблицы итогов КП
			КонецЦикла; // по виду долга
		КонецЦикла; // по тек документу
	КонецЦикла;// по кред. документу

	// движения по подотчетным лицам, включаемые в книгу покупок
	СписокНужныхКодов = СоздатьОбъект("СписокЗначений");
	СписокНужныхКодов.ДобавитьЗначение(глКО.АвансовыйОтчет); 
	СписокНужныхКодов.ДобавитьЗначение(глКО.АвансовыйОтчетВал); 
	// зачитываем сразу в момент получения авансового отчета о закупленных ТМЦ
	
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала "+ ПериодПо+";
	|ФирмаРег 		= Регистр.ПодотчетныеЛица.Фирма;
	|СуммаРуб 		= Регистр.ПодотчетныеЛица.СуммаРуб;
	|КодОперации 	= Регистр.ПодотчетныеЛица.КодОперации;
	|Функция СуммаРубРасход = Расход(СуммаРуб);
	|Группировка 	Документ;
	|Условие	(ФирмаРег 	= Фирма);
	|Условие	(КодОперации 	в СписокНужныхКодов);
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;                                   
	
    НайденнаяСФ = "";            
	ТИКнигаПокупок = СоздатьОбъект("ТаблицаЗначений");
		
	Пока Запрос.Группировка("Документ") = 1 Цикл
		ВремКнигаПокупок.УстановитьЗначениеФильтра("КредДокумент",Запрос.Документ,1);  
		ВремКнигаПокупок.УстановитьЗначениеФильтра("ВидДолга",                   ,0); // нет фильтра
		ВремКнигаПокупок.ВыгрузитьИтоги(ТИКнигаПокупок,1,1);      
		
		// если есть остаток по книге покупок - проверим выписку СФ
		Если ТИКнигаПокупок.КоличествоСтрок() > 0 Тогда
    		Если ПроверкаСФДокумента(Запрос.Документ,НайденнаяСФ) = 0 Тогда
	    		Продолжить;
		    КонецЕсли;
		КонецЕсли;
		     
		ТИКнигаПокупок.ВыбратьСтроки();
		ОстПогСуммаРуб = Запрос.СуммаРубРасход; // сумма авансового отчета

		Пока (ТИКнигаПокупок.ПолучитьСтроку() = 1)
		и    (ОстПогСуммаРуб <> 0)
		Цикл
			
			СписСуммаРуб = Макс(0,Мин(-ТИКнигаПокупок.СуммаРуб,ОстПогСуммаРуб));
			Если СписСуммаРуб <> 0 Тогда
				КоэффСпис    = ?(ТИКнигаПокупок.СуммаРуб = 0,0,СписСуммаРуб/(-ТИКнигаПокупок.СуммаРуб));
				СписСуммаНДС = Окр(-ТИКнигаПокупок.СуммаНДС * КоэффСпис,2,1);
				СписСуммаНП  = Окр(-ТИКнигаПокупок.СуммаНП  * КоэффСпис,2,1);
				
				Регистр.КнигаПокупок.КредДокумент   = ТИКнигаПокупок.КредДокумент;
				Регистр.КнигаПокупок.ВидДолга		= ТИКнигаПокупок.ВидДолга;
				Регистр.КнигаПокупок.СтавкаНДС		= ТИКнигаПокупок.СтавкаНДС;
				
				Регистр.КнигаПокупок.СуммаРуб		= СписСуммаРуб;
				Регистр.КнигаПокупок.СуммаНДС		= СписСуммаНДС;
				Регистр.КнигаПокупок.СуммаНП	    = СписСуммаНП;
				
				Регистр.КнигаПокупок.КодОперации    = глКО.Прочее;
				Регистр.КнигаПокупок.ДокументОплаты	= Запрос.Документ;
				
				Регистр.КнигаПокупок.ДвижениеПриходВыполнить();
				
				ОстПогСуммаРуб = ОстПогСуммаРуб - СписСуммаРуб;
			КонецЕсли; // есть что списывать
		КонецЦикла; // по строкам таблицы итогов КП
	КонецЦикла; // по тек документу
	
КонецПроцедуры // ПроведениеПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПроведения(ВидыДвижений)
	
	// Проверка заполненности обязательных реквизитов.
	Если глВсеРеквизитыДокументаЗаполнены(Контекст,"Фирма")=0 Тогда
		Возврат;
	КонецЕсли;  
	
	// проверим, не был ли уже проведен документ за этот месяц по этой же фирме
	ДокКП = СоздатьОбъект("Документ.КнигаПокупок");
	ДокКП.ВыбратьДокументы(НачМесяца(ДатаДок),КонМесяца(ДатаДок));
	Пока ДокКП.ПолучитьДокумент()=1 Цикл
	    Если (ДокКП.ТекущийДокумент()<>ТекущийДокумент()) и 
			 (ДокКП.Проведен()=1) и
			 (ДокКП.Фирма = Фирма) Тогда    
			 	
	        глНеПроводить(Контекст,"За " + Формат(ДатаДок, "Д ММММГГГГ")+
			"по фирме """+Фирма.Наименование+
			""" формирование книги покупок уже проведено ("+ДокКП.ТекущийДокумент()+")!");
			Возврат;
			
	    КонецЕсли;
	КонецЦикла;       
	                     
	// Проведение по регистрам оперативного учета.	
	Если (ПустоеЗначение(ВидыДвижений) = 1) ИЛИ (Найти(ВидыДвижений, "Регистр") <> 0) Тогда
		ПроведениеПоРегистрам();
		
		Если СтатусВозврата() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	глПриПроведении(Контекст, ВидыДвижений);
	
КонецПроцедуры //ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ДокПодч = СоздатьОбъект("Документ");