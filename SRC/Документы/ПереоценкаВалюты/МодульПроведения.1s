////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
// Текущий документ - для запросов.
Перем ТекущийДокумент;
// Валютные виды долга.
Перем ВидыДолгаВал;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ПровестиПоРегистру(Рег, Запрос, ФлагРуб, ФлагУпр)
//
// Параметры:
//  Рег     - "Регистр" - регистр, который переоценивается.
//  Запрос  - "Запрос" - запрос, выполненный по регистру.
//  ФлагРуб - 0 или 1 - переоценивать или нет бух. ресурс регистра.
//  ФлагУпр - 0 или 1 - переоценивать или нет упр. ресурс регистра.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Рассчитывает курсовые разницы по руб. и упр. ресурсам и выполняет движение по регистру.
//
Процедура ПровестиПоРегистру(Рег, Запрос, ФлагРуб, ФлагУпр)
	
	Если Запрос.пВалюта.Выбран() = 0 Тогда
        Возврат;
	КонецЕсли;
	
	СуммаУпр = 0; СуммаРуб = 0;
	
	// Курсовая разница в бух. валюте.
	Если ФлагРуб <> 0 Тогда
		СуммаРуб = глПересчет(Запрос.фСуммаВал, Запрос.пВалюта, ДатаДок, глРубли, ДатаДок);
		СуммаРуб = Окр(СуммаРуб-Запрос.фСуммаРуб, 2);
	КонецЕсли;
	
	// Курсовая разница в упр. валюте.
	Если ФлагУпр <> 0 Тогда
		СуммаУпр = глПересчет(Запрос.фСуммаВал, Запрос.пВалюта, ДатаДок, глДоллары, ДатаДок);
		СуммаУпр = Окр(СуммаУпр-Запрос.фСуммаУпр, 2);
	КонецЕсли;
	
	// Направление движения определяется знаком разницы в бух. валюте.
	Если      0 < СуммаРуб Тогда
		Рег.СуммаРуб   = СуммаРуб;
		Рег.СуммаУпр   = СуммаУпр;
		Рег.ДвижениеПриходВыполнить();
		
	ИначеЕсли СуммаРуб < 0 Тогда
		Рег.СуммаРуб   = -СуммаРуб;
		Рег.СуммаУпр   = -СуммаУпр;
		Рег.ДвижениеРасходВыполнить();
	
	// Если курсовая разница в бух. валюте отсутствует,
	// направление движения определяется знаком разницы в упр. валюте.
	ИначеЕсли 0 < СуммаУпр Тогда
		Рег.СуммаРуб   = СуммаРуб;
		Рег.СуммаУпр   = СуммаУпр;
		Рег.ДвижениеПриходВыполнить();
		
	ИначеЕсли СуммаУпр < 0 Тогда
		Рег.СуммаРуб   = -СуммаРуб;
		Рег.СуммаУпр   = -СуммаУпр;
		Рег.ДвижениеРасходВыполнить();
	
	КонецЕсли;
	
КонецПроцедуры // ПровестиПоРегистру()

//******************************************************************************
// ПровестиПоКассе()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Расчет курсовых разниц по регистру "Касса".
//
Процедура ПровестиПоКассе()
	
	// Текст запроса.
	Текст = "
	|пФирма    = Регистр.Касса.Фирма;
	|пКасса    = Регистр.Касса.Касса;
	|пВалюта   = Регистр.Касса.Валюта;
	|пСуммаВал = Регистр.Касса.СуммаВал;
	|
	|Функция фСуммаВал = КонОст(пСуммаВал);
	|
	|Условие(пФирма = Фирма);
	|
	|Группировка пКасса;
	|Группировка пВалюта;
	|
	|Без итогов;
	|";
	
	// Период запроса.
	Если СравнитьТА() < 0 Тогда
		Текст = "
		|Период с ТекущийДокумент по ТекущийДокумент;
		|"+Текст;
	КонецЕсли;
	
	// Остаток в бух. валюте.
	Если КассаРуб <> 0 Тогда
		Текст = Текст+"
		|пСуммаРуб = Регистр.Касса.СуммаРуб;
		|Функция фСуммаРуб = КонОст(пСуммаРуб);
		|";
	КонецЕсли;
	
	// Остаток в упр. валюте.
	Если КассаУпр <> 0 Тогда
		Текст = Текст+"
		|пСуммаУпр = Регистр.Касса.СуммаУпр;
		|Функция фСуммаУпр = КонОст(пСуммаУпр);
		|";
	КонецЕсли;
	
	// Выполнение запроса.
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(Текст) = 0 Тогда
        глНеПроводить(Контекст,"Не выполнен запрос по регистру!");
		Возврат;
	КонецЕсли;
	
	// Формирование движений.
	Регистр.Касса.Фирма                   = Фирма;
	Регистр.Касса.КодОперации             = глКО.КурсоваяРазница;
	Регистр.Касса.ДвижениеДенежныхСредств = ДвижениеДенежныхСредств;
	
	// Обработка запроса.
	Пока Запрос.Группировка() = 1 Цикл
		Регистр.Касса.Касса               = Запрос.пКасса;
		Регистр.Касса.Валюта              = Запрос.пВалюта;
			
		ПровестиПоРегистру(Регистр.Касса, Запрос, КассаРуб, КассаУпр);
	КонецЦикла;
	
КонецПроцедуры // ПровестиПоКассе()

//******************************************************************************
// ПровестиПоБанку()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Расчет курсовых разниц по регистру "Банк".
//
Процедура ПровестиПоБанку()
	
	// Текст запроса.
	Текст = " 
	|пФирма    = Регистр.Банк.Фирма;
	|пСчет     = Регистр.Банк.БанковскийСчет;
	|пВалюта   = Регистр.Банк.БанковскийСчет.ВалютаСчета;
	|пСуммаВал = Регистр.Банк.СуммаВал;
	|
	|Функция фСуммаВал = КонОст(пСуммаВал);
	|
	|Условие(пФирма = Фирма);
	|
	|Группировка пСчет;
	|
	|Без итогов;
	|";
	
	// Период запроса.
	Если СравнитьТА() < 0 Тогда
		Текст = "
		|Период с ТекущийДокумент по ТекущийДокумент;
		|"+Текст;
	КонецЕсли;
	
	// Остаток в бух. валюте.
	Если БанкРуб <> 0 Тогда
		Текст = Текст+"
		|пСуммаРуб = Регистр.Банк.СуммаРуб;
		|Функция фСуммаРуб = КонОст(пСуммаРуб);
		|";
	КонецЕсли;
	
	// Остаток в упр. валюте.
	Если БанкУпр <> 0 Тогда
		Текст = Текст+"
		|пСуммаУпр = Регистр.Банк.СуммаУпр;
		|Функция фСуммаУпр = КонОст(пСуммаУпр);
		|";
	КонецЕсли;
	
	// Выполнение запроса.
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(Текст) = 0 Тогда
        глНеПроводить(Контекст,"Не выполнен запрос по регистру!");
		Возврат;
	КонецЕсли;
	
	// Формирование движений.
	Регистр.Банк.Фирма                   = Фирма;
	Регистр.Банк.КодОперации             = глКО.КурсоваяРазница;
	Регистр.Банк.ДвижениеДенежныхСредств = ДвижениеДенежныхСредств;
	
	// Обработка запроса.
	Пока Запрос.Группировка() = 1 Цикл
		Регистр.Банк.БанковскийСчет      = Запрос.пСчет;
		
		ПровестиПоРегистру(Регистр.Банк, Запрос, БанкРуб, БанкУпр);		
	КонецЦикла;
	
КонецПроцедуры // ПровестиПоБанку()

//******************************************************************************
// ПровестиПоПодотчетнымЛицам()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Расчет курсовых разниц по регистру "ПодотчетныеЛица".
//
Процедура ПровестиПоПодотчетнымЛицам()
	
	// Текст запроса.
	Текст = " 
	|пФирма    = Регистр.ПодотчетныеЛица.Фирма;
	|пФизЛицо  = Регистр.ПодотчетныеЛица.ФизЛицо;
	|пВалюта   = Регистр.ПодотчетныеЛица.Валюта;
	|пКредДок  = Регистр.ПодотчетныеЛица.КредДокумент;
	|пСуммаВал = Регистр.ПодотчетныеЛица.СуммаВал;
	|
	|Функция фСуммаВал = КонОст(пСуммаВал);
	|
	|Условие(пФирма = Фирма);
	|
	|Группировка пФизЛицо;
	|Группировка пВалюта;
	|Группировка пКредДок;
	|
	|Без итогов;
	|";
	
	// Период запроса.
	Если СравнитьТА() < 0 Тогда
		Текст = "
		|Период с ТекущийДокумент по ТекущийДокумент;
		|"+Текст;
	КонецЕсли;
	
	// Остаток в бух. валюте.
	Если ПодотчетныеЛицаРуб <> 0 Тогда
		Текст = Текст+"
		|пСуммаРуб = Регистр.ПодотчетныеЛица.СуммаРуб;
		|Функция фСуммаРуб = КонОст(пСуммаРуб);
		|";
	КонецЕсли;
	
	// Остаток в упр. валюте.
	Если ПодотчетныеЛицаУпр <> 0 Тогда
		Текст = Текст+"
		|пСуммаУпр = Регистр.ПодотчетныеЛица.СуммаУпр;
		|Функция фСуммаУпр = КонОст(пСуммаУпр);
		|";
	КонецЕсли;
	
	// Выполнение запроса.
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(Текст) = 0 Тогда
        глНеПроводить(Контекст,"Не выполнен запрос по регистру!");
		Возврат;
	КонецЕсли;
	
	// Формирование движений.
	Регистр.ПодотчетныеЛица.Фирма            = Фирма;
	Регистр.ПодотчетныеЛица.КодОперации      = глКО.КурсоваяРазница;
	
	// Обработка запроса.
	Пока Запрос.Группировка() = 1 Цикл
		Регистр.ПодотчетныеЛица.ФизЛицо      = Запрос.пФизЛицо;
		Регистр.ПодотчетныеЛица.Валюта       = Запрос.пВалюта;
		Регистр.ПодотчетныеЛица.КредДокумент = Запрос.пКредДок;
		
		ПровестиПоРегистру(Регистр.ПодотчетныеЛица, Запрос, ПодотчетныеЛицаРуб, ПодотчетныеЛицаУпр);		
	КонецЦикла;
	
КонецПроцедуры // ПровестиПоПодотчетнымЛицам()

//******************************************************************************
// ПровестиПоПоставщикам()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Расчет курсовых разниц по регистру "Поставщики".
//
Процедура ПровестиПоПоставщикам()
	
	// Текст запроса.
	Текст = "
	|пФирма    = Регистр.Поставщики.Фирма;
	|пДоговор  = Регистр.Поставщики.Договор;
	|пВалюта   = Регистр.Поставщики.Договор.ВалютаВзаиморасчетов;
	|пВидДолга = Регистр.Поставщики.ВидДолга;
	|пКредДок  = Регистр.Поставщики.КредДокумент;
	|пСуммаВал = Регистр.Поставщики.СуммаВал;
	|
	|Функция фСуммаВал = КонОст(пСуммаВал);
	|
	|Условие(пФирма = Фирма);
	|
	|Группировка пДоговор;
	|Группировка пВидДолга;
	|Группировка пКредДок;
	|
	|Без итогов;
	|";
	
	// Период запроса.
	Если СравнитьТА() < 0 Тогда
		Текст = "
		|Период с ТекущийДокумент по ТекущийДокумент;
		|"+Текст;
	КонецЕсли;
	
	// Переоцениваемые виды долга.
	Если ПоставщикиРезидентыУпр = 0 Тогда
		Текст = Текст+"
		|Условие(пВидДолга в ВидыДолгаВал);
		|";
	ИначеЕсли (ПоставщикиРуб = 0) И (ПоставщикиУпр = 0) Тогда
		Текст = Текст+"
		|Условие(НЕ(пВидДолга в ВидыДолгаВал));
		|";
	КонецЕсли;

	// Остаток в бух. валюте.
	Если ПоставщикиРуб <> 0 Тогда
		Текст = Текст+"
		|пСуммаРуб = Регистр.Поставщики.СуммаРуб;
		|Функция фСуммаРуб = КонОст(пСуммаРуб);
		|";
	КонецЕсли;
	
	// Остаток в упр. валюте.
	Если (ПоставщикиУпр <> 0) ИЛИ (ПоставщикиРезидентыУпр <> 0) Тогда
		Текст = Текст+"
		|пСуммаУпр = Регистр.Поставщики.СуммаУпр;
		|Функция фСуммаУпр = КонОст(пСуммаУпр);
		|";
	КонецЕсли;
	
	// Выполнение запроса.
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(Текст) = 0 Тогда
        глНеПроводить(Контекст,"Не выполнен запрос по регистру!");
		Возврат;
	КонецЕсли;
	
	// Формирование движений.
	Регистр.Поставщики.Фирма            = Фирма;
	Регистр.Поставщики.КодОперации      = глКО.КурсоваяРазница;
	
	// Обработка запроса.
	Пока Запрос.Группировка() = 1 Цикл
		Регистр.Поставщики.Договор      = Запрос.пДоговор;
		Регистр.Поставщики.ВидДолга     = Запрос.пВидДолга;
		Регистр.Поставщики.КредДокумент = Запрос.пКредДок;
		
		Если ВидыДолгаВал.Принадлежит(Запрос.пВидДолга) = 1 Тогда
			// Взаиморасчеты с нерезидентами.
			ПровестиПоРегистру(Регистр.Поставщики, Запрос, ПоставщикиРуб, ПоставщикиУпр);
		Иначе
			// Взаиморасчеты с резидентами.
			ПровестиПоРегистру(Регистр.Поставщики, Запрос, 0, ПоставщикиРезидентыУпр);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПровестиПоПоставщикам()

//******************************************************************************
// ПровестиПоПокупателям()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Расчет курсовых разниц по регистру "Покупатели".
//
Процедура ПровестиПоПокупателям()
	
	// Текст запроса.
	Текст = "
	|пФирма    = Регистр.Покупатели.Фирма;
	|пДоговор  = Регистр.Покупатели.Договор;
	|пВалюта   = Регистр.Покупатели.Договор.ВалютаВзаиморасчетов;
	|пСтавкаНП = Регистр.Покупатели.СтавкаНП;
	|пВидДолга = Регистр.Покупатели.ВидДолга;
	|пКредДок  = Регистр.Покупатели.КредДокумент;
	|пСуммаВал = Регистр.Покупатели.СуммаВал;
	|
	|Функция фСуммаВал = КонОст(пСуммаВал);
	|
	|Условие(пФирма = Фирма);
	|
	|Группировка пДоговор;
	|Группировка пСтавкаНП;
	|Группировка пВидДолга;
	|Группировка пКредДок;
	|
	|Без итогов;
	|";
	
	// Период запроса.
	Если СравнитьТА() < 0 Тогда
		Текст = "
		|Период с ТекущийДокумент по ТекущийДокумент;
		|"+Текст;
	КонецЕсли;
	
	// Переоцениваемые виды долга.
	Если ПокупателиРезидентыУпр = 0 Тогда
		Текст = Текст+"
		|Условие(пВидДолга в ВидыДолгаВал);
		|";
	ИначеЕсли (ПокупателиРуб = 0) И (ПокупателиУпр = 0) Тогда
		Текст = Текст+"
		|Условие(НЕ(пВидДолга в ВидыДолгаВал));
		|";
	КонецЕсли;

	// Остаток в бух. валюте.
	Если ПокупателиРуб <> 0 Тогда
		Текст = Текст+"
		|пСуммаРуб = Регистр.Покупатели.СуммаРуб;
		|Функция фСуммаРуб = КонОст(пСуммаРуб);
		|";
	КонецЕсли;
	
	// Остаток в упр. валюте.
	Если (ПокупателиУпр <> 0) ИЛИ (ПокупателиРезидентыУпр <> 0) Тогда
		Текст = Текст+"
		|пСуммаУпр = Регистр.Покупатели.СуммаУпр;
		|Функция фСуммаУпр = КонОст(пСуммаУпр);
		|";
	КонецЕсли;
	
	// Выполнение запроса.
	Запрос = СоздатьОбъект("Запрос");
	Если Запрос.Выполнить(Текст) = 0 Тогда
        глНеПроводить(Контекст,"Не выполнен запрос по регистру!");
		Возврат;
	КонецЕсли;
	
	// Формирование движений.
	Регистр.Покупатели.Фирма             = Фирма;
	Регистр.Покупатели.КодОперации       = глКО.КурсоваяРазница;
	
	// Обработка запроса.
	Пока Запрос.Группировка() = 1 Цикл
		Регистр.Покупатели.Договор       = Запрос.пДоговор;
		Регистр.Покупатели.СтавкаНП      = Запрос.пСтавкаНП;
		Регистр.Покупатели.ВидДолга      = Запрос.пВидДолга;
		Регистр.Покупатели.КредДокумент  = Запрос.пКредДок;
		
		// СуммаНП не переоценивается
		// Себестоимость не переоценивается
			
		Если ВидыДолгаВал.Принадлежит(Запрос.пВидДолга) = 1 Тогда
			// Взаиморасчеты с нерезидентами.
			ПровестиПоРегистру(Регистр.Покупатели, Запрос, ПокупателиРуб, ПокупателиУпр);
		Иначе
			// Взаиморасчеты с резидентами.
			ПровестиПоРегистру(Регистр.Покупатели, Запрос, 0, ПокупателиРезидентыУпр);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПровестиПоПокупателям()

//******************************************************************************
// ПроведениеПоРегистрам()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Проведение по регистрам оперативного учета.
//
Процедура ПроведениеПоРегистрам()
	
	// Удаление движений по регистрам.
	Для Номер = 1 По Метаданные.Регистр() Цикл
		ОчиститьДвижения("Регистр."+Метаданные.Регистр(Номер).Идентификатор);
	КонецЦикла;
	
	Если (КассаРуб <> 0) ИЛИ (КассаУпр <> 0) Тогда
		ПровестиПоКассе();
	КонецЕсли;
	
	Если (БанкРуб <> 0) ИЛИ (БанкУпр <> 0) Тогда
		ПровестиПоБанку();
	КонецЕсли;
	
	Если (ПодотчетныеЛицаРуб <> 0) ИЛИ (ПодотчетныеЛицаУпр <> 0) Тогда
		ПровестиПоПодотчетнымЛицам();
	КонецЕсли;
	
	Если (ПоставщикиРуб <> 0) ИЛИ (ПоставщикиУпр <> 0) ИЛИ (ПоставщикиРезидентыУпр <> 0) Тогда
		ПровестиПоПоставщикам();
	КонецЕсли;
	
	Если (ПокупателиРуб <> 0) ИЛИ (ПокупателиУпр <> 0) ИЛИ (ПокупателиРезидентыУпр <> 0) Тогда
		ПровестиПоПокупателям();
	КонецЕсли;
	
КонецПроцедуры // ПроведениеПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПроведения(ВидыДвижений)
	
	// Проверка заполненности обязательных реквизитов.
	Если глВсеРеквизитыДокументаЗаполнены(Контекст,"Фирма")=0 Тогда
		Возврат;
	КонецЕсли;  
	
	// Проведение по регистрам оперативного учета.	
	Если (ПустоеЗначение(ВидыДвижений) = 1) ИЛИ (Найти(ВидыДвижений, "Регистр") <> 0) Тогда
		ПроведениеПоРегистрам();
		
		Если СтатусВозврата() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	глПриПроведении(Контекст, ВидыДвижений);
	
КонецПроцедуры //ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
// Текущий документ - для запросов.
ТекущийДокумент = ТекущийДокумент();
	
// Валютные виды долга.
ВидыДолгаВал = СоздатьОбъект("СписокЗначений");
ВидыДолгаВал.ДобавитьЗначение(глВД.АвансВал);
ВидыДолгаВал.ДобавитьЗначение(глВД.ДолгВал);
ВидыДолгаВал.ДобавитьЗначение(глВД.ПрочееВал);
