////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ПроведениеПоРегистрам(ЗнакДвижения)
//
// Параметры:
//  ЗнакДвижения - число, определяющее движения по документу:
//               -1 - расход по регистрам Поставщики и КнигаПокупок  
//                    (Остатки долга Поставщику)
//                0 - расход по регистру КнигаПокупок                
//                    (Остатки книги покупок при свертке ИБ)
//               +1 - приход по регистру Поставщики 
//                    (Остатки долга поставщика нам)                                               
//                    
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Проведение по регистрам оперативного учета.
//
Процедура ПроведениеПоРегистрам(ЗнакДвижения)
	
	// Удаление движений по регистрам.
	Для Номер = 1 По Метаданные.Регистр() Цикл
		ОчиститьДвижения("Регистр."+Метаданные.Регистр(Номер).Идентификатор);
	КонецЦикла;

	РегВзаим = Регистр.Поставщики;
	РегКнига = Регистр.КнигаПокупок;
	
	РегВзаим.Фирма        = Фирма;
	РегВзаим.Договор      = Договор;
	
	РегВзаим.КодОперации  = Перечисление.КодыОпераций.ВводОстатков;
	РегКнига.КодОперации  = Перечисление.КодыОпераций.ВводОстатков;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		
		Если глВсеРеквизитыДокументаЗаполнены(Контекст,"ВидДолга")=0 Тогда
			Возврат;
		КонецЕсли;
		
		РегВзаим.КредДокумент = ?(КредДокумент.Выбран()=1,КредДокумент,ТекущийДокумент());
		РегВзаим.ВидДолга  = ВидДолга;
		
		РегВзаим.СуммаВал = СуммаВал;
		РегВзаим.СуммаУпр = СуммаУпр;
		РегВзаим.СуммаРуб = СуммаРуб;
		
		РегКнига.КредДокумент = ?(КредДокумент.Выбран()=1,КредДокумент,ТекущийДокумент());
		РегКнига.ВидДолга  = ВидДолга;
		РегКнига.СтавкаНДС = СтавкаНДС;
			
		РегКнига.СуммаРуб = СуммаРуб;
		РегКнига.СуммаНДС = СуммаНДС;
		РегКнига.СуммаНП  = СуммаНП;
				
		Если ЗнакДвижения = -1 Тогда

			РегВзаим.ДвижениеРасходВыполнить();
			
			Если (Найти(",ДолгЗаТоварыПринятые,ДолгВал,ПрочееВал,Аванс,АвансВал,", "," + ВидДолга.Идентификатор() + ",") = 0)
			   и (ФлагСвертки = 0) Тогда
				РегКнига.ДвижениеРасходВыполнить();
			КонецЕсли;
			
		ИначеЕсли ЗнакДвижения = +1 Тогда

			РегВзаим.ДвижениеПриходВыполнить();

		ИначеЕсли ЗнакДвижения = 0 Тогда

			РегКнига.ДвижениеРасходВыполнить();

		КонецЕсли;
		
	КонецЦикла;
   	
КонецПроцедуры // ПроведениеПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПроведения(ВидыДвижений)   
	
	Если      ВидОперации = Перечисление.ВидыОперацийПоВводуОстатков.ОстаткиДолгаПоставщику Тогда
		ЗнакДвижения = -1;
	ИначеЕсли ВидОперации = Перечисление.ВидыОперацийПоВводуОстатков.ОстаткиДолгаПоставщикаНам Тогда
		ЗнакДвижения = +1;
	ИначеЕсли ВидОперации = Перечисление.ВидыОперацийПоВводуОстатков.ОстаткиКнигиПокупок Тогда
		ЗнакДвижения = 0;
	КонецЕсли;

	// Проверка заполненности обязательных реквизитов.
	СтрокаРеквизитов = "";
	
	Если ФлагСвертки = 0 Тогда
		СтрокаРеквизитов = "Фирма,Контрагент,Договор";
	ИначеЕсли ЗнакДвижения <> 0 Тогда
		СтрокаРеквизитов = "Фирма,Контрагент";
	Иначе
		СтрокаРеквизитов = "Фирма";
	КонецЕсли;

	Если глВсеРеквизитыДокументаЗаполнены(Контекст,СтрокаРеквизитов)=0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проведение по регистрам оперативного учета.	
	Если (ПустоеЗначение(ВидыДвижений) = 1) ИЛИ (Найти(ВидыДвижений, "Регистр") <> 0) Тогда
		ПроведениеПоРегистрам(ЗнакДвижения);
		
		Если СтатусВозврата() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	глПриПроведении(Контекст, ВидыДвижений);
	
КонецПроцедуры //ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
