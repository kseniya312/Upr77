////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// ВидДолгаПоСтатусу(СтатусПартии)
//
// Параметры:
//  СтатусПартии - значение перечисления статуса партии
//
// Возвращаемое значение:
//  Значение перечисления видов долга.
//
// Описание:
//  Возвращает вид долга, соответствующий статусу партии.
//
Функция ВидДолгаПоСтатусу(СтатусПартии)
		
	Если (СтатусПартии	= глСП.Т_Купленный) или
		 (СтатусПартии	= глСП.Т_Тара) Тогда
		Возврат глВД.ДолгЗаТовары;
		
	ИначеЕсли СтатусПартии	= глСП.Т_Принятый Тогда
		Возврат глВД.ДолгЗаТоварыПринятые;
		
	ИначеЕсли СтатусПартии	= глСП.Т_ВРознице Тогда
		Возврат глВД.ДолгЗаТовары;
		
	ИначеЕсли СтатусПартии	= глСП.Продукция Тогда
		Возврат глВД.ДолгЗаПродукцию;
		
	ИначеЕсли Лев(СтатусПартии.Идентификатор(),2)	= "М_" Тогда
		Возврат глВД.ДолгЗаМатериалы;
		
	Иначе     
		глСообщениеПроведения("Неизвестный статус партии: "+СтатусПартии, ТекущийДокумент());
	КонецЕсли;	
	
КонецФункции // ВидДолгаПоСтатусу()

//******************************************************************************
// ПроведениеПоРегистрам()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Проведение по регистрам оперативного учета.
//
Процедура ПроведениеПоРегистрам()
	
	Перем ТаблСписПартий, ТаблСписПартийКом;
	Перем ВремРегистры;
	Перем ВремПартииОтданные, ВремПокупатели;
	
	//++Ерошенко 06.06.2007
	// добавлено по просьбе Ивановой по образцу Поступления ТМЦ
	Если ПустоеЗначение(Принят_к_учету)=1 Тогда
		НеПроводитьДокумент();
		сообщить("Документ не принят к учёту, движения по нему сформированы не будут!","!");
		возврат;
	КонецЕсли;
	//--
	
	// Удаление движений по регистрам.
	Для Номер = 1 По Метаданные.Регистр() Цикл
		ОчиститьДвижения("Регистр."+Метаданные.Регистр(Номер).Идентификатор);
	КонецЦикла;

	ТаблСписПартий=СоздатьОбъект("ТаблицаЗначений");
	ТаблицаУслуг = СоздатьОбъект("ТаблицаЗначений");  
	
	Если глЕстьРеквизитШапки("ФлагСвертки", ДокОснование.Вид())=1 Тогда
		Если (ДокОснование.ФлагСвертки = 1)
		или (ДокОснование.Комментарий = "Для ввода остатков") Тогда	
			КурсРубля = глКурсДляВалюты(глРубли, ДатаДок);
			ВыбратьСтроки();
			Пока ПолучитьСтроку()=1 Цикл
				Если (ПустоеЗначение(ВидСтатус) = 1) Тогда
					НазначитьТип("ВидСтатус", "Перечисление.ВидыТМЦ");                           
					ВидСтатус     = Перечисление.ВидыТМЦ.Товар;
				КонецЕсли;
				
				Если (Номенклатура.ВидНоменклатуры <> Перечисление.ВидыНоменклатуры.Услуга) 
				и    (Себестоимость = 0)
				Тогда
					
					Себестоимость = глПересчет(Сумма 
					                           - ?(СуммаВклНДС = 1,СуммаНДС,0)
											   - ?(СуммаВклНП  = 1,СуммаНП, 0),
											   Валюта, Курс,
											   глРубли,КурсРубля);
		            СебестоимостьЦена = ?(Количество = 0, 0, Себестоимость/Количество);
					СебестоимостьНДС  = СуммаНДС;
	
				КонецЕсли;
				// Т.К. основания нет, если не выбрана конкретная партия, надо создать новую по возврату
				Если ПустоеЗначение(Партия) = 1 Тогда
				    
					Если ПустоеЗначение(Номенклатура) = 1 Тогда
				        СтатусВозврата(0);
						Сообщить("В строке "  + НомерСтроки + " не выбрана номеклатура");
						Возврат;
					КонецЕсли;
					
					СпрПартии = СоздатьОбъект("Справочник.Партии");
					СпрПартии.ИспользоватьВладельца(Номенклатура);
					СпрПартии.Новый();
					СпрПартии.Записать();           
					Партия = СпрПартии.ТекущийЭлемент();	
				КонецЕсли;                                 
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	ТаблицаТМЦ   = глПодготовитьТаблицуДокумента(Контекст,1,ТаблицаУслуг);
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	
	СписокПараметров.ДобавитьЗначение(ТекущийДокумент(),"ТекДок");
	СписокПараметров.ДобавитьЗначение(Склад,		"Склад");
	СписокПараметров.ДобавитьЗначение(Фирма,		"Фирма");
	СписокПараметров.ДобавитьЗначение(Фирма,		"ФирмаДляОстатковТМЦ");
	СписокПараметров.ДобавитьЗначение(Контрагент,	"Контрагент");
	СписокПараметров.ДобавитьЗначение(Договор,		"Договор");
	СписокПараметров.ДобавитьЗначение(1,			"ЗнакДвижения");
	
	СписокПараметров.ДобавитьЗначение(КодОперации,	"КодОперации");
	
	ВремРегистры = СоздатьОбъект("Регистры");
	Если КодОперации = Перечисление.КодыОпераций.ВозвратОтРеализатора Тогда
		ВремПартииОтданные = ВремРегистры.ПартииОтданные;
		глФильтрПартийТМЦ(Контекст,ТаблицаТМЦ, СписокПараметров, ВремПартииОтданные);
	Иначе
		ВремПокупатели = ВремРегистры.Покупатели;
		глФильтрДолгов(Контекст, Договор, СписокПараметров, ВремПокупатели);
	КонецЕсли;                        
	
	Если ИтогиАктуальны() = 0 Тогда   
		ВремРегистры.Актуальность(1);
		ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
	
	//получим остатки долгов
	
	глПриходОстатковТМЦ	(Контекст,ТаблицаТМЦ,СписокПараметров);
	
	Если (КодОперации=Перечисление.КодыОпераций.ВозвратОтПокупателя)
	 или (КодОперации=Перечисление.КодыОпераций.ВозвратОтПокупателяЕНВД) Тогда
		
		глОприходованиеПартийТМЦВозврат(Контекст,ТаблицаТМЦ,СписокПараметров,ТаблСписПартий,ТаблСписПартийКом);
		                             
		// обороты продаж - в минус
		ТаблСписПартий.ВыбратьСтроки();
		Пока ТаблСписПартий.ПолучитьСтроку()=1 Цикл
			глДвижениеОборотовПродаж(Контекст,
									Фирма,
									Контрагент,
									ТаблСписПартий.Партия.Поставщик,
									ТаблСписПартий.Номенклатура,
									-ТаблСписПартий.Количество,
									-ТаблСписПартий.СуммаУпр,
									-ТаблСписПартий.СуммаПродУпр,
									Объект);
		КонецЦикла;
		
		ТаблСписПартий.Свернуть("СтатусПартии,СтавкаНП,СтавкаНДС,ДоговорКомитента","СуммаПрод,СуммаПродУпр,СуммаПродРуб,НДСПрод,НППрод,СуммаБезНДС");
		ТаблСписПартий.НоваяКолонка("КредДокумент");
		ТаблСписПартий.НоваяКолонка("ВидДолга");
		ТаблСписПартий.НоваяКолонка("Сумма");
		ТаблСписПартий.НоваяКолонка("СуммаУпр");
		ТаблСписПартий.НоваяКолонка("СуммаРуб");
		ТаблСписПартий.НоваяКолонка("СуммаНДС");
		ТаблСписПартий.НоваяКолонка("СуммаНП");
		ТаблСписПартий.НоваяКолонка("Себестоимость");
		ТаблСписПартий.НоваяКолонка("ОблагаетсяНП");
		
		ТаблСписПартий.ВыбратьСтроки();
		Пока ТаблСписПартий.ПолучитьСтроку()=1 Цикл
			ТаблСписПартий.КредДокумент = ТекущийДокумент();
			ТаблСписПартий.ВидДолга		= ВидДолгаПоСтатусу(ТаблСписПартий.СтатусПартии);
			ТаблСписПартий.Себестоимость= ТаблСписПартий.СуммаБезНДС;
			ТаблСписПартий.СуммаНДС		= ТаблСписПартий.НДСПрод;
			ТаблСписПартий.СуммаНП		= ТаблСписПартий.НППрод;
			ТаблСписПартий.Сумма		= ТаблСписПартий.СуммаПрод;
			ТаблСписПартий.СуммаУпр		= ТаблСписПартий.СуммаПродУпр;
			ТаблСписПартий.СуммаРуб		= ТаблСписПартий.СуммаПродРуб;
			ТаблСписПартий.ОблагаетсяНП = УчитыватьНП;
		КонецЦикла;                                                                       
		                                                            
		СписокПараметров.Установить("ЗнакДвижения", - 1); // идет уменьшение долга
		глДвижениеДолгов(Контекст, Договор, ТаблСписПартий,СписокПараметров,ВремПокупатели);
		                              
		// запомним, что мы вернули принятые на комиссию товары...
		ТаблСписПартийКом.ВыбратьСтроки();
		Пока ТаблСписПартийКом.ПолучитьСтроку()=1 Цикл                    
			Регистр.РеализованныйТовар.Фирма 			= ТаблСписПартийКом.Фирма;
			Регистр.РеализованныйТовар.Договор 			= ТаблСписПартийКом.Партия.ДоговорПоставщика;
			Регистр.РеализованныйТовар.Номенклатура 	= ТаблСписПартийКом.Номенклатура;
			Регистр.РеализованныйТовар.Партия 			= ТаблСписПартийКом.Партия;
			Регистр.РеализованныйТовар.ДокПродажи		= ТекущийДокумент();
			Регистр.РеализованныйТовар.Количество 		= - ТаблСписПартийКом.Количество;
			Регистр.РеализованныйТовар.ПродСтоимость 	= - (ТаблСписПартийКом.СуммаПродРуб - ТаблСписПартийКом.НППрод);
			Регистр.РеализованныйТовар.ДвижениеПриходВыполнить();
		КонецЦикла;      
		
		// реализация услуг   
	глДвижениеПартийУслуг(Контекст, ТаблицаУслуг, СписокПараметров, 1);
		
		// сначала уменьшим обороты продаж по услугам
		ТаблицаУслуг.ВыбратьСтроки();
		Пока ТаблицаУслуг.ПолучитьСтроку()=1 Цикл
			глДвижениеОборотовПродаж(Контекст,
									Фирма,
									Контрагент,
									"",
									ТаблицаУслуг.Номенклатура,
									-ТаблицаУслуг.Количество,
									0,
									-ТаблицаУслуг.СуммаУпр,
									Объект);
		КонецЦикла;
		
		// теперь задолженность покупателя за отпущенные услуги
		СписокПараметров.Установить("ЗнакДвижения",-1); // уменьшился долг клиента
		
		ТаблицаУслуг.Свернуть("СтавкаНДС,СтавкаНП","Сумма,СуммаУпр,СуммаРуб,СуммаНДС,СуммаНП");
		
		ТаблицаУслуг.НоваяКолонка("КредДокумент");
		ТаблицаУслуг.НоваяКолонка("ВидДолга");
		ТаблицаУслуг.НоваяКолонка("Себестоимость");
		ТаблицаУслуг.НоваяКолонка("ДоговорКомитента");
		ТаблицаУслуг.НоваяКолонка("ОблагаетсяНП");
	
		ТаблицаУслуг.ВыбратьСтроки();
		Пока ТаблицаУслуг.ПолучитьСтроку()=1 Цикл
		
			ТаблицаУслуг.КредДокумент 	= ТекущийДокумент();
			ТаблицаУслуг.ВидДолга		= глВД.ДолгЗаУслуги;
			ТаблицаУслуг.Себестоимость	= 0;
			ТаблицаУслуг.ОблагаетсяНП 	= УчитыватьНП;
		
		КонецЦикла;                                                                       
	    глДвижениеДолгов(Контекст, Договор, ТаблицаУслуг, СписокПараметров, ВремПокупатели);
		
	Иначе // отданные на реализацию возвращаем
		глСписаниеПартийТМЦ(Контекст,ТаблицаТМЦ,СписокПараметров,ВремПартииОтданные, ТаблСписПартий);
		    
		ТаблСписПартий.ВыбратьСТроки();
		Пока  ТаблСписПартий.ПолучитьСтроку()=1 Цикл
			Регистр.ПартииНаличие.ПривязыватьСтроку(ТаблСписПартий.НомерСтрокиДокумента);

			//измерения
			Регистр.ПартииНаличие. Фирма 			= Фирма;
			Регистр.ПартииНаличие. МОЛ				= Склад.МОЛ;
			Регистр.ПартииНаличие. Номенклатура		= ТаблСписПартий.Номенклатура;
			Регистр.ПартииНаличие. СтатусПартии		= ТаблСписПартий.СтатусПартии;
			Регистр.ПартииНаличие. Партия			= ТаблСписПартий.Партия;
			Регистр.ПартииНаличие. ДатаПартии		= ДатаДок;

			// ресурсы
			Регистр.ПартииНаличие. Количество		= ТаблСписПартий.Количество;
			Регистр.ПартииНаличие. СуммаУпр			= ТаблСписПартий.СуммаУпр;
			Регистр.ПартииНаличие. СуммаРуб			= ТаблСписПартий.СуммаРуб;
			Регистр.ПартииНаличие. СуммаБезНДС		= ТаблСписПартий.СуммаБезНДС;

			// реквизиты
			Регистр.ПартииНаличие. КодОперации		= глКО.ВозвратОтРеализатора;
			
			Регистр.ПартииНаличие.ДвижениеПриходВыполнить();
		КонецЦикла;
		
		// проверим, а не указал ли услуги - их отдавать на релизацию нельзя
		ТаблицаУслуг.ВыбратьСтроки();
		Если ТаблицаУслуг.ПолучитьСтроку()= 1 Тогда
			глНеПроводить(Контекст,"Услуги возвращать от комиссионера невозможно! ("+ТаблицаУслуг.Номенклатура+")");
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	                                  
КонецПроцедуры // ПроведениеПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//                                     

Функция ПолучитьОстатокСуммуВозвратовПоДокументуОснования()
	ДокПодч = СоздатьОбъект("Документ");
	ДокПодч.ВыбратьПодчиненныеДокументы(,,ДокОснование);
	СуммаВозвратов = 0;
	СуммаПродажи = 0;
	
	Если ДокОснование.Вид() = "Реализация" Тогда
		Если ДокОснование.Вид() = "Реализация" Тогда  
			Если ДокОснование.ВыданоСоСкидкой = 1 Тогда
				СуммаПродажи = СуммаПродажи + ДокОснование.СуммаВзаиморасчетов;
			Иначе
				СуммаПродажи = СуммаПродажи + ДокОснование.Итог("СуммаБезСкидки");
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;	
	
	Пока ДокПодч.ПолучитьДокумент() = 1 Цикл
		Если ДокПодч.ПометкаУдаления() = 1 Тогда
			Продолжить;
		КонецЕсли;
		Если (ДокПодч.Вид() = "ВозвратОтПокупателя") Тогда
			Если (ДокПодч.КодОперации  = глКО.ВозвратОтПокупателя)
			и (ДокПодч.Проведен() = 1)
			и (ДокПодч.ТекущийДокумент() <> ТекущийДокумент()) Тогда
				СуммаВозвратов = СуммаВозвратов + ДокПодч.СуммаВзаиморасчетов;		
			КонецЕсли; 
		ИначеЕсли (ДокПодч.Вид() = "Реализация") 
					и (ДокПодч.Проведен() = 1) Тогда
			СуммаПродажи = СуммаПродажи + ДокПодч.СуммаВзаиморасчетов;
			ДокПодчРеал = СоздатьОбъект("Документ");
			ДокПодчРеал.ВыбратьПодчиненныеДокументы(,,ДокПодч);
			Пока ДокПодчРеал.ПолучитьДокумент() = 1 Цикл
				Если ДокПодчРеал.ПометкаУдаления() = 1 Тогда
					Продолжить;
				КонецЕсли;
				Если (ДокПодчРеал.Вид() = "ВозвратОтПокупателя") Тогда
					Если (ДокПодчРеал.КодОперации  = глКО.ВозвратОтПокупателя)
					и (ДокПодчРеал.Проведен() = 1)
					и (ДокПодчРеал.ТекущийДокумент() <> ТекущийДокумент()) Тогда
						СуммаВозвратов = СуммаВозвратов + ДокПодчРеал.СуммаВзаиморасчетов;		
					КонецЕсли; 
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

	Возврат СуммаПродажи - СуммаВозвратов;
КонецФункции	

Процедура ОбработкаПроведения(ВидыДвижений)
	
	Если ДокОснование.Выбран() = 1 Тогда
		//++Ирина МЭТР
		Если ДокОснование.Вид() = "Реализация"	Тогда
			Если ДокОснование.ФлагСвертки = 1 Тогда
				//Возврат;
			КонецЕсли; 
		КонецЕсли;
		//--Ирина МЭТР
	КонецЕсли;
	
	// Проверка заполненности обязательных реквизитов.
	Если глВсеРеквизитыДокументаЗаполнены(Контекст,
		"Фирма,Склад,Валюта,Контрагент,Договор")=0 Тогда
		Возврат;
	КонецЕсли;  
	    
	// проверка: на основании реализационного документа нельзя  вводить
	// документ купли - продажи и наоборот
	Если ДокОснование.Выбран() = 1 Тогда                        
		НельзяВводить = 0;
		Если ДокОснование.Вид() = "Реализация"	Тогда
			Если (   (ДокОснование.КодОперации = глКО.ПередачаНаРеализацию)
			      и  (( КодОперации  = глКО.ВозвратОтПокупателя) или ( КодОперации  = глКО.ВозвратОтПокупателяЕНВД) ))
			или  (   (ДокОснование.КодОперации = глКО.Продажа)
			      и  (КодОперации  = глКО.ВозвратОтРеализатора))
			Тогда     
				НельзяВводить = 1;
			КонецЕсли;  
		ИначеЕсли КодОперации  = глКО.ВозвратОтРеализатора Тогда // остались только нереализационные основания
			НельзяВводить = 1;
		КонецЕсли;
		Если НельзяВводить = 1 Тогда
			глНеПроводить(Контекст,"На основании документа """+глНазваниеДокументаВЖурнале(ДокОснование)+"""
			|нельзя вводить """ + глНазваниеДокументаВЖурнале(Контекст) + """!");
			Возврат;
		КонецЕсли;  
	КонецЕсли;
	
	// Облагаться ЕНВД может только возврат на розничный склад
	Если ОблагаетсяЕНВД = 1 Тогда
		Если КодОперации  <> глКО.ВозвратОтПокупателяЕНВД Тогда
			глНеПроводить(Контекст,"Код операции не соответствует флагу учета ЕНВД!"); 
			Возврат;
	    КонецЕсли;
	КонецЕсли;
	          
	//проверка на наличие возвратов и общей суммы возвратов
	Если ДокОснование.Выбран() = 1 Тогда
		СуммаПродажОстаток = ПолучитьОстатокСуммуВозвратовПоДокументуОснования();
		
		Если СуммаПродажОстаток < СуммаВзаиморасчетов Тогда
			глНеПроводить(Контекст,"Общая сумма возвратов превышает сумму отгрузки!"); 
			Возврат;	
		КонецЕсли;	
	КонецЕсли;
	// Проведение по регистрам оперативного учета.	
	Если (ПустоеЗначение(ВидыДвижений) = 1) ИЛИ (Найти(ВидыДвижений, "Регистр") <> 0) Тогда
		ПроведениеПоРегистрам();
		
		Если СтатусВозврата() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	глПриПроведении(Контекст, ВидыДвижений); 
	//если ДатаДок<=Дата("31.12.05") Тогда
	//	Регистр.Взаиморасчеты2.Контрагент	= Контрагент;
	//	Регистр.Взаиморасчеты2.Долг			= -Итог("Сумма");
	//	Регистр.Взаиморасчеты2.Скидка		= 0;
	//	Регистр.Взаиморасчеты2.Валюта		= Валюта;
	//	Регистр.Взаиморасчеты2.ВидОплаты	= глКО.СтарыеДанные;
	//	Регистр.Взаиморасчеты2.ДвижениеПриходВыполнить();
	//Иначе
		Регистр.Взаиморасчеты2.Контрагент	= Контрагент;
		Регистр.Взаиморасчеты2.Склад 		= Склад;
		Регистр.Взаиморасчеты2.Долг			= -Итог("Сумма");
		Регистр.Взаиморасчеты2.Скидка		= 0;
		Регистр.Взаиморасчеты2.Валюта		= Валюта;
		Если ПустоеЗначение(ДокОснование) = 1 Тогда
			Регистр.Взаиморасчеты2.ДокументРасчета	= ТекущийДокумент();
		Иначе
			Регистр.Взаиморасчеты2.ДокументРасчета	= глПолучитьОснование(Контекст);
		КонецЕсли;                                                                   
		Если пустоеЗначение(проект)=1 тогда
			сообщить("Т.к. в проекте не указан вид движения денег - принимаем НАЛИЧНЫЕ","i");
			Регистр.Взаиморасчеты2.ВидОплаты	= глКО.Наличные;
		иначе
			Регистр.Взаиморасчеты2.ВидОплаты	= Проект.ВидОплаты;
		КонецЕсли;
		Регистр.Взаиморасчеты2.ДвижениеПриходВыполнить();
		//КонецЕсли;		
		Если ДокОснование.Выбран() = 1 Тогда                        
			Если ДокОснование.Вид() = "РасходнаяНакладнаяРозницаБезнал"	Тогда
				Если (ДокОснование.АналитикаСчетБезналПроценты.ДатаДок >= дата("01.03.2017")) Тогда //или (ДокОснование.ПровестиПереносОплаты = 1) Тогда
					Регистр.ПереносОплаты.Контрагент 					= ДокОснование.АналитикаСчетБезналПроценты.КонтрагентЮЛ;
					Регистр.ПереносОплаты.СчетБезнал		 			= ДокОснование.АналитикаСчетБезналПроценты;
					Регистр.ПереносОплаты.СуммаОтгрузки		 			= Итог("Сумма");
					Регистр.ПереносОплаты.ДвижениеПриходВыполнить(); 
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;		
	
КонецПроцедуры //ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
