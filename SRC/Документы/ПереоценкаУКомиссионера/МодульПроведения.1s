//******************************************************************************
// ПроведениеПоРегистрам()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Проведение по регистрам оперативного учета.
//
Процедура ПроведениеПоРегистрам()
	
	Перем ТаблСписПартий, ТаблСписПартийКом;
	Перем ВремРегистры;
	Перем ВремПартииОтданные;
	
	// Удаление движений по регистрам.
	Для Номер = 1 По Метаданные.Регистр() Цикл
		ОчиститьДвижения("Регистр."+Метаданные.Регистр(Номер).Идентификатор);
	КонецЦикла;
   	
	ТаблицаДокумента=глПодготовитьТаблицуДокумента(Контекст);
	ТаблицаДокумента.НоваяКолонка("Партия","Справочник.Партии"); // в этом документе выбора партии нет
	
	СписокПараметров=СоздатьОбъект("СписокЗначений");
	
	СписокПараметров.ДобавитьЗначение(ТекущийДокумент(),"ТекДок");
	СписокПараметров.ДобавитьЗначение(Фирма,		"Фирма");
	СписокПараметров.ДобавитьЗначение(Контрагент,	"Контрагент");
	СписокПараметров.ДобавитьЗначение(Договор,		"Договор");
	
	СписокПараметров.ДобавитьЗначение(глКО.Перемещение,"КодОперации");	
	                                      
	ВремРегистры = СоздатьОбъект("Регистры");
	
	ВремПартииОтданные = ВремРегистры.ПартииОтданные;
	глФильтрПартийТМЦ(Контекст,ТаблицаДокумента, СписокПараметров, ВремПартииОтданные);
	
	Если ИтогиАктуальны() = 0 Тогда
		ВремРегистры.Актуальность(1);
		ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
	
	глСписаниеПартийТМЦ(Контекст,ТаблицаДокумента,СписокПараметров,ВремПартииОтданные,ТаблСписПартий,ТаблСписПартийКом);
	                            
	// пропишем обороты продаж
	ТаблСписПартий.ВыбратьСтроки();
	Пока ТаблСписПартий.ПолучитьСтроку()=1 Цикл
		// измерения
		Регистр.ПартииОтданные.Фирма            = ТаблСписПартий.Фирма;
		Регистр.ПартииОтданные.Договор          = Договор;
		Регистр.ПартииОтданные.Номенклатура     = ТаблСписПартий.Номенклатура;
		Регистр.ПартииОтданные.СтатусПартии     = ТаблСписПартий.СтатусПартии;
		Регистр.ПартииОтданные.Партия           = ТаблСписПартий.Партия;
		Регистр.ПартииОтданные.ДокументПередачи = ТекущийДокумент();
		// ресурсы
		Регистр.ПартииОтданные.Количество       = ТаблСписПартий.Количество;
		Регистр.ПартииОтданные.СуммаУпр         = ТаблСписПартий.СуммаУпр;
		Регистр.ПартииОтданные.СуммаРуб         = ТаблСписПартий.СуммаРуб;
		Регистр.ПартииОтданные.СуммаБезНДС      = ТаблСписПартий.СуммаБезНДС;
		Регистр.ПартииОтданные.ПродСтоимость    = ТаблСписПартий.СуммаПрод;
		// реквизиты
		Регистр.ПартииОтданные.КодОперации      = глКО.Перемещение;
		Регистр.ПартииОтданные.ДвижениеПриходВыполнить();
	КонецЦикла;
		
КонецПроцедуры // ПроведениеПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПроведения(ВидыДвижений)
	
	// Проверка заполненности обязательных реквизитов.
	Если глВсеРеквизитыДокументаЗаполнены(Контекст,
		"Фирма,Валюта,Контрагент,Договор")=0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проведение по регистрам оперативного учета.	
	Если (ПустоеЗначение(ВидыДвижений) = 1) ИЛИ (Найти(ВидыДвижений, "Регистр") <> 0) Тогда
		ПроведениеПоРегистрам();
		
		Если СтатусВозврата() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	глПриПроведении(Контекст, ВидыДвижений);
	
КонецПроцедуры //ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
