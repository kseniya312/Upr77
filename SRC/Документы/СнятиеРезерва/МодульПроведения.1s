////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Проведение по регистрам оперативного учета.
//
Процедура ПроведениеПоРегистрам() 

	// Удаление движений по регистрам.
	Для Номер = 1 По Метаданные.Регистр() Цикл
		ОчиститьДвижения("Регистр."+Метаданные.Регистр(Номер).Идентификатор);
	КонецЦикла;

	// В таблице значений будет собираться информация по заявкам, с которых
	// снимается резерв и номеклатуре
		
	ВремРег          = СоздатьОбъект("Регистры");
	ВремРезервы      = ВремРег.РезервыТМЦ2;
	ВремРезервы.УстановитьЗначениеФильтра("Склад", Склад);
	                                               
	Если ИтогиАктуальны() = 0 Тогда
	    ВремРезервы.ВременныйРасчет();
		ВремРег.Актуальность(1);
		ВремРег.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
	
	ВыбратьСтроки();      
	Пока ПолучитьСтроку() = 1  Цикл  
		Если Заявка.флРезервировать=1 Тогда	//Были резервы
			ВремРезервы.УстановитьФильтр(Номенклатура, Склад, Заявка);
			ТЗ = СоздатьОбъект("ТаблицаЗначений");
			ВремРезервы.ВыгрузитьИтоги(ТЗ,1,1);
			ТекНоменк	= Номенклатура;
			х			= ПолучитьПустоеЗначение(х);
			у			= 1;
			Колво 		= 0;
			НовоеКолво	= 0;
			
			Если ТЗ.НайтиЗначение(ТекНоменк,х,у)=1 Тогда
				Колво	= ТЗ.ПолучитьЗначение(х,"Количество");
			КонецЕсли;
			
			Если (Колво <> Количество) 
			и (КоличествоНовое <> 0) Тогда
				НовоеКолво = Колво + (КоличествоНовое - Количество);
				Если НовоеКолво < 0 Тогда
					НовоеКолво = 0;
				КонецЕсли;	
			Иначе
				НовоеКолво = КоличествоНовое;
			КонецЕсли;
			
			Если Колво = НовоеКолво Тогда
				Продолжить;
			КонецЕсли;
			
			Регистр.РезервыТМЦ2.Номенклатура      	= Номенклатура;
			Регистр.РезервыТМЦ2.Склад             	= Склад; 
			Регистр.РезервыТМЦ2.Заявка				= Заявка;
			
			Если Колво < НовоеКолво Тогда
				Регистр.РезервыТМЦ2.Количество 			= НовоеКолво - Колво;
				Регистр.РезервыТМЦ2.ДвижениеПриходВыполнить(); 
			Иначе	
				Регистр.РезервыТМЦ2.Количество 			= Колво - НовоеКолво;
				Регистр.РезервыТМЦ2.ДвижениеРасходВыполнить(); 
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; // По строкам документа         
КонецПроцедуры // ПроведениеПоРегистрам() 

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПроведения(ВидыДвижений)
	
	// Проверка заполненности обязательных реквизитов.
	СписОбязРеквизитов = "Фирма";
	Если СокрЛП(ВидОперации) = "Снятие резерва по складу"  Тогда
		СписОбязРеквизитов = СписОбязРеквизитов + ",Склад";
	КонецЕсли;                                                         
	
	Если глВсеРеквизитыДокументаЗаполнены(Контекст, СписОбязРеквизитов) = 0 Тогда
	    глНеПроводить(Контекст, "Не все обязательные реквизиты документа заполнены!");
		Возврат;
	КонецЕсли;                 
	
	// Проведение по регистрам оперативного учета.	
	Если (ПустоеЗначение(ВидыДвижений) = 1) ИЛИ (Найти(ВидыДвижений, "Регистр") <> 0) Тогда
		ПроведениеПоРегистрам();
		
		Если СтатусВозврата() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	глПриПроведении(Контекст, ВидыДвижений);
	
КонецПроцедуры // ОбработкаПроведения()
