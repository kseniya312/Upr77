////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем НачальнаяДатаДокумента; 
Перем СтароеЮрЛицо;
Перем СтараяФирма;
Перем СписокДействий;

//*****************************************************************************
// ЗаголовокФормы()
//
// Возвращаемое значение: 
//	Строка - заголовок документа.
//
// Вызывается из формул элементов диалога:
//  Заголовок документа.
//
// Описание:
//  Формирует название документа и заголовок формы диалога.
//
Функция ЗаголовокФормы()
	
	Перем Заголовок, Название;

	Заголовок	= глНазваниеДокументаВЖурнале(Контекст);
	Название 	= Заголовок+" №";
	
	Если Выбран() = 1 Тогда  
		Если Проведен() = 1 Тогда
			Заголовок = Заголовок + ". Проведен";
		Иначе
			Заголовок = Заголовок + ". Не проведен";
		КонецЕсли;
	Иначе
		Заголовок = Заголовок + ". Новый";
	КонецЕсли;
	
	Форма.Заголовок(Заголовок);
	
	Возврат Название;
	
КонецФункции // ЗаголовокФормы()

//*****************************************************************************
// ПоКнопкеОснование()
// 
// Параметры: 
//  Нет
//
// Возвращаемое значение: 
//  Нет
//
// Описание:
// 	Вызывается по кнопке выбора документа основания                  
//
Процедура ПоКнопкеОснование()
	
	// если документ основание уже есть, откроем его
	Если ПустоеЗначение(СторнируемыйДокумент) = 0 Тогда
		ОткрытьФорму(СторнируемыйДокумент);
		Возврат;
	КонецЕсли;
	
	// если документа - основания нет, позволяем выбрать его
	СторнируемыйДокумент = глВыбратьОснование(Контекст);
	
	Если ПустоеЗначение(СторнируемыйДокумент) = 0 Тогда
	    Если Метаданные.Документ(СторнируемыйДокумент.Вид()).ОперативныйУчет = 0  Тогда
			 	
			Предупреждение("Сторнирование документа данного вида не предусмотрено!", 60);
			СторнируемыйДокумент = ПолучитьПустоеЗначение("Документ");
		КонецЕсли;
	КонецЕсли;
	
	Фирма	= СторнируемыйДокумент.Фирма;
    Проект	= СторнируемыйДокумент.Проект;
	
	Если СтараяФирма <> Фирма Тогда  
		// только если изменили
		глПриИзмененииФирмы(Контекст);
		СтараяФирма = Фирма; 
	КонецЕсли;

КонецПроцедуры // ПоКнопкеОснование()
                        
//******************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Скопирован) 
	                  
	глЗаполнитьШапку(Контекст, Скопирован);
	
	Если Скопирован = 1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;

КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ВводНаОсновании(Основание)

	Если Метаданные.Документ(Основание.Вид()).ОперативныйУчет = 0  Тогда
		Предупреждение("Сторнирование документа данного вида не предусмотрено!",60);
		СтатусВозврата(0); Возврат;
	КонецЕсли;
	
	СторнируемыйДокумент	= Основание;   
	глЗаполнитьШапкуНаОсн(Контекст, Основание);
	
КонецПроцедуры // ВводНаОсновании()

//******************************************************************************
//	Предопределенная процедура.
//
Процедура ПриОткрытии()
	 
	Парам = Форма.Параметр;

	Если ТипЗначенияСтр(Парам)="СписокЗначений" Тогда   //отрытие формы в скрытом режиме для выполнения действий
		
		Если ПустоеЗначение(Парам.Получить("Команда"))=1 Тогда   	// если не получено действие
			Сообщить("Такое действие для документа """+ПредставлениеВида()+""" не предусмотрено !","I");
			СтатусВозврата(0);Возврат;
		КонецЕсли;
		
		Сообщить("Действие  """+Парам.Получить("Команда")+""" для документа """+ПредставлениеВида()+""" не предусмотрено !","I");
		СтатусВозврата(0);Возврат; 
			
	КонецЕсли;
              
	глПроверкаРазрешенияРедактирования(Контекст);
	
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнЗаписать.Доступность(0);
		Форма.кнПровести.Доступность(0);
		Форма.кнОК.Доступность(0);
	КонецЕсли;
	
	// кнопка по умолчанию
	Если (Форма.ТолькоПросмотр() = 1) ИЛИ ((ДатаДок < Макс(РабочаяДата(), ПолучитьДатуТА()) ) И (Выбран() = 1)) Тогда
	    Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
	    Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;

	НачальнаяДатаДокумента = ДатаДок;
	СтароеЮрЛицо           = ЮрЛицо;
	СтараяФирма            = Фирма;
	
КонецПроцедуры // ПриОткрытии()     

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗаписи()
	
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	ПриЗаписиДокумента(Контекст);
КонецПроцедуры // ПриЗаписи()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Отчет о бухгалтерских проводках");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Перейти в журнал");
