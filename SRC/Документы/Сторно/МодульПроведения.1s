////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
//	Предопределенная процедура.
//
Процедура ОбработкаПроведения()
	
	Если ПустоеЗначение(СторнируемыйДокумент) = 1 Тогда
		глНеПроводить(Контекст, "Не выбран сторнируемый документ.");
		Возврат;
	КонецЕсли;
	
	// Проверка сторнированности документа.
	
	Объект = СоздатьОбъект("Документ");
	Объект.ВыбратьПодчиненныеДокументы(,, СторнируемыйДокумент);
	
	Пока Объект.ПолучитьДокумент() = 1 Цикл
		Если (Объект.Вид() = "Сторно") И (Объект.Проведен() = 1) И (Объект.ТекущийДокумент() <> ТекущийДокумент()) Тогда
			глНеПроводить(Контекст, "Сторнируемый документ уже сторнирован документом № "+Объект.НомерДок+" от "+Объект.ДатаДок);
			Возврат;
		КонецЕсли;
	КонецЦикла;                                      
	
	Если Метаданные.Документ(СторнируемыйДокумент.Вид()).ОперативныйУчет = 1 Тогда
		
		Для Сч = 1 По Метаданные.Регистр() Цикл
			
			Рег       = СоздатьОбъект("Регистр." + Метаданные.Регистр(Сч).Идентификатор);
			РегСторно = Регистр.ПолучитьАтрибут(Метаданные.Регистр(Сч).Идентификатор);

			Если Рег.ВыбратьДвиженияДокумента(СторнируемыйДокумент)=1 Тогда
				
				Пока Рег.ПолучитьДвижение()=1 Цикл
					
					МДОперация = ?(Рег.Приход=1,"Приход","Расход");
					
					// заполняем измерения
					Для Сч2 = 1 По  Метаданные.Регистр(Сч).Измерение() Цикл                     
						РегСторно.УстановитьАтрибут(Метаданные.Регистр(Сч).Измерение(Сч2).Идентификатор, Рег.ПолучитьАтрибут(Метаданные.Регистр(Сч).Измерение(Сч2).Идентификатор));
					КонецЦикла;
					
					// заполняем реквизиты
					Для Сч2 = 1 По  Метаданные.Регистр(Сч).Реквизит() Цикл                     
						ЗначениеРеквизита = Рег.ПолучитьАтрибут(Метаданные.Регистр(Сч).Реквизит(Сч2).Идентификатор);
						Если ТипЗначенияСтр(ЗначениеРеквизита) = "Число" Тогда
							ЗначениеРеквизита = - ЗначениеРеквизита;
						КонецЕсли;
						РегСторно.УстановитьАтрибут(Метаданные.Регистр(Сч).Реквизит(Сч2).Идентификатор, ЗначениеРеквизита);
					КонецЦикла;      
					
					// Номер строки документа-основания
					РегСторно.ПривязыватьСтроку(Рег.НомерСтроки());   
					
					// заполняем ресурсы
					Для Сч2 = 1 По  Метаданные.Регистр(Сч).Ресурс() Цикл                     
						РегСторно.УстановитьАтрибут(Метаданные.Регистр(Сч).Ресурс(Сч2).Идентификатор, -Рег.ПолучитьАтрибут(Метаданные.Регистр(Сч).Ресурс(Сч2).Идентификатор));
					КонецЦикла;
					
					// движение сторно       
					
					Если Рег.Приход = 1	Тогда					
						РегСторно.ДвижениеПриходВыполнить();
					Иначе
						РегСторно.ДвижениеРасходВыполнить();
					КонецЕсли;	 
					
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
	
КонецПроцедуры // ОбработкаПроведения()
