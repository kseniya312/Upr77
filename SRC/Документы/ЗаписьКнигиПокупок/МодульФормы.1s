////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//      
Перем СписокДействий; // для механизма кнопки "Действия"
Перем НачальнаяДатаДокумента;
    
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//                  
//*****************************************************************************
// ТекстВалюты(Вал)
//
// Параметры: 
//  Вал - элемент справочника "Валюты"
//
// Возвращаемое значение: 
//  Строка валюты
//
// Описание:
// 	Возвращает название валюты или строку "<нет валюты>"
//
Функция ТекстВалюты(Вал) 
	
	Возврат ?(ПустоеЗначение(Вал) = 0, Вал.Наименование, "<нет валюты>");
	
КонецФункции                                                                    

//******************************************************************************
// ОбновитьНадписи()
//
// Описание:
// 	Обновляет текст информационных надписей в форме документа
Процедура ОбновитьНадписи()
	
	Форма.РамкаВсего	.Заголовок("Всего ("+ТекстВалюты(глРубли)+")");
	Форма.РамкаБезНДС	.Заголовок("Без НДС ("+ТекстВалюты(глРубли)+")");
	Форма.РамкаНДС		.Заголовок("НДС ("+ТекстВалюты(глРубли)+")");
	
КонецПроцедуры //ОбновитьНадписи()

//*****************************************************************************
// ЗаголовокФормы()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение: 
//  Название операции
//
// Описание:
// 	Формирует название операции и заголовок формы диалога
//
Функция ЗаголовокФормы() 
	
	Перем Заголовок, Название;
            
	Заголовок 	= глНазваниеДокументаВЖурнале(Контекст);
	Название 	= Заголовок+" №";
	
	Если Выбран() = 1 Тогда  
		Если Проведен() = 1 Тогда
			Заголовок = Заголовок + ". Проведен";
		Иначе
			Заголовок = Заголовок + ". Не проведен";
		КонецЕсли;
	Иначе
		Заголовок = Заголовок + ". Новый";
	КонецЕсли;
	Форма.Заголовок(Заголовок);               
	Возврат Название;
	
КонецФункции // ЗаголовокФормы

//******************************************************************************
// ЗаполнениеПоПолученному()
//
// Параметры:
//  ДокОсн		- счет-фактура полученный
//  ОписаниеОш	- описание ошибки
//
// Возвращаемое значение:
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание:
//  Производит заполнение документа по счету-фактуре полученному
//
Функция ЗаполнениеПоПолученному(ДокОсн, ОписаниеОш)
	
	Если ДокОсн.АвтоКнигаПокупок = 1 Тогда
		ОписаниеОш	= "По выбранному счету - фактуре установлен
		              | автоматический режим формирования книги покупок!";
		Возврат 0;
	КонецЕсли;
	
	// заполнять будем на основании основания СФ
	Если ДокОсн.ДокОснование.Выбран()=1 Тогда
		ДокОсн	= ДокОсн.ДокОснование; // СФ выписана на основании
	КонецЕсли;
	        
	ОбщРег = СоздатьОбъект("Регистры");
	РегКнигаПокупок = ОбщРег.КнигаПокупок;
	
	РегКнигаПокупок.УстановитьЗначениеФильтра("КредДокумент", ДокОсн, 1);

	// если нужно, то выполним временный расчет регистра
	Если Выбран() = 0 Тогда
		// Новый документ
		Если ДатаДок < ПолучитьДатуТА() Тогда
			// выписываемый задним числом
			РегКнигаПокупок.ВременныйРасчет();
			ОбщРег.РассчитатьРегистрыПо(ДатаДок);
		КонецЕсли;
	Иначе 
		// Сохраненный документ. 
		Если СравнитьТА() < 1 Тогда
			//Позиция не больше ТА.
			РегКнигаПокупок.ВременныйРасчет();
			ОбщРег.РассчитатьРегистрыНа(ТекущийДокумент());
		КонецЕсли;
	КонецЕсли;
	
	УдалитьСтроки(); // очистим наш документ
	                                      
	ТабЗнач = СоздатьОбъект("ТаблицаЗначений");
	РегКнигаПокупок.ВыгрузитьИтоги(ТабЗнач);
	ТабЗнач.Свернуть("СтавкаНДС","СуммаРуб, СуммаНДС, СуммаНП");
	                 
	СуммаОплаты = - ТабЗнач.Итог("СуммаРуб");
	Если СуммаОплаты = 0 Тогда
		ОписаниеОш	= "" + глПредставлениеДокумента(ДокОснование) + " полностью погашен!
		              |Возможно, счет-фактура уже учтен при проведении документа ""Формирование книги покупок"".";
		Возврат 0;
	ИначеЕсли ВвестиЧисло(СуммаОплаты, "Укажите сумму записи", 15, 2, 60) <> 1 Тогда
		Возврат 1;
	КонецЕсли;                  
	
	Если СуммаОплаты > - ТабЗнач.Итог("СуммаРуб") Тогда
		ОписаниеОш	= "Указанная сумма превышает непогашенный остаток по СФ!";
		Возврат 0;
	КонецЕсли;
	
	КоэффОплаты = ?(ТабЗнач.Итог("СуммаРуб") = 0, 0, СуммаОплаты / (- ТабЗнач.Итог("СуммаРуб")));
	    
	ПогашСумма = 0;
	ТабЗнач.ВыбратьСтроки();
	Пока ТабЗнач.ПолучитьСтроку() = 1 Цикл
		НоваяСтрока();
		Сумма 		= - ТабЗнач.СуммаРуб * КоэффОплаты;
		СуммаНП 	= - ТабЗнач.СуммаНП  * КоэффОплаты;
		СуммаНДС 	= - ТабЗнач.СуммаНДС * КоэффОплаты;
		СтавкаНДС   =   ТабЗнач.СтавкаНДС;
		
		ПогашСумма  = ПогашСумма + Сумма;
		Если ТабЗнач.НомерСтроки = ТабЗнач.КоличествоСтрок() Тогда
			// последняя строка  - надо скомпенсировать накопленную разницу
			Сумма = Сумма + (СуммаОплаты - ПогашСумма);
		КонецЕсли;
	КонецЦикла;
	
	Возврат 1; // нет ошибок
	
КонецФункции // ЗаполнениеПоПолученному()   
                                                                  
//******************************************************************************
// ЗаполнениеПоВыданному(ДокОсн, ОписаниеОш)
//
// Параметры:
//  ДокОсн, 	- счет-фактура выданный
//  ОписаниеОш	- описание ошибки
//
// Возвращаемое значение:
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание:
//  Производит заполнение документа по счету-фактуре выданному
//
Функция ЗаполнениеПоВыданному(ДокОсн, ОписаниеОш)
	
	Если ДокОсн.АвтоКнигаПродаж = 1 Тогда
		ОписаниеОш	= "По выбранному счету - фактуре установлен
		              | автоматический режим формирования книги продаж!";
		Возврат 0;
	КонецЕсли;
	
	// заполнять будем на основании основания СФ
	Если ДокОсн.ДокОснование.Выбран()=1 Тогда
		ДокОсн	= ДокОсн.ДокОснование; // СФ выписана на основании
	КонецЕсли;
	        
	ОбщРег = СоздатьОбъект("Регистры");
	РегКнигаПродаж = ОбщРег.КнигаПродаж;
	
	РегКнигаПродаж.УстановитьЗначениеФильтра("КредДокумент", ДокОсн, 1);

	// если нужно, то выполним временный расчет регистра
	Если Выбран() = 0 Тогда
		// Новый документ
		Если ДатаДок < ПолучитьДатуТА() Тогда
			// выписываемый задним числом
			РегКнигаПродаж.ВременныйРасчет();
			ОбщРег.РассчитатьРегистрыПо(ДатаДок);
		КонецЕсли;
	Иначе 
		// Сохраненный документ. 
		Если СравнитьТА() < 1 Тогда
			//Позиция не больше ТА.
			РегКнигаПродаж.ВременныйРасчет();
			ОбщРег.РассчитатьРегистрыНа(ТекущийДокумент());
		КонецЕсли;
	КонецЕсли;
	
	УдалитьСтроки(); // очистим наш документ
	                                      
	ТабЗнач = СоздатьОбъект("ТаблицаЗначений");
	РегКнигаПродаж.ВыгрузитьИтоги(ТабЗнач);
	ТабЗнач.Свернуть("СтавкаНДС","СуммаРуб, СуммаНДС, СуммаНП");
	            
	// Надо поменять знак, потому что в регистре с минусом
	СуммаОплаты = (-1) * ТабЗнач.Итог("СуммаРуб");        
	
	Если СуммаОплаты = 0 Тогда
		ОписаниеОш	= ""+глПредставлениеДокумента(ДокОснование)+" полностью погашен!
		              |Возможно: 
				      |1) счет-фактура уже учтен при проведении документа ""Формирование книги продаж"";
				      |2) счет-фактура сопровождает реализацию только комиссионных товаров.";
		// Нечего отражать в книге. Укажем  две наиболее вероятные причины
		Возврат 0;
	ИначеЕсли ВвестиЧисло(СуммаОплаты,"Укажите сумму записи",15,2,60)<> 1 Тогда
		Возврат 1;
	КонецЕсли;                  
	
	Если СуммаОплаты > (-1) * ТабЗнач.Итог("СуммаРуб") Тогда
		ОписаниеОш	= "Указанная сумма превышает непогашенный остаток по СФ";
		Возврат 0;
	КонецЕсли;
	
	КоэффОплаты = ?(ТабЗнач.Итог("СуммаРуб") = 0,0,СуммаОплаты/ТабЗнач.Итог("СуммаРуб"));
        
	ПогашСумма = 0;
	ТабЗнач.ВыбратьСтроки();
	Пока ТабЗнач.ПолучитьСтроку() = 1 Цикл
		НоваяСтрока();
		Сумма 		= ТабЗнач.СуммаРуб * КоэффОплаты;
		СуммаНП 	= ТабЗнач.СуммаНП  * КоэффОплаты;
		СуммаНДС 	= ТабЗнач.СуммаНДС * КоэффОплаты;
		СтавкаНДС   = ТабЗнач.СтавкаНДС;
		
		ПогашСумма  = ПогашСумма + Сумма;
		Если ТабЗнач.НомерСтроки = ТабЗнач.КоличествоСтрок() Тогда
			// последняя строка  - надо скомпенсировать накопленную разницу
			Сумма = Сумма + (СуммаОплаты - ПогашСумма);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПустоеЗначение(ДополнительнаяИнформация) = 1 Тогда
		ДополнительнаяИнформация = "зачет НДС с аванса";
	КонецЕсли;
	
	Возврат 1; // нет ошибок
	
КонецФункции // ЗаполнениеПоВыданному()   
                                                                  
//******************************************************************************
// ЗаполнитьПоОснованию(ДокОсн)
//
// Параметры: 
//  ДокОсн		- документ, на основании которого нужно заполнить
//  ОписаниеОш	- описание ошибки
//
// Возвращаемое значение:
//  1 - успешное завершение, 0 - произошла ошибка
//
// Описание:
//  Выбирает способ заполнения состава в зависимости от вида
//  документа основания и вызывает соответствующую способу фукнцию
//  заполнения.
//
Функция ЗаполнитьПоОснованию(Знач ДокОсн, ОписаниеОш)
	
	Если "СчетФактураПолученный" = ДокОсн.Вид() Тогда
		Рез	= ЗаполнениеПоПолученному(ДокОсн, ОписаниеОш);
	ИначеЕсли "СчетФактураВыданный" = ДокОсн.Вид() Тогда
		Рез	= ЗаполнениеПоВыданному(ДокОсн, ОписаниеОш);
	Иначе
		Рез	= 0;
		ОписаниеОш	= "На основании """ + ДокОсн.ПредставлениеВида() + " документ не заполняется!";
	КонецЕсли;
	
	Возврат Рез;
	
КонецФункции // ЗаполнитьПоОснованию()

//*****************************************************************************
// ПоКнопкеЗаполнить()
// 
// Параметры: 
//  Нет
//
// Возвращаемое значение: 
//  Нет
//
// Описание:
// 	Вызывается по кнопке "Заполнить". 
// 	Проверяет, выбран или нет документ основание. Если состав заполнен,
//  то очищает его и вызывает функцию заполнения.
//
Процедура ПоКнопкеЗаполнить()
	Перем ОписаниеОш;
	
	Если ДокОснование.Выбран()=0 Тогда
		Предупреждение("Не выбран документ - основание.", 60);
	КонецЕсли;
	
	Если КоличествоСтрок() <> 0 Тогда
		Если Вопрос("Заполнить остатками по документу - основанию?" +
		            РазделительСтрок +
					"(перед заполнением табличная часть будет очищена)", "Да+Нет", 60) <> "Да" Тогда
			Возврат;
		КонецЕсли;
		УдалитьСтроки();
	
	КонецЕсли;
	
	Если ЗаполнитьПоОснованию(ДокОснование, ОписаниеОш) = 0 Тогда
		Предупреждение(ОписаниеОш, 60);
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеЗаполнить()

//******************************************************************************
// ПересчетТаблЧасти(ИмяРеквизита)
//
// Параметры:
//  ИмяРеквизита
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Реквизиты табличной части.
//
// Описание:
//  Производит пересчеты при изменении реквизитов табличной части.
//
Процедура ПересчетТаблЧасти(ИмяРеквизита)
	
	Если (ИмяРеквизита = "Сумма") или (ИмяРеквизита = "СтавкаНДС") Тогда
		СуммаНДС 	= глВыделяемыйНДС(СтавкаНДС)*(Сумма - СуммаНП);
		
	ИначеЕсли ИмяРеквизита = "СуммаНП" Тогда           
		СуммаНДС 	= глВыделяемыйНДС(СтавкаНДС)*(Сумма - СуммаНП);
		
	КонецЕсли;
	
КонецПроцедуры // ПересчетТаблЧасти()    

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//                           
//******************************************************************************
// Предопределенная процедура
//
Процедура ВводНаОсновании(ДокументОснование)
	Перем ОписаниеОш;
	
	СинонимДокумента	= ПредставлениеВида();
	СинонимОснования	= ДокументОснование.ПредставлениеВида();
	
	Список	= глПолучитьСписокВводимыхНаОсновании(ДокументОснование);
	Поз		= Список.НайтиЗначение(Вид());
	
	// выполним проверку, можно ли вводить документ на основании ДокументОснование
	Если Поз = 0 Тогда
		// ДокументОснование не найден в списке разрешенных
		Предупреждение("Документ """ + СинонимДокумента +
		                """ нельзя вводить на основании """ + СинонимОснования + """", 60);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	// если документ основание - СчетФактураВыданный, то он обязательно должен
	// быть на аванс
	Если "СчетФактураВыданный" = ДокументОснование.Вид() Тогда
	    Если ДокументОснование.СФНаАванс = 0 Тогда
			Предупреждение("Документ """ + СинонимОснования +
			                """ не является счет-фактурой на аванс. Вводить """ + СинонимДокумента + """ не требуется!", 60);
			СтатусВозврата(0);
			Возврат;
	    КонецЕсли;
	КонецЕсли;
	                         
	глЗаполнитьШапкуНаОсн(Контекст, ДокументОснование);   
	                                                       
	ДатаОплаты = Формат(РабочаяДата(),"ДДДММГГГГ");
	
	Если ЗаполнитьПоОснованию(ДокументОснование, ОписаниеОш) = 0 Тогда
		Предупреждение(ОписаниеОш, 60);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

КонецПроцедуры // ВводНаОсновании()
                    
//******************************************************************************
// Предопределенная процедура
//
Процедура ВводНового()
	
	Предупреждение("Документ вводится только на основании счетов-фактур", 60);
	СтатусВозврата(0);
	
КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	                         
	Парам = Форма.Параметр;
	Если ТипЗначенияСтр(Парам)="СписокЗначений" Тогда
		Если ПустоеЗначение(Парам.Получить("Команда"))=1 Тогда
			Сообщить("В форму документа "+Вид()+" передан неверный параметр!","I");
			СтатусВозврата(0);Возврат;
		КонецЕсли;
		
		Если (Парам.Получить("Команда") = "ПечатьНаПринтер")
		или  (Парам.Получить("Команда") = "ПечатьНаЭкран") 
		Тогда
			Предупреждение("Документ не имеет печатной формы!", 60);
			СтатусВозврата(0);Возврат;
		Иначе
			Сообщить("В форму документа "+Вид()+" передана неверная команда "+Парам.Получить("Команда")+"!","I");
			СтатусВозврата(0);Возврат;
		КонецЕсли;
	КонецЕсли;

	//Инициализирум список действий по кнопке "Действия"
	СписокДействий = СоздатьОбъект("СписокЗначений");
	СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
	СписокДействий.ДобавитьЗначение("Отчет о бухгалтерских проводках");
	СписокДействий.ДобавитьЗначение("Структура подчиненности");
	СписокДействий.ДобавитьЗначение("Перейти в журнал");
	
	НачальнаяДатаДокумента 	= ДатаДок;  
	
	Форма.Сумма			.ВыполнятьФормулуТолькоПриИзменении(1);
	Форма.СуммаНДС		.ВыполнятьФормулуТолькоПриИзменении(1);
	Форма.СуммаНП		.ВыполнятьФормулуТолькоПриИзменении(1);
	Форма.СтавкаНДС		.ВыполнятьФормулуТолькоПриИзменении(1);
	
	глПроверкаРазрешенияРедактирования(Контекст);
       
	// если дата проведенного документа больше ТА, то открываем только на просмотр,
	// так как его все равно не удастся сохранить после редактирования.
	Если (Проведен() = 1) И (ДатаДок > ПолучитьДатуТА()) Тогда
		Форма.ТолькоПросмотр(1);
	КонецЕсли;
	
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
	    // СДЕЛАТЬ НЕДОСТУПНЫМИ КНОПКИ МОДИФИКАЦИИ ДОКУМЕНТА
		Форма.кнОК.Доступность(0);
		Форма.кнЗаписать.Доступность(0);
		Форма.кнЗаполнить.Доступность(0);
		Форма.кнПровести.Доступность(0);
	КонецЕсли;
                   
	СписокДействий.ДобавитьЗначение("Ввести на основании");
	
	// кнопка по умолчанию
	Если (Форма.ТолькоПросмотр() = 1) ИЛИ ((ДатаДок < Макс(РабочаяДата(), ПолучитьДатуТА()) ) И (Выбран() = 1)) Тогда
	    Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
	    Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;
	
	глАктивизироватьРеквизит(Контекст);
	
	//Если документ еще не проведен, тогда 
	//проведение делаем только в потоке
	Если ( Проведен() = 0 ) Тогда
		ПроводитьПослеТА(1,1);
	КонецЕсли;                 
	
	ОбновитьНадписи();
	
	Форма.ДокОснование.НеИзменятьВид(1);
                
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр)
	
	Если ИдентЭлемДиалога = "ДатаОплаты" Тогда
		ВыбранноеЗначение = ?(ПустоеЗначение(Дата(СокрЛП(ДатаОплаты))) = 1, ДатаДок, Дата(СокрЛП(ДатаОплаты)));
		Если ВвестиЗначение(ВыбранноеЗначение, "Укажите дату оплаты счета-фактуры", "Дата") = 1 Тогда
			ДатаОплаты = ?(ПустоеЗначение(ВыбранноеЗначение)=1, "", Формат(ВыбранноеЗначение, "Д"));
		КонецЕсли;
		
	ИначеЕсли ИдентЭлемДиалога = "ДополнительнаяИнформация" Тогда
		СписЗнач = СоздатьОбъект("СписокЗначений");
		СписЗнач.ДобавитьЗначение("частичная оплата");
		СписЗнач.ВыбратьЗначение(ДополнительнаяИнформация,,,,2);
		
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()    
                             
//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаВыбораЗначения(ВыбЗнач, ИдентЭлемДиалога, ФлагСтандОбр)
	Перем ОписаниеОш;
	
	Если (ИдентЭлемДиалога = "ДокОснование") и (ПустоеЗначение(ВыбЗнач) = 0) Тогда
		Фирма     = ВыбЗнач.Фирма;          
		
		Если ЗаполнитьПоОснованию(ВыбЗнач, ОписаниеОш) = 0 Тогда
			Предупреждение(ОписаниеОш, 60);
			ФлагСтандОбр = 0;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаВыбораЗначения()      

//******************************************************************************
//Предопределенная процедура
Процедура ПриЗаписи() 
	
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);                        
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
    ПриЗаписиДокумента(Контекст);                  
КонецПроцедуры // ПриЗаписи()