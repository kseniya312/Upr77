////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
Перем РабочийКО;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ФильтрКассы(ВремКасса)
//
// Параметры:
//  ВремКасса
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Установка фильтра по регистру Касса.
//
Процедура ФильтрКассы(ВремКасса)
	
	ВремКасса.УстановитьЗначениеФильтра("Фирма",	Фирма,	1);
	ВремКасса.УстановитьЗначениеФильтра("Касса",	Касса,	1);
	ВремКасса.УстановитьЗначениеФильтра("Валюта",	ВалютаИсх,	1);
	Если ИтогиАктуальны() = 0 Тогда
		ВремКасса.ВременныйРасчет(1);
	КонецЕсли;
	
КонецПроцедуры // ФильтрКассы()

//******************************************************************************
// ОбработкаПроведенияПоКассе(ВремКасса)
//
// Параметры:
//  ВремКасса - рассчитанный регистр "Касса"
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Осуществляет движения по регистру "Касса".
//
Процедура ОбработкаПроведенияПоКассе(ВремКасса)
	
	//Сначала спишем исходную валюту
	Регистр.Касса.Фирма		= Фирма;
	Регистр.Касса.Касса 	= Касса;
	Регистр.Касса.Валюта 	= ВалютаИсх;      
	Регистр.Касса.ДвижениеДенежныхСредств = ДвижениеДенежныхСредств;
	
	ОстатокВал = ВремКасса.Остаток(Фирма,Касса,ВалютаИсх,"СуммаВал"); 
	ОстатокУпр = ВремКасса.Остаток(Фирма,Касса,ВалютаИсх,"СуммаУпр"); 
	ОстатокРуб = ВремКасса.Остаток(Фирма,Касса,ВалютаИсх,"СуммаРуб"); 
	
	//СуммаУпр = глПересчет(СуммаИсх,ВалютаИсх,КурсИсх,глДоллары,ДатаДок);
	Если ВалютаИсх=глДоллары Тогда
		СуммаУпр = СуммаИсх;
	ИначеЕсли ВалютаКон=глДоллары Тогда
		СуммаУпр = СуммаКон;
	Иначе
		СуммаУпр = глПересчет(СуммаИсх,ВалютаИсх,КурсИсх,глДоллары,ДатаДок);
	КонецЕсли;
	
	Если ВалютаИсх=глРубли Тогда
		СуммаРуб = СуммаИсх;
	ИначеЕсли ВалютаКон=глРубли Тогда
		СуммаРуб = СуммаКон;
	Иначе
		СуммаРуб = глПересчет(СуммаИсх,ВалютаИсх,КурсИсх,глРубли,ДатаДок);
	КонецЕсли;
	
	Если ОстатокВал>0 Тогда
		ПогаситьВал = Мин(ОстатокВал,СуммаИсх);
		ПогаситьУпр = Окр(ОстатокУпр*ПогаситьВал/ОстатокВал,2,1);
		ПогаситьРуб = Окр(ОстатокРуб*ПогаситьВал/ОстатокВал,2,1);
		                               
		СписатьУпр = Окр(СуммаУпр * ПогаситьВал/СуммаИсх,2,1);
		СписатьРуб = Окр(СуммаРуб * ПогаситьВал/СуммаИсх,2,1);
		КурсРазницаУпр = СписатьУпр - ПогаситьУпр;
		КурсРазницаРуб = СписатьРуб - ПогаситьРуб;
		
		Если (КурсРазницаУпр<>0) или (КурсРазницаРуб<>0) Тогда
			
			Регистр.Касса.КодОперации = глКО.КурсоваяРазница;
			
			Если КурсРазницаРуб < 0 Тогда
				Регистр.Касса.СуммаВал 	= 0;
				Регистр.Касса.СуммаУпр 	= - КурсРазницаУпр;
				Регистр.Касса.СуммаРуб 	= - КурсРазницаРуб;
				
				Регистр.Касса.ДвижениеРасходВыполнить();
			Иначе
				Регистр.Касса.СуммаВал 	= 0;
				Регистр.Касса.СуммаУпр 	= КурсРазницаУпр;
				Регистр.Касса.СуммаРуб 	= КурсРазницаРуб;
				
				Регистр.Касса.ДвижениеПриходВыполнить();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;                                                  
	        
	Регистр.Касса.СуммаВал 	= СуммаИсх;
	Регистр.Касса.СуммаУпр 	= СуммаУпр;
	Регистр.Касса.СуммаРуб 	= СуммаРуб;
		
	Регистр.Касса.КодОперации = глКО.ОбменВалют;

	Регистр.Касса.СтатьяДР	= СписокСтатейДР;
	Регистр.Касса.ФизЛицо	= Подчинение1;
		
	Регистр.Касса.ДвижениеРасходВыполнить();


	//Теперь припишем приходы по др. валюте

	Регистр.Касса.Фирма		= Фирма;
	Регистр.Касса.Касса 	= Касса;
	Регистр.Касса.Валюта 	= ВалютаКон;
		
	Регистр.Касса.СуммаВал 	= СуммаКон;
	Регистр.Касса.СуммаУпр 	= глПересчет(СуммаКон,ВалютаКон,КурсКон,глДоллары,ДатаДок);
	Если ВалютаИсх=глРубли Тогда
		Регистр.Касса.СуммаРуб 	= СуммаИсх;
	ИначеЕсли ВалютаКон=глРубли Тогда
		Регистр.Касса.СуммаРуб 	= СуммаКон;
	Иначе
		Регистр.Касса.СуммаРуб 	= глПересчет(СуммаКон,ВалютаКон,КурсКон,глРубли,ДатаДок);
	КонецЕсли;
	
	Регистр.Касса.КодОперации = глКО.ОбменВалют;
	Регистр.Касса.ДвижениеДенежныхСредств = ДвижениеДенежныхСредств;
	Если РабочийКО			= глКО.СтатьиДР тогда
		Регистр.Касса.СтатьяДР	= СписокСтатейДР;
		Регистр.Касса.ФизЛицо	= Подчинение1;
	КонецЕсли;
		
	Регистр.Касса.ДвижениеПриходВыполнить();	
	
	
	
КонецПроцедуры // ОбработкаПроведенияПоКассе()
//******************************************************************************
// ОбработкаПроведенияПрочее()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Осуществляет проведение по прочим операциям, которые не проходят по регистрам
//взаиморасчетов.
//
Процедура ОбработкаПроведенияПрочее()
	
	
	ВремРегистры = СоздатьОбъект("Регистры");
	
	ВремКасса = ВремРегистры.Касса;
	ФильтрКассы(ВремКасса);
	
	Если ИтогиАктуальны() = 0 Тогда
		ВремРегистры.Актуальность(1);
		ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
	                                                  
	ОбработкаПроведенияПоКассе(ВремКасса);
	
КонецПроцедуры // ОбработкаПроведенияПрочее()

//******************************************************************************
// ПроведениеПоРегистрам()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
// Описание:
//  Проведение по регистрам оперативного учета.
//
Процедура ПроведениеПоРегистрам()
	
	// Удаление движений по регистрам.
	Для Номер = 1 По Метаданные.Регистр() Цикл
		ОчиститьДвижения("Регистр."+Метаданные.Регистр(Номер).Идентификатор);
	КонецЦикла;
   	
	ОбработкаПроведенияПрочее();
КонецПроцедуры // ПроведениеПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//
//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПроведения(ВидыДвижений)
	
	// Проверка заполненности обязательных реквизитов.
	СписокОбязатРеквизитов = "Фирма,ВалютаИсх,ВалютаКон,Касса,СуммаИсх,СуммаКон";

	Если глВсеРеквизитыДокументаЗаполнены(Контекст,СписокОбязатРеквизитов)=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВалютаИсх <> глРубли Тогда
		РабочийКО = глКО.ЗначениеПоИдентификатору(""+КодОперации.Идентификатор()+"Вал");
	Иначе
		РабочийКО = КодОперации;
	КонецЕсли;

	// Проведение по регистрам оперативного учета.	
	Если (ПустоеЗначение(ВидыДвижений) = 1) ИЛИ (Найти(ВидыДвижений, "Регистр") <> 0) Тогда
		ПроведениеПоРегистрам();
		
		Если СтатусВозврата() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	глПриПроведении(Контекст, ВидыДвижений);
	
КонецПроцедуры //ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
