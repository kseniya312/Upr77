//**************************************************************

Процедура глСоздатьСтруктуруРегистра(ИмяРег,Таб) Экспорт
	//*****
	Перем Идент,ТипРегистра,ТипКолонки;
	Перем МДОбъект,ТекОбъект;
	Перем К;
	//*****
	Если Таб.КоличествоКолонок()>0 Тогда
		Таб.Очистить();
	КонецЕсли;
	//*****
	МДОбъект=Метаданные.Регистр(ИмяРег);
	ТипРегистра=МДОбъект.ТипРегистра;
	//*****
	Если ТипРегистра="Остатки" Тогда        
		Таб.НоваяКолонка("Приход","Число",1,0); 
	КонецЕсли;
	Таб.НоваяКолонка("НомерСтрокиДокумента","Число",6,0);
	//*****
	Для К=1 По МДОбъект.Измерение() Цикл
		ТекОбъект=МДОбъект.Измерение(К);
		Идент=ТекОбъект.Идентификатор;
		ТипКолонки=ТекОбъект.Тип+?(ПустоеЗначение(ТекОбъект.Вид)=0,"."+ТекОбъект.Вид,"");
		Таб.НоваяКолонка(Идент,ТипКолонки,ТекОбъект.Длина,ТекОбъект.Точность);
	КонецЦикла;
	//*****
	Для К=1 По МДОбъект.Ресурс() Цикл
		ТекОбъект=МДОбъект.Ресурс(К);
		Идент=ТекОбъект.Идентификатор;
		Таб.НоваяКолонка(Идент,"Число",ТекОбъект.Длина,ТекОбъект.Точность);
	КонецЦикла;
	//*****
	Для К=1 По МДОбъект.Реквизит() Цикл
		ТекОбъект=МДОбъект.Реквизит(К);
		Идент=ТекОбъект.Идентификатор;
		ТипКолонки=ТекОбъект.Тип+?(ПустоеЗначение(ТекОбъект.Вид)=0,"."+ТекОбъект.Вид,"");
		Таб.НоваяКолонка(Идент,ТипКолонки,ТекОбъект.Длина,ТекОбъект.Точность,"3");
	КонецЦикла;
	//*****
КонецПроцедуры //глСоздатьСтруктуруРегистра

//**************************************************************

//---Harleq--- { ПРОЦЕДУРА СЧИТЫВАНИЯ ДВИЖЕНИЙ РЕГИСТРА ДЛЯ ВЫБОРОЧНОГО ПРОВЕДЕНИЯ 
Процедура глСчитатьДвижениеРегистра(Конт,ИмяРег,Таб) Экспорт
	//*****
	Перем Идент,ТипРегистра,ТипКолонки;
	Перем МДОбъект,ТекОбъект;
	Перем К;
	Перем Рег;
	//*****                                     
	глСоздатьСтруктуруРегистра(ИмяРег,Таб);
	
	МДОбъект=Метаданные.Регистр(ИмяРег);
	ТипРегистра=МДОбъект.ТипРегистра;
	//*****
	
	Рег=СоздатьОбъект("Регистр."+ИмяРег);
	Рег.ВыбратьДвиженияДокумента(Конт.ТекущийДокумент());
	
	Пока Рег.ПолучитьДвижение() = 1 Цикл
		Таб.НоваяСтрока();
		Если ТипРегистра="Остатки" Тогда
			Таб.Приход = Рег.Приход;
		КонецЕсли;    
		Таб.НомерСтрокиДокумента = Рег.НомерСтроки();
		//*****
		Для К=1 По МДОбъект.Измерение() Цикл
			ТекОбъект=МДОбъект.Измерение(К);
			Идент=ТекОбъект.Идентификатор;
			Таб.УстановитьЗначение(Таб.НомерСтроки,Идент,Рег.ПолучитьАтрибут(Идент));
		КонецЦикла;
		//*****
		Для К=1 По МДОбъект.Ресурс() Цикл
			ТекОбъект=МДОбъект.Ресурс(К);
			Идент=ТекОбъект.Идентификатор;
			Таб.УстановитьЗначение(Таб.НомерСтроки,Идент,Рег.ПолучитьАтрибут(Идент));
		КонецЦикла;
		//*****
		Для К=1 По МДОбъект.Реквизит() Цикл
			ТекОбъект=МДОбъект.Реквизит(К);
			Идент=ТекОбъект.Идентификатор;
			Таб.УстановитьЗначение(Таб.НомерСтроки,Идент,Рег.ПолучитьАтрибут(Идент));
		КонецЦикла;
	КонецЦикла;
	//*****
КонецПроцедуры //глСоздатьСтруктуруРегистра
                              
//**************************************************************

Процедура глЗаписатьВРегистр(Конт,Рег,Таб) Экспорт
	//*****
	Перем НомСтр,НомКол,ВсегоСтр,ВсегоКол;
	Перем СписИзм,СписРес;
	Перем МДОбъект,ТипРегистра;
	Перем Идент,ЗначРекв;
	Перем ФлДвиж;
	//*****
	СписИзм="";       //Список измерений и реквизитов
	СписРес="";       //Список ресурсов
	МДОбъект=Метаданные.Регистр(Рег.Вид());
	ТипРегистра=МДОбъект.ТипРегистра;
	ВсегоКол=Таб.КоличествоКолонок();
	//*****
	Для НомКол=1 По ВсегоКол Цикл
		Идент=Таб.ПолучитьПараметрыКолонки(НомКол);
		Если (Идент="Приход") ИЛИ (Идент="НомерСтрокиДокумента") Тогда
			СписИзм=СписИзм+?(ПустаяСтрока(СписИзм)=1,"",",")+Идент;
		Иначе
			Если (МДОбъект.Измерение(Идент).Выбран()=1) ИЛИ (МДОбъект.Реквизит(Идент).Выбран()=1) Тогда
				СписИзм=СписИзм+?(ПустаяСтрока(СписИзм)=1,"",",")+Идент;
			ИначеЕсли МДОбъект.Ресурс(Идент).Выбран()=1 Тогда
				СписРес=СписРес+?(ПустаяСтрока(СписРес)=1,"",",")+Идент;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	//*****
	Таб.Свернуть(СписИзм,СписРес);
	СписРес=СписРес+",";
	ВсегоКол=Таб.КоличествоКолонок();
	ВсегоСтр=Таб.КоличествоСтрок();
	//*****
	Для НомСтр=1 По ВсегоСтр Цикл
		Конт.ПривязыватьСтроку(Таб.ПолучитьЗначение(НомСтр,"НомерСтрокиДокумента"));
		ФлДвиж=0;
		Для НомКол=1 По ВсегоКол Цикл
			Идент=Таб.ПолучитьПараметрыКолонки(НомКол);
			Если (Идент<>"Приход") И (Идент<>"НомерСтрокиДокумента") Тогда
				ЗначРекв=Таб.ПолучитьЗначение(НомСтр,Идент);
				Рег.УстановитьАтрибут(Идент,ЗначРекв);
				Если Найти(СписРес,Идент+",")>0 Тогда
					Если (ЗначРекв<>0)И(ФлДвиж=0) Тогда //---Harleq--- Добавил (ФлДвиж=0)
						ФлДвиж=1;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если ФлДвиж=1 Тогда
			Если ТипРегистра="Остатки" Тогда
				Если Таб.ПолучитьЗначение(НомСтр,"Приход")=1 Тогда
					Рег.ДвижениеПриходВыполнить();
				Иначе
					Рег.ДвижениеРасходВыполнить();
				КонецЕсли;
			Иначе
				Рег.ДвижениеВыполнить();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	//*****
КонецПроцедуры //глЗаписатьВРегистр

//**************************************************************

Функция ИмяАтр(Знач стр, Знач ч)
    Возврат СокрЛП( стр) + Формат( ч, "Ч+2.0");
КонецФункции

//**************************************************************
//
Процедура ОбработкаПроведения()
	Перем ТабДвиж;
	Перем Атр;
	Перем Имя;
    	Перем Рег;
	Перем х;
	
	Если КоличествоСтрок() = 0 Тогда
		Сообщить( "Пустой документ " + ТекущийДокумент(), "!");
		СтатусВозврата( 0);
		Возврат;
	КонецЕсли;
	Рег = Метаданные.Регистр( СокрЛП( ИмяРегистра));
	ТабДвиж = СоздатьОбъект( "ТаблицаЗначений");
	глСоздатьСтруктуруРегистра( СокрЛП( ИмяРегистра), ТабДвиж);
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл;
		ТабДвиж.НоваяСтрока();
		Если флРасход = 0 Тогда
			ТабДвиж.Приход = 1;    
		КонецЕсли;
		Для х = 1 По Рег.Измерение() Цикл
			Имя = Рег.Измерение( х).Идентификатор;
			Атр = ИмяАтр( "ЗначИзмерения", х);
			ТабДвиж.УстановитьЗначение(ТабДвиж.НомерСтроки, Имя,ПолучитьАтрибут( Атр));
		КонецЦикла;
		Для х = 1 По Рег.Ресурс() Цикл
			Имя = Рег.Ресурс( х).Идентификатор;
			Атр = ИмяАтр( "ЗначРесурса", х);
			ТабДвиж.УстановитьЗначение(ТабДвиж.НомерСтроки, Имя,ПолучитьАтрибут( Атр));
		КонецЦикла;
	КонецЦикла;
	Рег = Регистр.ПолучитьАтрибут(СокрЛП( ИмяРегистра));
	глЗаписатьВРегистр( Контекст, Рег, ТабДвиж);

КонецПроцедуры
